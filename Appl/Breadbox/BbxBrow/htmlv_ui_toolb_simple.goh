/**
  * "Simple Toolbar", reworked for the FreeGEOS project
  */

@object GenInteractionClass SimpleToolbar = {
    GI_comp =
         @SimpleToolbarNavigateGroup
        ,@SimpleToolbarViewGroup         /* this group is pulled in from the indicators */
        ,@SimpleToolbarPrint
        ,@StatusIndicators;               /* these are pulled in from the indicators */

    HINT_EXPAND_WIDTH_TO_FIT_PARENT ;
    HINT_CUSTOM_CHILD_SPACING = { SST_PIXELS | 2 } ;  /* 2 doesn't override toolbox */
    HINT_ORIENT_CHILDREN_HORIZONTALLY ;
    HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY ;
    //HINT_ALLOW_CHILDREN_TO_WRAP ;
    HINT_CENTER_CHILDREN_VERTICALLY;
    HINT_TOOLBOX ;
}

/*
 * Navigational buttons
 */
@object GenInteractionClass SimpleToolbarNavigateGroup = {
    GI_comp =
        @SimpleToolbarGoto,
        @SimpleToolbarBack,
        @SimpleToolbarNext,
        @SimpleToolbarReload,
        @SimpleToolbarStop,
        @SimpleToolbarHome
@ifdef COMPILE_OPTION_JUMP_FEATURE        
        ,@SimpleToolbarJump
@endif
        ;

    HINT_CUSTOM_CHILD_SPACING = { SST_PIXELS | 0 } ;
    HINT_ORIENT_CHILDREN_HORIZONTALLY ;
}

/* 
 * Goto button in toolbar 
 */
@object GenTriggerClass SimpleToolbarGoto = {
    GI_states = @default & ~GS_USABLE;    
    GI_visMoniker = list { @SimpleToolbarGotoMoniker, @GPCGoIconMoniker } ;
    GTI_destination = @GotoDialog;
    GTI_actionMsg = MSG_GEN_INTERACTION_INITIATE;
    
    HINT_USE_ICON_TEXT_COMBINATION_MONIKER = { 0, 2 } ;
    ATTR_GEN_FOCUS_HELP = @SimpleToolbarGotoHelp ;
}
@visMoniker SimpleToolbarGotoMoniker = "Goto" ;
@chunk TCHAR SimpleToolbarGotoHelp[] = _TEXT("Goto a Web Page") ;

@object GenTriggerClass SimpleToolbarHome = {
    GI_visMoniker = list { @HomeMoniker, @GPCHomeIconMoniker } ;
    GTI_destination = process ;
    GTI_actionMsg = MSG_HMLVP_HOME_TRIGGER ;
    ATTR_GEN_FOCUS_HELP = @HomeHelp ;
    
    HINT_USE_ICON_TEXT_COMBINATION_MONIKER = { 0, 2 } ;
}

@object GenTriggerClass SimpleToolbarBack = {
    GI_visMoniker = list { @BackMoniker, @GPCBackIconMoniker } ;
    GTI_destination = (TO_APP_MODEL) ;
    GTI_actionMsg = MSG_URL_DOCUMENT_BACK ;
    
    ATTR_GEN_FOCUS_HELP = @BackHelp ;
    ATTR_GEN_FOCUS_DISABLED_HELP = @BackDisabledHelp ;
    HINT_USE_ICON_TEXT_COMBINATION_MONIKER = { 0, 2 } ;
}

@object GenTriggerClass SimpleToolbarNext = {
    GI_visMoniker = list { @NextMoniker, @GPCNextIconMoniker } ;
    GTI_destination = (TO_APP_MODEL) ;
    GTI_actionMsg = MSG_URL_DOCUMENT_FORWARD ;
    
    ATTR_GEN_FOCUS_HELP = @NextHelp ;
    ATTR_GEN_FOCUS_DISABLED_HELP = @NextDisabledHelp ;
    HINT_USE_ICON_TEXT_COMBINATION_MONIKER = { 0, 2 } ;
}

@object GenTriggerClass SimpleToolbarReload = {
    GI_visMoniker = list { @ReloadMoniker, @GPCReloadIconMoniker } ;
    GTI_destination = (TO_APP_MODEL) ;
    GTI_actionMsg = MSG_URL_DOCUMENT_RELOAD ;
    
    ATTR_GEN_FOCUS_HELP = @ReloadHelp ;
    ATTR_GEN_FOCUS_DISABLED_HELP = @ReloadDisabledHelp ;
    // GI_states = @default & ~GS_USABLE;
    HINT_USE_ICON_TEXT_COMBINATION_MONIKER = { 0, 2 } ;
}

@object GenTriggerClass SimpleToolbarStop = {
    GI_visMoniker = list { @StopMoniker, @GPCStopIconMoniker } ;
    GTI_destination = @HTMLVApp ;
    GTI_actionMsg = MSG_HMLVA_STOP_TRIGGER ;
    
    ATTR_GEN_FOCUS_HELP = @StopHelp ;
    HINT_USE_ICON_TEXT_COMBINATION_MONIKER = { 0, 2 } ;
}

/* 
 * Goto dialog for Goto button in toolbar 
 */
@object GenInteractionClass GotoDialog = {
    GI_visMoniker = "Goto";
    GI_comp = @GotoPrompt, @GotoEntry, @GotoGoto, @GotoDont;
    GII_visibility = GIV_DIALOG;
    GII_attrs = @default  | GIA_NOT_USER_INITIATABLE | GIA_MODAL;
}
@object GenTextClass GotoPrompt = {
    GTXI_text = "Enter a Web Site to Goto:";
    GI_attrs = @default | GA_READ_ONLY;
}
@object GenTextClass GotoEntry = {
    GTXI_text = "";
    GTXI_maxLength = HTML_MAX_BUF-1;
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    
    HINT_DEFAULT_FOCUS;
    HINT_FIXED_SIZE = { SST_AVG_CHAR_WIDTHS|50, 0 };
}
@object GenTriggerClass GotoGoto = {
    GI_visMoniker = "Goto";
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    GTI_destination = process;
    GTI_actionMsg = MSG_HMLVP_GOTO;
    HINT_SEEK_REPLY_BAR;
    HINT_DEFAULT_DEFAULT_ACTION;
}
@object GenTriggerClass GotoDont = {
    GI_visMoniker = "Don't Goto";
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    
    HINT_SEEK_REPLY_BAR;
}

/* 
 * Print button in toolbar 
 */
 @object GenTriggerClass SimpleToolbarPrint = {
    GI_states = (@default) & ~GS_USABLE;
    GI_visMoniker = list { @GPCPrintIconMoniker, @PrintMoniker } ;
    GTI_destination = @PrintControl ;
    GTI_actionMsg = MSG_PRINT_CONTROL_INITIATE_PRINT ;
    
    HINT_USE_ICON_TEXT_COMBINATION_MONIKER = { 0, 2 } ;
    ATTR_GEN_FOCUS_HELP = @PrintHelp ;
}
@chunk TCHAR PrintHelp[] = _TEXT("Send copy of current page to the printer") ;


@ifdef COMPILE_OPTION_JUMP_FEATURE
@object GenTriggerClass SimpleToolbarJump = {
    /*apparently no Jump icon moniker was ever created*/
    GI_visMoniker = list { @JumpMoniker, @GPCPrintIconMoniker } ;
    GTI_destination = @JumpDialog;
    GTI_actionMsg = MSG_GEN_INTERACTION_INITIATE;
    
    HINT_USE_ICON_TEXT_COMBINATION_MONIKER = { 0, 2 } ;
    ATTR_GEN_FOCUS_HELP = @JumpHelp ;
}
@chunk TCHAR JumpHelp[] = _TEXT("Jump to page") ;
@object GenInteractionClass JumpDialog = {
    GI_visMoniker = "Jump";
    GI_comp = @JumpEntry, @JumpJump, @JumpDont;
    GII_visibility = GIV_DIALOG;
    GII_attrs = @default  | GIA_NOT_USER_INITIATABLE | GIA_MODAL;
}
@object GenTextClass JumpEntry = {
    GI_visMoniker = 'J',"Jump To:";
    GTXI_text = "";
    GTXI_maxLength = HTML_MAX_BUF-1;
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    
    HINT_DEFAULT_FOCUS;
}
@object GenTriggerClass JumpJump = {
    GI_visMoniker = "Jump";
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    GTI_destination = process;
    GTI_actionMsg = MSG_HMLVP_JUMP;
    
    HINT_SEEK_REPLY_BAR;
    HINT_DEFAULT_DEFAULT_ACTION;
}
@object GenTriggerClass JumpDont = {
    GI_visMoniker = "Don't Jump";
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    HINT_SEEK_REPLY_BAR;
}
@endif // COMPILE_OPTION_JUMP_FEATURE

@visMoniker HomeMoniker = "Home" ;
@visMoniker BackMoniker = "Back" ;
@visMoniker NextMoniker = "Next" ;
@visMoniker ReloadMoniker = "Reload" ;
@visMoniker StopMoniker = "Stop" ;
@visMoniker PrintMoniker = "Print" ;
@ifdef GLOBAL_INTERNET_BUILD
    @visMoniker JumpMoniker = "Jump" ;
@endif
