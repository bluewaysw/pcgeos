@include "options.goh"
@ifndef COMPILE_OPTION_TURN_OFF_LOGO
@include "glbanim.goh"
@endif

@ifdef COMPILE_OPTION_PARENTAL_CONTROL
@include <parentc.goh>
@endif

@ifdef COMPILE_OPTION_IDIAL_CONTROL
@include <Objects/iDialCC.goh>
@endif

/*
 * Application Object
 */
@start AppResource;

@visMoniker TextMoniker = "WebMagick";
@include "Art/appicon.goh"

@object HTMLVApplicationClass HTMLVApp = {
         GI_visMoniker = @list{@TextMoniker,@Moniker0,@Moniker1,
                         @Moniker2,@Moniker3,@Moniker4}
    ATTR_GEN_INIT_FILE_CATEGORY = HTMLVIEW_CATEGORY;
    ATTR_GEN_INIT_FILE_PROPAGATE_TO_CHILDREN;
    ATTR_GEN_HELP_FILE = "WebMagick Help File";
    /* ATTR_GEN_APPLICATION_SAVE_OPTIONS_TRIGGER = @SaveOptionsButton; */
    ATTR_GEN_APPLICATION_PRINT_CONTROL = @PrintControl;

    gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_SELF_LOAD_OPTIONS) =
         @PrintControl
@ifdef COMPILE_OPTION_FONT_AND_SIZE_CONTROL_ON
        ,@FontControl
        ,@SizeControl
@endif
@ifdef COMPILE_OPTION_CUSTOMIZE_TOOL_BARS
        ,@ToolControl
@endif
        ,@DocumentControl
        ,@EditControl
        ,@SearchReplaceControl
@ifdef COMPILE_OPTION_MULTIPLE_WINDOWS
        ,@htmlv_ui_obj0050
@endif
        ,@ViewControl;

    gcnList(MANUFACTURER_ID_GEOWORKS,MGCNLT_ACTIVE_LIST) =
         @PrintControl
@ifdef COMPILE_OPTION_FONT_AND_SIZE_CONTROL_ON
        ,@FontControl
        ,@SizeControl
@endif
@ifdef COMPILE_OPTION_CUSTOMIZE_TOOL_BARS
        ,@ToolControl
@endif
        ,@DocumentControl
        ,@EditControl
        ,@SearchReplaceControl
@ifdef COMPILE_OPTION_MULTIPLE_WINDOWS
        ,@htmlv_ui_obj0050
@endif
        ,@ViewControl;

@ifdef COMPILE_OPTION_CUSTOMIZE_TOOL_BARS
    gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_STARTUP_LOAD_OPTIONS) =
        @ToolControl;
@endif

    gcnList(MANUFACTURER_ID_GEOWORKS,MGCNLT_APP_STARTUP) =
        @DocumentControl;

    gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_WINDOWS) =
        @HTMLVPrimary;

    GI_comp = @HTMLVPrimary;
}
@end AppResource;


/*
 * File Menu
 */
@start FileResource;
@include "htmlv_ui_men_file.goh"
@end FileResource;


/*
 * Edit Menu
 */
@start EditResource;
@include "htmlv_ui_men_edit.goh"
@end EditResource;


/*
 * View Menu
 */
@start ViewResource;
@include "htmlv_ui_men_view.goh"
@end ViewResource;


/*
 * Options Menu
 */
@start OptionsResource;
@include "htmlv_ui_men_options.goh"
@end OptionsResource;


/*
 * Navigational Menus
 */
@start NavigateResource;
@include "htmlv_ui_men_nav.goh"
@include "htmlv_ui_men_history.goh"
@include "htmlv_ui_men_kidguard.goh"
@include "htmlv_ui_men_autobrowse.goh"
@include "htmlv_ui_men_js.goh"
@end NavigateResource;


/*
 * Bookmark Menu
 */
@ifdef COMPILE_OPTION_BOOKMARKS
    @include "bookui.goh"
@endif

/*
 * Favorites Menu
 */
@ifdef COMPILE_OPTION_FAVORITES // this is where we roll
    @include "favorui.goh"
@endif

/*
 * Local Menu
 */
@ifdef COMPILE_OPTION_LOCAL_PAGES
@start LocalUIResource;
    @include "localui.goh"
@end LocalUIResource;
@endif

/*
 * HTML Menu
 */
@start HTMLMenuResource;
    @include "htmlv_ui_men_html.goh"
@end HTMLMenuResource;

/*
 * Search Menu
 */
@start SearchResource;
@ifdef COMPILE_OPTION_SEARCH_MENU
    @include "htmlv_ui_men_search.goh"
@endif
@end SearchResource;


/*
 * Window Menu
 */
@start WindowResource;
@ifdef COMPILE_OPTION_MULTIPLE_WINDOWS
    @object GenDisplayControlClass htmlv_ui_obj0050 = {
        GDCII_attrs = GDCA_MAXIMIZED_NAME_ON_PRIMARY;
    }
@endif
@end WindowResource;

/*
 * Download Dialog
 */
@start DownloadDialogResource;
@include "htmlv_ui_dlg_dwnl.goh"
@object GenInteractionClass DownloadDlg = {
    GI_visMoniker = "Save Web File";
    GII_visibility = GIV_DIALOG;
    GII_attrs = @default | GIA_NOT_USER_INITIATABLE
                         | GIA_INITIATED_VIA_USER_DO_DIALOG
                         | GIA_MODAL;
    GI_comp = @DownloadFileSel,@DownloadBtn,@DownloadAbortBtn,@DownloadNameText;
}
@end DownloadDialogResource;

/*
 * Cache Cleanup Dialog
 */
@start CacheCleanupDialogResource;
@object GenInteractionClass CacheCleanupDialog = {
    GI_visMoniker = "Cache Cleanup";
    GII_visibility = GIV_DIALOG;
    GII_attrs = @default | GIA_NOT_USER_INITIATABLE
                         | GIA_MODAL;
    GI_comp = @CacheCleanupText;
}

@object GenTextClass CacheCleanupText = {
    GI_attrs = @default | GA_READ_ONLY;
    GTXI_text = "Cleaning up previous browsing data. Please wait a moment.";
    HINT_FIXED_SIZE = {
        SST_AVG_CHAR_WIDTHS | 40,
        0,
        0
    };
}
@end CacheCleanupDialogResource;

/*
 * Status indicators, to be used in the status bar or elsewhere
 */
@start StatusResource;
    @include "htmlv_ui_status_indctrs.goh"
@end StatusResource;

/*
 * the mighty toolbars and URL entry field
 */
@start ToolbarResource;

@include "htmlv_ui_url.goh" // FIXME: make an additional compile flag for old and new simple variants

@ifdef COMPILE_OPTION_SIMPLE_BUTTON_TOOLBAR
@ifdef COMPILE_OPTION_FREEGEOS_BUTTON_TOOLBAR
    @include "htmlv_ui_toolb_simple_fg.goh"
@else
    @include "htmlv_ui_toolb_simple.goh"
@endif
@endif

@ifdef COMPILE_OPTION_LITTLE_BUTTON_TOOLBAR
    @include "htmlv_ui_toolb_small.goh"
@endif

@ifdef COMPILE_OPTION_BIG_BUTTON_TOOLBAR
    @include "htmlv_ui_toolb_big.goh"
@endif

@end ToolbarResource;

/*
 * Help UI resources
 */
@ifdef GLOBAL_INTERNET_BUILD
@start HelpUIResource;
    @visMoniker HelpWindowMoniker = "Help";
    @object GenInteractionClass HelpWindow = {
        GI_comp = @HelpTop;
        GII_visibility = GIV_DIALOG;
        GII_attrs = @default  | GIA_NOT_USER_INITIATABLE;
        HINT_INTERACTION_MAKE_RESIZABLE;
        HINT_SIZE_WINDOW_AS_RATIO_OF_PARENT = {
            SWSS_RATIO | PCT_50,
            SWSS_RATIO | PCT_80
        };
        HINT_POSITION_WINDOW_AT_RATIO_OF_PARENT = {
            SWSS_RATIO | PCT_50,
            SWSS_RATIO | PCT_15
        };
    }
    @object GenInteractionClass HelpTop = {}
@end HelpUIResource;

@start HelpFrameResource;
    @object URLFrameClass HelpFrame = {}
@end HelpFrameResource;
@endif

//@include "init/expireui.goh"

/*
 * Parental control stuff
 */

@ifdef COMPILE_OPTION_PARENTAL_CONTROL
@start PCRootResource;
    @object ParentalControlClass PCRoot = {}
@end PCRootResource;

@start PCResource;
    @include "htmlv_ui_dlg_kidguard.goh"
@end PCResource;
@endif


/*
 * load the tool monikers
 */
@start ToolMonikerResource, data;
@include "Art/toolicon.goh"
@end ToolMonikerResource;

/*
 * load other icons
 * FIXME: find out what we really need in which UI
 */
@include "icons/icons.goh"

/*
* Interface - bringing it all together
*/
@start Interface;

@chunk TCHAR HomeHelp[]           = _TEXT("Display home page") ;
@chunk TCHAR BackHelp[]           = _TEXT("Display previous page") ;
@chunk TCHAR BackDisabledHelp[]   = _TEXT("No previous page to display") ;
@chunk TCHAR NextHelp[]           = _TEXT("Display the next page") ;
@chunk TCHAR NextDisabledHelp[]   = _TEXT("A next page does not exist") ;
@chunk TCHAR StopHelp[]           = _TEXT("Stop the current download") ;
@chunk TCHAR ReloadHelp[]         = _TEXT("Update the current page") ;
@chunk TCHAR ReloadDisabledHelp[] = _TEXT("No page is available to update") ;
@chunk TCHAR LoadGHelp[]          = _TEXT("Load graphics for current page") ;
@chunk TCHAR LoadGDisabledHelp[]  = _TEXT("No graphics are available for loading") ;

@ifdef COMPILE_OPTION_HTML_EDITOR
@chunk TCHAR EditHelp[]           = _TEXT("Edit the current page") ;
@chunk TCHAR EditDisabledHelp[]   = _TEXT("No page is available to edit") ;
@endif


@object GenPrimaryClass HTMLVPrimary = {
    /*
     *              Main Window
     */
    ATTR_GEN_HELP_CONTEXT = "TOC";
    ATTR_GEN_INIT_FILE_PROPAGATE_TO_CHILDREN;
    HINT_PRIMARY_NO_FILE_MENU;
    GI_comp = @FileMenu
             ,@EditMenu
             ,@ViewMenu
@ifdef COMPILE_OPTION_NAVIGATION_MENU
             ,@NavigateMenu
@endif
             ,@OptionsMenu
@ifdef COMPILE_OPTION_BOOKMARKS
             ,@BookmarkMenu
@endif
@ifdef COMPILE_OPTION_FAVORITES
             ,@FavoritesMenu
@endif
@ifdef COMPILE_OPTION_HISTORY_ON_MENU_BAR
             ,@HistoryMenu
@endif
@ifdef COMPILE_OPTION_LOCAL_PAGES
             ,@LocalMenu
@endif
@ifdef COMPILE_OPTION_HTML_MENU
             ,@HTMLMenu
@endif
@ifdef COMPILE_OPTION_MULTIPLE_WINDOWS
             ,@WindowMenu
@endif
@ifdef COMPILE_OPTION_SEARCH_MENU
             ,@SearchMenu
@endif
@ifdef COMPILE_OPTION_PARENTAL_CONTROL
             ,@PCMenu
@endif
@ifdef COMPILE_OPTION_AUTO_BROWSE
@ifdef GLOBAL_INTERNET_BUILD
             ,@AutoBrowseMenu
@endif
@endif
@if defined(JAVASCRIPT_SUPPORT) && defined(DO_ERROR_CHECKING)
              ,@JSMenu
@endif
              ,@MainWrapper // this is where the HTML Display Group resides!
@ifdef COMPILE_OPTION_LOCAL_PAGES
             ,@LocalSaveDlg
@endif
@ifdef GLOBAL_INTERNET_BUILD
@ifdef COMPILE_OPTION_SIMPLE_BUTTON_TOOLBAR
             ,@JumpDialog
@endif
             ,@HelpWindow
@endif
@if defined(COMPILE_OPTION_TOGGLE_BARS) && defined(COMPILE_OPTION_SIMPLE_BUTTON_TOOLBAR)
             ,@GotoDialog
@endif
@ifdef COMPILE_OPTION_PARENTAL_CONTROL
             ,@PCToggleOffDlg
             ,@PCToggleOnDlg
             ,@PCSiteAlreadyAddedDlg
             ,@PCSiteNotInListDlg
@endif
@ifdef COMPILE_OPTION_IDIAL_CONTROL
             ,@IDialControl
@endif
             ,@CacheCleanupDialog
             ,@DownloadDlg
           //,@ExpireDialog
             ;
    HINT_PRIMARY_FULL_SCREEN ;
    ATTR_GEN_WINDOW_KBD_OVERRIDE = KO_KEYBOARD_REQUIRED;
}

/*
 * the menus
 */

@object GenInteractionClass FileMenu = {
    GI_attrs = @default | GA_KBD_SEARCH_PATH;
    GII_visibility = GIV_POPUP;
    ATTR_GEN_INIT_FILE_PROPAGATE_TO_CHILDREN;
    ATTR_GEN_INTERACTION_GROUP_TYPE = {GIGT_FILE_MENU};
    GI_comp = @DocCtrlHolder
             ,@DocOpenTrigger
             //,@CustomFileSelDBox
             ,@ClearCacheTrigger
             ,@AboutGroup
             ,@htmlv_ui_obj0005
             ;
}

@object GenInteractionClass EditMenu = {
    GI_attrs = @default | GA_KBD_SEARCH_PATH;
    GII_visibility = GIV_POPUP;
    ATTR_GEN_INTERACTION_GROUP_TYPE = (GIGT_EDIT_MENU);
    GI_comp =
        @EditControl,
@ifdef COPY_TO_SCRAPBOOK
        @CopyToScrapbook,
@endif
        @SearchReplaceControl;
}

@object GenInteractionClass ViewMenu = {
    GI_attrs = @default | GA_KBD_SEARCH_PATH;
    GII_visibility = GIV_POPUP;
    ATTR_GEN_INTERACTION_GROUP_TYPE = (GIGT_VIEW_MENU);
    ATTR_GEN_INIT_FILE_PROPAGATE_TO_CHILDREN;
    GI_comp = @ViewControl,
@if defined(COMPILE_OPTION_TOGGLE_BARS) && defined(COMPILE_OPTION_SIMPLE_BUTTON_TOOLBAR)
              @GraphicsGroup,
@else
              @HTMLSettingsGroup,
              @LoadGraphicsGroup,
@endif
              @NavigateReload
@ifdef COMPILE_OPTION_RECALCULATE_BUTTON_ON
            , @RecalculateButton
@endif
@if defined(COMPILE_OPTION_TOGGLE_BARS) && defined(COMPILE_OPTION_SIMPLE_BUTTON_TOOLBAR)
            , @ToggleToolbarGroup
@endif
              ;
}

@object GenInteractionClass NavigateMenu = {
    GI_attrs = @default | GA_KBD_SEARCH_PATH;
    GI_visMoniker = 'N',"Navigate";
    GII_visibility = GIV_POPUP;
    GI_comp = @NavigateHome,@NavigateBack,@NavigateForward,@NavigateStop
@ifndef COMPILE_OPTION_HISTORY_ON_MENU_BAR
              , @HistoryMenu
@endif
@ifdef COMPILE_OPTION_AUTO_BROWSE
@ifndef GLOBAL_INTERNET_BUILD
              ,@htmlv_ui_obj0023, @htmlv_ui_obj0024, @BulkTestWithIndexTrigger
@endif
@endif
              ;
}

@object GenInteractionClass OptionsMenu = {
    GII_visibility = GIV_POPUP;
    ATTR_GEN_INTERACTION_GROUP_TYPE = GIGT_OPTIONS_MENU;
    ATTR_GEN_INIT_FILE_PROPAGATE_TO_CHILDREN;

@if !defined(COMPILE_OPTION_FONT_AND_SIZE_CONTROL_ON) && \
    !defined(COMPILE_OPTION_SHOW_AND_HIDE_TOOLBAR) && \
    !defined(COMPILE_OPTION_ENABLE_SETTINGS) && \
    !defined(COMPILE_OPTION_ENABLE_MISCELLANEOUS) && \
    !defined(COMPILE_OPTION_CUSTOMIZE_TOOL_BARS) && \
     defined(COMPILE_OPTION_AUTO_SAVE_OPTIONS) && \
    !defined(COMPILE_OPTION_ENABLE_NUMCONN_OPTION)
    GI_states = (@default) & ~GS_USABLE;
@endif

    GI_comp =
@ifdef COMPILE_OPTION_FONT_AND_SIZE_CONTROL_ON
              @htmlv_ui_obj0026,
@endif
@ifdef COMPILE_OPTION_SHOW_HIDE_TOOLBAR
              @ShowHideToolbarGroup,
@endif
              @htmlv_ui_obj0033
             ,@htmlv_ui_obj0041
@ifdef COMPILE_OPTION_CUSTOMIZE_TOOL_BARS
             ,@htmlv_ui_obj0046
@endif
@ifdef COMPILE_OPTION_ENABLE_NUMCONN_OPTION
             ,@NumConnBooleanGroup
@endif
@ifndef COMPILE_OPTION_AUTO_SAVE_OPTIONS
             ,@SaveOptionsGroup
@endif
              ;
}

@ifdef COMPILE_OPTION_BOOKMARKS
@object GenInteractionClass BookmarkMenu = {
    GI_attrs = @default | GA_KBD_SEARCH_PATH;
    GI_visMoniker = 'B', "Bookmarks" ;
    GII_visibility = GIV_POPUP;
    GI_comp = @BookmarkTriggerAdd,
              @BookmarkTriggerList,
              @BookmarksDialogList,
              @BookmarkEditDialog ;
}
@endif

@ifdef COMPILE_OPTION_FAVORITES
@object GenInteractionClass FavoritesMenu = {
    GI_attrs = @default | GA_KBD_SEARCH_PATH;
    GI_visMoniker = 'a', "Favorites" ;
    GII_visibility = GIV_POPUP;
    GI_comp = @FavoriteTriggerAdd,
              @FavoriteEditDialog,
              @FavoriteEditGroupDialog,
              @FavoritesDialogList,
              @FavoriteTriggerList,
              @FavoritesMoreTrigger,
              @FavoriteCreateDialog,
              @FavoritesListDialog,
              @FavoriteCreateGroupDialog ;
}
@endif

@ifdef COMPILE_OPTION_LOCAL_PAGES
@object GenInteractionClass LocalMenu = {
    GI_attrs = @default | GA_KBD_SEARCH_PATH;
    GI_visMoniker = 'L', "Local" ;
    GII_visibility = GIV_POPUP;
    GI_comp = @LocalSaveTrigger,
              @LocalDelete,
              @LocalUpdateTrigger,
              @LocalList;
}
@endif

@ifdef COMPILE_OPTION_HTML_MENU
@object GenInteractionClass HTMLMenu = {
    GI_attrs = @default | GA_KBD_SEARCH_PATH;
    GI_visMoniker = 'T', "HTML" ;
    GII_visibility = GIV_POPUP;
    GI_comp = @SaveViewHTMLGroup
@ifdef COMPILE_OPTION_HTML_EDITOR
             ,@EditTrigger
             ,@DefineHTMLEditor
@endif
              ;
    ATTR_GEN_INIT_FILE_PROPAGATE_TO_CHILDREN;
}
@endif

@ifdef COMPILE_OPTION_MULTIPLE_WINDOWS
@object GenInteractionClass WindowMenu = {
    GII_visibility = GIV_POPUP;
    ATTR_GEN_INTERACTION_GROUP_TYPE = GIGT_WINDOW_MENU;
    GI_comp = @htmlv_ui_obj0050;
}
@endif

@ifdef COMPILE_OPTION_SEARCH_MENU
@object GenInteractionClass SearchMenu = {
    GI_visMoniker = 'S', "Search";
    GII_visibility = GIV_POPUP;
    GI_comp = @Search1, @Search2, @Search3, @Search4;
}
@endif

/*
 * main UI wrapper: top bar, display group, status bar
 */
 @object GenInteractionClass MainWrapper = {
    GI_attrs = @default | GA_TARGETABLE;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
    HINT_ORIENT_CHILDREN_VERTICALLY;
    GI_comp = @htmlv_top_bar, @HTMLDisplayGroup, @StatusBar;
}

/*
 * construct top bar from topbarLeft + spinning globe (or not)
 */
@object GenInteractionClass htmlv_top_bar = {
@ifndef COMPILE_OPTION_TURN_OFF_LOGO
    GI_comp = @htmlv_top_bar_left, @SpinningLogo ;
@else
    GI_comp = @htmlv_top_bar_left ;
@endif
@ifdef COMPILE_OPTION_BIG_BUTTON_TOOLBAR
    HINT_BOTTOM_JUSTIFY_CHILDREN;
@else
    HINT_CENTER_CHILDREN_VERTICALLY ;
@endif
    HINT_ORIENT_CHILDREN_HORIZONTALLY ;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT ;
    //HINT_ALLOW_CHILDREN_TO_WRAP;
    HINT_TOOLBOX;
}

/*
 * left side of the top bar: navigational buttons and URL bar
 */
@object GenInteractionClass htmlv_top_bar_left = {
@ifdef COMPILE_OPTION_SIMPLE_BUTTON_TOOLBAR
@ifdef COMPILE_OPTION_FREEGEOS_BUTTON_TOOLBAR
    GI_comp = @SimpleToolbar, @URLToolbar, @SecureStatus
@ifdef COMPILE_OPTION_PARENTAL_CONTROL
    ,@ParentalControlStatus
@endif
    ;
    HINT_ORIENT_CHILDREN_HORIZONTALLY ;
    HINT_CENTER_CHILDREN_VERTICALLY ;
@else
    GI_comp = @SimpleToolbar, @SimpleURLToolbar;
    HINT_ORIENT_CHILDREN_VERTICALLY ;
@endif
    HINT_CUSTOM_CHILD_SPACING = { SST_PIXELS | 10 };
@endif
@ifdef COMPILE_OPTION_LITTLE_BUTTON_TOOLBAR
    GI_comp = @MainToolbar, @URLToolbar ;
@endif
@ifdef COMPILE_OPTION_BIG_BUTTON_TOOLBAR
    GI_comp = @IconBar, @URLToolbar ;
    HINT_CUSTOM_CHILD_SPACING = { SST_PIXELS|0 };
@endif
    HINT_EXPAND_WIDTH_TO_FIT_PARENT ;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT ;
}

/*
 * The HTML Content area
 */
@object GenDisplayGroupClass HTMLDisplayGroup = {
    HINT_INITIAL_SIZE = {
        SST_PCT_OF_FIELD_WIDTH | PCT_70,
        SST_PCT_OF_FIELD_HEIGHT | PCT_55,
        0
    };
    HINT_DEFAULT_FOCUS;
    HINT_DEFAULT_TARGET;
    HINT_DISPLAY_GROUP_TILE_VERTICALLY;
}

/*
 * the status bar
 */

@ifdef COMPILE_OPTION_FREEGEOS_BUTTON_TOOLBAR
@object GenInteractionClass AdditionalStatusbarToolbar = {
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
    HINT_CENTER_CHILDREN_VERTICALLY;
    //HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_TOOLBOX;
    GI_comp = @SimpleToolbarViewGroup
             ,@SimpleToolbarPrint;
}

@object GenInteractionClass StatusBar = {
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
    HINT_CENTER_CHILDREN_VERTICALLY;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_TOOLBOX;
    GI_comp = @ProgressIndicatorBox
             ,@LinkTextBox
@ifdef COMPILE_OPTION_SHOW_ALLOC_WATCHER_READ_OUT
             ,@AllocWatcherReadout
@endif
             ,@AdditionalStatusbarToolbar;
}
@endif

@ifndef COMPILE_OPTION_FREEGEOS_BUTTON_TOOLBAR
@object GenInteractionClass StatusBar = {
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
    HINT_CENTER_CHILDREN_VERTICALLY;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_TOOLBOX;
    GI_comp = @LinkTextBox
@ifndef COMPILE_OPTION_TOGGLE_BARS
@ifdef COMPILE_OPTION_SECURE_STATUS
	      ,@SecureStatus
@endif
@ifdef COMPILE_OPTION_PARENTAL_CONTROL
	      ,@ParentalControlStatus
@endif
@endif
@ifdef COMPILE_OPTION_SHOW_ALLOC_WATCHER_READ_OUT
          ,@AllocWatcherReadout
@endif
          ;
}
@endif

/*
 * setup idial control
 */
@ifdef COMPILE_OPTION_IDIAL_CONTROL
    @object IDialControlClass IDialControl = {};
@endif

/*
 * set up globe
 */
@ifndef COMPILE_OPTION_TURN_OFF_LOGO
    @object GlobeAnimClass SpinningLogo = {
        GAI_autoRun = FALSE;
    }
@endif

@end Interface;
