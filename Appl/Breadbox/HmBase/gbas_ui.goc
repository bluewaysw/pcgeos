
/**************************************************************
 *  ==CONFIDENTIAL INFORMATION==
 *  COPYRIGHT 1994-2000 BREADBOX COMPUTER COMPANY --
 *  ALL RIGHTS RESERVED  --
 *  THE FOLLOWING CONFIDENTIAL INFORMATION IS BEING DISCLOSED TO YOU UNDER A
 *  NON-DISCLOSURE AGREEMENT AND MAY NOT BE DISCLOSED OR FORWARDED BY THE
 *  RECIPIENT TO ANY OTHER PERSON OR ENTITY NOT COVERED BY THE SAME
 *  NON-DISCLOSURE AGREEMENT COVERING THE RECIPIENT. USE OF THE FOLLOWING
 *  CONFIDENTIAL INFORMATION IS RESTRICTED TO THE TERMS OF THE NON-DISCLOSURE
 *  AGREEMENT.
 **************************************************************/

/***********************************************************************
 *
 *     
 * PROJECT:	Breadbox GPCBase
 * FILE:		gbas_ui.goc
 * AUTHOR:	John F. Howard,   11/98
 *
 * DESCRIPTION:
 *	This file has UI code for the application.
 *
 * WHATS NEW:   based on NBase2 2.0 of 8/17/98 and HBase 1.01 of 4/23/97
 *
 *	11/23/98		Begin GPC version
 *                  starting with major UI pieces  
 *	11/24/98		Continuing with UI...
 *                  added Impex, find, attrs
 *	11/25/98		Continuing with UI...
 *                  added sort dbox
 *                  view field functionality
 *	11/27/98		added type functionality - less date check & store
 *   11/28/98		added date check & store - need date retrieval!
 *   11/30/98		fixed a few type bugs
 *                  started date retrieval with ShowData()
 *   12/01/98		finished date retreival
 *                  big start on sorting
 *   12/02/98		finished primary field sort
 *                  finished sort functionality
 *                  did create report db &
 *                  and sel fields sub db
 *   12/03/98       added selnum & selrec dbs
 *   12/04/98       finished create rep db stuff
 *                  worked on modify functionality
 *   12/07/98       did the trig/menu en/disable stuff thru-out
 *                  finished find functionality &
 *                  some other little stuff
 *                  started report window
 *   12/08/97		more report window (swat)...
 *                  fixed closing mod in swat - not NC :-(...
 *                  added rep win buttons
 *                  more on reports
 *   12/09/97		report formatting  [sum & print remain...]
 *   12/10/97		finished summing
 *   12/11/98		added printing, code clean-up,
 *                  about dbox
 *   12/12/98		fixed enter order
 *   12/14/98		added imp dbl click
 *   12/17/98       ver 0.2.0
 *				added date field type conversion on changing type
 *   12/21/98		added default sort button
 *                     and sort on enter/modify
 *   12/17/98       ver 0.3.0
 *   12/28/98		changed field sep from tilde to xa6
 *   01/19/99		Bug fixing...
 *                     84, 91, 93, 94, 95, 123
 *                  Fixed #41
 *   01/20/99		Fixed 1st rec num wrong bug and bug #108
 *                  fixed bug #120
 *                  send to GPC for build 5
 *   01/23/99		messed with pcx moniker conversion
 *   01/27/99		more back & forth w/Tony and Ian on mkrs...
 *                  got the gen & glyph mods into the SDK & working
 *                    in a test applet - only prob is background color
 *                  report module crashes BIG in build 5 :-(
 *   01/28/99		fixed above crash
 *                  all the icons are in!
 *   01/29/99		finished combo icons and added glyph sep lines...
 *                  added compile switch for the GPC mkrs and sep lines...
 *                  version to 0.4.0 for build 6
 *   01/30/99		doc token back to GBDd  ver 0.4.1
 *   02/02/99		added tiny doc icon
 *   02/04/99       did Tony's UI review mods
 *                  version to 0.5.0
 *   02/05/99		added open full screen in CUI
 *   02/06/99		fixed crash on opening report (seen in NT only)
 *   02/19/99		fixed tiny icon not showing up
 *							version 0.6.0
 *   02/21/99		changed help file name
 *   03/05/99		bug fixing...
 *                  288, 324, 328, 336, 339, 371, 377, 385, 434,...
 *   03/06/99       440, 454, 455, 473 & 558 & 366
 *                        version 0.7.0
 *   03/22/99       fixed 572, 322, 635
 *   03/23/99		fixed 123, 317, 321
 *   03/26/99		version 0.8.0
 *
 *   08/11/99		@send to @call when ptrs invloved
 *                  extern file
 *                  fixed 108, did 540
 *   08/12/99		updated curRec on doc close
 *                  and reset view and sort fields to 1
 *   08/13/99		did chg 460
 *                  version 0.9.0 out to GPC
 *   08/25/99		fixed 861
 *                  version 0.10.0 out to GPC
 *   09/01/99       tried to get a done dbox
 *   09/02/99       fixed 734 - made Create new dbox modal
 *                  fixed 861
 *                  fixed 829
 *                  version 0.11.0 out to GPC
 *   09/10/99       messed w/Done dbox some more with build20
 *   09/11/99		finally finished Done dbox!!!  (brianc clarified docs)
 *   09/15/99		version 0.12.0 to GPC
 *   10/05/99		added full screen CUI & maximize AUI #990
 *   10/06/99		version 0.13.0 to GPC
 *   10/10/99		fixed bug 886 (Vis -> Gen modified)
 *   10/11/99		fixed 944, 952, 953, 955, 956, 960, 946
 *                  need more info on 942, 943, 948
 *   10/12/99		fixed 942, started 948
 *   10/13/99       finished 948
 *   10/18/99		investigated 1080 [system] and 1095 [no bug]
 *                  version 0.14.0 to GPC
 *   10/19/99		more 1095 investigation
 *   10/20/99		fixed 1095 FSFC_...
 *                  fixed 1080 HINT-INFREQ
 *                  fixed 943
 *                  version 0.15.0 to GPC
 *   11/09/99		fixed 1279, 1284
 *   11/10/99		version 0.16 to GPC
 *   12/09/99		fixed 952 for good and ever!
 *                  fixed 1403 (trapped para char [field_sep])
 *   12/10/99		added 5000 rec limit
 *                  version 0.17 to GPC
 *   12/16/99		fixed find dbox button alignment
 *                  version 0.18 to GPC
 *   01/12/00		source code into Perforce
 *   02/18/00		fixed bugs 1883 & 1884
 *
 *
 ***********************************************************************/

/*
 ***************************************************************************
 *		Include files
 ***************************************************************************
 */

@include <stdapp.goh>
@include "gpcbase.goh"
@include "hbicons.goh"
@include "btns.goh"
//@include "bbox.goh"
@include "findicon.goh"
@include "rpticons.goh"

/* this define turns on/off the GPC combo icon/text monikers
 * and glyph horizontal separator lines  */
@define COMPILEGPC

/*
 ***************************************************************************
 *		Class & Method Declarations
 ***************************************************************************
 */
/* VLTextClass has to be defined here to avoid a
   glue error.  And all the objects and methods of this
   class have to be in the same source file!!*/

/* This is needed to intercept and initialize for LargeText model
 * and for printing   */
@class GPCBaseVLTextClass, VisLargeTextClass;
@endc  
@classdecl	GPCBaseVLTextClass;

/* classes defined in gpcbase.goh */
@classdecl	GPCBaseProcessClass, neverSaved;
@classdecl	GPCBaseApplicationClass;
@classdecl	RepeatingTriggerClass;
@classdecl	GPCBaseDocumentClass;
@classdecl	GPCBaseFieldTextClass;
@classdecl	GPCBaseDisplayTextClass;
@classdecl	GPCBaseFindTextClass;
@classdecl	GPCBaseEnterTextClass;

/* methods in the doc methods .goc file */
@extern method GPCBaseProcessClass, MSG_GEN_PROCESS_OPEN_APPLICATION;
@extern method GPCBaseDocumentClass, MSG_GEN_DOCUMENT_INITIALIZE_DOCUMENT_FILE;
@extern method GPCBaseDocumentClass, MSG_GEN_DOCUMENT_ATTACH_UI_TO_DOCUMENT;
@extern method GPCBaseDocumentClass, MSG_GEN_DOCUMENT_DETACH_UI_FROM_DOCUMENT;
/*@extern method GPCBaseDocumentClass,
                  MSG_GEN_DOCUMENT_WRITE_CACHED_DATA_TO_FILE; */
@extern method GPCBaseDocumentClass, MSG_GEN_DOCUMENT_SAVE_AS_COMPLETED;
@extern method GPCBaseDocumentClass, MSG_GEN_DOCUMENT_CLOSE;
@extern method GPCBaseProcessClass, MSG_GEN_PROCESS_INSTALL_TOKEN;

/* methods in the field methods .goc file*/
@extern method GPCBaseProcessClass, MSG_OPEN_CHANGE_ATTR;
@extern method GPCBaseProcessClass, MSG_OPEN_NEW_RECORD;
@extern method GPCBaseProcessClass, MSG_CHANGE_ATTR;
@extern method GPCBaseProcessClass, MSG_DISPLAY_NAMES;
@extern method GPCBaseProcessClass, MSG_DISPLAY_FIELDS;
@extern method GPCBaseProcessClass, MSG_CHANGE_NUM_FIELDS_SHOWING;
@extern method GPCBaseProcessClass, MSG_FIELDS_DLIST_MONIKER_QUERY;
@extern method GPCBaseProcessClass, MSG_FIELD_TYPE_STATUS;
@extern method GPCBaseFieldTextClass, MSG_VIS_TEXT_SET_USER_MODIFIED;

/* methods in the data methods .goc file*/
@extern method GPCBaseProcessClass, MSG_ENTER_DATA;
@extern method GPCBaseProcessClass, MSG_DELETE_DATA;
@extern method GPCBaseEnterTextClass, MSG_GEN_TEXT_SET_MODIFIED_STATE;
@extern method GPCBaseEnterTextClass, MSG_VIS_TEXT_SET_USER_MODIFIED;

/* methods in the navigation methods .goc file*/
@extern method GPCBaseProcessClass, MSG_NAV_RECORDS;
@extern method GPCBaseProcessClass, MSG_RECORDS_DLIST_MONIKER_QUERY;
@extern method GPCBaseProcessClass, MSG_RECORDS_DLIST_APPLY;
@extern method RepeatingTriggerClass, MSG_META_START_SELECT;
@extern method RepeatingTriggerClass, MSG_REPEATING_TRIGGER_CLICK;
@extern method RepeatingTriggerClass, MSG_META_LOST_MOUSE_EXCL;
@extern method GPCBaseDisplayTextClass, MSG_VIS_TEXT_SET_USER_MODIFIED;

/* methods in the import methods .goc file*/
@extern method GPCBaseProcessClass, MSG_OPEN_IMPORT;
@extern method GPCBaseProcessClass, MSG_IMPORT_CSV;
@extern method GPCBaseProcessClass, MSG_IMPORT_SELECTOR_ACTION;
@extern method GPCBaseApplicationClass, MSG_CANCEL_IMPORT;

/* methods in the export methods .goc file*/
@extern method GPCBaseProcessClass, MSG_OPEN_EXPORT;
@extern method GPCBaseProcessClass, MSG_EXPORT_CSV;
@extern method GPCBaseApplicationClass, MSG_CANCEL_EXPORT;

/* methods in the search methods .goc file*/
@extern method GPCBaseProcessClass, MSG_OPEN_FIND;
@extern method GPCBaseProcessClass, MSG_SEARCH_FIELDS;
@extern method GPCBaseProcessClass, MSG_SEARCH_DONT_CONTINUE;
@extern method GPCBaseProcessClass, MSG_FIND_TEXT_STATUS;
@extern method GPCBaseFindTextClass, MSG_META_KBD_CHAR;

/* methods in the sort methods .goc file*/
@extern method GPCBaseProcessClass, MSG_OPEN_SORT;
@extern method GPCBaseProcessClass, MSG_SET_SORT_PARAMS;
@extern method GPCBaseProcessClass, MSG_CHANGE_VIEW_FIELD;
@extern method GPCBaseProcessClass, MSG_SORT_DATA;
/*@extern method GPCBaseProcessClass, MSG_DEFAULT_SORT; */
@extern method GPCBaseProcessClass, MSG_VIEW_SORT_STATUS;
@extern method GPCBaseProcessClass, MSG_FIRST_SORT_STATUS;
@extern method GPCBaseProcessClass, MSG_SECOND_SORT_STATUS;
@extern method GPCBaseProcessClass, MSG_THIRD_SORT_STATUS;
@extern method GPCBaseProcessClass, MSG_SORT_ORDER_CHANGED;

/* methods in the report methods .goc file*/
@extern method GPCBaseProcessClass, MSG_OPEN_CREATE_REPORT_DB;
@extern method GPCBaseProcessClass, MSG_REPORT_SHOW_RECORDS_LIST_APPLY;
@extern method GPCBaseProcessClass, MSG_REPORT_SHOW_FIELDS_LIST_APPLY;
@extern method GPCBaseProcessClass, MSG_REPORT_SHOW_NUM_FIELDS_LIST_APPLY;
@extern method GPCBaseProcessClass, MSG_SELECT_RECORDS_DLIST_STATUS;
@extern method GPCBaseProcessClass, MSG_SELECT_RECORDS_CLEAR_CHECKS;
@extern method GPCBaseProcessClass, MSG_CREATE_REPORT;
//@extern method GPCBaseProcessClass, MSG_CLOSE_REPORT_WINDOW;
@extern method GPCBaseProcessClass, MSG_DO_SELECT_ALL;
/*
 ***************************************************************************
 *		UI Objects
 ***************************************************************************
 */

@start  AppResource;
@object GPCBaseApplicationClass GPCBaseApp = {
    GI_visMoniker = list {
			 @AppIcon,
			 @AppTinyIcon,
		    @GPCBaseTextMoniker
		    }
    GI_comp = @GPCBasePrimary;
    gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_WINDOWS) = @GPCBasePrimary;
    gcnList(MANUFACTURER_ID_GEOWORKS,MGCNLT_ACTIVE_LIST) = @GPCBaseDocumentControl;
    gcnList(MANUFACTURER_ID_GEOWORKS,MGCNLT_APP_STARTUP) = @GPCBaseDocumentControl;
@ifdef COMPILEGPC
    gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_SELF_LOAD_OPTIONS) =
			 @GPCBaseDocumentControl, @GPCBasePrintControl,
			 @GPCBaseEditControl, @NewRecEditControl;
@else
    gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_SELF_LOAD_OPTIONS) =
			 @GPCBaseDocumentControl, @GPCBasePrintControl,
			 @GPCBaseEditControl, @GPCBReportEditControl,
			 @NewRecEditControl;
@endif
	 ATTR_GEN_APPLICATION_PRINT_CONTROL = @GPCBasePrintControl;
	 ATTR_GEN_HELP_FILE = "HB Help";
}

@visMoniker GPCBaseTextMoniker = "HomeBase";
@end AppResource



@start Interface;
@object GPCBasePrimaryClass GPCBasePrimary = {
    GI_comp = @GPCBFileMenu, @MainMenu, @MainGroup, @GPCBDialogs;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    ATTR_GEN_HELP_CONTEXT = "TOC";
 //   ATTR_GEN_DISPLAY_NOT_MAXIMIZABLE;
    ATTR_GEN_PRIMARY_DONE_ON_CLOSE = {
			0, /*GPDD_explainString*/
			@GPCBaseDocumentControl, /*GPDD_documentControl*/
			0, /*GPDD_documentParent*/
			0, /*GPDD_printControl*/
			0, /*GPDD_saveMoniker*/
			0, /*GPDD_saveString*/
			0, /*GPDD_printMoniker*/
			0, /*GPDD_printString*/
			0, /*GPDD_exitMoniker*/
			@GPCBaseExitText, /*GPDD_exitString*/
			0, /*GPDD_multiDocExitString*/
			0, /*GPDD_backMoniker*/
			0}; /*GPDD_backString*/
    HINT_SIZE_WINDOW_AS_RATIO_OF_FIELD = {
	  SWSS_RATIO | PCT_85,  SWSS_RATIO | PCT_85 };
    HINT_CENTER_WINDOW;
}

@visMoniker GPCBaseExitText = "Close the document and exit HomeBase";

@object GenInteractionClass GPCBFileMenu = {
@ifdef GPC_ONLY
	 GI_comp = @GPCBaseDocumentControl, @ImpExSubGroup, 
		    @ExitItem;
@else
    GI_comp = @GPCBaseDocumentControl, @ImpExSubGroup;
@endif
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    GII_visibility = GIV_POPUP;
    ATTR_GEN_INTERACTION_GROUP_TYPE = (GIGT_FILE_MENU);
}

@object GenDocumentControlClass GPCBaseDocumentControl = {
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    GDCI_features = @default & ~GDCF_SUPPORTS_TEMPLATES;
	 GDCI_documentToken = {"HBDA", 16431};
    GDCI_noNameText = "No File";
    @localize "Shown on title bar if no file is acitve";
    GDCI_documentGroup = GPCBaseDocumentGroup;
    GDCI_enableDisableList = GPCBEnableList;
}

@chunk optr GPCBEnableList[] = {
    @ImpExSubGroup, @MainMenu, @MainGroup
};

@object GenInteractionClass ImpExSubGroup = {
    GI_comp = @ImportSMenu, @ExportSMenu;
    GI_states = @default & ~GS_ENABLED; /* on doc enable list */
    GII_visibility = GIV_SUB_GROUP;
}

@object GenTriggerClass ImportSMenu = {
    GI_visMoniker = 'I', "Import";
    @localize "Import item in File menu";
    GTI_destination = process;
    GTI_actionMsg = MSG_OPEN_IMPORT;
    HINT_TRIGGER_BRINGS_UP_WINDOW;
}

@object GenTriggerClass ExportSMenu = {
    GI_visMoniker = 'E', "Export";
    @localize "Export item in File menu";
    GTI_destination = process;
    GTI_actionMsg = MSG_OPEN_EXPORT;
    HINT_TRIGGER_BRINGS_UP_WINDOW;
}

@ifdef GPC_ONLY
@object GenTriggerClass ExitItem = {
	 GI_visMoniker = 'x', "Exit HomeBase";
    @localize "Exit item in File menu";
  /*  GI_kbdAccelerator = F3;  */
    ATTR_GEN_TRIGGER_INTERACTION_COMMAND = {IC_EXIT};
}
@endif

@end Interface;

/* -------------------------------------------------------------*/
@start Menu;
@object GenInteractionClass MainMenu = {
    GI_comp = @GPCBaseEditControl, @RecordMenu, @FieldMenu;
    GI_states = @default & ~GS_ENABLED; /* on doc enable list */
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
}

@object GenEditControlClass GPCBaseEditControl = {
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    GII_visibility = GIV_POPUP;
    ATTR_GEN_INTERACTION_GROUP_TYPE = GIGT_EDIT_MENU;
@ifdef COMPILEGPC
    ATTR_GEN_CONTROL_PROHIBIT_UI = GECF_UNDO
						   | GECF_SELECT_ALL
						   | GECF_CLIP_ART;
@else
    ATTR_GEN_CONTROL_PROHIBIT_UI = GECF_UNDO
						   | GECF_SELECT_ALL;
@endif
}

@object GenInteractionClass RecordMenu = {
    GI_visMoniker = 'R', "Records";
    @localize "Records main menu item";
    GI_comp = @CreateRecordItem, @DuplicateRecordItem, @DeleteRecordItem,
		    @SortFindSubGrp, @CreateReportSubGrp;
    GI_attrs = @default | GA_KBD_SEARCH_PATH;
    GII_visibility = GIV_POPUP;
}

@object GenTriggerClass CreateRecordItem = {
    GI_visMoniker = 'C', "Create New";
    @localize "item in Records menu";
    GTI_destination = process;
    GTI_actionMsg = MSG_OPEN_NEW_RECORD;
    HINT_TRIGGER_BRINGS_UP_WINDOW;
}

@object GenTriggerClass DuplicateRecordItem = {
    GI_visMoniker = 'D', "Duplicate";
    @localize "item in Records menu";
    GTI_destination = process;
    GTI_actionMsg = MSG_OPEN_NEW_RECORD;
    HINT_TRIGGER_BRINGS_UP_WINDOW;
}

@object GenTriggerClass DeleteRecordItem = {
    GI_visMoniker = 'l', "Delete";
    @localize "item in Records menu";
    GTI_destination = process;
    GTI_actionMsg = MSG_DELETE_DATA;
    HINT_TRIGGER_BRINGS_UP_WINDOW;
}

@object GenInteractionClass SortFindSubGrp = {
    GI_comp = @SortRecordsItem, @FindRecordItem;
    GI_attrs = @default | GA_KBD_SEARCH_PATH;
    GII_visibility = GIV_SUB_GROUP;
}

@object GenTriggerClass SortRecordsItem = {
    GI_visMoniker = 'S', "Sort";
    @localize "item in Records menu";
    GTI_destination = process;
    GTI_actionMsg = MSG_OPEN_SORT;
    HINT_TRIGGER_BRINGS_UP_WINDOW;
}

@object GenTriggerClass FindRecordItem = {
    GI_visMoniker = 'F', "Find";
    GI_attrs = @default | GA_KBD_SEARCH_PATH;
    @localize "item in Records menu";
    GTI_destination = process;
    GTI_actionMsg = MSG_OPEN_FIND;
    HINT_TRIGGER_BRINGS_UP_WINDOW;
@ifdef COMPILEGPC
    GI_kbdAccelerator = control 'F';
@ifdef	PRODUCT_DBCS
    ATTR_GEN_EXTRA_KBD_ACCELERATORS = C_SYS_F2 & KS_CHAR_MASK;
@else
    ATTR_GEN_EXTRA_KBD_ACCELERATORS = (KS_CHAR_SET_CONTROL << 8) | VC_F2;
@endif
@endif
}

@object GenInteractionClass CreateReportSubGrp = {
    GI_comp = @CreateReportItem;
    GII_visibility = GIV_SUB_GROUP;
}

@object GenTriggerClass CreateReportItem = {
    GI_visMoniker = 'R', "Create a Report";
    @localize "item in Records menu";
    GTI_destination = process;
    GTI_actionMsg = MSG_OPEN_CREATE_REPORT_DB;
    HINT_TRIGGER_BRINGS_UP_WINDOW;
}

@object GenInteractionClass FieldMenu = {
    GI_visMoniker = 'd', "Fields";
    @localize "Fields main menu item";
    GI_comp = @FieldAttrsItem;
    GII_visibility = GIV_POPUP;
}

@object GenTriggerClass FieldAttrsItem = {
    GI_visMoniker = 'A', "Set Attributes";
    @localize "item in Fields menu";
    GTI_destination = process;
    GTI_actionMsg = MSG_OPEN_CHANGE_ATTR;
    HINT_TRIGGER_BRINGS_UP_WINDOW;
}

@end Menu;

/* -------------------------------------------------------------*/
@start DocumentUI;
@object GenDocumentGroupClass GPCBaseDocumentGroup ={
    GDGI_untitledName = "HomeBase Untitled";
    @localize "untitled GPCBase document";
    GDGI_protocolMajor = 3;
    GDGI_protocolMinor = 0;
    GDGI_documentClass = &GPCBaseDocumentClass;
    GDGI_documentControl = GPCBaseDocumentControl;
}
@end DocumentUI;

@start Interface;
/* -------------------------------------------------------------*/
@object GenInteractionClass MainGroup = {
    GI_comp = @MainTopGroup, @MainBottomGroup;
    GI_states = @default & ~GS_ENABLED; /* on doc enable list */
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_MINIMUM_SIZE = {SST_PIXELS | 630, 0, 0};
}

@object GenInteractionClass MainTopGroup = {
    GI_comp = @NavButtonsGroup, @RecordsGroup, @FieldsGroup;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
}

@end Interface;

@start NavButtons;
/* -------------------------------------------------------------*/
@object GenInteractionClass NavButtonsGroup = {
    GI_comp = @FirstButton, @PrevButton, @NextButton, @LastButton;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
    HINT_CENTER_CHILDREN_VERTICALLY;
}

@object GenTriggerClass FirstButton = {
    GI_visMoniker = list {@FirstButtonIcon};
    GI_states = @default & ~GS_ENABLED;
    GTI_destination = process;
    GTI_actionMsg = MSG_NAV_RECORDS;
    ATTR_GEN_TRIGGER_ACTION_DATA = FIRST;
}

@object RepeatingTriggerClass PrevButton = {
    GI_visMoniker = list {@PrevButtonIcon};
    GI_states = @default & ~GS_ENABLED;
    GTI_destination = process;
    GTI_actionMsg = MSG_NAV_RECORDS;
    ATTR_GEN_TRIGGER_ACTION_DATA = PREV;
}

@object RepeatingTriggerClass NextButton = {
    GI_visMoniker = list {@NextButtonIcon};
    GI_states = @default & ~GS_ENABLED;
    GTI_destination = process;
    GTI_actionMsg = MSG_NAV_RECORDS;
    ATTR_GEN_TRIGGER_ACTION_DATA = NEXT;
}

@object GenTriggerClass LastButton = {
    GI_visMoniker = list {@LastButtonIcon};
    GI_states = @default & ~GS_ENABLED;
    GTI_destination = process;
    GTI_actionMsg = MSG_NAV_RECORDS;
    ATTR_GEN_TRIGGER_ACTION_DATA = LAST;
}

@end NavButtons;

@start Interface;
/* -------------------------------------------------------------*/
@object GenInteractionClass RecordsGroup = {
    GI_visMoniker = "Total: 0 Records";
    @localize "title over records scrolling list window";
    GI_comp = @RecordsDListSubGroup;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
    HINT_PLACE_MONIKER_ABOVE;
}

@object GenInteractionClass RecordsDListSubGroup = {
    GI_comp = @RecordsDList;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
}

@object GenDynamicListClass RecordsDList = {
    GDLI_numItems = 0;
    GIGI_selection = 0;
    GIGI_applyMsg = MSG_RECORDS_DLIST_APPLY;
    GIGI_destination = process;
    GDLI_queryMsg = MSG_RECORDS_DLIST_MONIKER_QUERY;
    HINT_INITIAL_SIZE = {
		SST_AVG_CHAR_WIDTHS | 30,
		0,
		0};
    HINT_ITEM_GROUP_SCROLLABLE;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
}

/* -------------------------------------------------------------*/
@object GenInteractionClass FieldsGroup = {
    GI_visMoniker = "Record #";
    GI_comp = @FieldsDisplay;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_PLACE_MONIKER_ABOVE;
}

@object GenInteractionClass FieldsDisplay = {
    GI_comp = @Field1, @Field2, @Field3, @Field4,
		 @Field5, @Field6, @Field7, @Field8,
		 @Field9, @Field10;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    HINT_CENTER_CHILDREN_ON_MONIKERS;
    HINT_NO_TALLER_THAN_CHILDREN_REQUIRE;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_DRAW_IN_BOX;
}

@object GPCBaseDisplayTextClass Field1 = {
    GI_visMoniker = "Field 1";
    @localize "default field name";
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_maxLength = (DATA_LEN - 1);
    HINT_TEXT_WHITE_WASH_COLOR;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_DEFAULT_FOCUS;
}

@define FIELDNAMETEXT(FNAME,FMON) \
    @object GPCBaseDisplayTextClass FNAME = { \
	    GI_visMoniker = FMON; \
    @localize "default field name"; \
    GI_attrs = @default | GA_TARGETABLE  \
				    | GA_KBD_SEARCH_PATH; \
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT; \
    GTXI_maxLength = (DATA_LEN - 1); \
    HINT_TEXT_WHITE_WASH_COLOR; \
    HINT_EXPAND_WIDTH_TO_FIT_PARENT; \
    }

@FIELDNAMETEXT(Field2, "Field 2")
@FIELDNAMETEXT(Field3, "Field 3")
@FIELDNAMETEXT(Field4, "Field 4")
@FIELDNAMETEXT(Field5, "Field 5")
@FIELDNAMETEXT(Field6, "Field 6")
@FIELDNAMETEXT(Field7, "Field 7")
@FIELDNAMETEXT(Field8, "Field 8")
@FIELDNAMETEXT(Field9, "Field 9")
@FIELDNAMETEXT(Field10, "Field 10")


/* ------------------------------------------------------------- */
@object GenInteractionClass MainBottomGroup = {
    GI_comp = @RecordButtonsGroup, @FieldButtonsGroup;
    GI_attrs = @default | GA_TARGETABLE;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
}

@object GenInteractionClass RecordButtonsGroup = {
    GI_comp = @CreateButton, @DeleteButton;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
    HINT_ALIGN_LEFT_EDGE_WITH_OBJECT = @RecordsGroup;
}

@object GenTriggerClass CreateButton = {
    GI_visMoniker = "Create New";
    @localize "button to create a new record";
    GTI_destination = process ;
    GTI_actionMsg = MSG_OPEN_NEW_RECORD;
    HINT_TRIGGER_BRINGS_UP_WINDOW;
}

@object GenTriggerClass DeleteButton = {
    GI_visMoniker = "Delete";
    @localize "button to delete a record";
    GTI_destination = process ;
    GTI_actionMsg = MSG_DELETE_DATA;
    HINT_TRIGGER_BRINGS_UP_WINDOW;
}

@object GenInteractionClass FieldButtonsGroup = {
    GI_comp = @FindButton;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_ALIGN_LEFT_EDGE_WITH_OBJECT = @FieldsGroup;
}

@object GenTriggerClass FindButton = {
@ifdef COMPILEGPC
    GI_visMoniker = list {@FindButtonIcon, @FindButtonText};
@else
    GI_visMoniker = list {@FindButtonText};
@endif
    GTI_destination = process;
    GTI_actionMsg = MSG_OPEN_FIND;
    HINT_TRIGGER_BRINGS_UP_WINDOW;
@ifdef COMPILEGPC
    HINT_USE_ICON_TEXT_COMBINATION_MONIKER = {0, 10};
@endif
}

@visMoniker FindButtonText = "Find";
@end Interface;


/* ------------------------------------------------------------- */

@start Dialogs;
@object GenInteractionClass GPCBDialogs = {
    GI_comp = @FindDBox, @ContinueFindDBox, @ChangeAttrsDB, @NewRecDBox,
              @ImportDBox, @ImpStatusDBox,
		    @ExportDBox, @ExpStatusDBox,
		    @SortDBox, @CreateReportDBox, @ReportSelectFieldsDBox,
		    @ReportSelectRecordsDBox,
		    @ReportSelectNumFieldsDBox, @ReportGroup;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    GII_type = GIT_ORGANIZATIONAL;
    GII_visibility = GIV_DIALOG;
    GII_attrs = @default | GIA_NOT_USER_INITIATABLE;
}


/* -------------------------------------------------------------- */
@object GenInteractionClass FindDBox = {
@ifndef GPC_ONLY
    GI_visMoniker = "Find";
@endif
    GI_comp = @FDBSearchTextGlyph, @FDBSearchContainer;
    GII_visibility = GIV_DIALOG;
    ATTR_GEN_HELP_CONTEXT = "FindDB";
    HINT_POSITION_WINDOW_AT_RATIO_OF_PARENT = {
	  SWSS_RATIO | PCT_85,  SWSS_RATIO | PCT_85 };
    HINT_ORIENT_CHILDREN_HORIZONTALLY;   
}

@object GenGlyphClass FDBSearchTextGlyph = {
    GI_visMoniker = "Search for:";
    @localize "label of the search for text entry window";
}

@object GenInteractionClass FDBSearchContainer = {
@ifdef GPC_ONLY
    GI_comp = @FDBSearchText, @FDBButtons, @FDBCanx;
@else
    GI_comp = @FDBSearchText, @FDBSearch, @FDBSearchNext, @FDBCanx;
@endif
}

@object GPCBaseFindTextClass FDBSearchText = {
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_maxLength = (DATA_LEN - 1);
    GTXI_destination = process;
    ATTR_GEN_TEXT_STATUS_MSG = MSG_FIND_TEXT_STATUS;
    HINT_DEFAULT_FOCUS;
}

@ifdef GPC_ONLY
@object GenInteractionClass FDBButtons = {
    GI_comp = @FDBSearch, @FDBSearchNext;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    HINT_ALIGN_LEFT_EDGE_WITH_OBJECT = @FDBSearchText;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
/*    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY;
    HINT_INCLUDE_ENDS_IN_CHILD_SPACING; 
    HINT_MINIMIZE_CHILD_SPACING; */
}
@endif

@object GenTriggerClass FDBSearch = {
@ifdef GPC_ONLY
    GI_visMoniker = "Find";
@else
    GI_visMoniker = 'F', "Find";
@endif
    @localize "button to start searching";
    GTI_destination = process;
    GTI_actionMsg = MSG_SEARCH_FIELDS;
    ATTR_GEN_TRIGGER_ACTION_DATA = FIRST;
@ifndef GPC_ONLY
    HINT_DEFAULT_DEFAULT_ACTION;
    HINT_SEEK_REPLY_BAR;
@endif
}

@object GenTriggerClass FDBSearchNext = {
@ifdef GPC_ONLY
    GI_visMoniker = "Find Next";
@else
    GI_visMoniker = 'N', "Find Next";
@endif
    @localize "button to continue searching from this point in the database";
    GTI_destination = process;
    GTI_actionMsg = MSG_SEARCH_FIELDS;
    ATTR_GEN_TRIGGER_ACTION_DATA = NEXT;
@ifndef GPC_ONLY
    HINT_SEEK_REPLY_BAR;
@endif
}

@object GenTriggerClass FDBCanx = {
@ifdef GPC_ONLY
    GI_visMoniker = "Close";
    @localize "button to close the search dialog box";
@endif
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    HINT_SEEK_REPLY_BAR;
@ifndef GPC_ONLY
    ATTR_GEN_TRIGGER_INTERACTION_COMMAND = { IC_DISMISS };
@endif
}

@object GenInteractionClass ContinueFindDBox = {
    GI_visMoniker = "Complete";
    @localize "title of the continue find dialog box";
    GI_comp = @CFDBText, /*@CFDBYes,*/ @CFDBNo;
    GII_attrs = @default | GIA_MODAL;
    GII_visibility = GIV_DIALOG;
    ATTR_GEN_HELP_CONTEXT = "ContinueFindDB";
}

@object GenTextClass CFDBText = {
   GI_attrs = @default | GA_READ_ONLY;
   GTXI_text = @ContinueDescription ;
   HINT_MINIMUM_SIZE = {SST_AVG_CHAR_WIDTHS | 40, 0, 3}; 
}

@chunk char ContinueDescription[] =
		  "Your search has reached the last record.";
		/*  "Would you like to continue to search from "
		  "the first record?"; */
		  
/*@object GenTriggerClass CFDBYes = {
    GI_visMoniker = "Yes";
    @localize "button to continue searching from the start of the database";
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    GTI_destination = process;
    GTI_actionMsg = MSG_SEARCH_FIELDS;
    ATTR_GEN_TRIGGER_ACTION_DATA = FIRST;
    HINT_SEEK_REPLY_BAR;
}      */

@object GenTriggerClass CFDBNo = {
@ifdef GPC_ONLY
    GI_visMoniker =/* "No"*/ "OK";
    @localize "button to close the continue search dialog box";
@endif
    GTI_destination = process;
    GTI_actionMsg = MSG_SEARCH_DONT_CONTINUE;
    HINT_SEEK_REPLY_BAR;
@ifndef GPC_ONLY
    ATTR_GEN_TRIGGER_INTERACTION_COMMAND = { IC_OK };
@endif
}

/* ------------------------------------------------------------- */
@object GenInteractionClass NewRecDBox = {
    GI_visMoniker = "Create New Record";
    @localize "title of the create new record dialog box";
@ifdef COMPILEGPC
    GI_comp = @NewRecEditControl, @NewRecEditSepLine, @NewFieldData,
		    @SaveNewButton, @CanxNewButton;
@else
    GI_comp = @NewRecEditControl, @NewFieldData, @SaveNewButton,
		    @CanxNewButton;
@endif
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    GII_attrs = @default | GIA_MODAL;
    GII_visibility = GIV_DIALOG;
    ATTR_GEN_HELP_CONTEXT = "NewRecDB";
    HINT_NO_TALLER_THAN_CHILDREN_REQUIRE;
}

@object GenEditControlClass NewRecEditControl = {
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    GII_visibility = GIV_POPUP;
    ATTR_GEN_INTERACTION_GROUP_TYPE = GIGT_EDIT_MENU;
@ifdef COMPILEGPC
    ATTR_GEN_CONTROL_PROHIBIT_UI = GECF_UNDO
						   | GECF_SELECT_ALL
						   | GECF_CLIP_ART;
@else
    ATTR_GEN_CONTROL_PROHIBIT_UI = GECF_UNDO
						   | GECF_SELECT_ALL;
@endif
    HINT_AVOID_MENU_BAR;
    HINT_INFREQUENTLY_USED;
}

@ifdef COMPILEGPC
@object GenGlyphClass NewRecEditSepLine = {
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_GLYPH_SEPARATOR;
}
@endif

@object GenInteractionClass NewFieldData = {
    GI_comp = @NewData1, @NewData2, @NewData3, @NewData4,
		 @NewData5, @NewData6, @NewData7, @NewData8,
		 @NewData9, @NewData10;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    HINT_CENTER_CHILDREN_ON_MONIKERS;
}

@object GPCBaseEnterTextClass NewData1 = {
    GI_visMoniker = "Field 1";
    @localize "default field name";
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_maxLength = (DATA_LEN - 1);
    HINT_TEXT_WHITE_WASH_COLOR;
    HINT_DEFAULT_FOCUS;
}

@define ENTERDATATEXT(FNAME,FMON) \
	@object GPCBaseEnterTextClass FNAME = { \
	    GI_visMoniker = FMON; \
	    GI_attrs = @default | GA_TARGETABLE | GA_KBD_SEARCH_PATH; \
	    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT; \
	    GTXI_maxLength = (DATA_LEN - 1); \
	    HINT_TEXT_WHITE_WASH_COLOR; \
}

@ENTERDATATEXT(NewData2, "Field 2")
@ENTERDATATEXT(NewData3, "Field 3")
@ENTERDATATEXT(NewData4, "Field 4")
@ENTERDATATEXT(NewData5, "Field 5")
@ENTERDATATEXT(NewData6, "Field 6")
@ENTERDATATEXT(NewData7, "Field 7")
@ENTERDATATEXT(NewData8, "Field 8")
@ENTERDATATEXT(NewData9, "Field 9")
@ENTERDATATEXT(NewData10, "Field 10")


@object GenTriggerClass SaveNewButton = {
    GI_visMoniker = "Create";
    @localize "button to save the new data";
 //   GI_states = @default &~GS_ENABLED;  /* enabled by text */
    GTI_destination = process;
    GTI_actionMsg = MSG_ENTER_DATA;
    HINT_DEFAULT_DEFAULT_ACTION;
    HINT_SEEK_REPLY_BAR;
}

@object GenTriggerClass CanxNewButton = {
@ifdef GPC_ONLY
    GI_visMoniker = "Cancel";
    @localize "button to quit the new data DBox";
@endif
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    HINT_SEEK_REPLY_BAR;
@ifndef GPC_ONLY
    ATTR_GEN_TRIGGER_INTERACTION_COMMAND = { IC_DISMISS };
@endif
}
@end Dialogs;

@start ChangeAttrsDialog;
/* ------------------------------------------------------------- */
@object GenInteractionClass ChangeAttrsDB = {
    GI_visMoniker = "Field Attributes";
    @localize "title of the change field attributes dialog box";
@ifdef COMPILEGPC
    GI_comp = @NumberOfFields, @CADBSepLine, @Attributes, @ChangeTypesWarningDBox;
@else
    GI_comp = @NumberOfFieldsGroup, @Attributes, @ChangeTypesWarningDBox;
@endif
    GII_visibility = GIV_DIALOG;
    ATTR_GEN_HELP_CONTEXT = "ChangeAttrsDB";
    HINT_CENTER_CHILDREN_HORIZONTALLY;
    HINT_NO_TALLER_THAN_CHILDREN_REQUIRE;
}

@ifndef COMPILEGPC
@object GenInteractionClass NumberOfFieldsGroup = {
    GI_comp = @NumberOfFields;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_CENTER_CHILDREN_HORIZONTALLY;
    HINT_DRAW_IN_BOX;
}
@endif

@object GenValueClass NumberOfFields = {
    GI_visMoniker = "Number of Fields to Use in this Database:";
    @localize "text next to the number of fields spinner";
    GVLI_minimum = MakeWWFixed(1.0);
    GVLI_maximum = MakeWWFixed(10.0);
    GVLI_value = MakeWWFixed(10.0);
    GVLI_destination = process;
    ATTR_GEN_VALUE_STATUS_MSG = MSG_CHANGE_NUM_FIELDS_SHOWING;
    HINT_DEFAULT_FOCUS;
}

@ifdef COMPILEGPC
@object GenGlyphClass CADBSepLine = {
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_GLYPH_SEPARATOR;
}
@endif

@object GenInteractionClass Attributes = {
    GI_comp = @ChangeAttrFields, @AttrButtons;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
    HINT_NO_TALLER_THAN_CHILDREN_REQUIRE;
@ifndef COMPILEGPC
    HINT_DRAW_IN_BOX;
@endif
}

@object GenInteractionClass ChangeAttrFields = {
    GI_comp = @CATitles, @CAField1, @CAField2, @CAField3, @CAField4,
		    @CAField5, @CAField6, @CAField7, @CAField8,
		    @CAField9, @CAField10;
    HINT_CENTER_CHILDREN_ON_MONIKERS;
}

@object GenInteractionClass CATitles = {
    GI_comp = @CATName, @CATType;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
}

@object GenGlyphClass CATName = {
    GI_visMoniker = "Field Name:";
}

@object GenGlyphClass CATType = {
    GI_visMoniker = "Field Type:    ";
    HINT_ALIGN_RIGHT_EDGE_WITH_OBJECT = @ChangeAttrFields;
}

@object GenInteractionClass CAField1 = {
    GI_visMoniker = "Field 1";
    @localize "default field name";
    GI_comp = @CName1, @CType1;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
}

@object GPCBaseFieldTextClass CName1 = {
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_maxLength = (FIELD_NAME_LEN - 1);
    HINT_TEXT_WHITE_WASH_COLOR;
    HINT_DEFAULT_FOCUS;
}

@object GenItemGroupClass CType1 = {
    GI_comp = @TGeneral1, @TNumber1, @TDate1;
    GIGI_selection = T_GEN;
    GIGI_destination = process;
    ATTR_GEN_ITEM_GROUP_STATUS_MSG = MSG_FIELD_TYPE_STATUS;
    HINT_ITEM_GROUP_MINIMIZE_SIZE;
    HINT_ITEM_GROUP_DISPLAY_CURRENT_SELECTION;
}

@object GenItemClass TGeneral1 = {
    GI_visMoniker = "General";
    GII_identifier = T_GEN;
}

@object GenItemClass TNumber1 = {
    GI_visMoniker = "Number";
    GII_identifier = T_NUM;
}

@object GenItemClass TDate1 = {
    GI_visMoniker = "Date";
    GII_identifier = T_DAT;
}

/* and a macro to do the other 9 sets */
@define CHGFIELDATTRSLINE(FFIELD, FNAME, FTYPE, FNAMEMON, FTGEN, FTNUM, FTDAT) \
    @object GenInteractionClass FFIELD = { \
	   GI_visMoniker = FNAMEMON; \
	   GI_comp = @FNAME, @FTYPE; \
	   HINT_ORIENT_CHILDREN_HORIZONTALLY; \
    } \
    @object GPCBaseFieldTextClass FNAME = {  \
	   GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT; \
	   GTXI_maxLength = (FIELD_NAME_LEN - 1); \
	   HINT_TEXT_WHITE_WASH_COLOR;  \
    }  \
    @object GenItemGroupClass FTYPE = {  \
	   GI_comp = @FTGEN, @FTNUM, @FTDAT;  \
	   GIGI_selection = T_GEN; \
    GIGI_destination = process; \
    ATTR_GEN_ITEM_GROUP_STATUS_MSG = MSG_FIELD_TYPE_STATUS; \
	   HINT_ITEM_GROUP_MINIMIZE_SIZE; \
	   HINT_ITEM_GROUP_DISPLAY_CURRENT_SELECTION; \
    }  \
    @object GenItemClass FTGEN = { \
	   GI_visMoniker = "General"; \
	   GII_identifier = T_GEN; \
    }  \
    @object GenItemClass FTNUM = {  \
	   GI_visMoniker = "Number";  \
	   GII_identifier = T_NUM;  \
    } \
    @object GenItemClass FTDAT = {  \
	   GI_visMoniker = "Date";  \
	   GII_identifier = T_DAT;  \
    } 

@CHGFIELDATTRSLINE(CAField2, CName2, CType2, "Field 2", TGeneral2, TNumber2, TDate2)
@CHGFIELDATTRSLINE(CAField3, CName3, CType3, "Field 3", TGeneral3, TNumber3, TDate3)
@CHGFIELDATTRSLINE(CAField4, CName4, CType4, "Field 4", TGeneral4, TNumber4, TDate4)
@CHGFIELDATTRSLINE(CAField5, CName5, CType5, "Field 5", TGeneral5, TNumber5, TDate5)
@CHGFIELDATTRSLINE(CAField6, CName6, CType6, "Field 6", TGeneral6, TNumber6, TDate6)
@CHGFIELDATTRSLINE(CAField7, CName7, CType7, "Field 7", TGeneral7, TNumber7, TDate7)
@CHGFIELDATTRSLINE(CAField8, CName8, CType8, "Field 8", TGeneral8, TNumber8, TDate8)
@CHGFIELDATTRSLINE(CAField9, CName9, CType9, "Field 9", TGeneral9, TNumber9, TDate9)
@CHGFIELDATTRSLINE(CAField10, CName10, CType10, "Field 10", TGeneral10, TNumber10, TDate10)


@object GenInteractionClass AttrButtons = {
    GI_comp = @ChangeAttrsButton, @CanxChangeAttrsButton;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
    HINT_MAKE_REPLY_BAR;
}

@object GenTriggerClass ChangeAttrsButton = {
@ifdef GPC_ONLY
    GI_visMoniker = "Change";
    @localize "button to enter the changed field name(s) and Type(s)";
@endif
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    GI_states = @default &~GS_ENABLED; 
    GTI_destination = process;
    GTI_actionMsg = MSG_CHANGE_ATTR;
    HINT_DEFAULT_DEFAULT_ACTION;
@ifndef GPC_ONLY
    ATTR_GEN_TRIGGER_INTERACTION_COMMAND = { IC_OK };
@endif
}

@object GenTriggerClass CanxChangeAttrsButton = {
@ifdef GPC_ONLY
    GI_visMoniker = "Don't Change";
    @localize "button to quit the changed field name(s) dbox";
@endif
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
@ifndef GPC_ONLY
    ATTR_GEN_TRIGGER_INTERACTION_COMMAND = { IC_DISMISS };
@endif
}
/* -------------------------------------------------------------- */
@object GenInteractionClass ChangeTypesWarningDBox = {
    GI_visMoniker = "Change Types";
    @localize "Title of change types warning dialog box";
    GI_comp = @CTWText, @CTWChangeButton, @CTWDontChangeButton;
    GII_visibility = GIV_DIALOG;
    GII_attrs = @default | GIA_INITIATED_VIA_USER_DO_DIALOG
					| GIA_MODAL;
}

@object GenTextClass CTWText = {
    GI_attrs = @default | GA_READ_ONLY;
    HINT_MINIMUM_SIZE = {SST_AVG_CHAR_WIDTHS | 60, 0, 0}; 
}

@object GenTriggerClass CTWChangeButton = {
@ifdef GPC_ONLY
    GI_visMoniker = "Change";
    @localize "Moniker of the change button in change types warning dialog box";
@endif
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    ATTR_GEN_TRIGGER_INTERACTION_COMMAND = {IC_YES};
    HINT_SEEK_REPLY_BAR;
}

@object GenTriggerClass CTWDontChangeButton = {
@ifdef GPC_ONLY
    GI_visMoniker = "Don't Change";
    @localize "Moniker of the don't change button in change types warning dialog box";
@endif
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    ATTR_GEN_TRIGGER_INTERACTION_COMMAND = {IC_NO};
    HINT_SEEK_REPLY_BAR;
}

@end ChangeAttrsDialog;

@start ImpExDialogs;
/* -------------------------------------------------------------- */
@object GenInteractionClass ImportDBox = {
    GI_visMoniker = "Import from \"CSV\" Format";
    @localize "Title of Import from CSV file dialog box";
    GI_comp = @ImpFileSel, @ImpFieldSel, @ImpStart, @ImpCanx;
    GII_visibility = GIV_DIALOG;
    HINT_ORIENT_CHILDREN_VERTICALLY;
    HINT_CENTER_CHILDREN_HORIZONTALLY;
    ATTR_GEN_HELP_CONTEXT = "ImpDB";
}

@object GenFileSelectorClass ImpFileSel = {
 //   GFSI_fileCriteria = @default | FSFC_MASK_CASE_INSENSITIVE;
    GFSI_fileCriteria = FSFC_MASK_CASE_INSENSITIVE |
				    FSFC_DIRS |  
				    FSFC_NON_GEOS_FILES;
    GFSI_destination = process;
    GFSI_notificationMsg = MSG_IMPORT_SELECTOR_ACTION;
    ATTR_GEN_FILE_SELECTOR_NAME_MASK = {"*.CSV"};
    ATTR_GEN_PATH_DATA = {SP_DOCUMENT, "."};
    HINT_FILE_SELECTOR_NUMBER_OF_FILES_TO_SHOW = 8;
    HINT_FILE_SELECTOR_FILE_LIST_WIDTH = 40;
}


@object GenBooleanGroupClass ImpFieldSel = {
    GI_comp = @ImpField;
}

@object GenBooleanClass ImpField = {
    GI_visMoniker = "Use First Record as Field Names";
    GBI_identifier = I_FIELD;
}

@object GenTriggerClass ImpStart = {
    GI_visMoniker = "Import";
    @localize "Moniker of Import button in CSV file dialog box";
    GI_states = @default &~GS_ENABLED;  /* enabled by selection */
    GTI_destination = process;
    GTI_actionMsg = MSG_IMPORT_CSV;
    HINT_SEEK_REPLY_BAR;
    HINT_DEFAULT_DEFAULT_ACTION;
}

@object GenTriggerClass ImpCanx = {
@ifdef GPC_ONLY
    GI_visMoniker = "Don't Import";
    @localize "Moniker of Cancel button in CSV file dialog box";
@endif
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    HINT_SEEK_REPLY_BAR;
@ifndef GPC_ONLY
    ATTR_GEN_TRIGGER_INTERACTION_COMMAND = { IC_DISMISS };
@endif
}

/* -------------------------------------------------------------- */
@object GenInteractionClass ImpStatusDBox = {
    GI_visMoniker = "Import Status";
    @localize "title of the import status dialog box";
    GI_comp = @ImpStatGlyph, @ImpCanxTrigger;
    GII_visibility = GIV_DIALOG;
    GII_attrs = @default | GIA_MODAL;
    GII_type = GIT_PROGRESS;
    HINT_CENTER_CHILDREN_HORIZONTALLY;
}

@object GenGlyphClass ImpStatGlyph = {
    GI_visMoniker = "Importing...";
    @localize "tells the user the import is in progess";
}

@object GenTriggerClass ImpCanxTrigger = {
    GI_visMoniker = "Stop Importing";
    @localize "button for the user to stop the import";
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    GTI_destination = @GPCBaseApp;
    GTI_actionMsg = MSG_CANCEL_IMPORT;
    ATTR_GEN_TRIGGER_INTERACTION_COMMAND = {IC_STOP};
    HINT_SEEK_REPLY_BAR;
    HINT_TRIGGER_DESTRUCTIVE_ACTION;
}


/* -------------------------------------------------------------- */
@object GenInteractionClass ExportDBox = {
    GI_visMoniker = "Export to \"CSV\" Format";
    @localize "title of the export dialog box";
@ifdef COMPILEGPC
    GI_comp = @ExpNameGroup, @ExpSepLine, @ExpFileSel, @ExpButtonGroup;
@else
    GI_comp = @ExpNameGroup, @ExpFileSelGroup, @ExpButtonGroup;
@endif
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    GII_visibility = GIV_DIALOG;
    ATTR_GEN_HELP_CONTEXT = "ExpDB";
}

@object GenInteractionClass ExpNameGroup = {
    GI_comp = @ExpFileNameLine, @ExpFieldSel;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_CENTER_CHILDREN_HORIZONTALLY;
    HINT_DEFAULT_FOCUS;
@ifndef COMPILEGPC
    HINT_DRAW_IN_BOX;
@endif
}

@object GenInteractionClass ExpFileNameLine = {
    GI_comp = @ExpFileName, @ExpNameWarn;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
    HINT_CENTER_CHILDREN_VERTICALLY;
    HINT_DEFAULT_FOCUS;
}

@object GenTextClass ExpFileName = {
    GI_visMoniker = "File Name:";
    @localize "label for the export file name entry window";
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_maxLength = (12);
    ATTR_GEN_TEXT_SET_OBJECT_ENABLED_WHEN_TEXT_EXISTS =
	   @ExpStart;
    ATTR_GEN_TEXT_LEGAL_DOS_FILENAMES;
    ATTR_GEN_TEXT_MAKE_UPPERCASE;
    HINT_FIXED_SIZE = {SST_AVG_CHAR_WIDTHS | 16, 0, 0};
    HINT_TEXT_WHITE_WASH_COLOR;
    HINT_DEFAULT_FOCUS;
}

@object GenGlyphClass ExpNameWarn = {
    GI_visMoniker = "(DOS file name)";
    @localize "reminder that export file name must be a DOS name";
}

@object GenBooleanGroupClass ExpFieldSel = {
    GI_comp = @ExpField;
}

@object GenBooleanClass ExpField = {
    GI_visMoniker = "Use Field Names as Column Headers";
    @localize "option to add field names as first record of export";
    GBI_identifier = E_FIELD;
}

@ifdef COMPILEGPC
@object GenGlyphClass ExpSepLine = {
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_GLYPH_SEPARATOR;
}
@else
@object GenInteractionClass ExpFileSelGroup = {
    GI_comp = @ExpFileSel;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_DRAW_IN_BOX;
}
@endif

@object GenFileSelectorClass ExpFileSel = {
    GI_visMoniker = "Select folder in which to save this file:";
    @localize "label for the file selector in the export dialog box";
 //   GFSI_fileCriteria = @default | FSFC_MASK_CASE_INSENSITIVE;
    GFSI_fileCriteria = FSFC_MASK_CASE_INSENSITIVE |
				    FSFC_DIRS |
				    FSFC_NON_GEOS_FILES;
	 GFSI_attrs	= @default | FSA_SHOW_FILES_DISABLED;
    ATTR_GEN_PATH_DATA = {SP_DOCUMENT, "."};
    ATTR_GEN_FILE_SELECTOR_NAME_MASK = {"*.CSV"};
    HINT_FILE_SELECTOR_NUMBER_OF_FILES_TO_SHOW = 8;
    HINT_FILE_SELECTOR_FILE_LIST_WIDTH = 40;
    HINT_PLACE_MONIKER_ABOVE;
}

@object GenInteractionClass ExpButtonGroup = {
    GI_comp = @ExpStart, @ExpCanx;
    HINT_MAKE_REPLY_BAR;
}

@object GenTriggerClass ExpStart = {
    GI_visMoniker = "Export";
    @localize "button to start exporting database";
    GI_states = @default &~GS_ENABLED;  /* enabled by text */
    GTI_destination = process;
    GTI_actionMsg = MSG_EXPORT_CSV;
    HINT_DEFAULT_DEFAULT_ACTION;
}

@object GenTriggerClass ExpCanx = {
@ifdef GPC_ONLY
    GI_visMoniker = "Don't Export";
    @localize "button to cancel exporting";
@endif
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
@ifndef GPC_ONLY
    ATTR_GEN_TRIGGER_INTERACTION_COMMAND = { IC_DISMISS };
@endif
}

/* -------------------------------------------------------------- */
@object GenInteractionClass ExpStatusDBox = {
    GI_visMoniker = "Export Status";
    @localize "title of the export status dialog box";
    GI_comp = @ExpStatGlyph, @ExpCanxTrigger;
    GII_visibility = GIV_DIALOG;
    GII_attrs = @default | GIA_MODAL;
    GII_type = GIT_PROGRESS;
    HINT_CENTER_CHILDREN_HORIZONTALLY;
}

@object GenGlyphClass ExpStatGlyph = {
    GI_visMoniker = "Exporting...";
    @localize "tells the user the export is in progess";
}

@object GenTriggerClass ExpCanxTrigger = {
    GI_visMoniker = "Stop Exporting";
    @localize "button for the user to stop the export";
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    GTI_destination = @GPCBaseApp;
    GTI_actionMsg = MSG_CANCEL_IMPORT;
    ATTR_GEN_TRIGGER_INTERACTION_COMMAND = {IC_STOP};
    HINT_SEEK_REPLY_BAR;
    HINT_TRIGGER_DESTRUCTIVE_ACTION;
}

@end ImpExDialogs;

@start Dialogs;
/* -------------------------------------------------------------- */
@object GenInteractionClass SortDBox = {
    GI_visMoniker = "Change Record Sorting";
    @localize "title of the change sorting dialog box";
@ifdef COMPILEGPC
    GI_comp = @SDBViewGroup, @SortSepLine, @SDBSortGroup;
@else
    GI_comp = @SDBViewGroup, @SDBSortGroup;
@endif
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    GII_visibility = GIV_DIALOG;
    ATTR_GEN_HELP_CONTEXT = "SortDB";
}

@object GenInteractionClass SDBViewGroup = {
    GI_comp = @SDBViewGlyph, @SDBViewList;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
    HINT_CENTER_CHILDREN_VERTICALLY;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
   /* HINT_ALIGN_RIGHT_EDGE_WITH_OBJECT = @SortDBox;*/
    HINT_RIGHT_JUSTIFY_CHILDREN;
@ifndef COMPILEGPC
    HINT_DRAW_IN_BOX;
@endif
}

@object GenGlyphClass SDBViewGlyph = {
    GI_visMoniker = "View Records by:";
/* need the glyph because the MINIMIZE _SIZE hint (below)
 * supresses the moniker of the list group. */
}

@object GenDynamicListClass SDBViewList = {
    GDLI_numItems = 10;
    GIGI_selection = 0;
    GIGI_numSelections = 1;
    GIGI_destination = process;
    GDLI_queryMsg = MSG_FIELDS_DLIST_MONIKER_QUERY;
    ATTR_GEN_ITEM_GROUP_STATUS_MSG = MSG_VIEW_SORT_STATUS;
    HINT_ITEM_GROUP_MINIMIZE_SIZE;
    HINT_ITEM_GROUP_DISPLAY_CURRENT_SELECTION; 
    HINT_MINIMUM_SIZE = {SST_AVG_CHAR_WIDTHS | 20, 0, 0}; 
}

@ifdef COMPILEGPC
@object GenGlyphClass SortSepLine = {
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_GLYPH_SEPARATOR;
}
@endif

/*@object GenInteractionClass SDBSortMaster = {
    GI_comp = @SDBSortGroup;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    /* to draw the horizontal line above */
/*    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
/*@ifdef COMPILEGPC
    HINT_MAKE_REPLY_BAR;
/*@endif
}
*/
@object GenInteractionClass SDBSortGroup = {
    GI_comp = @SDBSortFirstGroup, @SDBSortSecondGroup, @SDBSortThirdGroup,
		    @SDBButtonGroup;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
@ifndef COMPILEGPC
    HINT_DRAW_IN_BOX;
@endif
}

@object GenInteractionClass SDBSortFirstGroup = {
    GI_comp = @SDBSortFirstListLine, @SDBSortFirstOrder;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
}

@object GenInteractionClass SDBSortFirstListLine = {
    GI_comp = @SDBSortFirstGlyph, @SDBSortFirstList;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
    HINT_CENTER_CHILDREN_VERTICALLY;
    HINT_ALIGN_RIGHT_EDGE_WITH_OBJECT = @SDBSortFirstGroup;
}

@object GenGlyphClass SDBSortFirstGlyph = {
    GI_visMoniker = "Sort Records by:";
}

@object GenDynamicListClass SDBSortFirstList = {
    GDLI_numItems = 10;
    GIGI_selection = 0;
    GIGI_numSelections = 1;
    GIGI_destination = process;
    GDLI_queryMsg = MSG_FIELDS_DLIST_MONIKER_QUERY;
    ATTR_GEN_ITEM_GROUP_STATUS_MSG = MSG_FIRST_SORT_STATUS;
    HINT_ITEM_GROUP_MINIMIZE_SIZE;
    HINT_ITEM_GROUP_DISPLAY_CURRENT_SELECTION; 
    HINT_MINIMUM_SIZE = {SST_AVG_CHAR_WIDTHS | 20, 0, 0}; 
}

@object GenItemGroupClass SDBSortFirstOrder = {
    GI_comp = @SDBSortFirstUp, @SDBSortFirstDown;
    GIGI_selection = S_UP;
    GIGI_destination = process;
    ATTR_GEN_ITEM_GROUP_STATUS_MSG = MSG_SORT_ORDER_CHANGED;
    HINT_ORIENT_CHILDREN_VERTICALLY;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_RIGHT_JUSTIFY_CHILDREN;
}

@object GenItemClass SDBSortFirstUp = {
    GI_visMoniker = "Ascending Order (A-Z, 1-9)  ";
    GII_identifier = S_UP;
}

@object GenItemClass SDBSortFirstDown = {
    GI_visMoniker = "Descending Order (Z-A, 9-1)";
    GII_identifier = S_DOWN;
    HINT_ALIGN_LEFT_EDGE_WITH_OBJECT = @SDBSortFirstUp;
}


@object GenInteractionClass SDBSortSecondGroup = {
    GI_comp = @SDBSortSecondListLine, @SDBSortSecondOrder;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
}

@object GenInteractionClass SDBSortSecondListLine = {
    GI_comp = @SDBSortSecondGlyph, @SDBSortSecondList;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
    HINT_CENTER_CHILDREN_VERTICALLY;
    HINT_ALIGN_RIGHT_EDGE_WITH_OBJECT = @SDBSortSecondGroup;
}

@object GenGlyphClass SDBSortSecondGlyph = {
    GI_visMoniker = "Then by:";
}

@object GenDynamicListClass SDBSortSecondList = {
    GDLI_numItems = 11;
    GIGI_selection = 0;
    GIGI_numSelections = 1;
    GIGI_destination = process;
    GDLI_queryMsg = MSG_FIELDS_DLIST_MONIKER_QUERY;
    ATTR_GEN_ITEM_GROUP_STATUS_MSG = MSG_SECOND_SORT_STATUS;
    HINT_ITEM_GROUP_MINIMIZE_SIZE;
    HINT_ITEM_GROUP_DISPLAY_CURRENT_SELECTION; 
    HINT_MINIMUM_SIZE = {SST_AVG_CHAR_WIDTHS | 20, 0, 0}; 
}

@object GenItemGroupClass SDBSortSecondOrder = {
    GI_comp = @SDBSortSecondUp, @SDBSortSecondDown;
    GI_states = @default & ~GS_ENABLED;
    GIGI_selection = S_UP;
    GIGI_destination = process;
    ATTR_GEN_ITEM_GROUP_STATUS_MSG = MSG_SORT_ORDER_CHANGED;
    HINT_ORIENT_CHILDREN_VERTICALLY;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_RIGHT_JUSTIFY_CHILDREN;
}

@object GenItemClass SDBSortSecondUp = {
    GI_visMoniker = "Ascending Order (A-Z, 1-9)  ";
    GII_identifier = S_UP;
}

@object GenItemClass SDBSortSecondDown = {
    GI_visMoniker = "Descending Order (Z-A, 9-1)";
    GII_identifier = S_DOWN;
    HINT_ALIGN_LEFT_EDGE_WITH_OBJECT = @SDBSortSecondUp;
}

@object GenInteractionClass SDBSortThirdGroup = {
    GI_comp = @SDBSortThirdListLine, @SDBSortThirdOrder;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
}

@object GenInteractionClass SDBSortThirdListLine = {
    GI_comp = @SDBSortThirdGlyph, @SDBSortThirdList;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
    HINT_CENTER_CHILDREN_VERTICALLY;
    HINT_ALIGN_RIGHT_EDGE_WITH_OBJECT = @SDBSortThirdGroup;
}

@object GenGlyphClass SDBSortThirdGlyph = {
    GI_visMoniker = "Then by:";
}

@object GenDynamicListClass SDBSortThirdList = {
    GDLI_numItems = 11;
    GIGI_selection = 0;
    GIGI_numSelections = 1;
    GIGI_destination = process;
    GDLI_queryMsg = MSG_FIELDS_DLIST_MONIKER_QUERY;
    ATTR_GEN_ITEM_GROUP_STATUS_MSG = MSG_THIRD_SORT_STATUS;
    HINT_ITEM_GROUP_MINIMIZE_SIZE;
    HINT_ITEM_GROUP_DISPLAY_CURRENT_SELECTION; 
    HINT_MINIMUM_SIZE = {SST_AVG_CHAR_WIDTHS | 20, 0, 0}; 
}

@object GenItemGroupClass SDBSortThirdOrder = {
    GI_comp = @SDBSortThirdUp, @SDBSortThirdDown;
    GI_states = @default & ~GS_ENABLED;
    GIGI_selection = S_UP;
    GIGI_destination = process;
    ATTR_GEN_ITEM_GROUP_STATUS_MSG = MSG_SORT_ORDER_CHANGED;
    HINT_ORIENT_CHILDREN_VERTICALLY;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_RIGHT_JUSTIFY_CHILDREN;
}

@object GenItemClass SDBSortThirdUp = {
    GI_visMoniker = "Ascending Order (A-Z, 1-9)  ";
    GII_identifier = S_UP;
}

@object GenItemClass SDBSortThirdDown = {
    GI_visMoniker = "Descending Order (Z-A, 9-1)";
    GII_identifier = S_DOWN;
    HINT_ALIGN_LEFT_EDGE_WITH_OBJECT = @SDBSortThirdUp;
}


@object GenInteractionClass SDBButtonGroup = {
    GI_comp = @SDBChange, /*@SDBDefault, */@SDBCanx;
    HINT_MAKE_REPLY_BAR;
}

@object GenTriggerClass SDBChange = {
@ifdef GPC_ONLY
    GI_visMoniker = "Change";
    @localize "button to start sorting database";
@endif
    GI_states = @default & ~GS_ENABLED;
    GTI_destination = process;
    GTI_actionMsg = MSG_SET_SORT_PARAMS;
@ifndef GPC_ONLY
    ATTR_GEN_TRIGGER_INTERACTION_COMMAND = { IC_OK };
@endif
}

/*@object GenTriggerClass SDBDefault = {
    GI_visMoniker = "Default Sort";
    @localize "button to start default sort of database";
    GI_states = @default & ~GS_ENABLED;
    GTI_destination = process;
    GTI_actionMsg = MSG_DEFAULT_SORT;
} */

@object GenTriggerClass SDBCanx = {
@ifdef GPC_ONLY
    GI_visMoniker = "Don't Change";
    @localize "button to cancel exporting";
@endif
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
@ifndef GPC_ONLY
    ATTR_GEN_TRIGGER_INTERACTION_COMMAND = { IC_DISMISS };
@endif
}
@end Dialogs;

@start CreateRepDialog;
/* -------------------------------------------------------------- */
@object GenInteractionClass CreateReportDBox = {
    GI_visMoniker = "Create a Report";
    @localize "title of the create report dialog box";
@ifdef COMPILEGPC
    GI_comp = @CRDBTitleGroup, @CRDBSepLine1, @CRDBRecordsSubGroup,
		    @CRDBSepLine2, @CRDBFNamesGroup, @CRDBSepLine3,
		    @CRDBShowSubGroup, @CRDBSepLine4, @CRDBNumericSubGroup,
		    @CRDBReplyGroup;
@else
    GI_comp = @CRDBTitleGroup, @CRDBRecordsGroup, @CRDBFNamesGroup,
		    @CRDBShowGroup, @CRDBNumericGroup, @CRDBReplyGroup;
@endif
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    GII_visibility = GIV_DIALOG;
    ATTR_GEN_HELP_CONTEXT = "CreateReportDB";
}

@object GenInteractionClass CRDBTitleGroup = {
    GI_comp = @CRDBTitle, @CRDBDate;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_CENTER_CHILDREN_ON_MONIKERS;
@ifndef COMPILEGPC
    HINT_DRAW_IN_BOX;
@endif
}

@object GenTextClass CRDBTitle = {
    GI_visMoniker = "Report Title:";
    @localize "label for the report title entry window";
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_maxLength = (35);
    HINT_MINIMUM_SIZE = {SST_AVG_CHAR_WIDTHS | 27, 0, 0};
    HINT_TEXT_WHITE_WASH_COLOR;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_DEFAULT_FOCUS;
}

@object GenBooleanGroupClass CRDBDate = {
    GI_comp = @IncludeDate;
    GBGI_selectedBooleans = R_DATE;
}

@object GenBooleanClass IncludeDate = {
    GI_visMoniker = "Include Today's Date";
    @localize "option to add date to report title";
    GBI_identifier = R_DATE;
}

@ifdef COMPILEGPC
@object GenGlyphClass CRDBSepLine1 = {
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_GLYPH_SEPARATOR;
}
@else
@object GenInteractionClass CRDBRecordsGroup = {
    GI_comp = @CRDBRecordsSubGroup;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_DRAW_IN_BOX;
}
@endif

@object GenInteractionClass CRDBRecordsSubGroup = {
    GI_visMoniker = "Records:     ";
    @localize "records section of create report dialog box";
    GI_comp = @CRDBRecords;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
}

@object GenItemGroupClass CRDBRecords = {
    GI_comp = @CRDBRAll, @CRDBRSelected;
    GIGI_selection = R_ALL;
    GIGI_applyMsg = MSG_REPORT_SHOW_RECORDS_LIST_APPLY;
    GIGI_destination = process;
    ATTR_GEN_ITEM_GROUP_SET_MODIFIED_ON_REDUNDANT_SELECTION;
    ATTR_GEN_SEND_APPLY_MSG_ON_APPLY_EVEN_IF_NOT_MODIFIED;
    HINT_ORIENT_CHILDREN_VERTICALLY;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_CENTER_CHILDREN_ON_MONIKERS;
}

@object GenItemClass CRDBRAll = {
    GI_visMoniker = "Show All";
    GII_identifier = R_ALL;
}

@object GenItemClass CRDBRSelected = {
    GI_visMoniker = "Show Selected Records...";
    GII_identifier = R_SELECTED;
}

@ifdef COMPILEGPC
@object GenGlyphClass CRDBSepLine2 = {
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_GLYPH_SEPARATOR;
}
@endif

@object GenInteractionClass CRDBFNamesGroup = {
    GI_comp = @CRDBFNames;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
@ifndef COMPILEGPC
    HINT_DRAW_IN_BOX;
@endif
}

@object GenBooleanGroupClass CRDBFNames = {
    GI_visMoniker = "Field Names:";
    GI_comp = @IncludeFNames;
    GBGI_selectedBooleans = R_NAME;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
}

@object GenBooleanClass IncludeFNames = {
    GI_visMoniker = "Include";
    @localize "option to include field names";
    GBI_identifier = R_NAME;
}

@ifdef COMPILEGPC
@object GenGlyphClass CRDBSepLine3 = {
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_GLYPH_SEPARATOR;
}
@else
@object GenInteractionClass CRDBShowGroup = {
    GI_comp = @CRDBShowSubGroup;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_DRAW_IN_BOX;
}
@endif

@object GenInteractionClass CRDBShowSubGroup = {
    GI_visMoniker = "Show Fields:";
    @localize "show fields section of create report dialog box";
    GI_comp = @CRDBFields;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
}

@object GenItemGroupClass CRDBFields = {
    GI_comp = @CRDBFFirst, @CRDBFSelected, @CRDBFAll;
    GIGI_selection = R_ALL;
    GIGI_applyMsg = MSG_REPORT_SHOW_FIELDS_LIST_APPLY;
    GIGI_destination = process;
    ATTR_GEN_ITEM_GROUP_SET_MODIFIED_ON_REDUNDANT_SELECTION;
    ATTR_GEN_SEND_APPLY_MSG_ON_APPLY_EVEN_IF_NOT_MODIFIED;
    HINT_ORIENT_CHILDREN_VERTICALLY;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_CENTER_CHILDREN_ON_MONIKERS;
}

@object GenItemClass CRDBFFirst = {
    GI_visMoniker = "1st Field Only";
    GII_identifier = R_FIRST;
}

@object GenItemClass CRDBFSelected = {
    GI_visMoniker = "Selected Fields...";
    GII_identifier = R_SELECTED;
}

@object GenItemClass CRDBFAll = {
    GI_visMoniker = "All Fields";
    GII_identifier = R_ALL;
}

@ifdef COMPILEGPC
@object GenGlyphClass CRDBSepLine4 = {
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_GLYPH_SEPARATOR;
}
@else
@object GenInteractionClass CRDBNumericGroup = {
    GI_comp = @CRDBNumericSubGroup;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_DRAW_IN_BOX;
}
@endif

@object GenInteractionClass CRDBNumericSubGroup = {
    GI_visMoniker = "Calculate Numeric Field Totals:";
    @localize "numeric fields section of create report dialog box";
    GI_comp = @CRDBNumeric;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_PLACE_MONIKER_ABOVE;
}

@object GenItemGroupClass CRDBNumeric = {
    GI_visMoniker = "                  ";
    @localize "spaces to align with rest of choices above it";
    GI_comp = @CRDBNNone, @CRDBNSelected, @CRDBNAll;
    GIGI_selection = R_NONE;
    GIGI_applyMsg = MSG_REPORT_SHOW_NUM_FIELDS_LIST_APPLY;
    GIGI_destination = process;
    ATTR_GEN_ITEM_GROUP_SET_MODIFIED_ON_REDUNDANT_SELECTION;
    ATTR_GEN_SEND_APPLY_MSG_ON_APPLY_EVEN_IF_NOT_MODIFIED;
    HINT_ORIENT_CHILDREN_VERTICALLY;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_CENTER_CHILDREN_ON_MONIKERS;
}

@object GenItemClass CRDBNNone = {
    GI_visMoniker = "None";
    GII_identifier = R_NONE;
}

@object GenItemClass CRDBNSelected = {
    GI_visMoniker = "Selected Numeric Fields...";
    GII_identifier = R_SELECTED;
}

@object GenItemClass CRDBNAll = {
    GI_visMoniker = "All Numeric Fields";
    GII_identifier = R_ALL;
}

@object GenInteractionClass CRDBReplyGroup = {
    GI_comp = @CRDBCreateButton, @CRDBCanxButton;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    HINT_MAKE_REPLY_BAR;
}

@object GenTriggerClass CRDBCreateButton = {
    GI_visMoniker = "Create";
    @localize "button to create a report";
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    GTI_destination = process;
    GTI_actionMsg = MSG_CREATE_REPORT;
}

@object GenTriggerClass CRDBCanxButton = {
@ifdef GPC_ONLY
    GI_visMoniker = "Don't Create";
    @localize "button to cancel report creation";
@endif
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
@ifndef GPC_ONLY
    ATTR_GEN_TRIGGER_INTERACTION_COMMAND = { IC_DISMISS };
@endif
}

/* -------------------------------------------------------------- */
@object GenInteractionClass ReportSelectRecordsDBox = {
    GI_visMoniker = "Select Records";
    @localize "title of dialog box to select records to show in report";
    GI_comp = @RSRRecordsDList, @RSRClear, @RSRDoneButton;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    GII_visibility = GIV_DIALOG;
    ATTR_GEN_HELP_CONTEXT = "ReportSelRecsDB";
    HINT_CENTER_CHILDREN_HORIZONTALLY;
    HINT_MINIMUM_SIZE = {0, SST_LINES_OF_TEXT | 0, 0};
}

@object GenDynamicListClass RSRRecordsDList = {
    GIGI_selection = 0;
    GIGI_destination = process;
    GDLI_queryMsg = MSG_RECORDS_DLIST_MONIKER_QUERY;
    ATTR_GEN_ITEM_GROUP_STATUS_MSG = MSG_SELECT_RECORDS_DLIST_STATUS;
    HINT_ITEM_GROUP_SCROLLABLE;
    HINT_INITIAL_SIZE = {
		SST_AVG_CHAR_WIDTHS | 30,
		SST_LINES_OF_TEXT | 15, 15};
 }

@object GenTriggerClass RSRClear = {
    GI_visMoniker = "Clear Checkmarks";
    @localize "button to clear record selection checkmarks";
    GI_states = @default & ~GS_ENABLED;
    GTI_destination = process;
    GTI_actionMsg = MSG_SELECT_RECORDS_CLEAR_CHECKS;
}

@object GenTriggerClass RSRDoneButton = {
    GI_visMoniker = "Done";
    @localize "button to end record selection";
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    HINT_SEEK_REPLY_BAR;
}

/* -------------------------------------------------------------- */
@object GenInteractionClass ReportSelectFieldsDBox = {
    GI_visMoniker = "Select Fields to Display";
    @localize "title of dialog box to select fields to show in report";
    GI_comp = @RSFFields, @RSFDoneButton;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    GII_visibility = GIV_DIALOG;
    ATTR_GEN_HELP_CONTEXT = "ReportSelFieldsDB";
}

@object GenBooleanGroupClass RSFFields = {
    GI_comp = @RSF1, @RSF2, @RSF3, @RSF4, @RSF5,
		    @RSF6, @RSF7, @RSF8, @RSF9, @RSF10;
    HINT_ORIENT_CHILDREN_VERTICALLY;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_CENTER_CHILDREN_ON_MONIKERS;
    HINT_MINIMUM_SIZE = {SST_AVG_CHAR_WIDTHS | 40, 0, 0};
}


@define RSFFIELDSITEMS(RSFOBJ, RSFNAME, RSFID) \
 @object GenBooleanClass RSFOBJ = { \
    GI_visMoniker = RSFNAME;  \
    GBI_identifier = RSFID; \
    }
@RSFFIELDSITEMS(RSF1, "Field 1", 1)
@RSFFIELDSITEMS(RSF2, "Field 2", 2)
@RSFFIELDSITEMS(RSF3, "Field 3", 4)
@RSFFIELDSITEMS(RSF4, "Field 4", 8)
@RSFFIELDSITEMS(RSF5, "Field 5", 16)
@RSFFIELDSITEMS(RSF6, "Field 6", 32)
@RSFFIELDSITEMS(RSF7, "Field 7", 64)
@RSFFIELDSITEMS(RSF8, "Field 8", 128)
@RSFFIELDSITEMS(RSF9, "Field 9", 256)
@RSFFIELDSITEMS(RSF10, "Field 10", 512)

@object GenTriggerClass RSFDoneButton = {
    GI_visMoniker = "Done";
    @localize "button to end field selection";
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    HINT_SEEK_REPLY_BAR;
}

/* -------------------------------------------------------------- */
@object GenInteractionClass ReportSelectNumFieldsDBox = {
    GI_visMoniker = "Select Fields to Total";
    @localize "title of dialog box to select fields to total in report";
    GI_comp = @RSNFFields, @RSNFDoneButton;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    GII_visibility = GIV_DIALOG;
    ATTR_GEN_HELP_CONTEXT = "ReportSelNumFieldsDB";
}

@object GenBooleanGroupClass RSNFFields = {
    GI_comp = @RSNF1, @RSNF2, @RSNF3, @RSNF4, @RSNF5,
		    @RSNF6, @RSNF7, @RSNF8, @RSNF9, @RSNF10;
    HINT_ORIENT_CHILDREN_VERTICALLY;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_CENTER_CHILDREN_ON_MONIKERS;
    HINT_MINIMUM_SIZE = {SST_AVG_CHAR_WIDTHS | 40, 0, 0};
}


@define RSNFFIELDSITEMS(RSNFOBJ, RSNFNAME, RSNFID) \
 @object GenBooleanClass RSNFOBJ = { \
    GI_visMoniker = RSNFNAME;  \
    GBI_identifier = RSNFID; \
    }
@RSNFFIELDSITEMS(RSNF1, "Field 1", 1)
@RSNFFIELDSITEMS(RSNF2, "Field 2", 2)
@RSNFFIELDSITEMS(RSNF3, "Field 3", 4)
@RSNFFIELDSITEMS(RSNF4, "Field 4", 8)
@RSNFFIELDSITEMS(RSNF5, "Field 5", 16)
@RSNFFIELDSITEMS(RSNF6, "Field 6", 32)
@RSNFFIELDSITEMS(RSNF7, "Field 7", 64)
@RSNFFIELDSITEMS(RSNF8, "Field 8", 128)
@RSNFFIELDSITEMS(RSNF9, "Field 9", 256)
@RSNFFIELDSITEMS(RSNF10, "Field 10", 512)

@object GenTriggerClass RSNFDoneButton = {
    GI_visMoniker = "Done";
    @localize "button to end field selection";
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    HINT_SEEK_REPLY_BAR;
}

@end CreateRepDialog;

@start Dialogs;
/* ------------------------------------------------------------------ */
@object GPCReportInteractionClass ReportGroup = {
    GI_visMoniker = "Reports Window";
    @localize "Title at the top of the Reports window";
    GI_comp = @ReportWindowMenu, @TextView;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    GII_visibility = GIV_DIALOG;
    HINT_INTERACTION_MAKE_RESIZABLE;
    HINT_INTERACTION_MAXIMIZABLE;
//    HINT_WINDOW_NO_SYS_MENU;
    HINT_POSITION_WINDOW_AT_RATIO_OF_PARENT = {
	SWSS_RATIO | PCT_5, SWSS_RATIO | PCT_5};
    HINT_SIZE_WINDOW_AS_RATIO_OF_FIELD = {
      SWSS_RATIO | PCT_80, SWSS_RATIO | PCT_80};
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
    ATTR_GEN_HELP_CONTEXT = "ReportWindow"; 
}

@object GenInteractionClass ReportWindowMenu = {
    GI_comp = @RWMTop, @RWMCloseButton;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
}

@object GenInteractionClass RWMTop = {
@ifdef COMPILEGPC
    GI_comp = @GPCBasePrintControl,
			    @GPCCopyButton, @GPCSelAllButton;
@else
    GI_comp = @GPCBasePrintControl, @GPCBReportEditControl;
@endif
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
}

@object PrintControlClass GPCBasePrintControl = {
	 GI_states = (@default | GS_ENABLED);

	/* GI_states = (@default & ~ GS_USABLE) | GS_ENABLED;  */
	 PCI_attrs = ((@default & ~PCA_PAGE_CONTROLS)
			  | PCA_MARK_APP_BUSY);
    PCI_output = @TextWindow;
    PCI_docNameOutput = @GPCBaseDocumentGroup;
    PCI_docSizeInfo   = {(7.5*72),   /* width less margins */
				 (10*72),    /* height less margins */
			 PT_PAPER,    /* layout */
			 {(.5*72), (.5*72), (.5*72), (.5*72)}}; /* L,T,R,B margins */
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_CENTER_MONIKER;
@ifdef COMPILEGPC
    ATTR_GEN_CONTROL_PROHIBIT_UI = PRINTCF_PRINT_TRIGGER;
    ATTR_GEN_CONTROL_APP_UI = @GPCPrintButton;
@else
    ATTR_GEN_INTERACTION_GROUP_TYPE = GIGT_PRINT_GROUP;
@endif
}

@ifdef COMPILEGPC
@object GenTriggerClass GPCPrintButton = {
    GI_visMoniker = list {@PrintButtonIcon, @PrintButtonText};
	 GI_states = (@default & ~ GS_USABLE) | GS_ENABLED;  
	 GTI_destination = @GPCBasePrintControl;
    GTI_actionMsg = MSG_PRINT_CONTROL_INITIATE_PRINT;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_CENTER_MONIKER;
    HINT_USE_ICON_TEXT_COMBINATION_MONIKER = {
			  ITMF_POSITION_ICON_ABOVE_TEXT, 5};
}
@visMoniker PrintButtonText = "Print...";
@endif   

@ifndef COMPILEGPC
@object GenEditControlClass GPCBReportEditControl = {
    ATTR_GEN_CONTROL_REQUIRE_UI = GECF_COPY
						   | GECF_SELECT_ALL;
    ATTR_GEN_CONTROL_PROHIBIT_UI = GECF_UNDO
						   | GECF_CUT
						   | GECF_PASTE
						   | GECF_DELETE;
    HINT_AVOID_MENU_BAR;
}
@endif

@ifdef COMPILEGPC
@object GenTriggerClass GPCCopyButton = {
    GI_visMoniker = list {@CopyButtonIcon, @CopyButtonText};
    GTI_destination = @TextWindow;
    GTI_actionMsg = MSG_META_CLIPBOARD_COPY;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_CENTER_MONIKER;
    HINT_USE_ICON_TEXT_COMBINATION_MONIKER = {
			  ITMF_POSITION_ICON_ABOVE_TEXT, 5};
}
@visMoniker CopyButtonText = "Copy";

@object GenTriggerClass GPCSelAllButton = {
    GI_visMoniker = list {@SelAllButtonIcon, @SelAllButtonText};
    GTI_destination = process;
    GTI_actionMsg = MSG_DO_SELECT_ALL;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_CENTER_MONIKER;
    HINT_USE_ICON_TEXT_COMBINATION_MONIKER = {
			  ITMF_POSITION_ICON_ABOVE_TEXT, 5};
}
@visMoniker SelAllButtonText = "Select All";
@endif

@object GenTriggerClass RWMCloseButton = {
    GI_visMoniker = "Close";
    @localize "button to close report window";
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
//    GTI_destination = process;
//    GTI_actionMsg = MSG_CLOSE_REPORT_WINDOW;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_ALIGN_BOTTOM_EDGE_WITH_OBJECT = @TextView;
    HINT_CENTER_MONIKER;
}

@object GenViewClass TextView = {
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    GVI_vertAttrs  = @default | GVDA_SCROLLABLE;
    GVI_attrs = @default | GVA_DRAG_SCROLLING
	    	    	 | GVA_WINDOW_COORDINATE_MOUSE_EVENTS
			 | GVA_DONT_SEND_KBD_RELEASES
			 | GVA_FOCUSABLE;
    GVI_content = @TextContent;
    HINT_DEFAULT_FOCUS;
    HINT_DEFAULT_TARGET;
    ATTR_GEN_VIEW_DOES_NOT_ACCEPT_TEXT_INPUT;
}

@object VisContentClass TextContent = {
    VCNI_view = @TextView;
    VCNI_attrs = @default | VCNA_LARGE_DOCUMENT_MODEL
	    	    	 | VCNA_WINDOW_COORDINATE_MOUSE_EVENTS
			 | VCNA_SAME_WIDTH_AS_VIEW;
    VI_attrs = @default & ~VA_MANAGED;
    VI_optFlags = @default & ~(VOF_GEOMETRY_INVALID | VOF_GEO_UPDATE_PATH);
    VI_geoAttrs = @default | VCGA_CUSTOM_MANAGE_CHILDREN;
}

@object GPCBaseVLTextClass TextWindow = {
    VTI_state = @default | VTS_SELECTABLE
		       	     | VTS_TARGETABLE;
 //   VTI_output = NullHandle;
    VLTI_displayMode = VLTDM_DRAFT_WITH_STYLES;
    VLTI_attrs = VLTA_EXACT_HEIGHT;
    VLTI_regionArray = @GPCBVLTextRegionArray;
    VTI_charAttrRuns = ((VTDS_12 << VTDCA_SIZE_OFFSET) || VTDF_URW_MONO);
}

@chunk ChunkArrayHeader GPCBVLTextRegionArray = {
    0, sizeof(VisLargeTextRegionArrayElement),
    0, sizeof(ChunkArrayHeader)
};

@end Dialogs;
@start TextAttrsResource, data;
@chunk VisTextCharAttr GPCBTextCharAttrs =
	CHAR_ATTR_STYLE_FONT_SIZE_STYLE_COLOR(
		2, CA_NULL_ELEMENT, FID_DTC_URW_MONO, 12, 0, C_BLACK);

@chunk VisTextParaAttr GPCBTitleParaAttrs =
	PARA_ATTR_STYLE_JUST_LEFT_RIGHT_PARA(
		0, 0, J_CENTER, 1, 1, 0);

@chunk VisTextParaAttr GPCBDocParaAttrs =
	PARA_ATTR_STYLE_JUST_LEFT_RIGHT_PARA(
		0, 0, J_LEFT, 1, 1, 0);
@end TextAttrsResource;


/***********************************************************************
 *
 * MESSAGE:	MSG_VIS_TEXT_FREE_ALL_STORAGE for GPCBaseVLTextClass
 *
 * DESCRIPTION: We must intercept FREE_ALL_STORAGE to do a little more work so that the
 *		text object is left in the correct state
 *              Most of this from vtext sample file
 *              Has to stay in this source file!!!!!
 *          called from MSG_CLOSE_REPORT_WINDOW
 ***********************************************************************/
@method GPCBaseVLTextClass, MSG_VIS_TEXT_FREE_ALL_STORAGE
{
    ChunkArrayHeader *cahp;
    VisLargeTextInstance *vself;

    @callsuper();
    LMemReAlloc(@GPCBVLTextRegionArray, sizeof(ChunkArrayHeader));
    cahp = LMemDeref(@GPCBVLTextRegionArray);
    cahp->CAH_count = 0;
    cahp->CAH_elementSize = sizeof(VisLargeTextRegionArrayElement);
    cahp->CAH_curOffset = 0;
    cahp->CAH_offset = sizeof(ChunkArrayHeader);

    vself = ObjDerefVis(oself);
    vself->VTI_storageFlags = (VTSF_DEFAULT_CHAR_ATTR |
							 VTSF_DEFAULT_PARA_ATTR);
    vself->VI_optFlags |= VOF_GEOMETRY_INVALID;
}


/***********************************************************************
 *
 * MESSAGE:	MSG_PRINT_START_PRINTING for GPCBaseVLTextClass
 *
 * DESCRIPTION:	Does the display printing
 *              Has to stay in this source file!!!!!
 *
 ***********************************************************************/
@method GPCBaseVLTextClass, MSG_PRINT_START_PRINTING
{
 word		curPage, numPages;
 XYValueAsDWord	textSize;     /* Image size of text. */
 VisLargeTextGetRegionPosReturnStruct    regPos;


    /* save region size */
    textSize = @call self::MSG_VIS_LARGE_TEXT_GET_DRAFT_REGION_SIZE(0);

    /* set region size for printing */
    @send self::MSG_VIS_LARGE_TEXT_SET_DRAFT_REGION_SIZE((7.5*72), (10*72));

    /* set number of pages to print */
    numPages = @call self::MSG_VIS_LARGE_TEXT_GET_REGION_COUNT();

    /* now do the printing */
    for (curPage=0; curPage < numPages; curPage++)  {
       GrSaveState(gstate);
	  @call self::MSG_VIS_LARGE_TEXT_GET_REGION_POS( &regPos, curPage );

       /* Clip it to match translation and margins */
	  GrSetClipRect(gstate, PCT_REPLACE,
					    0,      		/* left */
					    0,      		/* top */
					    (7.5 * 72),     /* right */
					    ((10*72) - 3)); /* bottom */
					   /* the -3 is a quarter line offset to prevent
                            an extra line from printing at the bottom
                            of a page */

       /* Translate the gstate to selected page */
	  GrApplyTranslationDWord(gstate, 0,
		   -(regPos.VLTGRPRS_yPosition));
       GrInitDefaultTransform(gstate);
	 /* Draw current page */
	  @call self::MSG_VIS_DRAW(DF_PRINT, gstate);

       GrRestoreState(gstate);
       GrNewPage(gstate, PEC_FORM_FEED);
    }  /* end of page printing for loop */

    @send GPCBasePrintControl::MSG_PRINT_CONTROL_PRINTING_COMPLETED();

    /* reset things back to where the user had them */
    @send self::MSG_VIS_LARGE_TEXT_SET_DRAFT_REGION_SIZE(
                  DWORD_X(textSize), DWORD_Y(textSize));

}

/***********************************************************************
 *
 * MESSAGE:	MSG_META_LARGE_END_SELECT for GPCBaseVLTextClass
 *
 * DESCRIPTION:	enables the copy button when there is text selected
 *
 *
 ***********************************************************************/
@method GPCBaseVLTextClass, MSG_META_LARGE_END_SELECT
{
VisTextRange	range;

    @callsuper();

    @call self::MSG_VIS_TEXT_GET_SELECTION_RANGE(&range);

    if (range.VTR_start == range.VTR_end)
	  @send GPCCopyButton::MSG_GEN_SET_NOT_ENABLED(VUM_NOW);
    else @send GPCCopyButton::MSG_GEN_SET_ENABLED(VUM_NOW);


}


