/**************************************************************
 *  ==CONFIDENTIAL INFORMATION==
 *  COPYRIGHT 1994-2000 BREADBOX COMPUTER COMPANY --
 *  ALL RIGHTS RESERVED  --
 *  THE FOLLOWING CONFIDENTIAL INFORMATION IS BEING DISCLOSED TO YOU UNDER A
 *  NON-DISCLOSURE AGREEMENT AND MAY NOT BE DISCLOSED OR FORWARDED BY THE
 *  RECIPIENT TO ANY OTHER PERSON OR ENTITY NOT COVERED BY THE SAME
 *  NON-DISCLOSURE AGREEMENT COVERING THE RECIPIENT. USE OF THE FOLLOWING
 *  CONFIDENTIAL INFORMATION IS RESTRICTED TO THE TERMS OF THE NON-DISCLOSURE
 *  AGREEMENT.
 **************************************************************/

/*-------------------------------------------------------------------------
 | Project:  Lysle's IRC Chat program       
 |           Copyright (c) 1998 Breadbox Computer Company
 |
 | Revision History:
 |    Who  Date:     Comments:
 |    ---  --------  ---------
 |    LES  05/01/98  Created.
 |                   (LES = Lysle Shields)
 |    
 *-----------------------------------------------------------------------*/
/* History   jfh
	03/06/99	bug fixing... 415, 416
	03/07/99	bug fixing... 417, 419 & 86ing priv msg when other guy exits
               version 0.2.0.0
	3/26/99	added multi launch capability
			converted all outgoing and incoming text to CODE_PAGE_LATIN_1
	4/16/99	code to Don for integration with browser & GPC portal
	8/20/99	got the code back from Don...
	8/23/99	fixed the no # sign bug in opening dialog box
	8/25/99	version 0.3.0 to GPC
	8/31/99	got new specs
	9/03/99   back to single channel
			started revamping the UI
     9/07/99	put channel name in primary title
               allowed slash cmds only if .ini set
			tweaked UI
               changed Priv Msg initiation
	9/08/99	worked on Ignore
	9/09/99	updated GPC build to 9/6/99
			tweaked UI some more
			fixed chanop desig
			topic now shows on primary
			v0.4.0  to GPC
	9/10/99	set up CUI & TV mods
			modified sort list obj to struct vs string
	9/13/99   added ignore functionality
			and mod the list to show ignored folks
			and sent off some spec/ui questions to GPC with this v0.5.0
	9/20/99   added Edit menu
	9/23/99	version 0.6 to GPC
    10/22/99   fixed bug #1060
			fixed bug 1063 by adding .txt to name & limiting
			   user entry to 8
			   Also set Document as virtual root in FileSels
			fixed bug 976
			fixed 1062 (@ shown only if canSlash set in .ini)
			  - need to test (SAT 10/22 - TBC)
			fixed bug 913 (put CRs at end vs beginning)
			  - need to test  (UNSAT 10/22 - TBC)
               added Include to Ignore button
			version 0.7 to GPC
    10/25/99   changed source file format .goc vs .goh
    10/26/99   fixed the unreported font/point change bug (added by GPC
			   changing to colors and not setting range start properly)
			added extra space at the end of each para #913 & 1059
    10/27/99   worked 1094 (error conditions)
			tweaked per specs v0.94
			added Chat Tip dbox
			put strings in sep resource
			version 0.8 to GPC
    11/04/99   worked to resolve bug list
    11/05/99   put back RPL_NOTOPIC
			added auto join to #422 (no MOD) to make work on GPC server
    11/09/99	made chat tip show only once (ini) bug 1197
               made Chat Browser signon order reflect current Browser
               added edit control to Priv msg dboxes bug 1196
    11/10/99   version 0.9 to GPC (at JonL's request)
			fixed 1194, worked on 1195
			worked on 1218
    11/11/99	fixed priv msg problem intro'd with PM edit menu
			  sent 0.9a off to Jon
			long log filenames fixed #1198
			suppressing enry window ctrlr notification #1195
			  closer but now ctrls greyed when entry selected
			messed with no access point bug #1199  asked Lysle
			   for WM code
			finished fixing 1218
    11/12/99   more on #1195
    11/17/99   v0.10 to GPC
    11/21/99	fixed bug #1199
			more on #1195
    11/22/99   more on #1195
    11/23/99   more on #1195
    11/27/99	code to Brian Chin @ GPC to mess with #1195
    12/01/99	Brian fixed it!!           
			worked on 1365
			and 1193
			v0.11 to GPC
    12/14/99	fixed #1430
    12/16/99   fixed #1426  used same error msg as in Browser
               v0.12 to GPC
    12/20/99   worked on intercepting pastes for CR/LF
    12/21/99   more of above - got normal paste intercepted but not
			quick copy  AND had to unfix 1195 to get this working
    12/23/99   got Quick copy working (added LARGE method)
			NOTE - commented out part of OPEN_APP (space between
				  paras) 'cause it gives Swat fits.
			... and put back after swatting.
    12/29/99   added Brian's new fix to 1195 
    01/11/00	intercepted pastes for priv msg dbox 
    01/12/00   source files into Perforce
    01/14/00   added KidGuard check & hourglass fix
    01/19/00   added don't show tip dbox selection
			fixed bug #1583 (main win scroll on startup)
    02/23/00   added Quick Help text strings
*********************************************************************/		

@include "gpcchat.goh"
//@include "bbox.goh"
@include "appcolor.goh"



@start	AppResource ;
    /*---------------------------------------------------------------------*/
    @object IRCApplicationClass IRCApp = {

	   GI_visMoniker = list { @IRCTextMoniker,
						 @IRCIconVGA,
						 @AppTinyMoniker }
	   GI_comp       = @IRCPrimary/*, @IRCChoicePrimary */;
	   gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_WINDOWS)
				  = @IRCPrimary;
	   gcnList(MANUFACTURER_ID_GEOWORKS,MGCNLT_ACTIVE_LIST) =
			 @IRCDisplayControl;
	   gcnList( MANUFACTURER_ID_GEOWORKS, GAGCNLT_SELF_LOAD_OPTIONS ) =
			 @IRCPointControl,
			 @IRCFontControl,
			 @IRCEditControl,
			 @IRCDisplayControl;
	   ATTR_GEN_INIT_FILE_CATEGORY = "chat" ;
	   ATTR_GEN_HELP_FILE = "chat";
	   ATTR_GEN_INIT_FILE_PROPAGATE_TO_CHILDREN;
    }
    /*---------------------------------------------------------------------*/
    @visMoniker IRCTextMoniker =
	   "Chat" ;
    /*---------------------------------------------------------------------*/
@end	AppResource

@start	Interface ;
    /*---------------------------------------------------------------------*/
    /* This guy only comes up from AUI */
/*    @object GenPrimaryClass IRCChoicePrimary  = {
	   GI_states = @default & (~GS_USABLE) ;
	   GI_attrs = @default | GA_KBD_SEARCH_PATH
					   | GA_TARGETABLE  ;
	   GI_comp = @IRCChoiceDialog;
        HINT_WINDOW_NO_SYS_MENU;
	   HINT_PRIMARY_NO_FILE_MENU;
	   HINT_NO_WIDER_THAN_CHILDREN_REQUIRE;
	   HINT_NO_TALLER_THAN_CHILDREN_REQUIRE;
	   HINT_DEFAULT_TARGET ;
    }

    @object GenInteractionClass IRCChoiceDialog = {
	   GI_comp = @IRCChoiceGroup, @IRCChoiceSepLine, @IRCChoiceReply;
	   GI_attrs = @default | GA_KBD_SEARCH_PATH ;
    }

@object GenItemGroupClass IRCChoiceGroup = {
    GI_comp = @IRCCOpenBrowser, @IRCCOwnDetails;
    GIGI_selection = 1;
    HINT_ITEM_GROUP_DISPLAY_CURRENT_SELECTION;
    HINT_ORIENT_CHILDREN_VERTICALLY;
}

@object GenItemClass IRCCOpenBrowser = {
    GI_visMoniker = "View a list of available Myturn.com chat rooms";
    GII_identifier = 1;
}

@object GenItemClass IRCCOwnDetails = {
    GI_visMoniker = "Enter chat server details";
    GII_identifier = 2;
}

@object GenGlyphClass IRCChoiceSepLine = {
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_GLYPH_SEPARATOR;
}

@object GenInteractionClass IRCChoiceReply = {
    GI_comp = @IRCChoiceGo, @IRCChoiceQuit;
    GI_attrs = @default | GA_KBD_SEARCH_PATH ;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY;
    HINT_INCLUDE_ENDS_IN_CHILD_SPACING;
}

@object GenTriggerClass IRCChoiceGo = {
    GI_visMoniker = "OK";
    GTI_destination = process;
    GTI_actionMsg = MSG_IRC_OPENING_AUI_CHOICE;
    HINT_SEEK_REPLY_BAR;
}

@object GenTriggerClass IRCChoiceQuit = {
    GI_visMoniker = "Quit";
    GTI_destination = @IRCApp;
    GTI_actionMsg = MSG_META_QUIT;
    HINT_SEEK_REPLY_BAR;
}

    /*---------------------------------------------------------------------*/
    @object GenPrimaryClass IRCPrimary  = {
	   GI_attrs = @default | GA_KBD_SEARCH_PATH
					   | GA_TARGETABLE  ;
	   GI_comp = @IRCMenuBar, /*@ChannelOne,*/ @IRCDisplayGroup,
			   @IRCDialogs, /*@IRCTipDialog,*/ @PCRoot;
	   ATTR_GEN_INIT_FILE_CATEGORY = "chat" ;
	   ATTR_GEN_INIT_FILE_PROPAGATE_TO_CHILDREN;
	   ATTR_GEN_HELP_CONTEXT = "TOC";
	   HINT_PRIMARY_NO_FILE_MENU;
	   HINT_EXPAND_HEIGHT_TO_FIT_PARENT ;
	   HINT_EXPAND_WIDTH_TO_FIT_PARENT ;
	   HINT_ORIENT_CHILDREN_HORIZONTALLY ;
	   HINT_DEFAULT_TARGET ;
    }

@object ParentalControlClass PCRoot = {
}

    /*---------------------------------------------------------------------*/
    @object GenInteractionClass IRCMenuBar = {
	   GI_attrs = @default | GA_KBD_SEARCH_PATH
					   | GA_TARGETABLE  ;
	   GI_comp = @IRCChat, @IRCEditControl, @IRCLogMenu, @IRCSettings,
			   @WindowMenu;
	   ATTR_GEN_INIT_FILE_PROPAGATE_TO_CHILDREN;
	 }

    /*---------------------------------------------------------------------*/
    @object GenInteractionClass IRCChat = {
	   GI_attrs = @default | GA_KBD_SEARCH_PATH ;
	   GI_visMoniker = 'C', "Chat" ;
		GI_comp = @IRCConnectDialog, @IRCCommandChangeChannel, @IRCCommandQuitChannel,
				@IRCChangeNickItem, @IRCSendPrivChat, @IRCIgnore,
				@AboutGroup,  @IRCQuitChat;
	   GII_visibility = GIV_POPUP ;
	   ATTR_GEN_INIT_FILE_PROPAGATE_TO_CHILDREN;
	   HINT_SEEK_MENU_BAR ;
    }

@object GenInteractionClass AboutGroup = {
	GI_comp = @AboutDBox;
	GII_visibility = GIV_SUB_GROUP;
}

@include "bbxreg.goh"

@object GenInteractionClass AboutDBox = {
	 GI_visMoniker = 'b', "About";
	 GI_comp = @AppName, @By, @BreadBoxLogo,
				  @Visit, @Web, @AboutSpacer1,
				  @Copyright, @BBoxLLC, @Rights, @AboutSpacer2;
	 GII_visibility = GIV_DIALOG;
	 GII_type = GIT_NOTIFICATION;
	 HINT_CENTER_CHILDREN_HORIZONTALLY;
}
@object GenTextClass AppName = {
    GI_attrs = @default | GA_READ_ONLY;
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
	 GTXI_text = "Chat";
    ATTR_GEN_TEXT_CHAR_ATTR = (ChunkHandle) @AppNameCharAttrs;
}
@chunk VisTextCharAttr AppNameCharAttrs =
	CHAR_ATTR_FONT_SIZE_STYLE(
		FID_DTC_URW_SANS, 36, TS_BOLD);

@object GenGlyphClass By = {
  GI_visMoniker = "by";
}
@object GenGlyphClass BreadBoxLogo = {
  GI_visMoniker = list { @BreadboxLogoMoniker}
}
@object GenGlyphClass Visit = {
  GI_visMoniker = "Visit us at...";
}
@object GenGlyphClass Web = {
  GI_visMoniker = "www.breadbox.com";
}
@object GenGlyphClass AboutSpacer1 = {
  GI_visMoniker = "   ";
}
@object GenGlyphClass Copyright = {
  GI_visMoniker = "Copyright 1994-2002";
}
@object GenGlyphClass BBoxLLC = {
  GI_visMoniker = "Breadbox Computer Company LLC";
}
@object GenGlyphClass Rights = {
  GI_visMoniker = "All Rights Reserved";
}
@object GenGlyphClass AboutSpacer2 = {
  GI_visMoniker = "   ";
}
	 /*---------------------------------------------------------------------*/
	 /* only used in openSystem version */
	 @object GenInteractionClass IRCCommandChangeChannel = {
		  GI_visMoniker = 'G', "Go to channel" ;
		  GI_comp = @IRCNewChannel, @IRCChannelOk ;
		  GI_attrs = @default | GA_KBD_SEARCH_PATH ;
		  GI_states = @default & (~GS_ENABLED) ;
		  GII_type = GIT_COMMAND ;
        GII_visibility = GIV_DIALOG ;
		  GII_attrs = @default | GIA_MODAL ;
	 }
    /*---------------------------------------------------------------------*/
    @object GenTextClass IRCNewChannel = {
		  GI_visMoniker = "Channel" ;
        GTXI_maxLength = 70 ;
        GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT | GTA_ALLOW_TEXT_OFF_END;
	   ATTR_GEN_TEXT_NO_SPACES;
	   ATTR_GEN_TEXT_SET_OBJECT_ENABLED_WHEN_TEXT_EXISTS = @IRCChannelOk;
	   HINT_EXPAND_WIDTH_TO_FIT_PARENT ;
	   HINT_DEFAULT_TARGET ;
        HINT_DEFAULT_FOCUS ;
    }
    /*---------------------------------------------------------------------*/
    @object GenTriggerClass IRCChannelOk = {
		GI_states = @default & (~GS_ENABLED) ;
	   GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE ;
	   ATTR_GEN_TRIGGER_INTERACTION_COMMAND = IC_OK ;
	   GTI_destination = @IRCApp ;
		GTI_actionMsg = MSG_IRC_ADD_CHANNEL ;
	   HINT_SEEK_REPLY_BAR ;
        HINT_DEFAULT_DEFAULT_ACTION ;
    }
	 /*---------------------------------------------------------------------*/
	 /* only used in openSystem version */
	 @object GenTriggerClass IRCChangeNickItem = {
		GI_visMoniker = 'n', "Change nickname" ;
		GI_states = @default & (~GS_USABLE) ;
		GTI_destination = @IRCChangeNickname;
		GTI_actionMsg = MSG_GEN_INTERACTION_INITIATE;
		HINT_TRIGGER_BRINGS_UP_WINDOW;
	 }

	 /*---------------------------------------------------------------------*/
    @object GenTriggerClass IRCCommandQuitChannel = {
	   GI_visMoniker = 'Q', "Quit channel" ;
	   GI_attrs = @default | GA_KBD_SEARCH_PATH 
					   | GA_SIGNAL_INTERACTION_COMPLETE ;
	   GI_states = @default & (~GS_ENABLED) ;
	   GTI_destination = @IRCApp ;
	   GTI_actionMsg = MSG_IRC_LEAVE_CHANNEL ;
	   GI_kbdAccelerator = ctrl 'Q' ;
    }
    /*---------------------------------------------------------------------*/
    @object GenTriggerClass IRCSendPrivChat = {
	   GI_visMoniker = 'S', "Send Private Message";
	   GI_states = @default & (~GS_ENABLED) ;
	   GTI_destination = process;
	   GTI_actionMsg = MSG_IRC_OPEN_PRIVATE_MESSAGE;
	   HINT_TRIGGER_BRINGS_UP_WINDOW;
    }

    @object GenTriggerClass IRCIgnore = {
	   GI_states = @default & (~GS_ENABLED) ;
	   GTI_destination = process;
	   GTI_actionMsg = MSG_IRC_SEND_IGNORE;
    }
@visMoniker IRCIgnoreMoniker = 'I', "Ignore User";
@visMoniker IRCIncludeMoniker = 'I', "Include User";


/*@object GenInteractionClass AboutSubGroup = {
    GI_comp = @AboutDBox;
    GII_visibility = GIV_SUB_GROUP;
}

@object GenInteractionClass AboutDBox = {
    GI_visMoniker = 'b', "About";
    GI_comp = @AppName, @By, @BreadBoxLogo, @Visit,
		    @Web, @Copyright;
    GII_visibility = GIV_DIALOG;
    GII_type = GIT_NOTIFICATION;
    HINT_CENTER_CHILDREN_HORIZONTALLY;
}

@object GenTextClass AppName = {
    GI_attrs = @default | GA_READ_ONLY;
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_text = "Chat";
    ATTR_GEN_TEXT_CHAR_ATTR = (ChunkHandle) @AppNameCharAttrs;
}

@chunk VisTextCharAttr AppNameCharAttrs =
	CHAR_ATTR_FONT_SIZE_STYLE(
		FID_DTC_URW_SANS, 36, TS_BOLD);

@object GenGlyphClass By = {
  GI_visMoniker = "by";
}

@object GenGlyphClass BreadBoxLogo = {
  GI_visMoniker = list {@BreadboxLogoMoniker}
}

@object GenGlyphClass Copyright = {
  GI_visMoniker = "Copyright 1994-2000 Breadbox Computer Company";
}

@object GenGlyphClass Visit = {
  GI_visMoniker = "Visit us at...";
}

@object GenGlyphClass Web = {
  GI_visMoniker = "www.breadbox.com";
}
*/
	 @object GenTriggerClass IRCQuitChat = {
	   GI_visMoniker = 'E', "Exit Chat";
	   GI_attrs = @default | GA_KBD_SEARCH_PATH ;
	  /* GI_states = @default & (~GS_ENABLED) ;*/
	   GTI_actionMsg = MSG_IRC_END_CONNECT ;
	   GTI_destination = process ;
    }

    /*---------------------------------------------------------------------*/
@object GenEditControlClass IRCEditControl = {
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    GII_visibility = GIV_POPUP;
    ATTR_GEN_INTERACTION_GROUP_TYPE = GIGT_EDIT_MENU;
    ATTR_GEN_CONTROL_PROHIBIT_UI = GECF_UNDO
						   | GECF_CLIP_ART;
}

    /*---------------------------------------------------------------------*/
    @object GenInteractionClass IRCLogMenu = {
	   GI_visMoniker = 'L', "Logging";
	   GI_comp = @IRCLogStart, @IRCLogAppend, @IRCLogStop;
	   GII_visibility = GIV_POPUP;
	   GI_attrs = @default | GA_KBD_SEARCH_PATH;
    }
    @object GenTriggerClass IRCLogStart = {
	   GI_visMoniker = 'S', "Start";
	   GI_attrs = @default | GA_KBD_SEARCH_PATH;
	   GTI_destination = @IRCStartDialog ;
	   GTI_actionMsg = MSG_GEN_INTERACTION_INITIATE;
	   HINT_TRIGGER_BRINGS_UP_WINDOW;
    }
    @object GenTriggerClass IRCLogAppend = {
	   GI_visMoniker = 'A', "Append";
	   GI_attrs = @default | GA_KBD_SEARCH_PATH;
	   GTI_destination = @IRCAppendDialog ;
	   GTI_actionMsg = MSG_GEN_INTERACTION_INITIATE; 
	   HINT_TRIGGER_BRINGS_UP_WINDOW;
    } 
    @object GenTriggerClass IRCLogStop = {
	   GI_visMoniker = 'p', "Stop";
	   GI_states = @default & (~GS_ENABLED) ;
	   GI_attrs = @default | GA_KBD_SEARCH_PATH;
	   GTI_destination = process;
	   GTI_actionMsg = MSG_IRC_STOP_LOGGING;
    }  

    /*---------------------------------------------------------------------*/

    @object GenInteractionClass IRCSettings = {
	   GI_visMoniker = 'O', "Options";
	   GI_comp =
		  @IRCFontControlGroup,
		  @IRCPointControlGroup;
	   GI_attrs = @default | GA_KBD_SEARCH_PATH
					   | GA_TARGETABLE  ;
	   GII_visibility = GIV_POPUP;
	   HINT_SEEK_MENU_BAR;
    }
    /*---------------------------------------------------------------------*/
    @object GenInteractionClass IRCFontControlGroup = {
        GI_comp = @IRCFontControl ;
        GI_visMoniker = 'F', "Set Font";
        GII_visibility = GIV_POPUP;
        GI_attrs = @default | GA_KBD_SEARCH_PATH;
    }
    /*---------------------------------------------------------------------*/
    @object FontControlClass IRCFontControl = {
	   GII_visibility = GIV_SUB_GROUP;
	//   GCI_output = @IRCText;
	   GCI_output = (TO_APP_TARGET);
    }
    /*---------------------------------------------------------------------*/
    @object GenInteractionClass IRCPointControlGroup = {
        GI_comp = @IRCPointControl ;
        GI_visMoniker = 'S', "Set Point Size";
        GII_visibility = GIV_POPUP;
        GI_attrs = @default | GA_KBD_SEARCH_PATH;
    }
    /*---------------------------------------------------------------------*/
    @object PointSizeControlClass IRCPointControl = {
        GII_visibility = GIV_SUB_GROUP;
	//   GCI_output = @IRCText;
	   GCI_output = (TO_APP_TARGET);
	   ATTR_GEN_CONTROL_REQUIRE_UI = (PSCF_10 | PSCF_12 | PSCF_14 | PSCF_18 |
							    PSCF_24);
	   ATTR_GEN_CONTROL_PROHIBIT_UI = (PSCF_36 | PSCF_54 |
							  PSCF_72 | PSCF_SMALLER | PSCF_LARGER |
							  PSCF_CUSTOM_SIZE);
    }

    /*---------------------------------------------------------------------*/
    @object GenInteractionClass WindowMenu = {
	   GI_comp = @IRCDisplayControl;
	   GI_attrs = @default | GA_KBD_SEARCH_PATH
					   | GA_TARGETABLE  ;
	   GII_visibility = GIV_POPUP;
	   ATTR_GEN_INTERACTION_GROUP_TYPE = { GIGT_WINDOW_MENU };
    }

    @object GenDisplayControlClass IRCDisplayControl = {
	   GI_attrs = @default | GA_KBD_SEARCH_PATH
					   | GA_TARGETABLE  ;
    }

    /*---------------------------------------------------------------------*/
    @object GenDisplayGroupClass IRCDisplayGroup = {
	   GI_attrs = @default | GA_KBD_SEARCH_PATH
					   | GA_TARGETABLE  ;
	   HINT_DEFAULT_FOCUS;
	   HINT_DEFAULT_TARGET;
	   HINT_DISPLAY_GROUP_FULL_SIZED_ON_STARTUP;
   //	   HINT_DISPLAY_GROUP_OVERLAPPING_ON_STARTUP;
	   HINT_DISPLAY_GROUP_TILE_VERTICALLY;
    }

@chunk char PrivateHelp[] = "Enter a private chat room with the selected individual" ;
@chunk char PrivateDisabledHelp[] = "No individual selected for a private chat" ;
@chunk char IgnoreHelp[] = "Ignore messages from the selected individual" ;
@chunk char IgnoreDisabledHelp[] = "No individual selected to ignore" ;
@chunk char IncludeHelp[] = "Include messages from the previously ignored individual" ;
@chunk char IncludeDisabledHelp[] = "No individual selected to include" ;
@chunk char SendHelp[] = "Send a message to the room" ;
@chunk char SendDisabledHelp[] = "No message to send" ;
@chunk char IRCIgnoreButtonMoniker = "Ignore";
@chunk char IRCIncludeButtonMoniker = "Include";


@end Interface;
    /*---------------------------------------------------------------------*/
@start ChanDisplayResource ;

    @object ChannelGenDisplayClass ChannelOne = {
	   GI_comp = @IRCLeft, @IRCRight/*, @IRCSettings*/;
	   GI_states = @default & ~GS_USABLE;
	   GDI_attributes = @default & ~GDA_USER_DISMISSABLE;
	   HINT_ORIENT_CHILDREN_HORIZONTALLY;
	   HINT_DEFAULT_TARGET ;
	/*   HINT_SIZE_WINDOW_AS_RATIO_OF_PARENT = {
						SWSS_RATIO | PCT_80,
						SWSS_RATIO | PCT_70}; */
	   HINT_DISPLAY_MAXIMIZED_ON_STARTUP;
}
                                                                          
    /*---------------------------------------------------------------------*/
    @object GenInteractionClass IRCLeft = {
	   GI_attrs = @default | GA_KBD_SEARCH_PATH ;
	   GI_comp = @IRCList, @IRCLeftButtons, @IRCEntryGlyphInter, @IRCLeftFiller;
	   HINT_EXPAND_HEIGHT_TO_FIT_PARENT ;
	   HINT_CENTER_CHILDREN_HORIZONTALLY ;
    }

    @object SortDynamicListClass IRCList = {
	   GI_visMoniker = "People here:" ;
	   GI_attrs = @default | GA_KBD_SEARCH_PATH ;
	   GIGI_behaviorType = GIGBT_EXCLUSIVE_NONE;
	   GIGI_destination = @IRCList ;
	   GIGI_applyMsg = MSG_SORT_LIST_APPLY;
	   GDLI_queryMsg = MSG_SORT_LIST_QUERY ;
	   HINT_EXPAND_HEIGHT_TO_FIT_PARENT ;
	   HINT_ITEM_GROUP_SCROLLABLE;
        HINT_PLACE_MONIKER_ABOVE;
    }

    @object GenInteractionClass IRCLeftButtons = {
	   GI_attrs = @default | GA_KBD_SEARCH_PATH ;
	   GI_comp = @IRCIgnoreButton, @IRCPrivMsgButton;
	   HINT_EXPAND_WIDTH_TO_FIT_PARENT ;
	   HINT_EXPAND_HEIGHT_TO_FIT_PARENT ;
	   HINT_ORIENT_CHILDREN_HORIZONTALLY ;
	   HINT_CENTER_CHILDREN_HORIZONTALLY ;
	   HINT_CENTER_CHILDREN_VERTICALLY ;
    }

    @object GenTriggerClass IRCIgnoreButton = {
	   GI_states = @default & (~GS_ENABLED) ;
	   GTI_destination = @IRCList;
	   GTI_actionMsg = MSG_SEND_IGNORE;
	   ATTR_GEN_FOCUS_HELP = @IgnoreHelp ;
	   ATTR_GEN_FOCUS_DISABLED_HELP = @IgnoreDisabledHelp ;
    }

    @object GenTriggerClass IRCPrivMsgButton = {
	   GI_visMoniker = "Private";
	   GI_states = @default & (~GS_ENABLED) ;
	   GTI_destination = @IRCList ;
	   GTI_actionMsg = MSG_OPEN_PRIVATE_MESSAGE;
	   ATTR_GEN_FOCUS_HELP = @PrivateHelp ;
	   ATTR_GEN_FOCUS_DISABLED_HELP = @PrivateDisabledHelp ;
    }

    @object GenInteractionClass IRCEntryGlyphInter = {
	   GI_attrs = @default | GA_KBD_SEARCH_PATH
					   | GA_TARGETABLE  ;
	   GI_comp = @IRCEntryGlyph;
	   HINT_EXPAND_WIDTH_TO_FIT_PARENT ;
	   HINT_RIGHT_JUSTIFY_CHILDREN;
	   HINT_CENTER_CHILDREN_VERTICALLY;
    }

    @object GenGlyphClass IRCEntryGlyph = {
	   GI_visMoniker = "Chat:";
    }

    @object GenInteractionClass IRCLeftFiller = {
        HINT_MINIMUM_SIZE = {0, SST_PIXELS | 3, 0};
    }

    /*---------------------------------------------------------------------*/
    @object GenInteractionClass IRCRight = {
	   GI_attrs = @default | GA_KBD_SEARCH_PATH
					   | GA_TARGETABLE  ;
	   GI_comp = @IRCText, @IRCEntryLine;
	   HINT_DEFAULT_TARGET ;
		HINT_EXPAND_WIDTH_TO_FIT_PARENT ;
	   HINT_EXPAND_HEIGHT_TO_FIT_PARENT ;
    }

    @object GenTextLimitClass IRCText = {
	   GI_attrs = @default | GA_READ_ONLY
					   | GA_KBD_SEARCH_PATH
	   				   | GA_TARGETABLE ;
	   GTXI_attrs = @default | GTA_INIT_SCROLLING | GTA_TAIL_ORIENTED;
	   HINT_EXPAND_WIDTH_TO_FIT_PARENT ;
	   HINT_EXPAND_HEIGHT_TO_FIT_PARENT ;
	   HINT_DEFAULT_TARGET ;
	   ATTR_GEN_TEXT_SELECTABLE;
	   ATTR_GEN_TEXT_PARA_ATTR = (ChunkHandle) @InitialParaAttrs ;
	   ATTR_GEN_TEXT_MULTIPLE_CHAR_ATTR_RUNS = @ParaCharAttrRuns;
	   HINT_TEXT_WHITE_WASH_COLOR;
    }

@CHAR_ATTR_ELEMENT_ARRAY_HEADER ParaCharAttrElements = {
    CHAR_ATTR_FONT_SIZE( FID_DTC_URW_SANS, 14 )
};
@RUN_ARRAY_HEADER(ParaCharAttrElements) ParaCharAttrRuns = {
  /*  TRAE_ALL( 0 ), */
    TRAE_ABS(0, 0),
    TRAE_END
};


    /* this sets the left margin to 3 pix and a hanging
	* outdent of 1 pix to have para starts just shy of the left edge.*/

    @chunk VisTextParaAttr InitialParaAttrs =
	    PARA_ATTR_STYLE_JUST_LEFT_RIGHT_PARA(
		    0, 0, J_LEFT, 3, 0, 1);

    /*---------------------------------------------------------------------*/
    @object GenInteractionClass IRCEntryLine = {
	   GI_attrs = @default | GA_KBD_SEARCH_PATH
					   | GA_TARGETABLE  ;
	   GI_comp = @IRCEntry, @IRCSend ;
	   HINT_EXPAND_WIDTH_TO_FIT_PARENT ;
	   HINT_ORIENT_CHILDREN_HORIZONTALLY ;
	   HINT_CENTER_CHILDREN_VERTICALLY ;
	   HINT_MINIMIZE_CHILD_SPACING;
		HINT_DEFAULT_TARGET ;
    }

    @object ChatTextClass IRCEntry = {
	   GI_attrs = @default | GA_KBD_SEARCH_PATH
					   | GA_TARGETABLE  ;
	   GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
	   GTXI_destination = @IRCEntry ;
	   GTXI_applyMsg = MSG_IRC_CHAT_SEND_LINE ;
	   GTXI_maxLength = 400 ;
	   ATTR_GEN_TEXT_SET_OBJECT_ENABLED_WHEN_TEXT_EXISTS =
		    @IRCSend;
	   HINT_EXPAND_WIDTH_TO_FIT_PARENT ;
	   HINT_TEXT_WHITE_WASH_COLOR;
		HINT_DEFAULT_TARGET ;
		HINT_DEFAULT_FOCUS ;
	 }

    @object GenTriggerClass IRCSend = {
	   GI_visMoniker = "Send";
	   GI_states = @default &~GS_ENABLED;  /* enabled by text */
	   GTI_destination = process ;
	   GTI_actionMsg = MSG_IRC_SEND_LINE;
	   /* I get and extra send on dblclick of trigger so let's try
	    * to absorb it with a dummy method.  It seems to work :-) */
	   ATTR_GEN_TRIGGER_CUSTOM_DOUBLE_PRESS = MSG_IRC_ABSORB_DBLCLICK;
	   ATTR_GEN_FOCUS_HELP = @SendHelp ;
	   ATTR_GEN_FOCUS_DISABLED_HELP = @SendDisabledHelp ;
    }

@end ChanDisplayResource ;
/*---------------------------------------------------------------------*/

@start Interface ;

/*    @object GenInteractionClass IRCTipDialog = {
	   GI_visMoniker = "Chat Tip" ;
	   GI_comp =  @IRCTipText, @IRCTipShowAgainGroup, @IRCTipOK;
	   GI_attrs = @default | GA_KBD_SEARCH_PATH;
	   GII_attrs = @default | GIA_NOT_USER_INITIATABLE ;
	   GII_visibility = GIV_DIALOG ;
	   HINT_CENTER_CHILDREN_HORIZONTALLY;
    }

    @object GenTextClass IRCTipText = {
	   GI_attrs = @default | GA_READ_ONLY ;
	   GTXI_text = "To participate in chat room conversations, enter your message in the \"Chat:\" text field at the bottom of the window. Then, click \"Send\".";
    }

    @object GenBooleanGroupClass IRCTipShowAgainGroup = {
	   GI_comp = @IRCTipShowItem;
    }

    @object GenBooleanClass IRCTipShowItem = {
	   GI_visMoniker = "Do not show this dialog box again.";
	   GBI_identifier = 1;
    }

    @object GenTriggerClass IRCTipOK = {
	   GI_visMoniker = "OK";
	   GTI_destination = process;
	   GTI_actionMsg = MSG_IRC_CLOSE_TIP;
	   HINT_SEEK_REPLY_BAR;
	   HINT_DEFAULT_DEFAULT_ACTION;
    }
*/
/*---------------------------------------------------------------------*/
    @object GenInteractionClass IRCDialogs = {
	   GI_attrs = @default | GA_KBD_SEARCH_PATH ;
	   /* CAUTION!!!!!! if you add/delete children, change PRIV_DBOX_START
	    * global accordingly!!!!!
	    */
	   GI_comp = @IRCStartDialog,
				@IRCAppendDialog, @IRCChangeNickname;
	   GII_attrs = @default | GIA_NOT_USER_INITIATABLE ;
	   GII_visibility = GIV_DIALOG ;
	   ATTR_GEN_INIT_FILE_PROPAGATE_TO_CHILDREN;
	   HINT_STAGGER_WINDOW;
    }

    /*---------------------------------------------------------------------*/
    @object GenInteractionClass IRCStartDialog = {
	   GI_visMoniker = "Start Logging" ;
	   GI_comp = @IRCStartNameStuff, @IRCStartLogSel,
			   @IRCStartOK, @IRCStartCanx;
	   GI_attrs = @default | GA_KBD_SEARCH_PATH ;
	   GII_visibility = GIV_DIALOG ;
    }

    @object GenFileSelectorClass IRCStartLogSel = {
	   GFSI_attrs = @default | FSA_SHOW_FILES_DISABLED
						| FSA_USE_VIRTUAL_ROOT;
	   GFSI_fileCriteria = @default | FSFC_MASK_CASE_INSENSITIVE;
	   ATTR_GEN_FILE_SELECTOR_NAME_MASK = {"*.TXT"};
	   ATTR_GEN_FILE_SELECTOR_VIRTUAL_ROOT = {SP_DOCUMENT, "."};
	   ATTR_GEN_PATH_DATA = {SP_DOCUMENT, "."};
	   HINT_FILE_SELECTOR_NUMBER_OF_FILES_TO_SHOW = 8;
	   HINT_FILE_SELECTOR_FILE_LIST_WIDTH = 40;
    }

    @object GenInteractionClass IRCStartNameStuff = {
	   GI_comp = @LogNameWarn, @LogFileName;
	   HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    }

    @object GenTextClass LogFileName = {
	   GI_visMoniker = "Log Name:";
	   @localize "label for the log file name entry window";
	   GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
	/* these 2 lines so name can be long dos name in GPC */
	   GTXI_maxLength = (FILE_LONGNAME_LENGTH - 4); /* so that we can add .txt */
	   ATTR_GEN_TEXT_LEGAL_FILENAMES;
	   ATTR_GEN_TEXT_SET_OBJECT_ENABLED_WHEN_TEXT_EXISTS =
		    @IRCStartOK;
	   HINT_FIXED_SIZE = {
			 SST_AVG_CHAR_WIDTHS | 28, 0, 0};
	   HINT_TEXT_WHITE_WASH_COLOR;
	   HINT_DEFAULT_FOCUS;
    }

    @object GenGlyphClass LogNameWarn = {
	   GI_visMoniker = "Save a record of the conversation in this chat room:";
	   @localize "reminder that log file name must be a DOS name";
    }

    @object GenTriggerClass IRCStartOK = {
	   GI_visMoniker = "Start Logging";
	   @localize "Moniker of start button in start logging file dialog box";
	   GI_states = @default &~GS_ENABLED;  /* enabled by text */
	   GTI_destination = process;
	   GTI_actionMsg = MSG_IRC_START_LOGGING;
	   HINT_SEEK_REPLY_BAR;
	   HINT_DEFAULT_DEFAULT_ACTION;
    }

    @object GenTriggerClass IRCStartCanx = {
	   GI_visMoniker = "Cancel";
	   @localize "Moniker of Cancel button in start logging dialog box";
	   GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
	   HINT_SEEK_REPLY_BAR;
    }

    @chunk char FileExistsErrMsg[] =
	   "There is already a file with that name.  " \
	   "Please enter a different name." ;
    @chunk char LogCreateErrMsg[] =
	   "There was a problem creating the log file." ;

/*---------------------------------------------------------------------*/
    @object GenInteractionClass IRCAppendDialog = {
	   GI_visMoniker = "Append Log" ;
	   GI_comp = @IRCAppendLogSel,
			   @IRCAppendOK, @IRCAppendCanx;
	   GI_attrs = @default | GA_KBD_SEARCH_PATH ;
	   GII_visibility = GIV_DIALOG ;
    }

    @object MyGenFileSelectorClass IRCAppendLogSel = {
	GI_visMoniker = "Select a chat log file to which to add the new chat log.";
	   GFSI_destination = process;
	   GFSI_notificationMsg = MSG_APPEND_LOG_SELECTOR_ACTION;
	   GFSI_fileCriteria = FSFC_DIRS | FSFC_NON_GEOS_FILES
					    | FSFC_MASK_CASE_INSENSITIVE;
	   GFSI_attrs = @default | FSA_USE_VIRTUAL_ROOT;
	   ATTR_GEN_FILE_SELECTOR_NAME_MASK = {"*.TXT"};
	   ATTR_GEN_FILE_SELECTOR_VIRTUAL_ROOT = {SP_DOCUMENT, "."};
	   ATTR_GEN_PATH_DATA = {SP_DOCUMENT, "."};
	   HINT_FILE_SELECTOR_NUMBER_OF_FILES_TO_SHOW = 8;
	   HINT_FILE_SELECTOR_FILE_LIST_WIDTH = 40;
	   HINT_PLACE_MONIKER_ABOVE;
    }

    @object GenTriggerClass IRCAppendOK = {
	   GI_visMoniker = "Append Log";
	   GI_states = @default &~GS_ENABLED;
	   @localize "Moniker of start button in append log file dialog box";
	   GTI_destination = process;
	   GTI_actionMsg = MSG_IRC_APPEND_LOGGING;
	   HINT_SEEK_REPLY_BAR;
	   HINT_DEFAULT_DEFAULT_ACTION;
    }

    @object GenTriggerClass IRCAppendCanx = {
	   GI_visMoniker = "Cancel";
	   @localize "Moniker of Cancel button in append log dialog box";
	   GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
	   HINT_SEEK_REPLY_BAR;
    }

    @chunk char AppendOpenErrMsg[] =
	   "There was a problem opening the selected log file." ;

    /*---------------------------------------------------------------------*/
    @object GenInteractionClass IRCConnectDialog = {
	   GII_attrs = @default |
				GIA_MODAL ;
        GII_type = GIT_COMMAND ;
        GII_visibility = GIV_DIALOG ;
	GI_visMoniker = 'C', "Connect to site" ;
	ATTR_GEN_INIT_FILE_PROPAGATE_TO_CHILDREN;
        GI_comp =
		  @ConnectSiteName,
		  @ConnectSitePort,
		  @ConnectSiteNick,
		  @ConnectSiteChannel,
		  @ConnectOK;
    }
    /*---------------------------------------------------------------------*/
    @object GenTextClass ConnectSiteName = {
	   GI_visMoniker = "Site:" ;
		GTXI_text = "irc.breadbox.com" ;
        HINT_EXPAND_WIDTH_TO_FIT_PARENT ;
	   ATTR_GEN_INIT_FILE_KEY = "site" ;
	   HINT_TEXT_SELECT_TEXT ;
	   GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT ;
    }
    /*---------------------------------------------------------------------*/
    @object GenTextClass ConnectSitePort = {
	   GI_visMoniker = "Port:" ;
        GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT ;
        GTXI_text = "6667" ;
        HINT_EXPAND_WIDTH_TO_FIT_PARENT ;
        ATTR_GEN_INIT_FILE_KEY = "port" ;
        HINT_TEXT_SELECT_TEXT ;
	   GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT ;
    }
    /*---------------------------------------------------------------------*/
    @object NickGenTextClass ConnectSiteNick = {
	   GI_visMoniker = "Nickname:" ;
	   GTXI_text = "MyNick" ;
        GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT ;
//	   GTXI_maxLength = 9 ;
	   GTXI_maxLength = GPC_NICK_LEN ;
	 /*  ATTR_GEN_TEXT_NO_SPACES;
	   ATTR_GEN_TEXT_ALPHA_NUMERIC; */
	   ATTR_GEN_TEXT_CUSTOM_FILTER = (ChunkHandle) @NickFilter;
	   ATTR_GEN_INIT_FILE_KEY = "nick" ;
	   HINT_EXPAND_WIDTH_TO_FIT_PARENT ;
	   HINT_TEXT_SELECT_TEXT ;
    }
    /*---------------------------------------------------------------------*/
    @object GenTextClass ConnectSiteChannel = {
	   GI_visMoniker = "Channel:" ;
	   GTXI_text = "\043TheBreadboxChannel" ;
	   GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT ;
	   GTXI_maxLength = 70 ;
	   HINT_EXPAND_WIDTH_TO_FIT_PARENT ;
	   ATTR_GEN_INIT_FILE_KEY = "channel" ;
	   HINT_TEXT_SELECT_TEXT ;
	   GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT ;
    }
    /*---------------------------------------------------------------------*/
    @object GenTriggerClass ConnectOK = {
	   GI_visMoniker = 'C', "Connect" ;
	   GTI_destination = process;
	   GTI_actionMsg = MSG_IRC_START_CONNECT;
	   HINT_SEEK_REPLY_BAR ;
	HINT_DEFAULT_DEFAULT_ACTION ;
    }
/*---------------------------------------------------------------------*/
	 @object GenInteractionClass IRCChangeNickname = {
        GI_attrs = @default | GA_KBD_SEARCH_PATH ;
	   GI_visMoniker = "Change Nickname" ;
        GII_type = GIT_COMMAND ;
        GII_visibility = GIV_DIALOG ;
	   GII_attrs = @default | GIA_MODAL ;
		GI_comp = @IRCNewNickname, @IRCNicknameOk ;
    }
    /*---------------------------------------------------------------------*/
    @object NickGenTextClass IRCNewNickname = {
        GI_visMoniker = 'N', "Nickname" ;
//	   GTXI_maxLength = 9 ;
	   GTXI_maxLength = GPC_NICK_LEN ;
	   GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT | GTA_ALLOW_TEXT_OFF_END;
	   ATTR_GEN_TEXT_SET_OBJECT_ENABLED_WHEN_TEXT_EXISTS = @IRCNicknameOk;
	 /*  ATTR_GEN_TEXT_NO_SPACES;
	   ATTR_GEN_TEXT_ALPHA_NUMERIC; */
	   ATTR_GEN_TEXT_CUSTOM_FILTER = (ChunkHandle) @NickFilter;
	   HINT_EXPAND_WIDTH_TO_FIT_PARENT ;
	   HINT_DEFAULT_TARGET ;
        HINT_DEFAULT_FOCUS ;
    }
    /*---------------------------------------------------------------------*/
    /* our server allows only 0-9, a-z, A-Z, and - */
    /* I would really like 5 prohibited zones but if I use five
	* the app crashes with a range end before start error...
	* so I'll trap the . and / with MSG...CHAR */
    @chunkArray VisTextCustomFilterData NickFilter = {
		 {C_NULL, C_COMMA},
	  /*  {C_PERIOD, C_SLASH}, */
	    {C_COLON, C_AT_SIGN},
	    {C_LEFT_BRACKET, C_BACKQUOTE},
	    {C_LEFT_BRACE, C_CARON}
	    };
    /*---------------------------------------------------------------------*/
    @object GenTriggerClass IRCNicknameOk = {
	   GI_states = @default & (~GS_ENABLED) ; /* enabled by text */
        GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE ;
        ATTR_GEN_TRIGGER_INTERACTION_COMMAND = IC_OK ;
        GTI_destination = process ;
        GTI_actionMsg = MSG_IRC_CHANGE_NICKNAME ;
        HINT_SEEK_REPLY_BAR ;
        HINT_DEFAULT_DEFAULT_ACTION ;
    }
    /*---------------------------------------------------------------------*/
    @chunk char MessageNicknameInUse[] =
        "The nickname you entered is already being used.  " \
        "Please enter a different name in the next dialog." ;


@end Interface

    /*---------------------------------------------------------------------*/
@start PrivDboxResource ;
    @object GenInteractionClass IRCPrivDialog = {
        GI_states = @default & ~GS_USABLE;
	   GI_visMoniker = "Private Message" ;
	   GI_comp = @IRCPrivEdit, @IRCPrivEditSepLine, @IRCPrivWindow,
			   @IRCPrivEntry, @IRCPrivOK, @IRCPrivClose;
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
	   GII_visibility = GIV_DIALOG ;
	   HINT_SIZE_WINDOW_AS_RATIO_OF_FIELD = {
						SWSS_RATIO | PCT_40,
						SWSS_RATIO | PCT_50};
	   HINT_INTERACTION_MAKE_RESIZABLE;
	   HINT_WINDOW_NO_SYS_MENU;
	   HINT_STAGGER_WINDOW;
    }

@object GenEditControlClass IRCPrivEdit = {
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
    GII_visibility = GIV_POPUP;
    ATTR_GEN_INTERACTION_GROUP_TYPE = GIGT_EDIT_MENU;
    ATTR_GEN_CONTROL_PROHIBIT_UI = GECF_UNDO
						   | GECF_SELECT_ALL
						   | GECF_CLIP_ART;
    HINT_AVOID_MENU_BAR;
    HINT_INFREQUENTLY_USED;
}

@object GenGlyphClass IRCPrivEditSepLine = {
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_GLYPH_SEPARATOR;
}

    @object GenTextLimitClass IRCPrivWindow = {
    GI_attrs = @default | GA_READ_ONLY
				    | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
	   GTXI_attrs = @default | GTA_INIT_SCROLLING | GTA_TAIL_ORIENTED;
	   ATTR_GEN_TEXT_SELECTABLE;
	   HINT_DEFAULT_TARGET ;
	   HINT_TEXT_WHITE_WASH_COLOR;
	   HINT_EXPAND_WIDTH_TO_FIT_PARENT ;
	   HINT_EXPAND_HEIGHT_TO_FIT_PARENT ;
	   HINT_INITIAL_SIZE = {
			 0, SST_LINES_OF_TEXT | 15, 0};
	   HINT_MINIMUM_SIZE = {
			 0, SST_LINES_OF_TEXT | 3, 0};
    }

    @object PrivTextClass IRCPrivEntry = {
    GI_attrs = @default | GA_TARGETABLE
				    | GA_KBD_SEARCH_PATH;
	   GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
	   GTXI_maxLength = 400 ;
	   ATTR_GEN_TEXT_SET_OBJECT_ENABLED_WHEN_TEXT_EXISTS =
		    @IRCPrivOK;
	   HINT_EXPAND_WIDTH_TO_FIT_PARENT ;
	   HINT_TEXT_WHITE_WASH_COLOR;
		HINT_DEFAULT_TARGET ;
		HINT_DEFAULT_FOCUS ;
    }

    @object GenTriggerClass IRCPrivOK = {
	   GI_visMoniker = "Send";
	   GI_states = @default &~GS_ENABLED;  /* enabled by text */
	   GTI_destination = process;/*IRCList ; */
	   GTI_actionMsg = MSG_IRC_SEND_PRIV; 
	   HINT_SEEK_REPLY_BAR;
	   HINT_DEFAULT_DEFAULT_ACTION;
    }

    @object GenTriggerClass IRCPrivClose = {
	   GI_visMoniker = "Close";
	   GTI_destination = process;/*@IRCList;*/
	   GTI_actionMsg = MSG_IRC_CLOSE_PRIV;
	   HINT_SEEK_REPLY_BAR;
    }

@end PrivDboxResource



