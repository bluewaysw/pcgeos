/*
 *
 * Project:	PC GEOS
 * Module:	desktop
 * File:	CUI/cuiNDWastebasketTemplate.ui
 *
 * Author:	Dave Litwin
 *
 *	This File contains the user Interface description for the Wastebasket
 *	folder/object
 *
 *	$Id: cuiNDWastebasketTemplate.ui,v 1.4 98/06/03 13:16:51 joon Exp $
 *
 */


start NDWastebasketObjectTemplate;
/* do not change object offset */
NDWastebasketObject = NDWastebasket {
	shellObjectAttrs = default -movable, -copyable, -deletable;
	ndObjType = "WOT_WASTEBASKET";
	popUpType = WPUT_SELECTION;
	hints = {
		ATTR_FOLDER_HELP_CONTEXT { char "oWB", 0 }
	}
}
end NDWastebasketObjectTemplate;

start NDWastebasketDummyResource;
NDWastebasketDummy = NDWastebasket {
	shellObjectAttrs = default -movable, -copyable, -deletable;
	ndObjType = "WOT_WASTEBASKET";
	popUpType = WPUT_SELECTION;
	hints = {
		ATTR_FOLDER_HELP_CONTEXT { char "oWB", 0 }
	}
}
end NDWastebasketDummyResource;

start NDWastebasketWindowTemplate;
/* do not change object offset */
NDWastebasketWindow = NEWDESK_PRIMARY {
	ENTRY_LEVEL_STATE(ELS_WASTEBASKET)
#if _NEWDESKBA
	children = NDWastebasketView,
		   NDWastebasketInfo,
		   NDWastebasketMenu,
		   NDWastebasketButtonGroup;
#else
#if GPC_NAMES_AND_DETAILS_TITLES
	children = NDWastebasketViewGroup, NDWastebasketInfo
#else
	children = NDWastebasketView, NDWastebasketInfo
#endif
			, NDWastebasketMenu, NDWastebasketMenuView;
#endif
	genStates = default -usable;
	hints = {
#if _NEWDESKBA
		ENTRY_LEVEL_HINTS,
		HINT_EL_EXCLUSIVE,
		ATTR_EL_HELP_CONTEXT { char "aWB", 0 },
#endif
		ATTR_GEN_HELP_CONTEXT {
			char "oWB", 0
		},
		HINT_SIZE_WINDOW_AS_DESIRED,
		HINT_PRIMARY_NO_FILE_MENU,
		HINT_PRIMARY_NO_EXPRESS_MENU,
                ATTR_GEN_WINDOW_CUSTOM_LAYER_ID {
			word 0
		}
	}
}


/* do not change object offset */
NDWastebasketView = DesktopView {
	output = null;		/* set when duplicated */
	viewAttributes = focusable, dragScrolling;
	vertAttributes = scrollable;
	horizAttributes = scrollable;
	docRight = FOLDER_PANE_HORIZ_DOC;
	docBottom = FOLDER_PANE_VERT_DOC;
	genAttributes = default +initiatesBusyState, +targetable;
	backGrColor = WASTEBASKET_BACKGROUND;
	hints = {
		HINT_DEFAULT_TARGET,
		HINT_DEFAULT_FOCUS,
		HINT_VIEW_REMOVE_SCROLLERS_WHEN_NOT_SCROLLABLE,
		HINT_INITIAL_SIZE {
			word	FOLDER_PANE_HORIZ_OPEN
			word	FOLDER_PANE_VERT_OPEN
		},
		HINT_MINIMUM_SIZE {
			word	0
			word	FOLDER_PANE_VERT_MIN
		}
	}
}

/* do not change object offset */
NDWastebasketInfo = GenText {
	genAttributes = readOnly;
	textDisplayAttributes = DESK_SINGLE_LINE_DISPLAY_ATTRIBUTES,
					allowTextOffEnd, singleLineText;
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_TEXT_FRAME
	}
}

/* do not change object offset */
NDWastebasketMenu = NDSortViewPopupMenu {
	moniker = 'F', "File";
	sortViewParent = NDWastebasketMenuOther;
	sortViewFlags = "mask NDSVPMF_NOT_FOR_MENU";
	visibility = popup;
	children = NDWastebasketMenuOpen,
		   NDWastebasketMenuGetInfo,
		   NDWastebasketMenuOther,
		   NDWastebasketMenuRecover,
		   NDWastebasketMenuEmptyWastebasket,
		   NDWastebasketMenuClose;
	hints = {
		HINT_INFREQUENTLY_USED			/* not pinnable */
	}
}

#if GPC_NAMES_AND_DETAILS_TITLES
/*---- names and details column titles ---*/
/* do not change object offset */
NDWastebasketViewGroup = GenInteraction {
	children = NDWastebasketTitles, NDWastebasketView;
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_CUSTOM_CHILD_SPACING {
			word 0
		}
	}
}

/* do not change object offset */
NDWastebasketTitles = GenInteraction {
	children = NDWastebasketTitleName, NDWastebasketTitleSize,
			NDWastebasketTitleDate
#if GPC_NO_NAMES_AND_DETAILS_ATTRS
	    		;
#else
			, NDWastebasketTitleAttr;
#endif
	genStates = default -usable;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_CUSTOM_CHILD_SPACING {
			word 0
		},
		HINT_TOOLBOX
	}
}
/* do not change object offset */
NDWastebasketTitleName = GenTrigger {
	moniker = "Name";
	action = MSG_SET_SORT_MODE, process;
	hints = {
		HINT_FIXED_SIZE {
			word	0
			word	0
		},
		ATTR_GEN_TRIGGER_ACTION_DATA {
			word	mask FIDS_NAME
		}
	}
}
/* do not change object offset */
NDWastebasketTitleSize = GenTrigger {
	moniker = "Size";
	action = MSG_SET_SORT_MODE, process;
	hints = {
		HINT_FIXED_SIZE {
			word	0
			word	0
		},
		ATTR_GEN_TRIGGER_ACTION_DATA {
			word	mask FIDS_SIZE
		}
	}
}
/* do not change object offset */
NDWastebasketTitleDate = GenTrigger {
	moniker = "Modified";
	action = MSG_SET_SORT_MODE, process;
	hints = {
		HINT_FIXED_SIZE {
			word	0
			word	0
		},
		ATTR_GEN_TRIGGER_ACTION_DATA {
			word	mask FIDS_DATE
		}
	}
}
/* do not change object offset */
NDWastebasketTitleAttr = GenTrigger {
	moniker = "Attr";
	hints = {
		HINT_FIXED_SIZE {
			word	0
			word	0
		}
	}
}
#endif

NDWastebasketMenuView = NDSortViewPopupMenu {
	moniker = 'V', "View";
	sortViewParent = NDWastebasketMenuView;
	sortViewPosition = 0;
	sortViewFlags = "mask NDSVPMF_VIEW_NOT_SUBMENU";
	visibility = popup;
	hints = {
		HINT_INFREQUENTLY_USED			/* not pinnable */
	}
}

NDWastebasketMenuOpen = GenTrigger {
	moniker = 'O', "Open";
	action = MSG_ND_FOLDER_SEND_AS_SELECTED_ITEMS, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		ATTR_GEN_TRIGGER_ACTION_DATA {
			word	MSG_OPEN_SELECT_LIST
			word	0
			word	ERROR_NO_SELECTION
		}
	}
}

NDWastebasketMenuGetInfo = GenTrigger {
	moniker = 'I', "File Info";
	action = MSG_ND_FOLDER_SEND_AS_SELECTED_ITEMS, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		HINT_TRIGGER_BRINGS_UP_WINDOW,
		ATTR_GEN_TRIGGER_ACTION_DATA {
			word	MSG_FM_GET_INFO
			word	0
			word	ERROR_NO_SELECTION
		}
	}
}

/*---- NDWastebasketMenuEmptyWastebasket ---*/
NDWastebasketMenuEmptyWastebasket = GenTrigger {
	moniker = 'E', "Empty Wastebasket";
	action = MSG_EMPTY_WASTEBASKET, process;
}

/*---- NDWastebasketMenuRecover ---*/
NDWastebasketMenuRecover = GenTrigger {
	moniker = 'R', "Recover Item";
	action = MSG_ND_FOLDER_SEND_AS_SELECTED_ITEMS, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		ATTR_GEN_TRIGGER_ACTION_DATA {
			word	MSG_FM_START_RECOVER
			word	0
			word	ERROR_NO_SELECTION
		}
	}
}

/*---- NDWastebasketMenuOther ---*/
NDWastebasketMenuOther = GenInteraction {
	children =	NDWastebasketMenuSelectAll;
}

NDWastebasketMenuSelectAll = GenTrigger {
	moniker = 'S', "Select All";
	action = MSG_ND_FOLDER_SEND_AS_WHITESPACE, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		ATTR_GEN_TRIGGER_ACTION_DATA {
			word	MSG_SELECT_ALL
		}
	}
}


/*---- NDWastebasketMenuClose ---*/
NDWastebasketMenuClose = GenTrigger {
	moniker = 'C', "Close";
	action = MSG_ND_FOLDER_SEND_AS_WHITESPACE, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		ATTR_GEN_TRIGGER_ACTION_DATA {
			word	MSG_FOLDER_CLOSE
		}
	}
}


#if _NEWDESKBA
/* Assistance Buttons for NDWasteBasket */
NDWastebasketButtonGroup = GenInteraction {
	children = NDWastebasketEmptyWastebasketButton,
		   NDWastebasketRecoverItemButton,
		   NDWastebasketCloseButton,
		   NDWastebasketHelpButton;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY,
		HINT_INCLUDE_ENDS_IN_CHILD_SPACING
	}
}

NDWastebasketEmptyWastebasketButton = GenTrigger {
	moniker = 'E', "Empty Wastebasket";
	action = MSG_EMPTY_WASTEBASKET, process;
}

NDWastebasketRecoverItemButton = GenTrigger {
	moniker = 'R', "Recover Item";
	action = MSG_ND_FOLDER_SEND_AS_SELECTED_ITEMS, "TO_OBJ_BLOCK_OUTPUT";
	genAttributes = default +initiatesInputHoldUp;
	hints = {
		ATTR_GEN_TRIGGER_ACTION_DATA {
			word	MSG_FM_START_RECOVER
			word	0
			word	ERROR_RECOVER_NO_SELECTION
		},
		HINT_TRIGGER_BRINGS_UP_WINDOW

	}
}

NDWastebasketCloseButton = GenTrigger {
	moniker = 'C', "Close";
	action = MSG_ND_FOLDER_SEND_AS_WHITESPACE, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		ATTR_GEN_TRIGGER_ACTION_DATA {
			word	MSG_FOLDER_CLOSE
		}
	}
}

NDWastebasketHelpButton = GenTrigger {
	action = MSG_META_BRING_UP_HELP, "TO_SELF";
	hints = {
		ATTR_GEN_DEFAULT_MONIKER {
			word GDMT_HELP;
		},
		ATTR_GEN_HELP_CONTEXT {
			char "oWB", 0
		}
	}
}
#endif		/* #if _NEWDESKBA */

/* only used for get nice pathname string for Window title */
NDWasteFolderPath = PathnameStorage {
	genAttributes = readOnly;
	textDisplayAttributes = DESK_SINGLE_LINE_DISPLAY_ATTRIBUTES,
					allowTextOffEnd, singleLineText;
}

end NDWastebasketWindowTemplate;
