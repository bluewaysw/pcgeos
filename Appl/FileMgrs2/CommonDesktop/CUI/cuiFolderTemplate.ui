/*
 *
 * Project:	PC GEOS
 * Module:	desktop
 * File:	UI/uiFolderTemplate.ui
 *
 * Author:	Brian Chin
 *
 *	This File contains the user Interface description for GeoManager's
 * folder window and folder object
 *
 *	$Id: cuiFolderTemplate.ui,v 1.1 97/04/04 15:01:44 newdeal Exp $
 *
 */


/*-----------------------------------------------------------------------------
				FolderWindow
-----------------------------------------------------------------------------*/

start FolderWindowTemplate;

/* do not change object offset */
FolderWindow = DeskDisplay {
#if DOCMGR
	children = NDFolderViewGroup;
#else
	children = FolderInfoGroup, FolderView;
#endif
#if _FCAB	/* no user-closing in GeoLauncher */
	attributes = default -userDismissable;
#endif		/* if _FCAB */
	genStates = default -usable;
	hints = {
		ATTR_GEN_WINDOW_KBD_OVERRIDE {KeyboardOverride KO_NO_KEYBOARD},
		ATTR_GEN_DISPLAY_NOT_MINIMIZABLE
	}
#if UI_CLOSE_IN_OVERLAP
	openState = "TRUE";	/* allow closing initial Folder Window */
#endif		/* if UI_CLOSE_IN_OVERLAP */
}

/* do not change object offset */
FolderView = DesktopView {
	output = null;		/* set when duplicated */
	viewAttributes = focusable,
				dragScrolling;
	vertAttributes = scrollable;
	horizAttributes = scrollable;
	docRight = FOLDER_PANE_HORIZ_DOC;
	docBottom = FOLDER_PANE_VERT_DOC;
	genAttributes = default +initiatesBusyState, +targetable;
	hints = {
		HINT_DEFAULT_TARGET,
		HINT_DEFAULT_FOCUS,
		HINT_INITIAL_SIZE {
			word	FOLDER_PANE_HORIZ_OPEN
			word	FOLDER_PANE_VERT_OPEN
		},
		HINT_MINIMUM_SIZE {
			word	0
			word	FOLDER_PANE_VERT_MIN
		}
	}
}

#ifdef DOCMGR

/*
 * Names and Details
 */

/* do not change object offset */
Dummy1 = Gen {
}

/* do not change object offset */
Dummy2 = Gen {
}

/* do not change object offset */
NDFolderViewGroup = GenInteraction {
	children = NDFolderTitles, FolderView;
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_CUSTOM_CHILD_SPACING {
			word 0
		}
	}
}

/* do not change object offset */
NDFolderTitles = GenInteraction {
	children = NDFolderTitleName, NDFolderTitleSize,
			NDFolderTitleDate;
	genStates = default -usable;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_CUSTOM_CHILD_SPACING {
			word 0
		},
		HINT_TOOLBOX
	}
}
/* do not change object offset */
NDFolderTitleName = GenTrigger {
	moniker = "Name";
	action = MSG_SET_SORT_MODE, process;
	hints = {
		HINT_FIXED_SIZE {
			word	0
			word	0
		},
		ATTR_GEN_TRIGGER_ACTION_DATA {
			word	mask FIDS_NAME
		}
	}
}
/* do not change object offset */
NDFolderTitleSize = GenTrigger {
	moniker = "Size";
	action = MSG_SET_SORT_MODE, process;
	hints = {
		HINT_FIXED_SIZE {
			word	0
			word	0
		},
		ATTR_GEN_TRIGGER_ACTION_DATA {
			word	mask FIDS_SIZE
		}
	}
}
/* do not change object offset */
NDFolderTitleDate = GenTrigger {
	moniker = "Modified";
	action = MSG_SET_SORT_MODE, process;
	hints = {
		HINT_FIXED_SIZE {
			word	0
			word	0
		},
		ATTR_GEN_TRIGGER_ACTION_DATA {
			word	mask FIDS_DATE
		}
	}
}
/* do not change object offset */
NDFolderTitleAttr = GenTrigger {
	moniker = "Attr";
	hints = {
		HINT_FIXED_SIZE {
			word	0
			word	0
		}
	}
}
#endif  /* DOCMGR */

#if (!_ZMGR || _PMGR)

FolderInfoGroup = GenInteraction {
#if _FCAB	/* no Up-Dir button for FileCabinet */
	children = FolderInfo;
#else
	children = FolderUpButton, FolderInfo;
#endif		/* if _FCAB */
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_LEFT_JUSTIFY_CHILDREN,
		HINT_CENTER_CHILDREN_VERTICALLY,
		HINT_TOOLBOX,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT
		/*HINT_FULL_JUSTIFY_CHILDREN_VERTICALLY*/
	}
}

#else	/* if (!_ZMGR  || _PMGR) */

/* ZMGR's FolderInfoGroup */
FolderInfoGroup = GenInteraction {
	children = FolderInfoPathGroup, FolderInfo;
	hints = {
		HINT_TOOLBOX,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}
FolderInfoPathGroup = GenInteraction {
	children = FolderUpButton, FolderInfoPath;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_LEFT_JUSTIFY_CHILDREN,
		HINT_CENTER_CHILDREN_VERTICALLY,
		HINT_TOOLBOX,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}
#if _PEN_BASED
FolderInfoPath = NoQTText {
#else
FolderInfoPath = PathnameStorage
#endif
	genAttributes = readOnly;
	textDisplayAttributes = DESK_SINGLE_LINE_DISPLAY_ATTRIBUTES,
					allowTextOffEnd, singleLineText;
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_INITIAL_SIZE {
			SpecWidth <SST_PIXELS, 10>,
			SpecHeight <0, 0>
		}
	}
}

#endif	/* if !_ZMGR */


#if !_FCAB	/* no Up-Dir button for File Cabinet */
FolderUpButton = DirTool {
	toolType = DIRTOOL_UPDIR;
	moniker = list { NDFolderDirUpMoniker, NDFolderDirUpMonikerBW }
	action = MSG_FOLDER_UP_DIR, "TO_OBJ_BLOCK_OUTPUT";
	kbdAccelerator = specificUI BACKSPACE;
	hints = {
	    ATTR_GEN_EXTRA_KBD_ACCELERATORS {
		KeyboardShortcut <0, 0, 1, 0, 0, C_SMALL_U> /* ctrl-u */
	    }
	}
}
#endif		/* if !_FCAB */

#if _PEN_BASED
FolderInfo = NoQTText {
#else
FolderInfo = GenText {
#endif
	genAttributes = readOnly;
	textDisplayAttributes = DESK_SINGLE_LINE_DISPLAY_ATTRIBUTES,
					allowTextOffEnd, singleLineText;
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_INITIAL_SIZE {
			SpecWidth <SST_PIXELS, 10>,
			SpecHeight <0, 0>
		}
	}
}
end FolderWindowTemplate;


/*-----------------------------------------------------------------------------
				FolderObject
-----------------------------------------------------------------------------*/

start FolderObjectTemplate;

FolderObject = Folder {
}

end FolderObjectTemplate;



