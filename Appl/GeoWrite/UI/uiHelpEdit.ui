/***********************************************************************
 *
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * PROJECT:	  PCGEOS
 * MODULE:	  uiHelpEdit.ui
 * FILE:	  uiHelpEdit.ui
 *
 * AUTHOR:  	  Gene Anderson: Sep 16, 1992
 *
 * REVISION HISTORY:
 *	Date	  Name	    Description
 *	----	  ----	    -----------
 *	12/2023	  RainerB   Renamed from Writer to GeoWrite
 *      10/15/00  dmedeiros added batch import/export features
 *	9/16/92	  gene	    Initial version
 *
 * DESCRIPTION:
 *	UI for Help Editor functions
 *
 *
 * 	$Id: uiHelpEdit.ui,v 1.1 97/04/04 15:55:21 newdeal Exp $
 *
 ***********************************************************************/

/*-----------------------------------------------------------------------------
	Help Edit menu
-----------------------------------------------------------------------------*/

start PrimaryUI;

HelpEditMenu = GenInteraction {
	genStates = default -usable;
	moniker = 'H', "Help Editor";
	visibility = popup;
	children =
		WriteTextHelpControl,
		HelpOptionsList,
		GenerateHelpFile
#ifdef BATCH_RTF
,
	    BatchImportHelpFilesTrigger,
	    BatchExportHelpFilesTrigger
	    
#endif
#ifdef PRODUCT_TOOLS
,
	    AutoStyleSheetInteraction
#endif
	    ;
}

end PrimaryUI;

start HelpEditUI;

WriteTextHelpControl = TextHelpControl {
}
HelpOptionsList = GenBooleanGroup {
 	selectedBooleans = "mask HO_COMPRESS";
	children = CompressBool;
}
CompressBool = GenBoolean {
	moniker = 'C', "Compress Data in File";
	identifier = "mask HO_COMPRESS";      
}
GenerateHelpFile = GenTrigger {
	genAttributes = default +initiatesBusyState;
	moniker = 'G', "Generate Help File";
	action = MSG_WRITE_DOCUMENT_GENERATE_HELP_FILE, "TO_APP_MODEL";
	
}

#ifdef PRODUCT_TOOLS
AutoStyleSheetInteraction = GenInteraction {
    visibility = dialog;
    type = command;
    moniker = 'S', "Set RTF Import Style Sheet";
    children = StyleSheetFileSelector, SelectStyleSheetTrigger;
    hints = {
	HINT_INTERACTION_SINGLE_USAGE
    }
}

StyleSheetFileSelector = GenFileSelector {
    fileCriteria = dirs, geosNonExecutables;
    action = MSG_SSC_LOAD_STYLE_SHEET_FILE_SELECTED, "TO_OBJ_BLOCK_OUTPUT";
    attributes = default +useVirtualRoot;
    hints = {
	HINT_FILE_SELECTOR_SINGLE_ACTION,
	ATTR_GEN_PATH_DATA {
	    GenFilePath <SP_DOCUMENT>
	},
	ATTR_GEN_FILE_SELECTOR_VIRTUAL_ROOT {
	    GenFilePath <SP_DOCUMENT, "">
	},
	ATTR_GEN_FILE_SELECTOR_TOKEN_MATCH {
	    GeodeToken       <"WDAT", MANUFACTURER_ID_GEOWORKS>
	}
    }
}

SelectStyleSheetTrigger = GenTrigger {
    moniker = 'S', "Set Style Sheet";
    genAttributes = default +signalInteractionComplete;
    action = MSG_WRITE_PROCESS_SET_IMPORT_STYLE_SHEET, process;
    hints = {
	HINT_SEEK_REPLY_BAR,
	ATTR_GEN_TRIGGER_INTERACTION_COMMAND { word IC_OK }
    }    
}
#endif

#ifdef BATCH_RTF
visMoniker batchImportMoniker = 'I', "Batch Import from RTF";
localize "Trigger caption for batch import (TOOLS build only) on help edit menu";

BatchImportHelpFilesTrigger = GenTrigger {
    moniker = batchImportMoniker;
    action = MSG_WRITE_PROCESS_INITIATE_BATCH_IMPORT_UI, process;
    hints = {
	HINT_TRIGGER_BRINGS_UP_WINDOW
    }
}

visMoniker batchExportMoniker = 'E', "Batch Export to RTF";
localize "Trigger caption for batch export (TOOLS build only) on help edit menu";

BatchExportHelpFilesTrigger = GenTrigger {
    moniker = batchExportMoniker;
    action = MSG_WRITE_PROCESS_INITIATE_BATCH_EXPORT_UI, process;
    hints = {
	HINT_TRIGGER_BRINGS_UP_WINDOW
    }
}

BatchHelpFilesInteraction = GenInteraction {
    attributes = default +notUserInitiatable, +modal, +initiatedViaUserDoDialog;
    visibility = dialog;
    type = command;
    children =
	WriterBatchDirSelector,
	WriterBatchRunTrigger;
}

visMoniker batchDirSelectorMoniker = "Select directory containing GeoWrite files to export:";
localize "Caption for the directory selector in the batch export dialog box.";

WriterBatchDirSelector = GenFileSelector vardataReloc {
	moniker = batchDirSelectorMoniker;
	fileCriteria = default +dirs;
	selection = 1;
	attributes = default +allowChangeDirs;	
	hints = {
	    ATTR_GEN_PATH_DATA { GenFilePath <SP_USER_DATA, "HELP\\SOURCE"> },
	    HINT_IS_POPUP_LIST,
	    HINT_PLACE_MONIKER_ABOVE,
	    HINT_ITEM_GROUP_SCROLLABLE,
	    HINT_FIXED_SIZE {
		SpecWidth <SST_AVG_CHAR_WIDTHS, 24>
		SpecHeight <>
	    }
	}
}

WriterBatchRunTrigger = GenTrigger {
	moniker = "Run";
	genAttributes = default +signalInteractionComplete;
	hints = { 
		HINT_SEEK_REPLY_BAR,
		HINT_DEFAULT_FOCUS,
		HINT_DEFAULT_TARGET,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND { word IC_OK }
	}
}
#endif

end HelpEditUI;
