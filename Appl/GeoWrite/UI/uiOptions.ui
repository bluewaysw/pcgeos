/*
 * Project:	GeoWrite
 * File:	UI/uiOptions.ui
 *
 * REVISION HISTORY:
 *	Date	  Name	    Description
 *	----	  ----	    -----------
 *	Tony Requist	    Initial version
 *	12/2023	  RainerB   Renamed from Writer to GeoWrite
 *
 *	This file contains the user interface description for the options
 * 	menu.
 *
 *	$Id: uiOptions.ui,v 1.1 97/04/04 15:55:19 newdeal Exp $
 *
 */

/*-----------------------------------------------------------------------------
	Options menu
-----------------------------------------------------------------------------*/

start PrimaryUI;

OptionsMenu = GenInteraction {

	children = SetUserLevelDialog, OptionsGroup, WriteToolControl,
		   SaveResetGroup;
	visibility = popup;
	hints = { ATTR_GEN_INTERACTION_GROUP_TYPE
			{ GenInteractionGroupType GIGT_OPTIONS_MENU } }
}

end PrimaryUI;

/* ------------------------------- */

#define RESOURCE_FOR_USER_LEVEL_DIALOG_TOP	OptionsMenuUI
#define RESOURCE_FOR_USER_LEVEL_DIALOG_INSIDES	UserLevelUI

#define USER_LEVEL_APPLICATION_OBJECT	WriteApp
#define USER_LEVEL_SET_USER_LEVEL_MSG		\
			MSG_WRITE_APPLICATION_SET_USER_LEVEL
#define USER_LEVEL_INITIATE_FINE_TUNE_MSG	\
			MSG_WRITE_APPLICATION_INITIATE_FINE_TUNE
#define USER_LEVEL_FINE_TUNE_MSG		\
			MSG_WRITE_APPLICATION_FINE_TUNE
#define USER_LEVEL_CHANGE_USER_LEVEL_MSG		\
			MSG_WRITE_APPLICATION_CHANGE_USER_LEVEL
#define USER_LEVEL_CANCEL_USER_LEVEL_MSG		\
			MSG_WRITE_APPLICATION_CANCEL_USER_LEVEL
#define USER_LEVEL_STATUS_MSG	MSG_WRITE_APPLICATION_USER_LEVEL_STATUS

#define USER_LEVEL_GLYPH_MONIKER_TEXT \
	"Select how simple or complex you want GeoWrite to be:"

#define	USER_LEVEL_INTRODUCTORY_TEXT \
	"Introductory text editing and formatting."

#define	USER_LEVEL_BEGINNING_TEXT	\
	"General-purpose word processing."

#define	USER_LEVEL_INTERMEDIATE_TEXT	\
	"Full-featured word processing with simple page layout and graphics."

#define	USER_LEVEL_ADVANCED_TEXT	\
	"Desktop publishing and advanced word processing."

#include <usrLevel.uih>

/***/

start UserLevelUI;

FeaturesList = GenBooleanGroup {
	children = EditFeaturesEntry, SimpleTextAttributesEntry,
		   SimplePageLayoutEntry, SimpleGraphicsLayerEntry,
		   CharacterMenuEntry, ColorEntry,

		   GraphicsLayerEntry, MiscOptionsEntry,
		   ComplexTextAttributesEntry,

		   RulerControlEntry, ComplexPageLayoutEntry,
		   ComplexGraphicsEntry,

		   HelpEditorEntry;
	selectedBooleans = "DEFAULT_FEATURES";
        hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_ALLOW_CHILDREN_TO_WRAP,
		HINT_WRAP_AFTER_CHILD_COUNT {
			word	7
		}
	}
}

EditFeaturesEntry = GenBoolean {
	moniker = 'E', "Edit Features";
	identifier = "mask WF_EDIT_FEATURES";
}

SimpleTextAttributesEntry = GenBoolean {
	moniker = 'S', "Simple Text Attributes";
	identifier = "mask WF_SIMPLE_TEXT_ATTRIBUTES";
}

SimplePageLayoutEntry = GenBoolean {
	moniker = 'P', "Simple Page Layout";
	identifier = "mask WF_SIMPLE_PAGE_LAYOUT";
}

SimpleGraphicsLayerEntry = GenBoolean {
	moniker = 'L', "Simple Graphics Layer";
	identifier = "mask WF_SIMPLE_GRAPHICS_LAYER";
}

CharacterMenuEntry = GenBoolean {
	moniker = 'M', "Character Menu";
	identifier = "mask WF_CHARACTER_MENU";
}

ColorEntry = GenBoolean {
	moniker = 'C', "Color";
	identifier = "mask WF_COLOR";
}


GraphicsLayerEntry = GenBoolean {
	moniker = 'G', "Graphics Layer";
	identifier = "mask WF_GRAPHICS_LAYER";
}

MiscOptionsEntry = GenBoolean {
	moniker = 'O', "Misc Options";
	identifier = "mask WF_MISC_OPTIONS";
}

ComplexTextAttributesEntry = GenBoolean {
	moniker = 'T', "Complex Text Attributes";
	identifier = "mask WF_COMPLEX_TEXT_ATTRIBUTES";
}


RulerControlEntry = GenBoolean {
	moniker = 'R', "Ruler Control";
	identifier = "mask WF_RULER_CONTROL";
}

ComplexPageLayoutEntry = GenBoolean {
	moniker = 'x', "Complex Page Layout";
	identifier = "mask WF_COMPLEX_PAGE_LAYOUT";
}

ComplexGraphicsEntry = GenBoolean {
	moniker = 'a', "Complex Graphics";
	identifier = "mask WF_COMPLEX_GRAPHICS";
}


HelpEditorEntry = GenBoolean {
	moniker = 'H', "Help Editor";
	identifier = "mask WF_HELP_EDITOR";
}

end UserLevelUI;

/* ------------------------------- */

start OptionsMenuUI;

OptionsGroup = GenInteraction {
	children = ShowToolsPopup, RulerPopup, WriteInstructionPopup,
		   MiscSettingsPopup;
}

SaveResetGroup = GenInteraction {
	children = SaveOptionsTrigger, ResetOptionsTrigger;
}

/* --- */


ShowToolsPopup = GenInteraction {
	moniker = 'S', "Show Tools";
	visibility = popup;
	children = ShowBarList;
}

ShowBarList = GenBooleanGroup {
	children = ShowStyleBarEntry, ShowFunctionBarEntry, ShowGraphicBarEntry,
#if _BITMAP_EDITING
		   ShowDrawingToolsEntry, ShowBitmapToolsEntry;
#else
		   ShowDrawingToolsEntry;
#endif
	applyMsg = MSG_WRITE_APPLICATION_UPDATE_BARS;
	destination = WriteApp;
	selectedBooleans = "DEFAULT_BAR_STATES";
        hints = {
		ATTR_GEN_INIT_FILE_KEY { char "showBars", 0 }
	}
}

ShowStyleBarEntry = GenBoolean {
	moniker = 'y', "Style Bar";
	identifier = "mask WBS_SHOW_STYLE_BAR";
}

ShowFunctionBarEntry = GenBoolean {
	moniker = 'F', "Function Bar";
	identifier = "mask WBS_SHOW_FUNCTION_BAR";
}

ShowGraphicBarEntry = GenBoolean {
	moniker = 'G', "Graphic Bar";
	identifier = "mask WBS_SHOW_GRAPHIC_BAR";
}

ShowDrawingToolsEntry = GenBoolean {
	moniker = 'D', "Drawing Tools";
	identifier = "mask WBS_SHOW_DRAWING_TOOLS";
}

#if _BITMAP_EDITING
ShowBitmapToolsEntry = GenBoolean {
	moniker = 'B', "Bitmap Tools";
	identifier = "mask WBS_SHOW_BITMAP_TOOLS";
}
#endif

/***/

MiscSettingsPopup = GenInteraction {
	children = MiscSettingsList;
	moniker = 'O', "Other Settings";
	visibility = popup;
}

MiscSettingsList = GenBooleanGroup {
	children = ConfirmationEntry, DisplaySectionNameEntry,
		   ShowInvisiblesEntry, AutomaticLayoutRecalcEntry,
		   PasteGraphicsToCurrentLayerEntry,
		   DoNotDeletePagesWithGraphicsEntry, DTPModeEntry;
        hints = { ATTR_GEN_INIT_FILE_KEY { char "miscSettings", 0 } }
	applyMsg = MSG_WRITE_APPLICATION_UPDATE_MISC_SETTINGS;
	destination = WriteApp;
	selectedBooleans = "DEFAULT_MISC_SETTINGS";
}

ShowInvisiblesEntry = GenBoolean {
	moniker = 'I', "Show Invisibles";
	identifier = "mask WMS_SHOW_INVISIBLES";
}

ConfirmationEntry = GenBoolean {
	moniker = 'C', "Confirm Actions";
	identifier = "mask WMS_CONFIRM";
}

DisplaySectionNameEntry = GenBoolean {
	moniker = 'D', "Display Section Name";
	identifier = "mask WMS_DISPLAY_SECTION_AND_PAGE";
}

AutomaticLayoutRecalcEntry = GenBoolean {
	moniker = 'A', "Automatic Layout Recalc";
	identifier = "mask WMS_AUTOMATIC_LAYOUT_RECALC";
}

PasteGraphicsToCurrentLayerEntry = GenBoolean {
	moniker = 'P', "Paste Graphics To Current Layer";
	identifier = "mask WMS_PASTE_GRAPHICS_TO_CURRENT_LAYER";
}

DoNotDeletePagesWithGraphicsEntry = GenBoolean {
	moniker = 'G', "Do Not Delete Pages With Graphics";
	identifier = "mask WMS_DO_NOT_DELETE_PAGES_WITH_GRAPHICS";
}

DTPModeEntry = GenBoolean {
	moniker = 'T', "DTP Mode (Never Delete pages)";
	identifier = "mask WMS_DTP_MODE";
}

/***/

RulerPopup = GenInteraction {
	children = WriteTextRulerControl, WriteRulerShowControl,
		   WriteRulerTypeControl;
	moniker = 'R', "Rulers";
	visibility = popup;
}

WriteTextRulerControl = TextRulerControl {
}

WriteRulerShowControl = RulerShowControl {
	rulerAttrs = default -showVertical;
	message = "MSG_WRITE_DISPLAY_UPDATE_RULERS";
}

WriteRulerTypeControl = RulerTypeControl {
	moniker = 'U', "Ruler Units";
	visibility = popup;
}

/***/

WriteInstructionPopup = GenInteraction {
	moniker = 'I', "Instructions";
	visibility = popup;
	children = WriteInstructionControl;
}
WriteInstructionControl = GrObjInstructionControl {
}

/***/

WriteToolControl = GenToolControl {
	moniker = 'T', "Customize Tool Bars";
	visibility = dialog;
	type = command;
	toolboxList = WriteToolboxList;
	toolGroupList = WriteToolGroupList;
	hints = {
		HINT_POSITION_WINDOW_AT_RATIO_OF_PARENT {
	     		SpecWinSizePair < mask SWSS_RATIO or PCT_10, \
					mask SWSS_RATIO or PCT_20>
		}
	}
}
chunk WriteToolboxList = data {
	UN_OPTR StyleToolbar
	UN_OPTR StyleToolbarName
	UN_OPTR FunctionToolbar
	UN_OPTR FunctionToolbarName
	UN_OPTR GraphicsToolbar
	UN_OPTR GraphicsToolbarName
	UN_OPTR GrObjToolsToolbar
	UN_OPTR GrObjToolsToolbarName
}
chunk WriteToolGroupList = data {

	; Style bar

	UN_OPTR	PageTools
	UN_OPTR	SpellTools
	UN_OPTR	SearchReplaceTools
	UN_OPTR	TextStyleSheetTools
	UN_OPTR	JustificationTools
	UN_OPTR	FontTools
	UN_OPTR	TextStyleTools
	UN_OPTR	PointSizeTools
	; Function bar

	UN_OPTR	DocumentTools
	UN_OPTR	PrintTools
	UN_OPTR	EditTools
	UN_OPTR	ViewTools
#ifdef DO_PIZZA
	UN_OPTR RowColumnDisplayTools
#endif
	UN_OPTR	HyphenationTools
	UN_OPTR	LineSpacingTools

	; Drawing tools

	UN_OPTR	GrObjDrawingTools
#if _BITMAP_EDITING
	UN_OPTR	GrObjBitmapTools
#endif

	; Graphic bar

	UN_OPTR	AreaColorTools
	UN_OPTR	LineColorTools
	UN_OPTR LineAttrTools
	UN_OPTR TextColorTools
	UN_OPTR	GroupTools
	UN_OPTR	DuplicateTools
	UN_OPTR	PasteInsideTools
	UN_OPTR	DepthTools
	UN_OPTR	FlipTools
}

chunk StyleToolbarName = "Style Bar (Below menu)";
chunk FunctionToolbarName = "Function Bar (Below menu)";

chunk GraphicsToolbarName = "Graphics Bar (Below menu)";
chunk GrObjToolsToolbarName = "Drawing Tools Area (Left side)";


SaveOptionsTrigger = GenTrigger {
	SAVE_OPTIONS_TRIGGER_VALUES
	action = MSG_META_SAVE_OPTIONS, WriteApp;
}

ResetOptionsTrigger = GenTrigger {
	RESET_OPTIONS_TRIGGER_VALUES
	action = MSG_WRITE_APPLICATION_QUERY_RESET_OPTIONS, WriteApp;
}

end OptionsMenuUI;

