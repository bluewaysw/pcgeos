/***********************************************************************
 *
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * PROJECT:	  GEOS
 * MODULE:	  Icon editor
 * FILE:	  uiMain.ui
 *
 * AUTHOR:  	  Steve Yegge, Sep  2, 1992
 *
 * REVISION HISTORY:
 *	Name	  Date	    Description
 *	----	  ----	    -----------
 *	stevey	9/ 2/92	  Initial version
 *
 * DESCRIPTION:
 *	
 *
 * 	$Id: uiMain.ui,v 1.1 97/04/04 16:06:24 newdeal Exp $
 *
 ***********************************************************************/

/* #include "Art/mkrIcon.ui" */
#include "Art/mkrIconDoc.ui"

/* ----------------------------------------------------------------------------
	Application Object
-----------------------------------------------------------------------------*/

start IconAppResource;

IconApp = IconApplication {
	moniker = list {
		IconLCMoniker,
		IconLMMoniker,
		IconSCMoniker,
		IconSMMoniker,
		IconSCGAMoniker,
		IconYCMoniker,
		IconYMMoniker,
		IconTMMoniker,
		IconTCGAMoniker,
		IconTextMoniker
	}
	children = IconPrimary;
	gcnList(MANUFACTURER_ID_GEOWORKS, GAGCNLT_WINDOWS) =
		IconPrimary;
	gcnList(MANUFACTURER_ID_GEOWORKS,MGCNLT_APP_STARTUP) =
		IconDocumentControl;
	gcnList(MANUFACTURER_ID_GEOWORKS,MGCNLT_ACTIVE_LIST) =
		IconDocumentControl, 
		IconDisplayControl,
		IconToolControl;
	gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_STARTUP_LOAD_OPTIONS) =
		IconToolControl;
	gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_SELF_LOAD_OPTIONS) =
		IconDocumentControl, 
		IconDisplayControl,
		IconEditControl,
		IconToolControl,
		IconBitmapToolControl,
		IconImportControl,
		IconAreaColorSelector,
		IconLineColorSelector,
		IconLineAttrControl;
}

visMoniker IconTextMoniker = "Icon Editor";

end	IconAppResource;

start AppSCMonikerResource, notDetachable;

visMoniker IconDatafileMonikerList = list {
		IconDocLCMoniker,
		IconDocLMMoniker,
		IconDocSCMoniker,
		IconDocSMMoniker,
		IconDocSCGAMoniker,
		IconDocYCMoniker,
		IconDocYMMoniker,
		IconDocTMMoniker,
		IconDocTCGAMoniker
}

end AppSCMonikerResource;

/* ----------------------------------------------------------------------------
	 Primary window.
-----------------------------------------------------------------------------*/

start	PrimaryUI;

IconPrimary = GenPrimary {

	children = IconFileMenu,
		   IconEditMenu,
		   IconOptionsMenu,
		   IconGraphicsMenu,
		   IconDisplayControl,
		   TopToolbar,
		   DisplayGroup,
		   BottomToolbar,
		   IconBitmapToolControl,
		   IconPreviewDialog,
		   ImportProgressDialog;
	hints = {
		HINT_PRIMARY_FULL_SCREEN,
		/*HINT_PRIMARY_NO_HELP_BUTTON,*/
		ATTR_GEN_HELP_CONTEXT {
			char "TOC", 0
		},
	}
}

IconFileMenu = GenInteraction {
	visibility = popup;
	children = IconDocumentControl,
		   IconImportControl,
		   ImportTokenInteraction,
		   ExportTokenDialog;
	hints = {
		ATTR_GEN_INTERACTION_GROUP_TYPE {
			 GenInteractionGroupType GIGT_FILE_MENU
		},
	}
}

end PrimaryUI;

/*-----------------------------------------------------------------------------	
			Import Control
-----------------------------------------------------------------------------*/

start	FileMenuUI;

IconImportControl = ImportControl {
	moniker = 'G', "Import Graphic";
	importDataClasses = graphics;
	importDestination = "TO_APP_MODEL";
	importMessage = MSG_DB_VIEWER_IMPORT;
}

end	FileMenuUI;

/*-----------------------------------------------------------------------------
			Primary UI
-----------------------------------------------------------------------------*/

start	PrimaryUI;

DisplayGroup = GenInteraction {
	type = organizational;
	children = LeftToolbar,
		   IconDisplayGroup,
		   RightToolbar;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
	}
}

TopToolbar = GenInteraction {
	children = IconFileToolGroup,
		   IconEditToolGroup,
		   IconBitmapEditToolGroup;
	hints = { 
		HINT_TOOLBOX,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_ALLOW_CHILDREN_TO_WRAP,
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY,
	 }
}

IconFileToolGroup = GenInteraction {
	type = organizational;
	children = IconFileTools;
	hints = {
		HINT_TOOLBOX,
		HINT_MINIMIZE_CHILD_SPACING,
	}
}

IconEditToolGroup = GenInteraction {
	type = organizational;
	children = IconEditTools;
	hints = {
		HINT_TOOLBOX,
		HINT_MINIMIZE_CHILD_SPACING,
	}
}

IconBitmapEditToolGroup = GenInteraction {
	type = organizational;
	children = IconBitmapEditTools;
}

/*-----------------------------------------------------------------------------
		Toolbox for the document control.
-----------------------------------------------------------------------------*/

IconFileTools = GenToolGroup {
	genStates = default +usable, +enabled;
	controller = IconDocumentControl;
	hints = {
		HINT_TOOLBOX,
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
	}
}

/*-----------------------------------------------------------------------------
		Edit Menu
-----------------------------------------------------------------------------*/

IconEditMenu = GenInteraction {
	children = IconEditControl;
	visibility = popup;
	hints = { 
		ATTR_GEN_INTERACTION_GROUP_TYPE {
			GenInteractionGroupType GIGT_EDIT_MENU
		},
		HINT_GEN_CONTROL_MODIFY_INITIAL_UI {
			GenControlUserData <
				mask GCUF_USER_TOOLBOX_UI,
				0,
				0,
				mask GECToolboxFeatures				
			>
		}
	}
}

IconEditTools = GenToolGroup {
	genStates = default +usable, +enabled;
	controller = IconEditControl;
	hints = {
		HINT_TOOLBOX,
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
	}
}

IconBitmapEditTools = GenToolGroup {
	controller = IconBitmapToolControl;
	hints = {
		HINT_TOOLBOX,
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
	}
}

end	PrimaryUI;

start	EditMenuUI;

IconEditControl = GenEditControl {
	visibility = subGroup;
	hints = {
		HINT_GEN_CONTROL_SCALABLE_UI_DATA {
			GenControlScalableUIEntry <
			GCSUIC_SET_TOOLBOX_FEATURES_IF_APP_LEVEL,
				UIIL_INTRODUCTORY,
				0
			>,<
			GCSUIC_SET_TOOLBOX_FEATURES_IF_APP_LEVEL,
				UIIL_ADVANCED,
				mask GECToolboxFeatures
			>
		}
	}
}

end	EditMenuUI;

start	PrimaryUI;

/*-----------------------------------------------------------------------------
			Bitmap Tools
-----------------------------------------------------------------------------*/

IconBitmapToolControl = VisBitmapToolControl vardataReloc {
	genStates = default +enabled;
	output = "TO_BITMAP";
	hints = {
		HINT_GEN_CONTROL_MODIFY_INITIAL_UI {
			GenControlUserData <
				mask GCUF_USER_TOOLBOX_UI,
				0,
				0,
				mask VBTCF_DRAW_RECT or \
				mask VBTCF_DRAW_ELLIPSE,
				mask VBTCF_FATBITS
			>
		},
		ATTR_GEN_CONTROL_PROHIBIT_TOOLBOX_UI {
			word mask VBTCF_FATBITS
		},
		ATTR_GEN_OUTPUT_TRAVEL_START {
			UN_OPTR IconApp
		}, 
		HINT_WRAP_AFTER_CHILD_COUNT {
			word 4
		},
	}
}

/*-----------------------------------------------------------------------------
		Area & line tools
-----------------------------------------------------------------------------*/

LeftToolbar = GenInteraction {
	type = organizational;
	children = IconAreaColorTools,
		   IconLineColorTools,
		   IconLineAttrTools;
	hints = {
		HINT_TOOLBOX,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
		HINT_ALLOW_CHILDREN_TO_WRAP,
		HINT_ORIENT_CHILDREN_VERTICALLY,
	} 
}

RightToolbar = GenInteraction {
	hints = {
		HINT_TOOLBOX,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
		HINT_ALLOW_CHILDREN_TO_WRAP,
		HINT_ORIENT_CHILDREN_VERTICALLY,
	} 
}

BottomToolbar = GenInteraction {
	hints = {
		HINT_TOOLBOX,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_ALLOW_CHILDREN_TO_WRAP,
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
	} 
}

FloatingToolbox = GenInteraction {
	moniker = 'T', "Floating Toolbox";
	visibility = dialog;
	hints = {
		HINT_ALLOW_CHILDREN_TO_WRAP,
		HINT_TOOLBOX,
		HINT_INTERACTION_MAKE_RESIZABLE,
	}
}

IconAreaColorTools = GenToolGroup {
	genStates = default +usable, +enabled;
	controller = IconAreaColorSelector;
	hints = {
		HINT_SAME_ORIENTATION_AS_PARENT,
		HINT_MINIMIZE_CHILD_SPACING,
	}
}

IconLineColorTools = GenToolGroup {
	genStates = default +usable, +enabled;
	controller = IconLineColorSelector;
	hints = {
		HINT_SAME_ORIENTATION_AS_PARENT,
		HINT_MINIMIZE_CHILD_SPACING,
	}
}

IconLineAttrTools = GenToolGroup {
	genStates = default +usable, +enabled;
	controller = IconLineAttrControl;
	hints = {
		HINT_SAME_ORIENTATION_AS_PARENT,
		HINT_MINIMIZE_CHILD_SPACING,
	}
}

end	PrimaryUI;
