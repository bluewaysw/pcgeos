@start Interface;

@object GenItemClass Distortion_None = {
    GI_visMoniker = "None";
    GII_identifier = DIST_NONE;
}

@object GenItemClass Distortion_Inflate = {
    GI_visMoniker = @list{ @InflateMoniker };
    GII_identifier = DIST_INFLATE;
}

@object GenItemClass Distortion_InflateP = {
    GI_visMoniker = @list{ @InflatePMoniker };
    GII_identifier = DIST_INFLATE_P;
}

@object GenItemClass Distortion_Bend = {
    GI_visMoniker = @list{ @BendDMoniker };
    GII_identifier = DIST_BEND;
}

@object GenItemClass Distortion_Book = {
    GI_visMoniker = @list{ @BookDMoniker };
    GII_identifier = DIST_BOOK;
}

@object GenItemClass Distortion_Sine = {
    GI_visMoniker = @list{ @sineMoniker };
    GII_identifier = DIST_SINE;
}

@object GenItemClass Distortion_Circular = {
    GI_visMoniker = @list{ @arcUpMoniker };
    GII_identifier = DIST_CIRC;
}

@object GenItemClass Perspective_None = {
    GI_visMoniker = "None";
    GII_identifier = DIST_NONE;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
}

@object GenItemClass Perspective_BF = {
    GI_visMoniker = @list{ @persBFMoniker };
    GII_identifier = DIST_PERS_BF;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
}

@object GenItemClass Perspective_AB = {
    GI_visMoniker = @list{ @persABMoniker };
    GII_identifier = DIST_PERS_AB;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
}

@object GenItemClass VRefTop = {
    GI_visMoniker = @list{ @hrefTMoniker };
    GII_identifier = VREF_TOP;
}

@object GenItemClass VRefCenter = {
    GI_visMoniker = @list{ @hrefCMoniker };
    GII_identifier = VREF_CENTER;
}

@object GenItemClass VRefBottom = {
    GI_visMoniker = @list{ @hrefBMoniker };
    GII_identifier = VREF_BOTTOM;
}

@object GenItemClass EffectPlain = {
    GI_visMoniker = @list{ @plainMoniker };
    GII_identifier = EFFECT_PLAIN;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
}

@object GenItemClass EffectShadow = {
    GI_visMoniker = @list{ @simpleMoniker };
    GII_identifier = EFFECT_SHADOW;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
}

@object GenItemClass EffectDouble = {
    GI_visMoniker = @list{ @doubleMoniker };
    GII_identifier = EFFECT_DOUBLE;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
}

@object GenItemClass EffectSolid = {
    GI_visMoniker = @list{ @solidMoniker };
    GII_identifier = EFFECT_SOLID;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
}

@object GenItemClass EffectFog = {
    GI_visMoniker = @list{ @fogMoniker };
    GII_identifier = EFFECT_FOG;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
}

@object GenItemClass Effect3D = {
    GI_visMoniker = @list{ @threeDMoniker };
    GII_identifier = EFFECT_3D;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
}

@object GenItemClass EffectOutline = {
    GI_visMoniker = @list{ @outlineMoniker };
    GII_identifier = EFFECT_OUTLINE;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
}

@object GenItemClass ShDir0 = {
  GII_identifier=0;
}

@object GenItemClass ShDir1 = {
  GII_identifier=1;
}

@object GenItemClass ShDir2 = {
  GII_identifier=2;
}

@object GenItemClass ShDir3 = {
  GII_identifier=3;
}

@object GenItemClass ShDir5 = {
  GII_identifier=5;
}

@object GenItemClass ShDir6 = {
  GII_identifier=6;
}

@object GenItemClass ShDir7 = {
  GII_identifier=7;
}

@object GenItemClass ShDir8 = {
  GII_identifier=8;
}

@object GenBooleanClass VPerspective = {
    GI_visMoniker = 'V',"Vertical Perspective";
    GBI_identifier = EFFECT_VPERS;
}

@object GenBooleanClass HPerspective = {
    GI_visMoniker = 'H',"Horizontal Perspective";
    GBI_identifier = EFFECT_HPERS;
}

@object GenItemGroupClass DistortionList = {
/*
 * group of menu items containing the various effects
 */
    GIGI_applyMsg = MSG_FontM_DISTORTION_CHANGED;
    GIGI_destination = FontMApp;
    GIGI_selection = DIST_NONE;
    HINT_ITEM_GROUP_MINIMIZE_SIZE;      /* create a popup for item names */
    HINT_ITEM_GROUP_DISPLAY_CURRENT_SELECTION;
    GI_comp =
      @Distortion_None,
      @Distortion_Inflate,
      @Distortion_InflateP,
      @Distortion_Bend,
      @Distortion_Book,
      @Distortion_Sine,
      @Distortion_Circular;
}

@object GenItemGroupClass DistPersList = {
/*
 * group of menu items containing the various perspective effects
 */
    GIGI_applyMsg = MSG_FontM_DIST_PERS_CHANGED;
    GIGI_destination = FontMApp;
    GIGI_selection = DIST_NONE;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
    HINT_ITEM_GROUP_TOOLBOX_STYLE;
    GI_comp =
      @Perspective_None,
      @Perspective_BF,
      @Perspective_AB;
}

@object GenItemGroupClass VRefLineList = {
/*
 * group of menu items containing the various reference line settings
 */
    GIGI_applyMsg = MSG_FontM_VREF_CHANGED;
    GIGI_destination = FontMApp;
    GIGI_selection = VREF_CENTER;
    HINT_ITEM_GROUP_TOOLBOX_STYLE;
    GI_comp =
      @VRefTop,
      @VRefCenter,
      @VRefBottom;
}

@object GenItemClass BendUp = {
    GI_visMoniker = @list{ @arrowUpMoniker };
    GII_identifier = OPT_BEND_UP;
}

@object GenItemClass BendDown = {
    GI_visMoniker = @list{ @arrowDownMoniker };
    GII_identifier = 0;
}

@object GenItemClass Item_Text = {
    GI_visMoniker = "Text";
    GII_identifier = COLITEM_TEXT;
}

@object GenItemClass Item_Text_Back = {
    GI_visMoniker = "Text Background";
    GII_identifier = COLITEM_TEXT_BACK;
}

@object GenItemClass Item_Outl1 = {
    GI_visMoniker = "1st Outline";
    GII_identifier = COLITEM_OUTLINE1;
}

@object GenItemClass Item_Outl2a = {
    GI_visMoniker = "2nd Outline";
    GII_identifier = COLITEM_OUTLINE2a;
}

@object GenItemClass Item_Outl2b = {
    GI_visMoniker = "2nd Outline (2)";
    GII_identifier = COLITEM_OUTLINE2b;
}

@object GenItemClass Item_Shadow = {
    GI_visMoniker = "Shadow";
    GII_identifier = COLITEM_SHADOW;
}

@object GenItemClass Item_Shadow2 = {
    GI_visMoniker = "Shadow 2";
    GII_identifier = COLITEM_SHADOW2;
}

@object GenItemClass Item_Shadow_Back = {
    GI_visMoniker = "Shadow Background";
    GII_identifier = COLITEM_SHADOW_BACK;
}

@object GenItemClass Item_View = {
    GI_visMoniker = "Preview background";
    GII_identifier = COLITEM_VIEW;
}

@object GenItemClass Grid_CharOnly = {
    GI_visMoniker = @list{ @charOnlyMoniker };
    GII_identifier = GRID_CHARONLY;
}

@object GenItemClass Grid_Both = {
    GI_visMoniker = @list{ @charGridMoniker };
    GII_identifier = GRID_BOTH;
}

@object GenItemClass Grid_Only = {
    GI_visMoniker = @list{ @gridOnlyMoniker };
    GII_identifier = GRID_ONLY;
}

@object GenItemClass GridExport_CharOnly = {
    GI_visMoniker = @list{ @charOnlyMoniker };
    GII_identifier = GRID_CHARONLY;
}

@object GenItemClass GridExport_Both = {
    GI_visMoniker = @list{ @charGridMoniker };
    GII_identifier = GRID_BOTH;
}

@object GenItemClass GridExport_Only = {
    GI_visMoniker = @list{ @gridOnlyMoniker };
    GII_identifier = GRID_ONLY;
}

@object GenItemGroupClass EffectList = {
/*
 * group of menu items containing the various effects
 */
    GIGI_applyMsg = MSG_FontM_EFFECT_CHANGED;
    GIGI_destination = FontMApp;
    GIGI_selection = EFFECT_PLAIN;
    HINT_ITEM_GROUP_TOOLBOX_STYLE;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
    HINT_DIVIDE_WIDTH_EQUALLY;
    GI_comp =
      @EffectPlain,
      @EffectShadow,
      @EffectDouble,
      @EffectSolid,
      @EffectFog,
      @Effect3D,
      @EffectOutline;
}

@object GenValueClass ShadowDepth = {
    GI_visMoniker = 'D',"Shadow Depth:";
    GVLI_minimum = MakeWWFixed(1);
    GVLI_maximum = MakeWWFixed(64);
    GVLI_displayFormat = GVDF_POINTS;
    GVLI_destination = FontMApp;
    GVLI_applyMsg = MSG_FontM_SHADOW_DEPTH_CHANGED;
}

@object GenValueClass SquishRatio = {
    GI_visMoniker = 'q',"Squish Ratio (%):";
    GVLI_minimum = MakeWWFixed(10);
    GVLI_maximum = MakeWWFixed(200);
    GVLI_displayFormat = GVDF_INTEGER;
    GVLI_destination = FontMApp;
    GVLI_applyMsg = MSG_FontM_SQUISH_RATIO_CHANGED;
}

@object GenValueClass SkewAngle = {
    GI_visMoniker = 'k',"Skew Angle:";
    GVLI_minimum = MakeWWFixed(-85);
    GVLI_maximum = MakeWWFixed(85);
    GVLI_displayFormat = GVDF_INTEGER;
    GVLI_destination = FontMApp;
    GVLI_applyMsg = MSG_FontM_SKEW_ANGLE_CHANGED;
}

@object GenItemGroupClass DirectionList = {
    GIGI_applyMsg = MSG_FontM_SHADOW_DIR_CHANGED;
    GIGI_destination = FontMApp;
    GIGI_selection = 8;
    HINT_ITEM_GROUP_MINIMIZE_SIZE;      /* create a popup for item names */
    HINT_ITEM_GROUP_DISPLAY_CURRENT_SELECTION;
    GI_comp =
      @ShDir0,
      @ShDir1,
      @ShDir2,
      @ShDir3,
      @ShDir5,
      @ShDir6,
      @ShDir7,
      @ShDir8;
}

@object GenBooleanGroupClass ShadowProperties = {
    GBGI_destination = FontMApp;
    GBGI_applyMsg = MSG_FontM_SHADOW_PROPS_CHANGED;
    HINT_ORIENT_CHILDREN_VERTICALLY;
    GI_comp =
      @VPerspective,
      @HPerspective;
}

@object GenBooleanClass SettingsEmbedFonts = {
    GI_visMoniker = 'G',"Convert Fonts to Graphics";
    GBI_identifier = 1;
}

@object GenInteractionClass DistortionMenu = {
    GI_visMoniker = 'D',"Distortion:";
    GI_comp =
      @DistortionList;
}

@object GenInteractionClass PerspectiveMenu = {
    GI_visMoniker = 'P',"Perspective:";
    HINT_CENTER_CHILDREN_HORIZONTALLY;
    GI_comp =
      @DistPersList;
}

@object GenValueClass Angle = {
    GI_visMoniker = 'A',"Angle:";
    GVLI_minimum = MakeWWFixed(1);
    GVLI_maximum = MakeWWFixed(359);
    GVLI_displayFormat = GVDF_INTEGER;
    GVLI_destination = FontMApp;
    GVLI_applyMsg = MSG_FontM_ANGLE_CHANGED;
}

@object GenValueClass WaveNumber = {
    GI_visMoniker = 'N',"Number of Waves:";
    GVLI_minimum = MakeWWFixed(1);
    GVLI_maximum = MakeWWFixed(99);
    GVLI_displayFormat = GVDF_INTEGER;
    GVLI_destination = FontMApp;
    GVLI_applyMsg = MSG_FontM_WAVE_NUMBER_CHANGED;
}

@object GenValueClass SizeFactor = {
    GI_visMoniker = 'S',"Size factor (%):";
    GVLI_minimum = MakeWWFixed(0);
    GVLI_maximum = MakeWWFixed(999);
    GVLI_displayFormat = GVDF_INTEGER;
    GVLI_destination = FontMApp;
    GVLI_applyMsg = MSG_FontM_SIZE_FACTOR_CHANGED;
}

@object GenInteractionClass VrefLine = {
    GI_visMoniker = 'H',"Hor. Reference:";
    GI_comp =
      @VRefLineList;
}

@object GenItemGroupClass UpDownList = {
/*
 * group of menu items containing the various reference line
 */
    GI_visMoniker = 'B',"Bending:";
    GIGI_applyMsg = MSG_FontM_UPDOWN_CHANGED;
    GIGI_destination = FontMApp;
    GIGI_selection = 0;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
    HINT_ITEM_GROUP_TOOLBOX_STYLE;
    GI_comp =
      @BendUp,
      @BendDown;
}

@object GenValueClass PerspFactor = {
    GI_visMoniker = 'v',"Perspective (%):";
    GVLI_minimum = MakeWWFixed(0);
    GVLI_maximum = MakeWWFixed(999);
    GVLI_displayFormat = GVDF_INTEGER;
    GVLI_destination = FontMApp;
    GVLI_applyMsg = MSG_FontM_PERSPECTIVE_FACTOR_CHANGED;
}

@object GenBooleanClass RoundedOutl = {
    GI_visMoniker = 'R',"Rounded Outline";
    GBI_identifier = EFFECT_ROUND;
}

@object GenBooleanClass EmbossedOutl = {
    GI_visMoniker = 'E',"Outline 2 Embossed";
    GBI_identifier = EFFECT_EMBOSSED;
}

@object GenBooleanClass OutlShadow = {
    GI_visMoniker = 'O',"Shadow Includes Outline";
    GBI_identifier = EFFECT_OUTLSHAD;
}

@object GenItemGroupClass WhichItem = {
    /*
     * group of items containing the things that can be colored
     */
    GIGI_applyMsg = MSG_FontM_COLITEM_CHANGED;
    GIGI_destination = FontMApp;
    GIGI_selection = COLITEM_TEXT;
    ATTR_GEN_NOT_PROPERTY;
    HINT_ITEM_GROUP_MINIMIZE_SIZE;      /* create a popup for item names */
    HINT_ITEM_GROUP_DISPLAY_CURRENT_SELECTION;
    GI_comp =
      @Item_Text,
      @Item_Text_Back,
      @Item_Outl1,
      @Item_Outl2a,
      @Item_Outl2b,
      @Item_Shadow,
      @Item_Shadow2,
      @Item_Shadow_Back,
      @Item_View;
}

@object GenBooleanClass SettingsMixedColors = {
    GI_visMoniker = 'x',"Use Mixed Colors for Halftones";
    GBI_identifier = 1;
}

@object GenBooleanClass SettingsLoadDefaults = {
    GI_visMoniker = 'f',"Auto-load Shadow Defaults";
    GBI_identifier = SETTINGS_LOADDEFAULTS;
}

@object GenBooleanClass SettingsAutoRedraw = {
    GI_visMoniker = 'A',"Disable Auto-ReCalc";
    GBI_identifier = SETTINGS_NOREDRAW;
}

@object GenBooleanClass SettingsGSOLComments = {
    GI_visMoniker = 'O',"Enable GSOL Information";
    GBI_identifier = SETTINGS_GSOLENABLE;
}

@object GenItemGroupClass GridDrawList = {
/*
 * group of menu items containing the various grid settings
 */
    GIGI_applyMsg = MSG_FontM_GRID_CHANGED;
    GIGI_destination = FontMApp;
    GIGI_selection = GRID_BOTH;
    GI_comp =
      @Grid_CharOnly,
      @Grid_Both,
      @Grid_Only;
}

@object GenItemGroupClass GridExportList = {
/*
 * group of menu items containing the various grid settings
 */
    GIGI_applyMsg = MSG_FontM_GRID_EXP_CHANGED;
    GIGI_destination = FontMApp;
    GIGI_selection = GRID_CHARONLY;
    GI_comp =
      @GridExport_CharOnly,
      @GridExport_Both,
      @GridExport_Only;
}

@object GenTriggerClass LoadDefaults = {
    GI_visMoniker = 'L',"Load Shadow Defaults";
    GTI_destination = FontMApp;
    GTI_actionMsg = MSG_FontM_LOAD_DEFAULT;
    ATTR_GEN_TRIGGER_IMMEDIATE_ACTION;
    HINT_TRIGGER_DESTRUCTIVE_ACTION;
}

@object GenTriggerClass LoadStartup = {
    GI_visMoniker = 'D',"Discard All Changes";
    GTI_destination = FontMApp;
    GTI_actionMsg = MSG_FontM_LOAD_STARTUP;
    ATTR_GEN_TRIGGER_IMMEDIATE_ACTION;
    HINT_TRIGGER_DESTRUCTIVE_ACTION;
}

@object GenTriggerClass RedrawTrigger2 = {
    GI_attrs = @default | GA_KBD_SEARCH_PATH;
    GI_visMoniker = 'R',"Recalculate now";
    GI_states = @default & ~GS_ENABLED;
    GTI_destination = EffectTextField;
    GTI_actionMsg = MSG_GEN_APPLY;
    ATTR_GEN_TRIGGER_IMMEDIATE_ACTION;
}

@object CharsetClass CharTable = {
}

@object GenInteractionClass ShadowType = {
    GI_visMoniker = 'T',"Type";
    GII_visibility = GIV_POPOUT;
    HINT_DRAW_IN_BOX;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_CENTER_CHILDREN_HORIZONTALLY;
    GI_comp =
      @EffectList;
}

@object GenInteractionClass ShadowSettings = {
    GI_visMoniker = "Options";
    GII_visibility = GIV_POPOUT;
    HINT_DRAW_IN_BOX;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_CENTER_CHILDREN_ON_MONIKERS;
    GI_comp =
      @ShadowDepth,
      @SquishRatio,
      @SkewAngle;
}

@object GenInteractionClass ShadowDir = {
    GI_visMoniker = 'r',"Direction/Form";
    GII_visibility = GIV_POPOUT;
    HINT_DRAW_IN_BOX;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    GI_comp =
      @DirectionList,
      @ShadowProperties;
}

@object GenTriggerClass RedrawTrigger3 = {
    GI_visMoniker = "ReCalc";
    GI_states = @default & ~GS_ENABLED;
    GTI_destination = EffectTextField;
    GTI_actionMsg = MSG_GEN_APPLY;
    HINT_SEEK_REPLY_BAR;
}

@object GenBooleanGroupClass EmbedBoolGroup = {
    GBGI_applyMsg = MSG_FontM_EMBED_CHANGED;
    GBGI_destination = FontMApp;
    GI_comp =
      @SettingsEmbedFonts;
}

@object GenInteractionClass Distortion = {
    HINT_CENTER_CHILDREN_ON_MONIKERS;
    GI_comp =
      @DistortionMenu,
      @PerspectiveMenu,
      @Angle,
      @WaveNumber,
      @SizeFactor,
      @VrefLine,
      @UpDownList,
      @PerspFactor;
}

@object GenTriggerClass RedrawTrigger4 = {
    GI_visMoniker = "ReCalc";
    GI_states = @default & ~GS_ENABLED;
    GTI_destination = EffectTextField;
    GTI_actionMsg = MSG_GEN_APPLY;
    HINT_SEEK_REPLY_BAR;
}

@object GenValueClass OutlineThickness1 = {
    GI_visMoniker = '1',"Outline 1 Thickness:";
    GVLI_minimum = MakeWWFixed(0);
    GVLI_maximum = MakeWWFixed(10);
    GVLI_displayFormat = GVDF_POINTS;
    GVLI_destination = FontMApp;
    GVLI_applyMsg = MSG_FontM_OUTLINE1_THCKN_CHANGED;
}

@object GenValueClass OutlineThickness2 = {
    GI_visMoniker = '2',"Outline 2 Thickness:";
    GVLI_minimum = MakeWWFixed(0);
    GVLI_maximum = MakeWWFixed(10);
    GVLI_displayFormat = GVDF_POINTS;
    GVLI_destination = FontMApp;
    GVLI_applyMsg = MSG_FontM_OUTLINE2_THCKN_CHANGED;
}

@object GenBooleanGroupClass OutlineProperties = {
    GBGI_destination = FontMApp;
    GBGI_applyMsg = MSG_FontM_OUTLINE_PROPS_CHANGED;
    HINT_ORIENT_CHILDREN_VERTICALLY;
    GI_comp =
      @RoundedOutl,
      @EmbossedOutl,
      @OutlShadow;
}

@object GenTriggerClass RedrawTrigger5 = {
    GI_visMoniker = "ReCalc";
    GI_states = @default & ~GS_ENABLED;
    GTI_destination = EffectTextField;
    GTI_actionMsg = MSG_GEN_APPLY;
    HINT_SEEK_REPLY_BAR;
}

@object GenInteractionClass WhichItemGr = {
    GI_visMoniker = 'I',"Item:";
    GI_comp =
      @WhichItem;
}

@object ColorSelectorClass ColorControl = {
    GI_visMoniker = 'C',"Color:";
    GCI_output = FontMApp;              /* send messages to app object */
    ATTR_GEN_CONTROL_PROHIBIT_UI = CSF_PATTERN;
    					/* no pattern selection */

    GI_attrs = GA_NOTIFY_VISIBILITY;    /*** workaround ***/
    ATTR_GEN_VISIBILITY_MESSAGE = MSG_FontM_COLOR_VISIBLE;
}

@object GenBooleanGroupClass MixedBoolGroup = {
    GBGI_applyMsg = MSG_FontM_MIXMODE_CHANGED;
    GBGI_destination = FontMApp;
    GI_comp =
      @SettingsMixedColors;
}

@object GenTriggerClass RedrawTrigger6 = {
    GI_visMoniker = "ReCalc";
    GI_states = @default & ~GS_ENABLED;
    GTI_destination = EffectTextField;
    GTI_actionMsg = MSG_GEN_APPLY;
    HINT_SEEK_REPLY_BAR;
}

@object GenBooleanGroupClass SettingsBoolGroup = {
    GBGI_selectedBooleans = SETTINGS_LOADDEFAULTS;
    GBGI_applyMsg = MSG_FontM_SETTINGS_CHANGED;
    GBGI_destination = FontMApp;
    GI_comp =
      @SettingsLoadDefaults,
      @SettingsAutoRedraw,
      @SettingsGSOLComments;
}

@object GenInteractionClass DrawGridMode = {
    GI_visMoniker = 'D',"Draw";
    GII_visibility = GIV_POPUP;
    GI_comp =
      @GridDrawList;
}

@object GenInteractionClass CopyGridMode = {
    GI_visMoniker = 'b',"Copy to Clipboard";
    GII_visibility = GIV_POPUP;
    GI_comp =
      @GridExportList;
}

@object GenToolGroupClass EditToolGroup = {
    GTGI_controller = EditControl;
    HINT_MINIMIZE_CHILD_SPACING;
    HINT_SAME_ORIENTATION_AS_PARENT;
}

@object GenToolGroupClass ViewToolGroup = {
    GTGI_controller = ViewControl;
    HINT_MINIMIZE_CHILD_SPACING;
    HINT_SAME_ORIENTATION_AS_PARENT;
}

@object GenToolGroupClass FontToolGroup = {
    GTGI_controller = FontControl;
    HINT_MINIMIZE_CHILD_SPACING;
    HINT_SAME_ORIENTATION_AS_PARENT;
}

@object GenToolGroupClass SizeToolGroup = {
    /* This GenToolGroup object helps manage the tools within
     * a toolbox GenInteraction. */

    GTGI_controller = SizeControl;
    HINT_MINIMIZE_CHILD_SPACING;
    HINT_SAME_ORIENTATION_AS_PARENT;
		/* The above help determine the tool geometry. */
}

@object GenToolGroupClass StyleToolGroup = {
    /* This GenToolGroup object helps manage the tools within
     * a toolbox GenInteraction. */

    GTGI_controller = StyleControl;
    HINT_MINIMIZE_CHILD_SPACING;
    HINT_SAME_ORIENTATION_AS_PARENT;
		/* The above help determine the tool geometry. */
}

@object GenTriggerClass RedrawTrigger = {
    GI_visMoniker = "ReCalc!";
    GI_states = @default & ~GS_ENABLED;
    GTI_destination = EffectTextField;
    GTI_actionMsg = MSG_GEN_APPLY;
    ATTR_GEN_TRIGGER_IMMEDIATE_ACTION;
}

@object GenTextClass EffectTextField = {
    GTXI_maxLength = 60;
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
                                        /* only one line of text because effect
                                           doesn't wrap either */
    GTXI_applyMsg = MSG_FontM_TEXT_CHANGED;
    GTXI_destination = FontMApp;        /* notify app object of text changes */
    ATTR_GEN_TEXT_CHAR_ATTR = (ChunkHandle) @InitialCharAttr;
                                        /* initial attributes for object */
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;    /* take all the space in the group */
    HINT_TEXT_WHITE_WASH_COLOR;         /* white background, GeoBanner style */
    HINT_TEXT_ALLOW_SMART_QUOTES;       /* smart quotes permitted */
}

@ifdef ADD_INFOTEXT
@object GenTextClass InfoText = {
    GI_attrs = @default | GA_READ_ONLY;	/* cannot edit info text */
    GTXI_maxLength = 80;
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
}
@endif

@object GenEditControlClass EditControl = {
    /* we support nothing but COPY... */
    ATTR_GEN_CONTROL_PROHIBIT_UI = GECF_UNDO | GECF_DELETE | GECF_CUT |
                                   GECF_SELECT_ALL;
    ATTR_GEN_CONTROL_PROHIBIT_TOOLBOX_UI = GECF_UNDO | GECF_DELETE | GECF_CUT |
                                   	   GECF_SELECT_ALL;
    GCI_output = (TO_PROCESS);          /* send messages to process object */
}

@object GenInteractionClass EditOptions = {
  GI_comp =
    @LoadDefaults,
    @LoadStartup,
    @RedrawTrigger2;
}

@object GenInteractionClass CharacterTable = {
    GI_visMoniker = 'T',"Character Table";
    GII_visibility = GIV_DIALOG;
    GII_type = GIT_COMMAND;
    HINT_TOOLBOX;
    GI_comp =
      @CharTable;
}

@object GenInteractionClass ShadowOptions = {
    GI_visMoniker = 'S',"Shadow";
    GII_visibility = GIV_DIALOG;
    GII_type = GIT_COMMAND;
    ATTR_GEN_HELP_CONTEXT = "ShadowOptions";
    GI_comp =
      @ShadowType,
      @ShadowSettings,
      @ShadowDir,
      @RedrawTrigger3;
}

@object GenInteractionClass DistortionOptions = {
    GI_visMoniker = 't',"Distortion";
    GII_visibility = GIV_DIALOG;
    GII_type = GIT_COMMAND;
    ATTR_GEN_HELP_CONTEXT = "DistortionOptions";
    GI_comp =
      @EmbedBoolGroup,
    /* The options in the following interaction are only turned on
       when "embedded fonts" are use */
      @Distortion,
      @RedrawTrigger4;
}

@object GenInteractionClass EffectOpt = {
    /*
     * effect settings dialog box
     */
    GI_visMoniker = 'e',"Outline";
    GII_visibility = GIV_DIALOG;
    GII_type = GIT_COMMAND;
    ATTR_GEN_HELP_CONTEXT = "EffectOptions";
    HINT_ORIENT_CHILDREN_VERTICALLY;
    GI_comp =
      @OutlineThickness1,
      @OutlineThickness2,
      @OutlineProperties,
      @RedrawTrigger5;
}

@object GenInteractionClass ColorOpt = {
    /*
     * color settings dialog box
     */
    GI_visMoniker = 'C',"Color";
    GII_visibility = GIV_DIALOG;
    GII_type = GIT_COMMAND;
    ATTR_GEN_HELP_CONTEXT = "ColorOptions";
    GI_comp =
      @WhichItemGr,
      @ColorControl,
      @MixedBoolGroup,
      @RedrawTrigger6;
}

@object GenInteractionClass SettingsMenu = {
  GI_comp =
    @SettingsBoolGroup;
}

@object GenInteractionClass GridOptionsGroup = {
  GI_comp =
    @DrawGridMode,
    @CopyGridMode;
}

@object FontAttrControlClass AttrControl = {
    GI_visMoniker = 'C',"Character Attributes";
    GII_visibility = GIV_DIALOG;
    GII_type = GIT_PROPERTIES;          /* usually delayed application */
    GCI_output = FontMApp;              /* changes are handled by app object */
}

@object GenInteractionClass MainToolbox = {
    HINT_TOOLBOX;			/* This GenInteraction is a toolbox. */
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_ALLOW_CHILDREN_TO_WRAP;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
    HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY;

    GI_comp =
      @EditToolGroup,
      @ViewToolGroup,
      @FontToolGroup,
      @SizeToolGroup,
      @StyleToolGroup,
      @RedrawTrigger;
}

@object GenInteractionClass TextEntryField = {
    /*
     *              Text entry field
     */
    GII_visibility = GIV_SUB_GROUP;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;    /* take all the space you can get */
    HINT_ORIENT_CHILDREN_HORIZONTALLY;

    GI_comp = @EffectTextField;
}

@object GenViewClass FontMView = {
    GVI_attrs = (@default | GVA_CONTROLLED) & ~GVA_FOCUSABLE;
    GVI_horizAttrs = @default | GVDA_SCROLLABLE;
    GVI_vertAttrs  = @default | GVDA_SCROLLABLE;
    GVI_docBounds = {0,0,0,0};
    GVI_content = process;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
}

@ifdef ADD_INFOTEXT
@object GenInteractionClass InfoBox = {
    HINT_DRAW_IN_BOX;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    GI_comp =
      @InfoText;
}
@endif

@object GenInteractionClass EditMenu = {
    /*
     *              Edit menu
     */
    GI_attrs = @default | GA_KBD_SEARCH_PATH;
    GII_visibility = GIV_POPUP;
    ATTR_GEN_INTERACTION_GROUP_TYPE = GIGT_EDIT_MENU;
                                        /* place into edit menu */
    GI_comp =
      @EditControl,
      @EditOptions,
      @CharacterTable;
}

@object GenViewControlClass ViewControl = {
    /*
     *              View menu
     */
    GII_visibility = GIV_POPUP;         /* create popup menu */
    GVCI_maxZoom = 400;                 /* maximum zoom factor */
    ATTR_GEN_INTERACTION_GROUP_TYPE = GIGT_VIEW_MENU;
                                        /* place into view menu */
    ATTR_GEN_CONTROL_REQUIRE_UI = GVCF_BIG_ENLARGE | GVCF_REDRAW;
    ATTR_GEN_CONTROL_PROHIBIT_UI = GVCF_APPLY_TO_ALL;
    ATTR_GEN_CONTROL_PROHIBIT_TOOLBOX_UI = GVCF_APPLY_TO_ALL;
}

@object GenInteractionClass OptionsMenu = {
    /*
     *              Options Menu
     */
    GII_visibility = GIV_POPUP;
    ATTR_GEN_INTERACTION_GROUP_TYPE = GIGT_OPTIONS_MENU;

    GI_comp =
      @ShadowOptions,
      @DistortionOptions,
      @EffectOpt,
      @ColorOpt,
      @SettingsMenu,
      @GridOptionsGroup;
}

@object FontControlClass FontControl = {
    GI_visMoniker = 'n',"Fonts";
    GII_visibility = GIV_POPUP;
    GCI_output = FontMApp;              /* changes are handled by app object */
}

@object PointSizeControlClass SizeControl = {
    GI_visMoniker = 'z',"Sizes";
    GII_visibility = GIV_POPUP;
    GCI_output = FontMApp;              /* changes are handled by app object */
}

@object TextStyleControlClass StyleControl = {
    GI_visMoniker = 'S',"Styles";
    GII_visibility = GIV_POPUP;
    GCI_output = FontMApp;              /* changes are handled by app object */
    ATTR_GEN_CONTROL_PROHIBIT_UI = TSCF_BOXED | TSCF_BUTTON | TSCF_INDEX |
                                   TSCF_ALL_CAP | TSCF_SMALL_CAP |
/*
                                   TSCF_UNDERLINE | TSCF_STRIKE_THRU |
*/
                                   TSCF_SUBSCRIPT | TSCF_SUPERSCRIPT;
    ATTR_GEN_CONTROL_PROHIBIT_TOOLBOX_UI =
    				   TSCF_BOXED | TSCF_BUTTON | TSCF_INDEX |
    				   TSCF_ALL_CAP | TSCF_SMALL_CAP |
/*
                                   TSCF_UNDERLINE | TSCF_STRIKE_THRU |
*/
                                   TSCF_SUBSCRIPT | TSCF_SUPERSCRIPT;
}

@object GenInteractionClass AttrMenu = {
    /*
     *              Attributes Menu
     */
    GI_visMoniker = 'A',"Attributes";
    GII_visibility = GIV_POPUP;
    GI_comp =
      @AttrControl;
}

@object GenPrimaryClass FontMPrimary = {
    ATTR_GEN_HELP_CONTEXT = "TOC";
    HINT_SIZE_WINDOW_AS_RATIO_OF_FIELD = {SWSS_RATIO|PCT_80,SWSS_RATIO|PCT_50};

    /* Items in main window */
    GI_comp =
      @MainToolbox,
      @TextEntryField,
      @FontMView,
@ifdef ADD_INFOTEXT
      @InfoBox,
@endif
      @EditMenu,
      @ViewControl,
      @OptionsMenu,

      /* Style controllers (appear as menus and as toolboxes) */
      @FontControl,
      @SizeControl,
      @StyleControl,
      @AttrMenu;
}

@end Interface;

@start AppResource;
@object FontMApplicationClass FontMApp = {
    GI_visMoniker = @list{ @FontMTextMoniker,@Moniker0,@Moniker1,@Moniker2, @Moniker4 }
    ATTR_GEN_HELP_FILE = HELP_FILENAME; /* use custom help filename */
    gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_SELF_LOAD_OPTIONS)=
      @ColorControl,
      @EditControl,
      @AttrControl,
      @ViewControl,
      @FontControl,
      @SizeControl,
      @StyleControl;
    gcnList(MANUFACTURER_ID_GEOWORKS,MGCNLT_ACTIVE_LIST)=
      @ColorControl,
      @EditControl,
      @AttrControl,
      @ViewControl,
      @FontControl,
      @SizeControl,
      @StyleControl;
    gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_WINDOWS)=
      @FontMPrimary;

    GI_comp =
        @FontMPrimary;
}
@end AppResource;
