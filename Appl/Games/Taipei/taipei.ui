/*
 * Copyright (c) GeoWorks 1995 -- All Rights Reserved
 *
 * Project:	PC/GEOS
 * Module:	Taipei (Trivia project: PC/GEOS application)
 * File:	taipei.ui
 *
 * Author:	Jason Ho
 *
 *	This file contains the user Interface description for this
 * application.
 *
 * RCS STAMP:
 *
 *	$Id: taipei.ui,v 1.1 97/04/04 15:14:34 newdeal Exp $
 *
 */

/* ----------------------------------------------------------------------------
	Include files
-----------------------------------------------------------------------------*/

/* first, read pre-defined class definitions. See the file
 * /staff/pcgeos/Include/generic.uih. */

#include "generic.uih"

#include "Art/mktaipei.ui"

#define _JEDI	0
#define _GIVE_HINT -1 


/*---------------------------------------------------------------------------
		 Definition of the TaipeiContent class
---------------------------------------------------------------------------*/
class TaipeiContent = VisContent {
	/*
	 * We change the geometry-attributes of the content so that it will
	 * not attempt to organize its children using the geometry manager.
	 *
	 * What we are doing here is redefining the default value for
	 * a field of the instance data for VisCompClass so that when objects
	 * of TaipeiContentClass are created in a .ui file they will not get 
	 * the default value from VisCompClass, but will instead get
	 * this new value.
	 */
	default visCompGeoAttrs = customManageChildren;

	/*
	 * Here is our own instance data.
	 */
	tilesLeft = wordComp : INITIAL_NUMBER_OF_TILES;

	/*
	 * All tiles are present right now: 144 elements
	 */
	stillIn = typeComp "ByteArray" : "1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1,1";

#if _GIVE_HINT

	/*
	 * Assume no tiles are free right now: 42 different types
	 */
	numFreeTileOfType = typeComp "TileTypeArray" : "0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0,0";
	hintTimeElapsed = byteComp : 0;
#endif
	selectedTileHandle = wordComp : 0;
	selectedTileType = wordComp : 0;
	miscFlags = bitFieldComp byte { freePairFound, triedToCheat,
			cheating, cheated }: {};
	lastRemovedTile1Handle = wordComp : 0;
	lastRemovedTile2Handle = wordComp : 0;
	time = wordComp : 0;
	timerHandle = wordComp : 0;
}
/*---------------------------------------------------------------------------
		 Definition of the TaipeiApplication class
---------------------------------------------------------------------------*/
class TaipeiApplication = GenApplication {
}


/*----------------------------------------------------------------------------
			  Application Object
-----------------------------------------------------------------------------*/
/* The very top-level generic object of an application MUST be a GenApplication
 * object. The taipei.gp file contains "appobj" statement which indicates
 * that this "TaipeiApp" object is in fact the top-level UI object.
 * This object should be in its own resource so that GeoManager can load it
 * quickly to grab the icon for this application. GeoManager will store this
 * icon in its "Token Database", so that the icon image can be reused quickly.*/

start	AppResource;

TaipeiApp = TaipeiApplication {
	children = TaipeiPrimary;	/* The GenApplication object only
					 * has one child: the main application
					 * window. */

	/*
	 * GCN lists (General Change Notification lists) are the systems
	 * way of communicating information to people who are interested
	 * in it. If an object has an interest in certain notifications
	 * it adds itself to the appropriate list and the system will deliver
	 * notifications to the items on that list when it is appropriate.
	 *
	 * GAGCNLT_WINDOWS is the place for windowed objects that you want
	 * to appear when your application starts up.
	 */
	gcnList(MANUFACTURER_ID_GEOWORKS, GAGCNLT_WINDOWS) = TaipeiPrimary;

	moniker = list {
		TaipeiTextMoniker,
	   /*	TaipeiLCMoniker
	   */
		TaipeiAppMoniker,
		TaipeiTinyMoniker
	}
	hints = {
		ATTR_GEN_HELP_FILE { char "taipei", 0 }
	}
}

visMoniker TaipeiTextMoniker = "Taipei Mahjongg";

end	AppResource;


/* ----------------------------------------------------------------------------
	 Primary window.
-----------------------------------------------------------------------------*/
/* Every application has at least one GenPrimary object. This object serves
 * as the main window of the application. Inside this window, various UI
 * components, such as menus or lists, can be placed. */

start	Interface;		/* this resource is for misc UI objects */


TaipeiPrimary = GenPrimary {
	children =	

			GameMenu,
			MoveMenu,
			TaipeiView,
			StatusBar,
			TaipeiAboutBox,
			TaipeiNoCheatBox,
#if _GIVE_HINT
			NoLegalMoveBox,
#endif
			WinGameWithCheatBox,
			WinGameNoCheatBox;
	/*
	 * The game looks weird if the window is resized.
	 * All disabled :P
	 * jfh - had to put them back so that it would look
	 * right in both AUI and CUI :-P''''
	 */
	hints = {
	 	HINT_SIZE_WINDOW_AS_DESIRED, 
		HINT_CENTER_CHILDREN_HORIZONTALLY,
		HINT_CENTER_CHILDREN_VERTICALLY,
		HINT_FULL_JUSTIFY_CHILDREN_VERTICALLY,
		HINT_INCLUDE_ENDS_IN_CHILD_SPACING,
	  /*	HINT_DISPLAY_NOT_RESIZABLE,
		ATTR_GEN_DISPLAY_NOT_MAXIMIZABLE,*/
		ATTR_GEN_HELP_CONTEXT {
			char	"TOC", 0
		},
		HINT_CENTER_WINDOW
	}

}



end Interface;






/*-----------------------------------------------------------------------------
			Interface Definitions
-----------------------------------------------------------------------------*/

start Interface;


/*-----------------------------------------------------------------------------
			Menu Bar
-----------------------------------------------------------------------------*/
GameMenu = GenInteraction {
	moniker = 'G', "Game";
	children = GameOperationGroup;
	visibility = popup;
	hints = {
		ATTR_GEN_INTERACTION_GROUP_TYPE {
			GenInteractionGroupType GIGT_FILE_MENU
		},
	}
}

MoveMenu = GenInteraction {
	moniker = 'M', "Move";
	children = 	
			UndoMoveTrigger,
#if _NDO2000
			CheatMoveTrigger,
#endif
#if _GIVE_HINT
			HintMoveTrigger;
#endif
	visibility = popup;
}

/*-----------------------------------------------------------------------------
				GameOperationGroup
-----------------------------------------------------------------------------*/
GameOperationGroup = GenInteraction {
	type = organizational;
	children = 	
			NewGameTrigger,
			RestartGameTrigger;
		 /*	AboutGameTrigger; */
}

NewGameTrigger = GenTrigger {
	moniker = 'N', "New Game";
	action = MSG_TAIPEI_CONTENT_ANOTHER_GAME, TaipeiViewContent;
	kbdAccelerator = ctrl 'N';
	genAttributes = default +initiatesInputIgnore;

}

RestartGameTrigger = GenTrigger {
	moniker = 'R', "Restart Game";
	kbdAccelerator = ctrl 'R';
	genAttributes = default +initiatesInputIgnore;
	action = MSG_TAIPEI_CONTENT_RESTART_GAME, TaipeiViewContent;
}


/*AboutGameTrigger = GenTrigger {
	moniker = 'A', "About Game";
        action = MSG_GEN_INTERACTION_INITIATE, TaipeiAboutBox; 
        hints = {
                HINT_TRIGGER_BRINGS_UP_WINDOW
        }
}


/*-----------------------------------------------------------------------------
				About dialog box
-----------------------------------------------------------------------------*/

TaipeiAboutBox = GenInteraction {
	moniker = "Taipei Mahjongg";
	children = 	SpaceGlyph0,
			AboutBitmapGroup,
			AboutTextGroup;
	type = notification;
	visibility = dialog;
	attributes = default +modal, +initiatedViaUserDoDialog;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY
	}
}

SpaceGlyph0 = GenGlyph {
        moniker = " ";
}

AboutBitmapGroup = GenInteraction {
	type = organizational;
	children = 	AboutBitmapGlyph;
	hints = {
		HINT_CENTER_CHILDREN_VERTICALLY,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT
	}
}

AboutBitmapGlyph = GenGlyph {
	moniker = list {
			DragonCMoniker
		}
	/*
	 * DragonCMoniker found in taipeiBitmap.ui
	 */
}

AboutTextGroup = GenInteraction {
	type = organizational;
	children =	SpaceGlyph1,
			AboutTextGlyph1,
			AboutTextGlyph2,
			AboutTextGlyph3,
			AboutTextGlyph4,
			SpaceGlyph2;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_LEFT_JUSTIFY_CHILDREN
	}
}

SpaceGlyph1 = GenGlyph {
        moniker = " ";
}

SpaceGlyph2 = GenGlyph {
        moniker = " ";
}

/*
 * text:
 * 
 * Special thanks to
 * Jim Kirkpatrick
 * who draws those
 * beautiful tiles!
 */
AboutTextGlyph1 = GenGlyph {
	moniker = "    Special thanks to ";
}

AboutTextGlyph2 = GenGlyph {
	moniker = "    Jim Kirkpatrick ";
}

AboutTextGlyph3 = GenGlyph {
	moniker = "    who draws those ";
}

AboutTextGlyph4 = GenGlyph {
	moniker = "    beautiful tiles! ";
}


/*-----------------------------------------------------------------------------
				MoveMenu Items
-----------------------------------------------------------------------------*/
UndoMoveTrigger = GenTrigger {
	moniker = 'U', "Undo";
	kbdAccelerator = ctrl 'Z';
	action = MSG_TAIPEI_CONTENT_UNDO, TaipeiViewContent;
	genStates = default -enabled;
}

#if _GIVE_HINT
HintMoveTrigger = GenTrigger {
	moniker = 'H', "Hint";
	kbdAccelerator = ctrl 'H';
	action = MSG_TAIPEI_CONTENT_HINT, TaipeiViewContent;
	genAttributes = default +initiatesInputIgnore;

}
#endif /* _GIVE_HINT */

CheatMoveTrigger = GenTrigger {
	moniker = 'C', "Cheat";
	action = MSG_TAIPEI_CONTENT_NO_CHEAT, TaipeiViewContent;

	hints = {
		ATTR_GEN_TRIGGER_CUSTOM_DOUBLE_PRESS {
			word MSG_TAIPEI_CONTENT_CHEAT_NOW
		}
	}
}

/*---------------------------------------------------------------------------*/
TaipeiView = GenView {
	/*
	 * These attributes will ensure that the view and content remain
	 * the same size. They work with the attributes of the content.
	 */
	horizAttributes = default +noLargerThanContent,
				+noSmallerThanContent; 

	vertAttributes  = default +noLargerThanContent,
				+noSmallerThanContent; 

	/*
	 * The output is the object which is handling events in the view.
	 * In our case this is the content, which was really designed
	 * specifically for this task.
	 */
	output = TaipeiViewContent;

	backColorR = GAME_BACKGROUND;
	docRight = GAME_WIDTH; 
	docBottom = GAME_HEIGHT; 
}

/*-----------------------------------------------------------------------------
				No cheat dialog box
-----------------------------------------------------------------------------*/
TaipeiNoCheatBox = GenInteraction {
	moniker = "Cheat?";
	children = 	SpaceGlyph4,
			NoCheatTextGlyph1,
			SpaceGlyph5;
	type = notification;
	visibility = dialog;
	attributes = default +initiatedViaUserDoDialog; /* modal */
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_CENTER_CHILDREN_HORIZONTALLY
	}
}

SpaceGlyph4 = GenGlyph {
        moniker = " ";
}

SpaceGlyph5 = GenGlyph {
        moniker = " ";
}

NoCheatTextGlyph1 = GenGlyph {
	moniker = "    Try harder!    ";
}

/*-----------------------------------------------------------------------------
				No legal move dialog box
-----------------------------------------------------------------------------*/
#if _GIVE_HINT
NoLegalMoveBox = GenInteraction {
	moniker = "Hint";
	children =	
			SpaceGlyph11,
			NoLegalMoveTextGlyph1,
			SpaceGlyph12,
			NewGameTrigger1,
			QuitTrigger1
			;
	type = multipleResponse;
	visibility = dialog;
	attributes = default +initiatedViaUserDoDialog, +modal;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_CENTER_CHILDREN_HORIZONTALLY
	}
}

SpaceGlyph11 = GenGlyph {
        moniker = " ";
}

SpaceGlyph12 = GenGlyph {
        moniker = " ";
}
NoLegalMoveTextGlyph1 = GenGlyph {
	moniker = "    No more moves!    ";
}
NewGameTrigger1 = GenTrigger {
	moniker = 'N', "New Game";
	genAttributes = default +signalInteractionComplete;
	action = MSG_TAIPEI_CONTENT_ANOTHER_GAME, TaipeiViewContent;
	hints = {
		HINT_SEEK_REPLY_BAR
	}
}

QuitTrigger1 = GenTrigger {
	moniker = 'D', "Done";
	genAttributes = default +signalInteractionComplete;
	action = MSG_META_QUIT, TaipeiApp;
	hints = {
		HINT_SEEK_REPLY_BAR
	}
}


#endif

/*-----------------------------------------------------------------------------
				Win with Cheat dialog box
-----------------------------------------------------------------------------*/
WinGameWithCheatBox = GenInteraction {
	moniker = "Taipei Mahjongg";
	children = 	SpaceGlyph6,
			WinGameWithCheatTextGlyph1,
			SpaceGlyph7,
			NewGameTrigger2,
			QuitTrigger2;
	type = multipleResponse;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_CENTER_CHILDREN_HORIZONTALLY
	}
	visibility = dialog;
	attributes = default +initiatedViaUserDoDialog, +modal;
}

SpaceGlyph6 = GenGlyph {
        moniker = " ";
}
WinGameWithCheatTextGlyph1 = GenGlyph {
	moniker = "       You cheated, didn't you?       ";
}
SpaceGlyph7 = GenGlyph {
        moniker = " ";
}
NewGameTrigger2 = GenTrigger {
	moniker = 'N', "New Game";
	genAttributes = default +signalInteractionComplete;
	action = MSG_TAIPEI_CONTENT_ANOTHER_GAME, TaipeiViewContent;
	hints = {
		HINT_SEEK_REPLY_BAR
	}
}
QuitTrigger2 = GenTrigger {
	moniker = 'D', "Done";
	genAttributes = default +signalInteractionComplete;
	/* #if _JEDI */
/*	action = MSG_GEN_LOWER_TO_BOTTOM, TaipeiApp; */
	/* #else */
	action = MSG_META_QUIT, TaipeiApp;
	/* #endif */
	hints = {
		HINT_SEEK_REPLY_BAR
	}
}




/*-----------------------------------------------------------------------------
				Win with  noCheat dialog box
-----------------------------------------------------------------------------*/
WinGameNoCheatBox = GenInteraction {
	moniker = "Taipei Mahjongg";
	children = 	SpaceGlyph8,
			WinGameBitmapGroup,
			WinGameNoCheatTextGroup,
			NewGameTrigger3,
			QuitTrigger3;
	type = multipleResponse;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
	}
	visibility = dialog;
	attributes = default +initiatedViaUserDoDialog, +modal;
}

SpaceGlyph8 = GenGlyph {
        moniker = " ";
}

WinGameBitmapGroup = GenInteraction {
	type = organizational;
	children = 	WinGameBitmapGlyph;
	hints = {
		HINT_CENTER_CHILDREN_VERTICALLY,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT
	}
}

WinGameBitmapGlyph = GenGlyph {
	moniker = list {
		DragonCMoniker
	    /*	WinPictureMoniker  get rid of Bart */
	}
	/*
	 * WinPictureMoniker & DragonCMoniker found in taipeiBitmap.ui
	 */
}

WinGameNoCheatTextGroup = GenInteraction {
	type = organizational;
	children =	SpaceGlyph9,
			WinGameNoCheatTextGlyph1,
			SpaceGlyph10;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_LEFT_JUSTIFY_CHILDREN
	}
}
SpaceGlyph9 = GenGlyph {
        moniker = " ";
}
SpaceGlyph10 = GenGlyph {
        moniker = " ";
}





WinGameNoCheatTextGlyph1 = GenGlyph {
	moniker = "       Simply Incredible!       ";
}
NewGameTrigger3 = GenTrigger {
	moniker = 'N', "New Game";
	genAttributes = default +signalInteractionComplete;
	action = MSG_TAIPEI_CONTENT_ANOTHER_GAME, TaipeiViewContent;
	hints = {
		HINT_SEEK_REPLY_BAR
	}
}
QuitTrigger3 = GenTrigger {
	moniker = 'D', "Done";
	genAttributes = default +signalInteractionComplete;
	/* #if _JEDI */
/*	action = MSG_GEN_LOWER_TO_BOTTOM, TaipeiApp; */
	/* #else */
	action = MSG_META_QUIT, TaipeiApp;
	/* #endif */
	hints = {
		HINT_SEEK_REPLY_BAR
	}
}



/*-----------------------------------------------------------------------------
				StatusBar
-----------------------------------------------------------------------------*/

StatusBar = GenInteraction {
	children = TaipeiTilesCount, TimeDisplay;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY,
	   /*	HINT_LEFT_JUSTIFY_CHILDREN, */
		HINT_CENTER_CHILDREN_VERTICALLY,
	}
}


TaipeiTilesCount = GenValue {
	genStates = default +enabled;
	moniker = "Number of Tiles left:";
	minimum = 0;
	maximum = INITIAL_NUMBER_OF_TILES;
	value = INITIAL_NUMBER_OF_TILES;
	increment = 1;
	hints = {
		HINT_VALUE_NOT_INCREMENTABLE,
		HINT_VALUE_NOT_DIGITALLY_EDITABLE,
	}
}

TimeDisplay = GenInteraction ignoreDirty {
	children = TimeLabel, TimeValue;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
	}
}


TimeLabel = GenText ignoreDirty {
	textDisplayAttributes = default +singleLineText;
	text = "Time wasted: ";
	genAttributes = default +readOnly;

	hints = {
		HINT_MINIMUM_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, 6>
			SpecHeight <>
		}
	}
}

TimeValue = GenText {
	textDisplayAttributes = default +singleLineText;
	genAttributes = default +readOnly;

	hints = {
		HINT_MINIMUM_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, 7>
			SpecHeight <>
		}
	}
}

end Interface;

/*---------------------------------------------------------------------------*/
/*	  AppInterface is run by the main application thread		     */
/*---------------------------------------------------------------------------*/
start AppInterface;

TaipeiViewContent = TaipeiContent {
	/*
	 * A content is associated with a view. In this case the TaipeiView
	 * and the TaipeiViewContent as tied to one another.
	 */
	view	= TaipeiView;

	/*
	 * By making it the same width/height as the view we can leave out
	 * any work we might do to define the size of our document. It will
	 * simply be the size of the view.
	 */
	contentAttrs = default +viewDocBoundsSetManually;

	/* +sameWidthAsSubview,
				+sameHeightAsSubview; */
	/*
	 * doc size
	 *	viewHeight = 430;
	 *	viewWidth = 500;
	 */
	viewHeight = GAME_HEIGHT;
	viewWidth = GAME_WIDTH;
}

end AppInterface;


#include "taipeiConfig.ui"

#include "taipeiBitmap.ui"
