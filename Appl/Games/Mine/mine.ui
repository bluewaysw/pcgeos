/*
 * Project:	PC/GEOS
 * Module:	Mine (Sample PC/GEOS application)
 * File:	mine.ui
 *
 * Author:	Insik Rhee
 *
 *	This file contains the user Interface description for this
 * application. This file is written in a language called ESPIRE, which is
 * compiled by the UIC compiler. See Spec/User/uic.doc for info.
 *
 * RCS STAMP:
 *	$Id: mine.ui,v 1.1 97/04/04 14:51:50 newdeal Exp $
 *
 */

/* ----------------------------------------------------------------------------
	Include files
-----------------------------------------------------------------------------*/


#include "generic.uih"
#include "game.uih"
/*#include "Art/mkrMineApp.ui"
*/
#include "Art/mkmines.ui"

/* ----------------------------------------------------------------------------
	Definitions
-----------------------------------------------------------------------------*/


/* ----------------------------------------------------------------------------
	Class Definitions
-----------------------------------------------------------------------------*/

class	MineField = Vis {
	Width = byteComp:0;
	Height = byteComp:0;
	Mines = byteComp:0;
	MinesLeft = byteComp:0;
	GameState = byteComp:NEW_GAME;
}

class	MineApplication = GenApplication {
}
/* ----------------------------------------------------------------------------
	Application Object
-----------------------------------------------------------------------------*/

start	AppResource;

MineApp = MineApplication vardataReloc {
	children = MinePrimary;	
	gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_WINDOWS) = MinePrimary;
	active_list = MineLevel0HighScoreControl, MineLevel1HighScoreControl,
		      MineLevel2HighScoreControl, MineLevel3HighScoreControl;
	moniker = list {	
		MineTextMoniker, 
	  /*	MonikerLCMoniker,
		MonikerLMMoniker,
		MonikerSCMoniker,
		MonikerSMMoniker,
		MonikerLCGAMoniker,
		MonikerSCGAMoniker,
		MonikerYCMoniker,
		MonikerYMMoniker,
		MonikerTMMoniker,
		MonikerTCGAMoniker
	  */
		MinesAppMoniker,
		MinesTinyMoniker
	}
	gcnList	(MANUFACTURER_ID_GEOWORKS,GAGCNLT_STARTUP_LOAD_OPTIONS) =
			DifficultyList,
			SoundInteraction;
	hints = {
	    ATTR_GEN_INIT_FILE_CATEGORY {
			char	"MineSweeper",0
		},
	    ATTR_GEN_HELP_FILE {
		char "sweeper", 0
	    }
	}
}

visMoniker MineTextMoniker = "Mine Sweeper";

end	AppResource;

/* ----------------------------------------------------------------------------
	 Primary window.
-----------------------------------------------------------------------------*/

start	Interface; 

MineSummonsGroup = GenInteraction {
	type = organizational;
	attributes = notUserInitiatable;
	children =  MineLevel0HighScoreControl, MineLevel1HighScoreControl,
		    MineLevel2HighScoreControl, MineLevel3HighScoreControl;
}
MineLevel0HighScoreControl = HighScore {
	fileName = "Mine Beginner Hall of Fame";
	creator = {
		<'MINE'>, MANUFACTURER_ID_GEOWORKS
	}
	scoreType = ST_ELAPSED_TIME;
	hints = {
		ATTR_HIGH_SCORE_SCORE_TITLE_MONIKER {
			lptr TimeString
		},
		ATTR_GEN_CONTROL_PROHIBIT_UI {
			word	mask HSF_SHOW_HIGH_SCORES_TRIGGER
			}/*,
		ATTR_GEN_CONTROL_DO_NOT_USE_LIBRARY_NAME_FOR_HELP,
		ATTR_GEN_HELP_CONTEXT {
			char "show best", 0
		} */
	}
	highScoreTitle = "Beginner Hall of Fame";
	highScoreLine2 = "You have one of the best times";
	attrs = default +lowScoreIsBetter;
}
MineLevel1HighScoreControl = HighScore {
	fileName = "Mine Intermediate Hall of Fame";
	creator = {
		<'MINE'>, MANUFACTURER_ID_GEOWORKS
	}
	scoreType = ST_ELAPSED_TIME;
	hints = {
		ATTR_HIGH_SCORE_SCORE_TITLE_MONIKER {
			lptr TimeString
		},
		ATTR_GEN_CONTROL_PROHIBIT_UI {
			word	mask HSF_SHOW_HIGH_SCORES_TRIGGER
			}/*,
		ATTR_GEN_CONTROL_DO_NOT_USE_LIBRARY_NAME_FOR_HELP,
		ATTR_GEN_HELP_CONTEXT {
			char "show best", 0
		} */
	}
	highScoreTitle = "Intermediate Hall of Fame";
	highScoreLine2 = "You have one of the best times";
	attrs = default +lowScoreIsBetter;
}
MineLevel2HighScoreControl = HighScore {
	fileName = "Mine Expert Hall of Fame";
	creator = {
		<'MINE'>, MANUFACTURER_ID_GEOWORKS
	}
	scoreType = ST_ELAPSED_TIME;
	hints = {
		ATTR_HIGH_SCORE_SCORE_TITLE_MONIKER {
			lptr TimeString
		},
		ATTR_GEN_CONTROL_PROHIBIT_UI {
			word	mask HSF_SHOW_HIGH_SCORES_TRIGGER
			}/*,
		ATTR_GEN_CONTROL_DO_NOT_USE_LIBRARY_NAME_FOR_HELP,
		ATTR_GEN_HELP_CONTEXT {
			char "show best", 0
		} */
	}
	highScoreTitle = "Expert Hall of Fame";
	highScoreLine2 = "You have one of the best times";
	attrs = default +lowScoreIsBetter;
}
MineLevel3HighScoreControl = HighScore {
	fileName = "Mine Master Hall of Fame";
	creator = {
		<'MINE'>, MANUFACTURER_ID_GEOWORKS
	}
	scoreType = ST_ELAPSED_TIME;
	hints = {
		ATTR_HIGH_SCORE_SCORE_TITLE_MONIKER {
			lptr TimeString
		},
		ATTR_GEN_CONTROL_PROHIBIT_UI {
			word	mask HSF_SHOW_HIGH_SCORES_TRIGGER
			}/*,
		ATTR_GEN_CONTROL_DO_NOT_USE_LIBRARY_NAME_FOR_HELP,
		ATTR_GEN_HELP_CONTEXT {
			char "show best", 0
		} */
	}
	highScoreTitle = "Master Hall of Fame";
	highScoreLine2 = "You have one of the best times";
	attrs = default +lowScoreIsBetter;
}
chunk TimeString = "Time";
MinePrimary = GenPrimary {
	children = MineView,StatView,MineTriggerGroup,MineOptionGroup,MineSummonsGroup; 
	hints = { 
		  HINT_SIZE_WINDOW_AS_DESIRED,
		  HINT_CENTER_CHILDREN_HORIZONTALLY,
		  HINT_CENTER_CHILDREN_VERTICALLY,
		  HINT_FULL_JUSTIFY_CHILDREN_VERTICALLY,
		  HINT_INCLUDE_ENDS_IN_CHILD_SPACING,
		  ATTR_GEN_HELP_CONTEXT {
		      char "TOC", 0
		  }/*,
		  HINT_CENTER_WINDOW */
	}
}


MineTriggerGroup = GenInteraction {
	moniker = 'G',"Game";
	children = MineTriggerSubGroup;
	visibility = popup;
	hints = { ATTR_GEN_INTERACTION_GROUP_TYPE { GenInteractionGroupType GIGT_FILE_MENU } }
}
MineTriggerSubGroup = GenInteraction {
	children = StartTrigger,ShowHighScoresTrigger;
}

StartTrigger = GenTrigger {
	moniker='N',"Start New Game";
	kbdAccelerator = ctrl 'N';
	action = MSG_MINE_START_NEW_GAME, TheMineField; 
}
ShowHighScoresTrigger = GenTrigger {
	moniker = 'S',"Show Best Times";
	action = MSG_SHOW_HIGH_SCORES, process;
	hints = {HINT_TRIGGER_BRINGS_UP_WINDOW}
}

MineOptionGroup = GenInteraction {
	children = DifficultyList,
			 SoundSubGroup,
			 SaveSubGroup;
	visibility = popup;
	moniker = 'O', "Options";
	hints = {
		ATTR_GEN_INTERACTION_GROUP_TYPE  {
			byte	GIGT_OPTIONS_MENU
		}
	}
}

DifficultyList = GenItemGroup {
	behaviorType = exclusive;
	selection = 1;
	numSelections = 1;
	children = Level0, Level1, Level2, Level3;
	destination = TheMineField;
	applyMsg = MSG_MINE_SET_LEVEL;
	hints = {
		ATTR_GEN_INIT_FILE_KEY {
			char	"Level",0
		}
	}
}

Level0 = GenItem {
	moniker = 'B',"Beginner";
	identifier = 0;
}
Level1 = GenItem {
	moniker = 'I',"Intermediate";
	identifier = 1;
}
Level2 = GenItem {
	moniker = 'E',"Expert";
	identifier = 2;
}
Level3 = GenItem {
	moniker = 'M',"Master";  
	identifier = 3;
}

SoundSubGroup = GenInteraction {
	visibility = subGroup;
	children = SoundInteraction;
	hints = {
		ATTR_GEN_INIT_FILE_PROPAGATE_TO_CHILDREN
	}
}

SoundInteraction = GenInteraction {
	visibility = popup;
	moniker = 'S', "Sound";
	children = SoundList;
	hints = {
		ATTR_GEN_INIT_FILE_PROPAGATE_TO_CHILDREN
	}
}

SoundList = GenItemGroup {
	children = SoundOnEntry, SoundOffEntry, SoundSystemDefaultEntry;
	selection = "MS_SOUND_USE_SYSTEM_DEFAULT";
	numSelections = 1;
	hints = {
		ATTR_GEN_INIT_FILE_KEY {
			char	"Sound",0
		}
	}
}

SoundOnEntry = GenItem {
	moniker = 'S',"Sound On";
	identifier = "MS_SOUND_ON";
}

SoundOffEntry = GenItem {
	moniker = 'O',"Sound Off";
	identifier = "MS_SOUND_OFF";
}

SoundSystemDefaultEntry = GenItem {
	moniker = 'P', "Use System Preference";
	identifier = "MS_SOUND_USE_SYSTEM_DEFAULT";
}

SaveSubGroup = GenInteraction {
	visibility = subGroup;
	children = SaveOptionsTrigger;
}

SaveOptionsTrigger = GenTrigger {
	moniker = 'O', "Save Options";
	action = MSG_META_SAVE_OPTIONS, MineApp;
}

StatView = GenInteraction {
	children = TimeCount,GameStat,MineCount;
	hints = { 
		  HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		  HINT_ORIENT_CHILDREN_HORIZONTALLY,
		  HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY,
		  HINT_INCLUDE_ENDS_IN_CHILD_SPACING,
		  HINT_CENTER_CHILDREN_VERTICALLY
	}
}
TimeCount = GenInteraction {
	children = TimeGlyph;
	moniker = "Time:";
	hints = {
			HINT_BOTTOM_JUSTIFY_CHILDREN
	}
}
TimeGlyph = GenGlyph {
	hints = {
			HINT_FIXED_SIZE {
				SpecWidth <SST_AVG_CHAR_WIDTHS, 5>
				SpecHeight<>
			}
		}
}
GameStat = GenTrigger{
	moniker = "New Game";
	action = MSG_MINE_START_NEW_GAME, TheMineField;
}
MineCount = GenValue {
	genAttributes = readOnly;
	moniker = "Mines:";
	maximum = 180;
}

end Interface;

/*-----------------------------------------------------------------------------
	GenView object
-----------------------------------------------------------------------------*/

start Interface;

/* This GenView object creates a window where the application can display
 * portions of the document as necessary. As we want this window to be
 * scrollable, the specific UI (Motif) will create scroll bars which the
 * user can interact with. Whenever a portion of the window needs to be
 * redrawn, the GenView object will invalidate a portion of the window,
 * causing a MSG_META_EXPOSED to be sent to the application. The application
 * will draw the document into the window as requested. The window keeps
 * track of a mask, which is used to clip the application's drawing operations,
 * so that only the invalid portion of the window is drawn to. */

MineView = GenView {

	horizAttributes = noLargerThanContent,noSmallerThanContent;
	vertAttributes  = noLargerThanContent,noSmallerThanContent;
	docRight = 256;
	docBottom = 256;
	output = MineVisContent;
	inkType = GVIT_INK_WITH_STANDARD_OVERRIDE;
	viewAttributes=sameColorAsParentWin;
}

end Interface;

/*-----------------------------------------------------------------------------
	VisObjects
-----------------------------------------------------------------------------*/
start AppVisObjectResource;

MineVisContent = VisContent {
	view = MineView;
	children = TheMineField;
}
TheMineField = MineField{
	right = 256;
	bottom= 256;
}

end AppVisObjectResource;
