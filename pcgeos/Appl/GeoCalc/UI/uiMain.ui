/***********************************************************************
 *
 *	Copyright (c) Geoworks 1990 -- All Rights Reserved
 *
 * PROJECT:	  PCGEOS
 * MODULE:	  
 * FILE:	  uiMain.ui
 *
 * AUTHOR:  	  Gene Anderson: Feb  7, 1991
 *
 * REVISION HISTORY:
 *	Date	  Name	    Description
 *	----	  ----	    -----------
 *	2/ 7/91	  gene	    Initial version
 *
 * DESCRIPTION:
 *	UI description for GeoCalc
 *
 * 	$Id: uiMain.ui,v 1.4 98/04/28 10:58:01 joon Exp $
 *
 ***********************************************************************/

/******************************************************************************
 ******************************************************************************

------------------------------------------------------------------------------
		Organization
------------------------------------------------------------------------------

Since the user interface for Simple File will be large, it is broken down
into managable pieces, both in terms of '.ui' files and in terms of resources.
For the most part a file corresponds to a single resource.  These are the
pieces:

File		Description
----		-----------
uiMain.ui	App, primary, display control, file, edit & styles menus
uiDisplay.ui	DisplayUI

Resource	Description
--------	-----------
Interface	display control, main menus items
ApplicationUI	Application GenPrimary
DocumentUI	GenDocumentControl and all GenDocument's
MenuUI		All top level menus
DisplayUI	Duplicated to make displays
ContentUI	Duplicated to make contents

Name Prefix	Description
-----------	-----------
GeoCalc		A "one per application" object
Doc		A document or display related "one per application" object
Display		A "one per display" object run by the UI

******************************************************************************
******************************************************************************/

/*
 * Standard include file
 */

#include "generic.uih"
#include "impex.uih"

#include "spool.uih"
#include "math.uih"

#include "Objects/Text/tCtrl.uih"		/* includes ruler.uih */
#include "ssheet.uih"
#include "Objects/spline.uih"
#include "grobj.uih"
#include "chart.uih"
#include "Objects/SSheet/sCtrlC.uih"

#include "Internal/prodFeatures.uih"

/* Defining BULLET will cause the Bullet-specific changes to be assembled */

#define _BULLET 0

/******************************************************************************
 This is the default setting for all the flags for the regular GeoCalc. BY
 default, all these the following flags are "on". If you need any  of the
 following features, then you can set the particular flags "off". Jedi is
 one of the example.

 _CHARTS	:	If this flag is "on", GeoCalc will support charting
			features, and the user is able to create graphical
			objects.
 _USE_IMPEX	:	If this flag is "on", GeoCalc will support import and
			export functions.
 _PT_SIZE	:	If this flag is "on", the user is allowed to adjust
			the size of the fonts used in GeoCalc.
 _CELL_NOTE	:	If this flag is "on", the user can create cell note
			for any particular cell in GeoCalc.
 _HEADER_FOOTER	:	If this flag is "on", the user can create header and
			footer for the geocalc document.
 _VIEW_CTRL	:	If this flag is "on", the View menu will appear in the
			menu bar. If this flag is "off", there will be no View
			menu in the menu bar. "Go to..." function (in the View
			menu origially) is moved into the Cell menu.
 _TEXT_CTRL	:	If this flag is "on", the user can choose what fonts
			to use, set the styles and color of the font, set the
			char. attr. of the font.
 _BORDER_C	:	If this flag is "on", the user is able to set the color
			of the border of the cell.
 _TOOL_BAR	:	If this flag is "on", GeoCalc will provide tool bars.
 _LEVELS	:	If this flag is "on", the user is able to set the user
			level in GeoCalc.
 _WIN_MENU	:	If this flag is "on", there will be Window menu in the
			menu bar and GeoCalc supports Multiple document
			cotrol.

 _APP_NAME_IS_SPREADSHEET:
			If this if "on", strings containing the word "NewCalc"
			will instead use "Spreadsheet".  Not all cases where
			this should happen have necessarily been found.  If
			you find one, change it to use this flag.

******************************************************************************/

#define _CHARTS		-1
#define _USE_IMPEX	-1
#define _PT_SIZE	-1
#define _CELL_NOTE	-1
#define _HEADER_FOOTER	-1
#define _VIEW_CTRL	-1
#define _TEXT_OPTS	-1
#define _BORDER_C	-1
#define _TOOL_BAR	-1
#define _LEVELS		-1
#define _WIN_MENU	-1
#define _APP_NAME_IS_SPREADSHEET	-1
#define	_PROTECT_CELL	-1
#define _BITMAP_EDITING  0


#define	_NIKE	0
#define	_JEDI	0
#define _DOVE	0


#ifdef SUPER_IMPEX
#define _SUPER_IMPEX 1
#else
#define _SUPER_IMPEX 0
#endif


/* For now, split views is ON for every product, but this may change */

#define _SPLIT_VIEWS 1

#if DO_PIZZA
#define _SPLIT_VIEWS 0
#endif


/*
 * Our include files
 */

#include "uiGeoCalc.uih"
#include "uiGeoCalcDisplay.uih"
#include "uiGeoCalcDocument.uih"
#include "uiGeoCalcSpreadsheet.uih"
#include "uiGeoCalcContent.uih"
#include "uiGeoCalcView.uih"
#include "uiGeoCalcEditBar.uih"

/*****************************************************************************/

start ApplicationUI;

/*
 * Top level object
 */

GCAppObj = GeoCalcApplication vardataReloc {
	children = GeoCalcPrimary;
	gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_WINDOWS) = GeoCalcPrimary;
	gcnList(MANUFACTURER_ID_GEOWORKS,MGCNLT_APP_STARTUP) =
		GCDocumentControl;
	gcnList(MANUFACTURER_ID_GEOWORKS,MGCNLT_ACTIVE_LIST) =
		GCDocumentControl
		,GCDisplayControl
		,GCRulerShowControl
#if _TEXT_OPTS
		,GCTabControl
#endif
#if _VIEW_CTRL
		,GCViewControl
#endif
		;

	gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_APP_TARGET_NOTIFY_SPREADSHEET_DATA_RANGE_CHANGE) =
		GCAppObj;
	gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_STARTUP_LOAD_OPTIONS) =
		UserLevelList
#if _TOOL_BAR
		,ShowBarList
#endif

/* We want to TEMPORARILY customize tool bar */
/* #if		!_NIKE */
		,GCToolControl
/* #endif */

		;
	gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_SELF_LOAD_OPTIONS) =
		GCEditBarControl,
		GCDocumentControl,
		GCDisplayControl,
		GCSearchControl,
		GCEditControl,
		GCPrintControl,
#if _VIEW_CTRL
		GCViewControl,
#endif
#if _USE_IMPEX
		GCImportControl,
		GCExportControl,
#endif
		/* ssheet controllers */

		GCColumnWidthControl,
		GCRowHeightControl,
		GCSSBorderControl,
#if _BORDER_C
		GCSSBorderColorControl,
#endif
		GCRecalcControl,
		GCSSOptionsControl,
		GCFillControl,
#if _CELL_NOTE
		GCNoteControl,
#endif
		GCSSEditControl,
		GCSortControl,
#if _HEADER_FOOTER
		GCHeaderFooterControl,
#endif
		GCDefineNameControl,
		GCChooseNameControl,
		GCChooseFunctionControl,
#if	_CHARTS
		GCChartControl,
#endif
		/* text controllers */
#if _TEXT_OPTS
		GCTextStyleControl,
		GCTextFontControl,
#endif
#if _PT_SIZE
		GCTextSizeControl,
#endif
#if _TEXT_OPTS
		GCTextJustificationControl,
		GCTextFGColorControl,
		GCTextBGColorControl,
		GCFontAttrControl,
		GCLineSpacingControl,
		GCParaSpacingControl,
		GCParaBGColorControl,
		GCTextStyleSheetControl,
		GCMarginControl,
		GCTabControl,
		GCDefaultTabsControl,
		GCBorderControl,
#endif
		/* GCDropCapControl, */
		/* GCHyphenationControl, */

		/* chart controllers */
#if	_CHARTS
		GCChartTypeControl,
		GCChartGroupControl,
		GCChartAxisControl,
		GCChartGridControl,
		GCChartMarkerControl,

		/* grobj controllers */

		GCGrObjToolControl,
#if _BITMAP_EDITING
		GCGrObjBitmapToolControl,
#endif

		GCAreaColorControl,
#if DO_PIZZA
#else
		GCAreaAttrControl,
#endif
/* removed for Wizard
		GCGrObjObscureAttrControl,
*/
		GCLineColorControl,
		GCLineAttrControl,
		GCFlipControl,
		GCScaleControl,
		GCRotateControl,
		GCSkewControl,
		GCTransformControl,
		GCConvertControl,
		GCCustomShapeControl,
		GCDepthControl,
		GCNudgeControl,
		GCAlignDistributeControl,
		GCGroupControl,
		GCBackgroundColorControl,
		GCBackgroundAttrControl,
		GCStartingGradientColorControl,
		GCEndingGradientColorControl,
		GCGradientFillControl,
		GCArcControl,
#if _BITMAP_EDITING
		GCBitmapFormatControl,
#endif
		GCDefaultAttributesControl,
		GCDuplicateControl,
		GCPasteInsideControl,
		GCMoveInsideControl,
		GCMultiDuplicateControl,
		GCCustomDuplicateControl,
		GCHandleControl,
		GCHideShowControl,
		GCDraftModeControl,
		GCGrObjCreateControl,
		GCGrObjStyleSheetControl,
		GCGrObjObscureAttrControl2,
		GCGrObjLocksControl,

		GCInstructionControl,
		/* spline controllers */
		GCSplinePointControl,
		GCSplineOpenCloseControl,
		GCSplineSmoothnessControl,
#endif
		/* ruler controllers */
		GCRulerTypeControl,
		GCRulerShowControl,
		/* math controllers */
		GCFloatFormatControl;

	moniker = list {

#ifdef GPC
		GPCGeoCalcSCMoniker,
		GPCGeoCalcTCMoniker,
#else
		GeoCalcLCMoniker,
		GeoCalcLMMoniker,
		GeoCalcSCMoniker,
		GeoCalcSMMoniker,
		GeoCalcYCMoniker,
		GeoCalcYMMoniker,
		GeoCalcSCGAMoniker,
		GeoCalcTinyTCMoniker,
		GeoCalcTinyTMMoniker,
		GeoCalcTinyTCGAMoniker,
#endif

#if _APP_NAME_IS_SPREADSHEET
		"Spreadsheet";
#else
		"GeoCalc";
#endif

	}
	hints = {
		ATTR_GEN_INIT_FILE_CATEGORY {
			char	"geocalc", 0
		},
		ATTR_GEN_HELP_FILE {
		    char "spreadsh", 0
		},
		ATTR_GEN_APPLICATION_PRINT_CONTROL { UN_OPTR GCPrintControl }/*, */
/*#if _LEVELS  */
/*		ATTR_GEN_APPLICATION_SAVE_OPTIONS_TRIGGER {
			UN_OPTR SaveOptionsTrigger
		},
		ATTR_GEN_APPLICATION_RESET_OPTIONS_TRIGGER {
			UN_OPTR ResetOptionsTrigger
		} */
/*#endif */
	}
}

end ApplicationUI;


start AppSCMonikerResource, notDetachable;

visMoniker GCDatafileMonikerList = list {
#ifdef GPC
		GPCGeoCalcDocSCMoniker,
		GPCGeoCalcDocTCMoniker
#else
		GeoCalcDocLCMoniker,
		GeoCalcDocLMMoniker,
		GeoCalcDocSCMoniker,
		GeoCalcDocSMMoniker,
		GeoCalcDocYCMoniker,
		GeoCalcDocYMMoniker,
		GeoCalcDocSCGAMoniker
#endif
}

end AppSCMonikerResource;


/*****************************************************************************/

start Interface;

/****************************************************************************
 *	Main window
 ***************************************************************************/
GeoCalcPrimary = GenPrimary vardataReloc {
	children =
		FileMenu,
		GCEditControl,
#if _VIEW_CTRL
		ViewMenu,
#endif
		OptionsMenu,
		CellMenu,
		FormulaMenu,
#if	_CHARTS
		ChartMenu,
		GraphicsMenu,
#endif
		AttributesMenu,
		GCDisplayControl,
#if _TOOL_BAR
		GCGraphicsBar,
		GCFunctionBar,
		GCStyleBar,
#endif
		GCEditBarTools,
		GCDisplayArea;

	hints = {
		HINT_PRIMARY_FULL_SCREEN,
		ATTR_GEN_HELP_CONTEXT {
			char "TOC", 0
		},
		ATTR_GEN_PRIMARY_DONE_ON_CLOSE {
		    lptr 0
		    UN_OPTR GCDocumentControl
		    UN_OPTR GCDisplayGroup
		    UN_OPTR GCPrintControl
		    lptr 0, 0
		    lptr 0, 0
		    lptr 0, offset GCDoneText, offset GCDoneMultiText
		    lptr 0, 0
		}
	}
}

#if _APP_NAME_IS_SPREADSHEET
visMoniker GCDoneText = "Close the document and exit Spreadsheet.";
visMoniker GCDoneMultiText = "Close Spreadsheet documents and exit.";
#else
visMoniker GCDoneText = "Close the document and exit GeoCalc.";
visMoniker GCDoneMultiText = "Close GeoCalc documents and exit.";
#endif

/****************************************************************************
 *
 *	_DOVE UI
 *
 ***************************************************************************/



/****************************************************************************
 *	Display Control
 *		All open documents (GeoCalcDoc objects) are children
 *		of this object
 ***************************************************************************/
GCDisplayControl= GenDisplayControl {

#if !_WIN_MENU
	genStates = default -usable;
#endif

	visibility = popup;
	hints = { 
		ATTR_GEN_INTERACTION_GROUP_TYPE { 
			GenInteractionGroupType GIGT_WINDOW_MENU 
		},
		HINT_DISPLAY_CONTROL_NO_FEATURES_IF_TRANSPARENT_DOC_CTRL_MODE,
		HINT_GEN_CONTROL_SCALABLE_UI_DATA { GenControlScalableUIEntry <
		     GCSUIC_SET_NORMAL_FEATURES_IF_APP_LEVEL,
		     UIIL_INTRODUCTORY,
		     mask GDCTF_DISPLAY_LIST>,
		    <GCSUIC_SET_NORMAL_FEATURES_IF_APP_LEVEL,
		     UIIL_INTERMEDIATE,
		     GDC_DEFAULT_FEATURES>}
	}
}


GCDisplayArea = GenInteraction {
	visibility = subGroup;
	children =
#if _CHARTS && _TOOL_BAR
		GCDrawingToolsBar,
#if _BITMAP_EDITING
		GCBitmapToolsBar,
#endif
#endif
		GCDisplayGroup;

	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT
	}
}

GCDisplayGroup = GeoCalcDisplayGroup {
	hints = {
		HINT_DEFAULT_FOCUS,
		HINT_DEFAULT_TARGET,
		HINT_INITIAL_SIZE {
			SpecWidth <SST_PCT_OF_FIELD_WIDTH, PCT_70>,
			SpecHeight <SST_PCT_OF_FIELD_HEIGHT, PCT_55>,
			0
		},
		HINT_DISPLAY_GROUP_FULL_SIZED_IF_TRANSPARENT_DOC_CTRL_MODE
	}
}

end Interface;

/****************************************************************************/


#ifdef GPC
start AppSCMonikerResource, data;
#include "../Art/gpcGeoCalcSC.ui"
#include "../Art/gpcGeoCalcTC.ui"
#include "../Art/gpcGeoCalcDocSC.ui"
#include "../Art/gpcGeoCalcDocTC.ui"
end AppSCMonikerResource;
#else
#include "../Art/mkrGeoCalc.ui"
#include "../Art/mkrGeoCalcTiny.ui"
#include "../Art/mkrGeoCalcDoc.ui"
#endif

#include "uiDisplay.ui"
#include "uiContent.ui"

#include "uiFile.ui"
#include "uiPrint.ui"
#include "uiEdit.ui"
#include "uiOptions.ui"
#include "uiCell.ui"
#include "uiFormula.ui"
#include "uiChart.ui"
#include "uiAttributes.ui"
#include "uiZoom.ui"

#include "uiName.ui"
#include "uiTool.ui"
#include "uiGraphic.ui"

#include "uiStrings.ui"
#include "uiGraphicBody.ui"
#include "uiLevel.ui"

