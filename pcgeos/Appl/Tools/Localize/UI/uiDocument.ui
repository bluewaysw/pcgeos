/***********************************************************************
 *
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * PROJECT:	  PC GEOS
 * MODULE:	  ResEdit 
 * FILE:	  uiDocument.ui
 *
 * AUTHOR:  	  Cassie Hartzog, Sep 22, 1992
 *
 * REVISION HISTORY:
 *	Name	  Date	    Description
 *	----	  ----	    -----------
 *	CH	9/22/92	  Initial version
 *
 * DESCRIPTION:
 *	
 *	This file contains the user interface description for the documents.
 *
 * 	$Id: uiDocument.ui,v 1.1 97/04/04 17:13:44 newdeal Exp $
 *
 ***********************************************************************/

/* ----------------------------------------------------------------------------
        Document Control
-----------------------------------------------------------------------------*/

start FileMenuUI;

ResEditDocumentControl = ResEditGenDocumentControl {
	/*
	 * Document file's token
	 */
        documentTokenChars = "'T', 'R', 'N', 'S'";
        documentTokenID = "MANUFACTURER_ID_GEOWORKS";

	/*
	 * Special attributes: allow multiple open files
	 */
	dcAttributes = default +multipleOpenFiles;
	features = default +displayNameOnPrimary, -supportsTemplates,
		-supportsUserSettableEmptyDocument;

 	noNameText = "No Document";  

	appDC = ResEditDocumentGroup;
	enableDisableList = ResEditEnableList; 

#if !DO_DBCS
	exportGroup = ResEditExportInteraction;
	importGroup = ResEditImportInteraction;
#endif /* !DO_DBCS */
	dialogImportText = "Import an ASCII Translation File";

	dialogGroup = ResEditDialogGroup;

	children =
		ResEditBatchStatus,
		ResEditBatchInteraction,
		ResEditPathsInteraction;

	dialogNewText = "Create New Translation File";
	dialogOpenText = "Open a Translation File";

	hints = {
		ATTR_GEN_PATH_DATA {
			GenFilePath <SP_DOCUMENT, "ResEdit">
		}
	}
}

chunk ResEditEnableList = data {
	UN_OPTR EditMenu
	UN_OPTR FilterMenu
 	UN_OPTR UtilitiesMenu 
	UN_OPTR FileMenuInteraction
	UN_OPTR PrintSubGroup
}

ResEditDialogGroup = GenInteraction {
	genStates = default -usable;
	children =
		ResEditPathsGroup,
		ResEditBatchGroup;
}


/* ----- Paths Interaction -------------------------------------------------*/

#include "../Art/mkrDialogPathsIC.ui"
#include "../Art/mkrDialogPathsIM.ui"

ResEditPathsGroup = GenInteraction {
	children = 
		ResEditPathsTrigger,
		ResEditPathsTextGlyph;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
	}
}	

ResEditPathsTrigger = GenTrigger {
	action = MSG_GEN_INTERACTION_INITIATE, ResEditPathsInteraction;
        moniker = list {
		DialogPathsICMoniker,
		DialogPathsIMMoniker
	}
}

ResEditPathsTextGlyph = GenGlyph {
        moniker = "Set Source & Destination Directories";
}

ResEditPathsInteraction = GenInteraction {
        moniker = "Set Source & Destination Directories";
	type = multipleResponse;
	attributes = modal, notUserInitiatable;
	visibility = dialog;
	children =
		ResEditPathsInstructions,
		ResEditPathsGeodeDirGroup,
		ResEditPathsCloseTrigger;
	hints = {
		ATTR_GEN_HELP_FILE {char "resedit", 0},
		ATTR_GEN_HELP_CONTEXT {char "dbPaths", 0}
	}
}	

ResEditPathsInstructions = GenText {
	text = "In order to create translation files, ResEdit needs to know the source directory holding the GEOS executables (both .GEO & .VM files).  In order to generate a translated executable, ResEdit also needs to know where to store the result.\\\r\\\rClick on the buttons below to set or modify these directory settings.";
	genAttributes = default +readOnly;
}

ResEditPathsGeodeDirGroup = GenInteraction {
	children =
		ResEditPathsSourceTrigger, 
		ResEditPathsDestinationTrigger;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY
	}
}

ResEditPathsSourceTrigger = GenTrigger {
	moniker = "Set Source Directory";
	action = MSG_GEN_INTERACTION_INITIATE, SourceFileInteraction;
	hints = {
		HINT_TRIGGER_BRINGS_UP_WINDOW
	}
}

ResEditPathsDestinationTrigger = GenTrigger {
	moniker = "Set Destination Directory";
	action = MSG_GEN_INTERACTION_INITIATE, DestFileInteraction;
	hints = {
		HINT_TRIGGER_BRINGS_UP_WINDOW
	}
}

ResEditPathsCloseTrigger = GenTrigger {
	CLOSE_TRIGGER_VALUES
	hints = {
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND {
			word	IC_DISMISS
		}
	}
}


/* ----- Batch Interaction -------------------------------------------------*/

#include "../Art/mkrDialogBatchIC.ui"
#include "../Art/mkrDialogBatchIM.ui"

ResEditBatchGroup = GenInteraction {
	children = 
		ResEditBatchTrigger,
		ResEditBatchTextGlyph;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY
	}
}	

ResEditBatchTrigger = GenTrigger {
	action = MSG_GEN_INTERACTION_INITIATE, ResEditBatchInteraction;
        moniker = list {
		DialogBatchICMoniker,
		DialogBatchIMMoniker
	}
}

ResEditBatchInteraction = GenInteraction {
	moniker = "Run Batch Process";
	type = command;
	attributes = modal;
	visibility = dialog;
	children =
		ResEditBatchDirSelector,
		ResEditBatchGeodeDirGroup,
		ResEditBatchSaveBooleanGroup,
		ResEditBatchOptionList,
		ResEditBatchRunTrigger;
	hints = {
		ATTR_GEN_HELP_FILE {char "resedit", 0},
		ATTR_GEN_HELP_CONTEXT {char "dbBatch", 0}
	}
}	

ResEditBatchTextGlyph = GenGlyph {
        moniker = "Run a Batch Process";
}

ResEditBatchGeodeDirGroup = GenInteraction {
	children =
		ResEditBatchSourceTrigger, 
		ResEditBatchDestinationTrigger;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY
	}
}

ResEditBatchSourceTrigger = GenTrigger {
	moniker = "Set Source Directory";
	action = MSG_GEN_INTERACTION_INITIATE, SourceFileInteraction;
	hints = {
		HINT_TRIGGER_BRINGS_UP_WINDOW
	}
}

ResEditBatchDestinationTrigger = GenTrigger {
	moniker = "Set Destination Directory";
	action = MSG_GEN_INTERACTION_INITIATE, DestFileInteraction;
	hints = {
		HINT_TRIGGER_BRINGS_UP_WINDOW
	}
}

ResEditBatchDirSelector = GenFileSelector vardataReloc {
	moniker = "Select directory containing translation files:";
	fileCriteria = default +dirs;
	selection = 1;
	attributes = default +allowChangeDirs;
	hints = {
		HINT_IS_POPUP_LIST,
		HINT_PLACE_MONIKER_ABOVE,
		HINT_ITEM_GROUP_SCROLLABLE,
		HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, 24>
			SpecHeight <>
		},
		ATTR_GEN_PATH_DATA {
			GenFilePath <SP_DOCUMENT, "RESEDIT">
		}
	}
}

ResEditBatchOptionList = GenItemGroup vardataReloc {
	behaviorType = exclusiveNone;
	destination = "TO_APP_MODEL";
	selection = MSG_RESEDIT_DOCUMENT_CREATE_EXECUTABLE;
	applyMsg = MSG_RESEDIT_DOCUMENT_SET_CHUNK_STATE_FILTERS;
	children = 
		ResEditBatchOptionTranslatedGeodes, 
		ResEditBatchOptionPatchFiles, 
		ResEditBatchOptionPatchAndNull;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		ATTR_GEN_DESTINATION_CLASS { UN_DD ResEditDocumentClass }
	}
}

ResEditBatchOptionTranslatedGeodes = GenItem {
	moniker = 'G', "Create Translated Geodes";
	identifier = MSG_RESEDIT_DOCUMENT_CREATE_EXECUTABLE;
}

ResEditBatchOptionPatchFiles = GenItem {
	moniker = 'P', "Create Patch Files";
	identifier = MSG_RESEDIT_DOCUMENT_CREATE_PATCH_FILE;
}

ResEditBatchOptionPatchAndNull = GenItem {
	moniker = 'N', "Create Null Geodes and Patch Files";
	identifier = MSG_RESEDIT_DOCUMENT_CREATE_NULL_PATCH_FILE;
}

ResEditBatchSaveBooleanGroup = GenBooleanGroup {
	children = 
		ResEditBatchSaveBoolean,
		ResEditBatchForceUpdateBoolean;
	selectedBooleans = "mask BF_SAVE_TRANSLATION_FILES";
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY
	}
}

ResEditBatchSaveBoolean = GenBoolean {
	moniker = 'S', "Save Translation Files (precludes later reverting)";
	identifier = "mask BF_SAVE_TRANSLATION_FILES";
}

ResEditBatchForceUpdateBoolean = GenBoolean {
	moniker = 'U', "Force Update of Translation Files";
	identifier = "mask BF_FORCE_UPDATE";
}

ResEditBatchRunTrigger = GenTrigger {
	moniker = "Run";
	genAttributes = default +signalInteractionComplete;
	action = MSG_RESEDIT_RUN_BATCH_JOB, process;
	hints = { 
		HINT_SEEK_REPLY_BAR,
		HINT_DEFAULT_FOCUS,
		HINT_DEFAULT_TARGET,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND { word IC_OK }
	}
}


/**************************************************************************/
/**** BATCH STATUS DIALOG *************************************************/

ResEditBatchStatus = GenInteraction {
	moniker = "Batch Status";
	visibility = dialog;
	attributes = default +notUserInitiatable, +modal;
	type = notification; 
	children =
		ResEditBatchTopGroup,
		ResEditBatchStatusText,
		ResEditBatchOKTrigger,
		ResEditBatchCancelTrigger;
	hints = {
		ATTR_GEN_DESTINATION_CLASS { UN_DD ResEditDocumentClass }
	}
}

ResEditBatchTopGroup = GenInteraction {
	children =
		ResEditBatchCurrentFileName,
		ResEditBatchFileNumberGroup,
		ResEditBatchResourceNumberGroup;
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_CENTER_CHILDREN_ON_MONIKERS
	}
}


/**** CURRENT FILE NAME ****/

ResEditBatchCurrentFileName = GenText {
	moniker = "Current file:";
	genAttributes = default +readOnly;
	textDisplayAttributes = default +singleLineText;
	text = "<none>";
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}


/**** CURRENT FILE NUMBER GROUP ****/

ResEditBatchFileNumberGroup = GenInteraction {
	moniker = "Processing file:";
	children =
		ResEditBatchCurrentFileNumber,
		ResEditBatchTotalFileNumber;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY
	}
}

ResEditBatchCurrentFileNumber = GenValue {
	value = 0;
	maximum = 999;
	genAttributes = default +readOnly;
}

ResEditBatchTotalFileNumber = GenValue {
	moniker = "of";
	value = 0;
	maximum = 999;
	genAttributes = default +readOnly;
}


/**** CURRENT RESOURCE NUMBER GROUP ****/

ResEditBatchResourceNumberGroup = GenInteraction {
	moniker = "Translating resource:";
	children =
		ResEditBatchCurrentResourceNumber,
		ResEditBatchTotalResourceNumber;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY
	}
}

ResEditBatchCurrentResourceNumber = GenValue {
	value = 0;
	maximum = 999;
	genAttributes = default +readOnly;
}
	
ResEditBatchTotalResourceNumber = GenValue {
	moniker = "of";
	value = 0;
	maximum = 999;
	genAttributes = default +readOnly;
}


/**** STATUS TEXT BOX  ****/

ResEditBatchStatusText = GenText {
	genAttributes = default +readOnly;
	textDisplayAttributes = default +initScrolling;
	hints = {
		HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, 60>
			SpecHeight <SST_LINES_OF_TEXT, 20>
		}
	}	
}

/**** TRIGGERS  ****/

ResEditBatchOKTrigger = GenTrigger {
	moniker = "OK";
	genAttributes = default +signalInteractionComplete;
	action = MSG_RESEDIT_APPLICATION_END_BATCH,
		ResEditApp;
	hints = { 
		HINT_SEEK_REPLY_BAR,
		HINT_DEFAULT_FOCUS,
		HINT_DEFAULT_TARGET,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND { word IC_OK }
	}
}

ResEditBatchCancelTrigger = GenTrigger {
	moniker = "Cancel";
	action = MSG_RESEDIT_APPLICATION_SET_BATCH_MODE,
		ResEditApp;
	hints = { 
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_TRIGGER_ACTION_DATA {
			byte	BM_CANCELLED
		}
	}
}

chunk ResEditBatchStartText = "Beginning batch process.";
chunk ResEditBatchOpenDocumentText = "Handling ";
chunk ResEditBatchUpdateText = "Updating translation file with ";
chunk ResEditBatchNoChange = "No translatable chunks changed.";
chunk ResEditBatchChunksChanged = " chunk(s) changed.";
chunk ResEditBatchChunksDeleted = " chunk(s) deleted.";
chunk ResEditBatchChunksAdded = " chunk(s) added.";
chunk ResEditBatchTempGeodeCreated = "Temporary geode successfully created.";
chunk ResEditBatchTempGeodeError = "Error in creating temporary geode.";
chunk ResEditBatchGeodeCreateError = "Error in creating destination geode.";
chunk ResEditBatchOpenTranslationError = "Error opening translation file.";
chunk ResEditBatchOpenLocalizationError = "Error opening localization file.";
chunk ResEditBatchNotTranslationFileError = "This file is not a translation file.";
chunk ResEditBatchEndText = "Batch process completed.";

end FileMenuUI;

/* ----------------------------------------------------------------------------
        Document Group
-----------------------------------------------------------------------------*/

/*
 * The GenDocumentGroup should be put in a resource of its own run by
 * the application thread.
 */

start AppDocUI;

ResEditDocumentGroup = GenDocumentGroup {
	/* 
	 * Name for untitled documents
  	 */
#if DO_DBCS
	defaultName = "New Trans";
#else
	defaultName = "Translation Untitled";
#endif
	/*
	 * The UI's document control object
	 */
	uiDC = ResEditDocumentControl;
 	/*
	 * Type of object to instantiate
	 */
	documentClass = ResEditDocumentClass;
	/*
	 * Document protocol
	 */
	protocolMajor = "DOCUMENT_PROTOCOL_MAJOR";
	protocolMinor = "DOCUMENT_PROTOCOL_MINOR";
	/* 
	 * GenDisplay to automatically duplicate, GenDisplayGroup to
	 * attach duplicated displays to and GenView to attach
	 * translation files to.
	 */
	genDisplayGroup = ResEditDisplayGroup;
	genDisplay = MainDisplay;
 	genView = RightView; 
}

end AppDocUI;

