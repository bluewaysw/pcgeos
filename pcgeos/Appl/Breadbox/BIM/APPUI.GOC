/**********************************************************************
 * APPUI.GOC - User Interface for AIM                                 *
 * Project - AIM Application                                          *
 * Started 12/27/98                                                   *
 **********************************************************************/

/**********************************************************************
 * Includes                                                           *
 **********************************************************************/

@include <stdapp.goh>
@include <Objects/clipbrd.goh>
@include "aim.goh"
@include <ExtUI/Tree.goh>
@include <parentc.goh>

@include "tocproto.goh"
@include "toccfg.goh"

//@include "Art/appicon1.goh"
//@include "Art/appicon2.goh"
@include "Art/bimicons.goh"

/**********************************************************************
 * AppResource                                                        *
 **********************************************************************/

@start AppResource;

@object GenApplicationClass AIMApp = {
    GI_visMoniker = list {@AIMMonikerText, @AIMAppLCMoniker, /*@AIMAppLMMoniker,
      @AIMAppSCMoniker, @AIMAppSMMoniker, @AIMAppLCGAMoniker,
		@AIMAppSCGAMoniker, */@AIMAppTCMoniker/*, @AIMAppTMMoniker,
      @AIMAppTCGAMoniker */};

    GI_comp = @AIMPrimary, @LoginDialog, @ConnectDialog, @EditControl
@ifdef SHOW_MESSAGES
        , @MessagePrimary
@endif
        ;
    gcnList(MANUFACTURER_ID_GEOWORKS, GAGCNLT_WINDOWS) = @AIMPrimary, 
      @LoginDialog
@ifdef SHOW_MESSAGES
        , @MessagePrimary
@endif
        ;
    GI_comp = @AIMPrimary, @LoginDialog, @ConnectDialog, @EditControl;
    gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_SELF_LOAD_OPTIONS) =
      @EditControl;
    gcnList(MANUFACTURER_ID_GEOWORKS,GAGCNLT_STARTUP_LOAD_OPTIONS) =
      @OptionsItems;
    ATTR_GEN_HELP_FILE = "BIMHelp";
}

@visMoniker AIMMonikerText = "Instant Messenger";

@end AppResource;

/**********************************************************************
 * StringsResource                                                    *
 **********************************************************************/

@start StringsResource, data;
@chunk char CantConnect[] = "Can't connect to server. Exiting.";
@chunk char Connected[] = "Connected to server.";
@end StringsResource;

/**********************************************************************
 * Interface                                                          *
 **********************************************************************/

@start Interface;

@object GenPrimaryClass AIMPrimary = {
    GI_states = @default & ~GS_USABLE;
@ifdef USE_SIMULATOR
	 GI_comp = @FileMenu, @OptionsMenu, @MainArea, @SimulatorDialog, @PCRoot;
@else
	 GI_comp = @FileMenu, @OptionsMenu, @MainArea, @PCRoot;
@endif
	 ATTR_GEN_HELP_CONTEXT = "TOC";
    ATTR_GEN_DISPLAY_NOT_MAXIMIZABLE;
    HINT_ORIENT_CHILDREN_VERTICALLY;
    HINT_PRIMARY_NO_FILE_MENU;
    HINT_SIZE_WINDOW_AS_DESIRED;
}

@object ParentalControlClass PCRoot = {
}

/********************** File Menu **********************/

@object GenInteractionClass FileMenu =
{
    GI_comp = @AboutGroup;
    GII_visibility = GIV_POPUP;
    ATTR_GEN_INTERACTION_GROUP_TYPE = (GIGT_FILE_MENU);
}

/********************** Options Menu **********************/

@object GenInteractionClass OptionsMenu =
{
    GI_visMoniker = 'O', "Options";
    GI_comp = @BuddyConfigDialog, @OptionsGroup;
    GII_visibility = GIV_POPUP;
    HINT_SEEK_MENU_BAR;
}

@object GenInteractionClass OptionsGroup =
{
    GI_comp = @OptionsItems;
}

@object GenBooleanGroupClass OptionsItems =
{
	 GI_comp = @OptionsSound, @OptionsFocus;
	 GBGI_selectedBooleans = AIM_DEFAULT_OPTIONS;
	 ATTR_GEN_INIT_FILE_CATEGORY = AIM_INIT_CATEGORY_AIM;
	 ATTR_GEN_INIT_FILE_KEY = "options";
}

@object GenBooleanClass OptionsSound =
{
	 GI_visMoniker = 'E', "Enable sounds";
	 GBI_identifier = AIM_OPTIONS_SOUND;
}

@object GenBooleanClass OptionsFocus =
{
	 GI_visMoniker = 'r', "Bring new messages to front";
	 GBI_identifier = AIM_OPTIONS_FOCUS;
}

/********************************************/

@object GenInteractionClass MainArea =
{
    GI_comp = @BuddyList;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
    /* Use this on NDO motif for title fit: */
    HINT_INITIAL_SIZE = { SST_AVG_CHAR_WIDTHS | 35,
	  SST_LINES_OF_TEXT | 15, 0 };
	/* Use this on GPC for title fit:
    HINT_INITIAL_SIZE = { SST_AVG_CHAR_WIDTHS | 26,
	  SST_LINES_OF_TEXT | 15, 0 }; */
}

@ifdef SHOW_MESSAGES
@object GenPrimaryClass MessagePrimary = {
    GI_comp = @MessageText;
    HINT_PRIMARY_NO_FILE_MENU;
    HINT_DISPLAY_MINIMIZED_ON_STARTUP;
}

@object GenTextClass MessageText = {
    GI_attrs = @default | GA_TARGETABLE;
    GTXI_attrs = @default | GTA_INIT_SCROLLING;
    GTXI_maxLength = 10000;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
}
@endif

@object GenEditControlClass EditControl = {
/*  GII_visibility = GIV_POPUP;
    ATTR_GEN_INTERACTION_GROUP_TYPE = GIGT_EDIT_MENU;

    ATTR_GEN_CONTROL_PROHIBIT_UI = GECF_UNDO;
    ATTR_GEN_CONTROL_PROHIBIT_TOOLBOX_UI = GECTF_UNDO;*/
}

/********************** About dialog box **********************/

@object GenInteractionClass AboutGroup = {
	GI_comp = @AboutDBox;
	GII_visibility = GIV_SUB_GROUP;
}

@include "bbxreg.goh"

@object GenInteractionClass AboutDBox = {
	 GI_visMoniker = 'b', "About";
	 GI_comp = @AppName, @By, @BreadBoxLogo,
				  @Visit, @Web, @AboutSpacer1,
				  @Copyright, @BBoxLLC, @Rights, @AboutSpacer2;
	 GII_visibility = GIV_DIALOG;
	 GII_type = GIT_NOTIFICATION;
	 HINT_CENTER_CHILDREN_HORIZONTALLY;
}
@object GenTextClass AppName = {
    GI_attrs = @default | GA_READ_ONLY;
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
	 GTXI_text = "Instant Messenger";
    ATTR_GEN_TEXT_CHAR_ATTR = (ChunkHandle) @AppNameCharAttrs;
}
@chunk VisTextCharAttr AppNameCharAttrs =
	CHAR_ATTR_FONT_SIZE_STYLE(
		FID_DTC_URW_SANS, 36, TS_BOLD);

@object GenGlyphClass By = {
  GI_visMoniker = "by";
}
@object GenGlyphClass BreadBoxLogo = {
  GI_visMoniker = list { @BreadboxLogoMoniker}
}
@object GenGlyphClass Visit = {
  GI_visMoniker = "Visit us at...";
}
@object GenGlyphClass Web = {
  GI_visMoniker = "www.breadbox.com";
}
@object GenGlyphClass AboutSpacer1 = {
  GI_visMoniker = "   ";
}
@object GenGlyphClass Copyright = {
  GI_visMoniker = "Copyright 1994-2002";
}
@object GenGlyphClass BBoxLLC = {
  GI_visMoniker = "Breadbox Computer Company LLC";
}
@object GenGlyphClass Rights = {
  GI_visMoniker = "All Rights Reserved";
}
@object GenGlyphClass AboutSpacer2 = {
  GI_visMoniker = "   ";
}

@end Interface;

/**********************************************************************
 * Login Dialog                                                       *
 **********************************************************************/

@start LoginResource;

@object GenInteractionClass LoginDialog = {
    GI_visMoniker = list { @LoginTitle };
	 GI_comp = @SignonNamePasswordGroup, @SignonRememberGroup,
				  @SignonStartButton, @SignonExitButton;
    GII_visibility = GIV_DIALOG;
    GII_attrs = @default | GIA_NOT_USER_INITIATABLE | GIA_MODAL;
    ATTR_GEN_HELP_CONTEXT = /*"Connecting"*/"TOC";
	 HINT_CENTER_CHILDREN_HORIZONTALLY;
}

@object GenInteractionClass SignonNamePasswordGroup = {
    GI_comp = @SignonScreenName, @SignonPassword;
	 HINT_RIGHT_JUSTIFY_CHILDREN;
}

@object GenTextClass SignonScreenName = {
    GI_visMoniker = "Screen Name";
    GTXI_maxLength = MAX_USER_NAME_LEN;
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    ATTR_GEN_TEXT_ALPHA_NUMERIC;
    ATTR_GEN_TEXT_ALLOW_SPACES;
    HINT_DEFAULT_FOCUS;
}

@object GenTextClass SignonPassword = {
    GI_visMoniker = "Password";
    GTXI_maxLength = MAX_PASSWORD_LEN;
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    ATTR_GEN_TEXT_CHAR_ATTR = (ChunkHandle) @PasswordTextAttrs;
    HINT_FIXED_SIZE = { 0, SST_LINES_OF_TEXT | 1, 0 } ;
}

@chunk VisTextCharAttr PasswordTextAttrs =
    CHAR_ATTR_FONT_SIZE(FID_BISON, 13);

@object GenBooleanGroupClass SignonRememberGroup = {
	 GI_comp = @RememberLoginName, @RememberLoginPword;
	 ATTR_GEN_INIT_FILE_CATEGORY = AIM_INIT_CATEGORY_AIM;
	 ATTR_GEN_INIT_FILE_KEY = "login";
    HINT_CENTER_CHILDREN_HORIZONTALLY;
}

@object GenBooleanClass RememberLoginName = {
	 GI_visMoniker = "Remember name";
	 GBI_identifier = AIM_LOGIN_NAME;
}

@object GenBooleanClass RememberLoginPword = {
	 GI_visMoniker = "Remember password";
	 GBI_identifier = AIM_LOGIN_PWORD;
}

@object GenTriggerClass SignonStartButton = {
	 GI_visMoniker = 'S', "Sign on";
    GTI_destination = process;
    GTI_actionMsg = MSG_AIM_START_LOGIN;
//    ATTR_GEN_TRIGGER_INTERACTION_COMMAND = IC_OK;
    HINT_SEEK_REPLY_BAR;
    HINT_DEFAULT_DEFAULT_ACTION;
}

@object GenTriggerClass SignonExitButton = {
    GI_visMoniker = 'x', "Exit";
    GTI_destination = @AIMApp;
    GTI_actionMsg = MSG_META_QUIT;
    HINT_SEEK_REPLY_BAR;
}

/**********************************************************************
 * Connect Dialog                                                     *
 **********************************************************************/

@object GenInteractionClass ConnectDialog =
{
    GI_visMoniker = list { @LoginTitle };
    GI_comp = @ConnectSpacer, @ConnectCancel;
    GII_visibility = GIV_DIALOG;
    GII_attrs = @default | GIA_NOT_USER_INITIATABLE | GIA_MODAL;
}

@object GenInteractionClass ConnectSpacer =
{
    GI_comp = @ConnectStatus;
    HINT_CENTER_CHILDREN_VERTICALLY;
    HINT_CENTER_CHILDREN_HORIZONTALLY;
    HINT_FIXED_SIZE = { SST_AVG_CHAR_WIDTHS | 40,
	  SST_LINES_OF_TEXT | 2, 0 };
}

@object GenGlyphClass ConnectStatus = {}

@object AimCancelTriggerClass ConnectCancel =
{
    GI_visMoniker = 'C', "Cancel";
    HINT_SEEK_REPLY_BAR;
}

@visMoniker LoginTitle = "Sign on to Instant Messenger";

@visMoniker TocCSConnecting = "1. Connecting...";
@visMoniker TocCSAuth = "2. Verifying name and password...";
@visMoniker TocCSConfig = "3. Loading configuration...";

@end LoginResource;

/**********************************************************************
 * Buddy List
 **********************************************************************/

@start Interface;

@ifndef USE_TREE

@object GenSimpleTreeListClass BuddyList =
{
    GDLI_queryMsg = MSG_AIM_QUERY_BUDDY_LIST;
    ATTR_GEN_ITEM_GROUP_CUSTOM_DOUBLE_PRESS = MSG_AIM_BUDDY_LIST_ACTION;
    GIGI_destination = process;
    GIGI_behaviorType = GIGBT_EXCLUSIVE;
    HINT_ITEM_GROUP_SCROLLABLE;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
}

@else

@object ExtUITreeClass BuddyList =
{
    EUITI_columnDesc = ChunkOf(@BuddyColumnDesc);
    EUITI_destination = process;
    EUITI_drawMsg = MSG_AIM_DRAW_BUDDY_LIST;
    EUITI_infoMsg = MSG_AIM_INFO_BUDDY_LIST;
    EUITRI_attachMsg = MSG_AIM_ATTACH_BUDDY_LIST;
    EUITRI_detachMsg = MSG_AIM_DETACH_BUDDY_LIST;
    
    ATTR_GEN_ITEM_GROUP_CUSTOM_DOUBLE_PRESS = MSG_AIM_BUDDY_LIST_ACTION;
    HINT_ITEM_GROUP_SCROLLABLE;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
}

@chunk ExtUITableColumn BuddyColumnDesc[] =
{
    { EUITCA_LEFT_JUSTIFIED, EUITCW_EXPAND }
};

@endif

@end Interface;

/**********************************************************************
 * Instant Message Window
 **********************************************************************/

@start IMWindowResource;

@object IMPrimaryClass IMPrimary =
{
    GI_states = @default & ~GS_USABLE;
    GI_comp = @IMTexts, @IMSend;
/*    HINT_SIZE_WINDOW_AS_DESIRED;*/
    HINT_PRIMARY_NO_FILE_MENU;
    HINT_ORIENT_CHILDREN_VERTICALLY;
    HINT_CENTER_CHILDREN_HORIZONTALLY;
    HINT_SIZE_WINDOW_AS_RATIO_OF_FIELD = {
						SWSS_RATIO | PCT_40,
						SWSS_RATIO | PCT_30};
}

@object GenInteractionClass IMTexts =
{
    GI_comp = @IMMainText, @IMOutbound;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
}

@object GenTextLimitClass IMMainText =
{
    /* GA_TARGETABLE is currently disabled until a workaround can be found
     * to have the text object properly scroll to the end when either
     * GTA_INIT_SCROLLING is set or when the scrollbar is added.  A test is
     * made in MSG_VIS_TEXT_SHOW_POSITION that blocks the scroll if the
     * object is targetable and is not currently the target.
     */
    GI_attrs = @default | GA_READ_ONLY;
    GTXI_attrs = @default | GTA_TAIL_ORIENTED | GTA_INIT_SCROLLING;
    GTXI_maxLength = 6000 ;
    GTXLI_maxLength = 4000;
    GTXLI_cutLength = 2000 ;
    ATTR_GEN_TEXT_SELECTABLE;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
/*    HINT_INITIAL_SIZE = {0, SST_LINES_OF_TEXT | 6, 0};*/
    HINT_TEXT_WHITE_WASH_COLOR; /* White background */
}

@object IMWindowTextClass IMOutbound =
{
    GI_attrs = @default | GA_TARGETABLE;
    GTXI_maxLength = 700;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
/*  HINT_EXPAND_HEIGHT_TO_FIT_PARENT; */
    HINT_MINIMUM_SIZE = {0, SST_LINES_OF_TEXT | 2, 0};
    HINT_TEXT_WHITE_WASH_COLOR; /* White background */
    HINT_DEFAULT_FOCUS;
    HINT_DEFAULT_TARGET;
}

@object GenTriggerClass IMSend =
{
    GI_visMoniker = 'S', "Send";
    GTI_destination = process;
    GTI_actionMsg = MSG_AIM_SEND_MESSAGE;
}

@end IMWindowResource;

/**********************************************************************
 * Simulator dialog
 **********************************************************************/

@ifdef USE_SIMULATOR

@start Interface;

@object GenInteractionClass SimulatorDialog =
{
    GI_visMoniker = "Simulator";
    GI_comp = @SDUpdateBuddy, @SDIMIn, @SDConfig, @SDQuote;
    GII_visibility = GIV_DIALOG;
    HINT_ORIENT_CHILDREN_VERTICALLY;
}

@object GenInteractionClass SDUpdateBuddy =
{
    GI_comp = @SDUBGo, @SDUBUser, @SDUBArgs;
    HINT_DRAW_IN_BOX;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
}

@object GenTriggerClass SDUBGo =
{
    GI_visMoniker = "UPDATE_BUDDY";
    GTI_destination = process;
    GTI_actionMsg = MSG_AIM_SIM_UPDATE_BUDDY;
}
    
@object GenTextClass SDUBUser =
{
    GI_visMoniker = "User:";
    GTXI_text = "LesInk";
    GTXI_maxLength = MAX_USER_NAME_LEN;
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
}

@object GenTextClass SDUBArgs =
{
    GI_visMoniker = "Args:";
    GTXI_text = ":T:0:0:0:A ";
    GTXI_maxLength = 20;
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
}

@object GenInteractionClass SDIMIn =
{
    GI_comp = @SDIIGo, @SDIIUser, @SDIIMessage;
    HINT_DRAW_IN_BOX;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
}

@object GenTriggerClass SDIIGo =
{
    GI_visMoniker = "IM_IN";
    GTI_destination = process;
    GTI_actionMsg = MSG_AIM_SIM_IM_IN;
}

@object GenTextClass SDIIUser =
{
    GI_visMoniker = "User:";
    GTXI_text = "LesInk";
    GTXI_maxLength = MAX_USER_NAME_LEN;
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
}

@object GenTextClass SDIIMessage =
{
    GI_visMoniker = "Message:";
    GTXI_text = "Testing";
    GTXI_maxLength = 100;
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
}

@object GenInteractionClass SDConfig =
{
    GI_comp = @SDCGo, @SDCData;
    HINT_DRAW_IN_BOX;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
}

@object GenTriggerClass SDCGo =
{
    GI_visMoniker = "CONFIG";
    GTI_destination = process;
    GTI_actionMsg = MSG_AIM_SIM_CONFIG;
}

@object GenTextClass SDCData =
{
    GTXI_text = "CONFIG:b sbehg\nb bboxcomp\nb lesink\np sbehg\np bboxcomp\nm 1\n";
    GTXI_maxLength = 200;
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
}

@object GenInteractionClass SDQuote =
{
    GI_comp = @SDQGo, @SDQCmd;
    HINT_DRAW_IN_BOX;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
}

@object GenTriggerClass SDQGo =
{
    GI_visMoniker = "Quote";
    GTI_destination = process;
    GTI_actionMsg = MSG_AIM_SIM_QUOTE;
}

@object GenTextClass SDQCmd =
{
    GTXI_text = "SIGN_ON:TOC1.0";
    GTXI_maxLength = 200;
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
}

@end Interface;

@endif /* USE_SIMULATOR */

/**********************************************************************
 * Buddy Configuration dialog
 **********************************************************************/

@start ConfigResource;

@object GenInteractionExClass BuddyConfigDialog =
{
    GI_visMoniker = 'B', "Buddy Configuration";
    GI_comp = @BCBuddyList, @BCActions, @BCPermitDeny, @BCDismiss;
    GII_visibility = GIV_DIALOG;
    GII_attrs = @default;
    GIEI_destination = process;
    GIEI_onDismissed = MSG_AIM_UPDATE_CONFIG;
    ATTR_GEN_HELP_CONTEXT = "Config";
}

@ifndef USE_TREE

@object GenSimpleTreeListClass BCBuddyList =
{
    GI_visMoniker = "Buddy List Setup";
    GDLI_queryMsg = MSG_AIM_QUERY_CONFIG_BUDDY_LIST;
    GIGI_destination = process;
    GIGI_behaviorType = GIGBT_EXCLUSIVE;
    ATTR_GEN_ITEM_GROUP_CUSTOM_DOUBLE_PRESS = MSG_AIM_CONFIG_BUDDY_LIST_ACTION;
    HINT_ITEM_GROUP_SCROLLABLE;
    HINT_PLACE_MONIKER_ABOVE;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
}

@endif

@object GenInteractionClass BCActions =
{
    GI_comp = @BCAddBuddy, @BCAddGroup, @BCRemove;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
}

@object GenInteractionClass BCAddBuddy = {
    GI_visMoniker = 'A', "Add Buddy";
    GI_comp = @BCAddBuddyName, @BCAddBuddyTrigger;
    GII_visibility = GIV_DIALOG;
    GII_type = GIT_COMMAND;
    HINT_INTERACTION_SINGLE_USAGE;
}

@object GenTextClass BCAddBuddyName = {
    GI_visMoniker = "Buddy";
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_maxLength = MAX_USER_NAME_LEN;
    ATTR_GEN_TEXT_ALPHA_NUMERIC;
    ATTR_GEN_TEXT_ALLOW_SPACES;
    HINT_DEFAULT_FOCUS;
}

@object GenTriggerClass BCAddBuddyTrigger = {
    GI_visMoniker = 'A', "Add";
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    GTI_destination = process;
    GTI_actionMsg = MSG_AIM_ADD_BUDDY;
    HINT_SEEK_REPLY_BAR;
    HINT_DEFAULT_DEFAULT_ACTION;
}

@object GenInteractionClass BCAddGroup = {
    GI_visMoniker = 'G', "Add Group";
    GI_comp = @BCAddGroupName, @BCAddGroupTrigger;
    GII_visibility = GIV_DIALOG;
    GII_type = GIT_COMMAND;
    HINT_INTERACTION_SINGLE_USAGE;
}

@object GenTextClass BCAddGroupName = {
    GI_visMoniker = "Group";
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_maxLength = MAX_GROUP_NAME_LEN;
    HINT_DEFAULT_FOCUS;
}

@object GenTriggerClass BCAddGroupTrigger = {
    GI_visMoniker = 'A', "Add";
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    GTI_destination = process;
    GTI_actionMsg = MSG_AIM_ADD_GROUP;
    HINT_SEEK_REPLY_BAR;
    HINT_DEFAULT_DEFAULT_ACTION;
}

@object GenTriggerClass BCRemove = {
    GI_visMoniker = 'R', "Remove Item";
    GTI_destination = process;
    GTI_actionMsg = MSG_AIM_REMOVE_BUDDY_OR_GROUP;
}

@object GenTriggerClass BCDismiss =
{
    ATTR_GEN_TRIGGER_INTERACTION_COMMAND = IC_DISMISS;
    HINT_SEEK_REPLY_BAR;
}

/**********************************************************************
 * Buddy Configuration dialog - Permit/Deny
 **********************************************************************/

@object GenInteractionExClass BCPermitDeny =
{
    GI_visMoniker = 'P', "Permit/Deny";
    GI_comp = @PDMode, @PDPermitDenyGroup, @PDDismiss;
    GII_visibility = GIV_DIALOG;
    GII_attrs = @default;
    GIEI_destination = process;
    GIEI_onDismissed = MSG_AIM_UPDATE_PDCONFIG;
    ATTR_GEN_HELP_CONTEXT = "Controls";
    HINT_ORIENT_CHILDREN_VERTICALLY;
    HINT_SEEK_REPLY_BAR;
}

@object GenInteractionClass PDPermitDenyGroup =
{
    GI_comp = @PDPermitGroup, @PDDenyGroup;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
}

@object GenItemGroupClass PDMode =
{
    GI_visMoniker = "When users want to send me a message...";
    GI_comp = @PDModePermitAll, @PDModePermitSome, @PDModeDenySome;
    GIGI_selection = TOC_PDMODE_PERMIT_ALL;
    GIGI_destination = process;
    GIGI_applyMsg = MSG_AIM_CONFIG_PDMODE;
    HINT_ITEM_GROUP_RADIO_BUTTON_STYLE;
    HINT_ORIENT_CHILDREN_VERTICALLY;
    HINT_DRAW_IN_BOX;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
}

@object GenItemClass PDModePermitAll =
{
    GI_visMoniker = "Permit all users";
    GII_identifier = TOC_PDMODE_PERMIT_ALL;
}

@object GenItemClass PDModePermitSome =
{
    GI_visMoniker = "Permit only users in the permit list";
    GII_identifier = TOC_PDMODE_PERMIT_SOME;
}

@object GenItemClass PDModeDenySome =
{
    GI_visMoniker = "Deny only users in the deny list";
    GII_identifier = TOC_PDMODE_DENY_SOME;
}

@object GenInteractionClass PDPermitGroup =
{
    GI_states = @default & ~GS_ENABLED;
    GI_comp = @PGList, @PGActions;
    HINT_ORIENT_CHILDREN_VERTICALLY;
    HINT_DRAW_IN_BOX;
}

@object GenInteractionClass PGActions =
{
    GI_comp = @PGAdd, @PGRemove;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
}

@object ConfigListClass PGList = {
    GI_visMoniker = "Permit List";
    CLI_groupType = CONFIG_LIST_TYPE_PERMIT;
    HINT_ITEM_GROUP_SCROLLABLE;
    HINT_PLACE_MONIKER_ABOVE;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
}

@object GenInteractionClass PGAdd = {
    GI_visMoniker = 'A', "Add Permit";
    GI_comp = @PGAddName, @PGAddTrigger;
    GII_visibility = GIV_DIALOG;
    GII_type = GIT_COMMAND;
    HINT_INTERACTION_SINGLE_USAGE;
}

@object GenTextClass PGAddName = {
    GI_visMoniker = "Name";
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_maxLength = MAX_USER_NAME_LEN;
    ATTR_GEN_TEXT_ALPHA_NUMERIC;
    ATTR_GEN_TEXT_ALLOW_SPACES;
    HINT_DEFAULT_FOCUS;
}

@object GenTriggerExClass PGAddTrigger = {
    GI_visMoniker = 'A', "Add";
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    GTI_destination = process;
    GTI_actionMsg = MSG_AIM_ADD_PD_USER;
    GTEI_optr1 = @PGList;
    GTEI_optr2 = @PGAddName;
    HINT_DEFAULT_DEFAULT_ACTION;
    HINT_SEEK_REPLY_BAR;
}

@object GenTriggerExClass PGRemove = {
    GI_visMoniker = 'R', "Remove Permit";
    GTI_destination = process;
    GTI_actionMsg = MSG_AIM_REMOVE_PD_USER;
    GTEI_optr1 = @PGList;
}
@object GenInteractionClass PDDenyGroup =
{
    GI_states = @default & ~GS_ENABLED;
    GI_comp = @DGList, @DGActions;
    HINT_ORIENT_CHILDREN_VERTICALLY;
    HINT_DRAW_IN_BOX;
}

@object GenInteractionClass DGActions =
{
    GI_comp = @DGAdd, @DGRemove;
    HINT_ORIENT_CHILDREN_HORIZONTALLY;
}

@object ConfigListClass DGList = {
    GI_visMoniker = "Deny List";
    CLI_groupType = CONFIG_LIST_TYPE_DENY;
    HINT_ITEM_GROUP_SCROLLABLE;
    HINT_PLACE_MONIKER_ABOVE;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_EXPAND_HEIGHT_TO_FIT_PARENT;
}

@object GenInteractionClass DGAdd = {
    GI_visMoniker = 'D', "Add Deny";
    GI_comp = @DGAddName, @DGAddTrigger;
    GII_visibility = GIV_DIALOG;
    GII_type = GIT_COMMAND;
    HINT_INTERACTION_SINGLE_USAGE;
}

@object GenTextClass DGAddName = {
    GI_visMoniker = "Name";
    GTXI_attrs = @default | GTA_SINGLE_LINE_TEXT;
    GTXI_maxLength = MAX_USER_NAME_LEN;
    ATTR_GEN_TEXT_ALPHA_NUMERIC;
    ATTR_GEN_TEXT_ALLOW_SPACES;
    HINT_DEFAULT_FOCUS;
}

@object GenTriggerExClass DGAddTrigger = {
    GI_visMoniker = 'A', "Add";
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;
    GTI_destination = process;
    GTI_actionMsg = MSG_AIM_ADD_PD_USER;
    GTEI_optr1 = @DGList;
    GTEI_optr2 = @DGAddName;
    HINT_DEFAULT_DEFAULT_ACTION;
    HINT_SEEK_REPLY_BAR;
}

@object GenTriggerExClass DGRemove = {
    GI_visMoniker = 'e', "Remove Deny";
    GTI_destination = process;
    GTI_actionMsg = MSG_AIM_REMOVE_PD_USER;
    GTEI_optr1 = @DGList;
}

@object GenTriggerClass PDDismiss =
{
    ATTR_GEN_TRIGGER_INTERACTION_COMMAND = IC_DISMISS;
    HINT_SEEK_REPLY_BAR;
}

@end ConfigResource;

/**********************************************************************
 * Icon Resource
 **********************************************************************/

@start IconResource, data;

@include "Art/oparrow.goh"
@include "Art/clarrow.goh"
@include "Art/away.goh"
@include "Art/arrived.goh"
@include "Art/departed.goh"
@include "Art/opfolder.goh"
@include "Art/clfolder.goh"

@end IconResource;

