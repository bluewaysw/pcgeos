;/*
COMMENT @********************************************************

	Copyright (c) Dirk Lausecker -- All Rights Reserved

PROJECT:	BestSound Driver

DATEI:		bsconst.def

AUTOR:		Dirk Lausecker

REVISION HISTORY:
	Name	Datum		Beschreibung
	----	-----		------------
	DL	08.08.98	Init
	DL	15.10.99	Warnings for Recording

Beschreibung:

*****************************************************************/@

BS_REC_ALLOCATE_BUFFER	enum	Warnings
BS_REC_GET_DATA_TIMEOUT	enum	Warnings
BS_SINGLE_START		enum	Warnings
BS_AI_START		enum	Warnings
BS_ISR			enum	Warnings
BS_ATTACH_STREAM	enum	Warnings
BS_DETTACH_STREAM	enum	Warnings
BS_NOTIFICATION		enum	Warnings
BS_FILL_REST		enum	Warnings
BS_DISCHARGE		enum	Warnings
BS_SECOND_LOCK		enum	Warnings
BS_SECOND_UNLOCK	enum	Warnings
BS_EC_TEST_ERROR_DS	enum	Warnings
BS_EC_TEST_ERROR_STREAM	enum	Warnings
GET_DSP_VERSION		enum	Warnings
MIXER_GET_CAP		enum	Warnings

;-----------------------------------------------------------------------------
;               STRUCTURES FOR DRIVER
;-----------------------------------------------------------------------------
DACFormatRange  struct
	DACFR_manufacturerID    word
	DACFR_format            word
	DACFR_min               word
	DACFR_max               word
DACFormatRange  ends

END_OF_DAC_FORMAT_LIST          equ     0ffffh

;-----------------------------------------------------------------------------
;               DSP COMMANDS AND VALUES
;-----------------------------------------------------------------------------
	;
	;  Value read from data port when reset went propperly
DSP_RESET_OK            	equ     0aah

DSP_DIRECT_PCM_MODE             equ     010h
DSP_DMA_PCM_MODE                equ     014h
DSP_DMA_4_TO_1_ADPCM_MODE       equ     016h
DSP_DMA_4_TO_1_REF_MODE         equ     017h
DSP_DMA_PCM_AI_MONO_LOW		equ     01ch
DSP_DMA_PCM_AI_MONO_HIGH	equ     090h	; AI Highspeed

DSP_SET_RATE_DIVISOR            equ     040h
DSP_SET_SAMPLE_RATE		equ	041h
DSP_SET_INPUT_RATE		equ	042h

DSP_SET_BLOCK_SIZE              equ     048h

DSP_DMA_2_TO_1_ADPCM_MODE       equ     074h
DSP_DMA_2_TO_1_REF_MODE         equ     075h
DSP_DMA_3_TO_1_ADPCM_MODE       equ     076h
DSP_DMA_3_TO_1_REF_MODE         equ     077h

DSP_DMA_HIGH_SPEED              equ     091h	; Singlecycle

DSP_DMA_8_BIT_AI		equ	0c0h

DSP_8BIT_OUTPUT			equ	0c6h
DSP_16BIT_OUTPUT		equ	0b6h
DSP_8BIT_INPUT			equ	0ceh
DSP_16BIT_INPUT			equ	0beh

DSP_MODE_8BIT_MONO		equ	000h
DSP_MODE_8BIT_STEREO		equ	020h

DSP_MODE_16BIT_MONO		equ	010h
DSP_MODE_16BIT_STEREO		equ	030h

DSP_EXIT_AI8			equ	0dah
DSP_EXIT_AI16			equ	0d9h

DSP_DMA_HALT                    equ     0d0h
DSP_TURN_ON_SPEAKER             equ     0d1h
DSP_TURN_OFF_SPEAKER            equ     0d3h
DSP_DMA_CONTINUE                equ     0d4h
DSP_EXIT_AUTOINIT		equ     0dah

DSP_GET_VERSION                 equ     0e1h

DSP_RECORD_DIRECT		equ	020h
DSP_RECORD_PCM_INPUT_AI		equ	02ch

;-----------------------------------------------------------------------------
;               DSP VERSION RANGES
;-----------------------------------------------------------------------------

	;
	;  To determine the "Model #" of the Sound Blaster
        ;       board.  Get the DSP Version #.

        ; SB 1.0, 1.5   : DSP version 1.05 or 2.00
        ; SB 2.0        : DSP version 2.01 - 3.00
        ; SBPro         : DSP version 3.01 - 4.00

	; (SBMicro      : ?? DSP version 1.05 or 2.00 ??)

;-----------------------------------------------------------------------------
;               PORT ADDRESSES FOR SOUND BLASTER BOARDS
;-----------------------------------------------------------------------------

SBMap   struct
	fmStatusLeft            byte    ; base + 0H
	fmDataLeft              byte    ; base + 1H
	fmStatusRight           byte    ; base + 2H
	fmDataRight             byte    ; base + 3H
	mixerAddress            byte    ; base + 4H
	mixerData               byte    ; base + 5H
	dspReset                word    ; base + 6H
	fmStatus                byte    ; base + 8H
	fmData                  byte    ; base + 9H
	dspDataRead             word    ; base + AH
	dspDataWrite            word    ; base + CH
	dspDataAvailable        byte    ; base + EH
	dspAckHighDMA		byte	; base + FH  16Bit-DMA quitt.
	cdRomReadData           byte    ; base +10H
	cdRomStatus             byte    ; base +11H
	cdRomReset              byte    ; base +12H
	cdRomEnable             byte    ; base +13H
SBMap   ends

fmSelectLeft            equ     fmStatusLeft
fmSelectRight           equ     fmStatusRight
fmSelect                equ     fmStatus
dspWriteStatus          equ     dspDataWrite
dspReadStatus           equ     dspDataAvailable	; 0eH
dspReadHighStatus	equ     dspAckHighDMA		; 0fH
dspCommand              equ     dspDataWrite
cdRomCommand            equ     cdRomReadData


;-----------------------------------------------------------------------------
;               Mixer Register Map
;-----------------------------------------------------------------------------
	; As the Mixer register is so sparse, I will just do defines.

MIXER_RESET             equ     00h
MIXER_VOICE_VOLUME      equ     04h
MIXER_MIC_MIXING        equ     0ah
MIXER_FILTER            equ     0ch
MIXER_OUTPUT            equ     0eh
MIXER_MASTER_VOLUME     equ     22h
MIXER_FM_VOLUME         equ     26h
MIXER_CD_VOLUME         equ     28h
MIXER_LINE_VOLUME       equ     2eh

MIXER_INTERRUPT_STATUS	equ	82h	; detecting INT Source

;-----------------------------------------------------------------------------
;               Macro's and Constants
;-----------------------------------------------------------------------------
        ;
        ;  The FM chip has a rather annoying feature: the fm chip
        ;  requires a rather large amount of time after a register
        ;  write so that it can get its act together.
        ;  Namely, you must 3.3 micro seconds after selecting a register
        ;  to change and 23 micro seconds after changing the register
        ;  before doing anything else.
FM_SELECT_DELAY                 equ     4       ; 3.3 micro second delay
FM_WRITE_DELAY                  equ     23      ; 23  micro second delay
DSP_RESET_DELAY                 equ     3       ; 3 micro second delay
DSP_AFTER_RESET_DELAY           equ     100     ; 100 micro second delay

TICKS_TO_DELAY_AFTER_SELECT     equ     FM_SELECT_DELAY * TICKS_PER_MICRO
TICKS_TO_DELAY_AFTER_WRITE      equ     FM_WRITE_DELAY  * TICKS_PER_MICRO
TICKS_TO_DELAY_FOR_DSP_RESET    equ     DSP_RESET_DELAY * TICKS_PER_MICRO
TICKS_TO_DELAY_AFTER_DSP_RESET  equ     DSP_AFTER_RESET_DELAY * TICKS_PER_MICRO

NUM_OF_VOICES                   equ     9       ; 20 independent voices
NUM_OF_DACS                     equ     1       ; 1 8-bit DAC

MAX_TIMES_THROUGH_WRITE_LOOP    equ     255
MAX_TIMES_THROUGH_READ_LOOP     equ     255

STANDARD_DAC_STREAM_SIZE        equ     2000h    ; 8k stream

;-----------------------------------------------------------------------------
;               FM Register Write Modes
;-----------------------------------------------------------------------------

	;
	;  There are certains times where we only want to write to
	;  a register if we think it has changed, and then there
	;  are times we want to write to a register no matter what.
	;  As all register writes pass through one routine, we have
	;  it check the current register settings and make the decision
	;  to write or not write.  In order to signal our intentions
        ;  we pass along this enumerated type:
FMRegisterWriteMode             etype   byte
        FMRWM_ALWAYS            enum    FMRegisterWriteMode
        FMRWM_ONLY_ON_CHANGE    enum    FMRegisterWriteMode

;-----------------------------------------------------------------------------
;               REGISTER ADDRESS MAP FOR FM_SYNTH CHIP
;-----------------------------------------------------------------------------

        ;
        ;  The Test register is addres 01h.  It must be initialized
        ;  to zero.

TestRegister    record
        TR_UNKNOWN      :2 = 0
        TR_DISTORT_WAVE :1 = 0
        TR_TEST         :5 = 0
TestRegister    end

        ;
        ;  The Timer registers (there are two) are address 02h, 03h.
        ;  Timer 1 is 80us, Timer 2 320us.
TimerRegister   record
        TR_TIMER        :8 = 0
TimerRegister   end

        ;
        ;  Timer control enables/disables timer 1 and timer 2.  Its
        ;  address of 04h
TimerControlRegister    record
	TCR_IRQ_RESET   :1 = 0
	TCR_MASK_T1     :1 = 0
	TCR_MASK_T2     :1 = 0
        TCR_UNKNOWN     :3 = 0
        TCR_START_T2    :1 = 0
        TCR_START_T1    :1 = 0
TimerControlRegister    end

        ;
        ;  Key board split is address 08h.  Don't ask me why.
KeyBoardSplitRegister   record
        KBSR_CSM        :1 = 0
        KBSR_SEL        :1 = 0
        :6
KeyBoardSplitRegister end

        ;
        ;  The timbre registers are from 20-35H.  One for each op_cell.
        ;  They control the "color" of the sound
TimbreRegister  record
        TR_AM           :1 = 0
        TR_VIB          :1 = 0
        TR_EG           :1 = 0
        TR_KSR          :1 = 0
        TR_MULTIPLE     :4 = 0
TimbreRegister  end

	;  The Output registers are from 40-55H.  They control the
        ;  volume of the sound.
OutputRegister  record
        OR_KSL          :2 = 0
	OR_TOTAL_LEVEL  :6 = 0
OutputRegister  end

        ;  The attack/decay registers are from 60-75H.  Attack/Decay/
        ;  Sustain/Release is a standard way of describing sound.
AttackDecayRegsiter     record
        ADR_ATTACK      :4 = 0
        ADR_DECAY       :4 = 0
AttackDecayRegister     end

        ;  The Sustain/Release registers are from 80-95H
SustainReleaseRegister  record
        SRR_SUSTAIN     :4 = 0
        SRR_RELEASE     :4 = 0
SustainReleaseRegister  end

        ;  The Frequencies registers are from A0-A8H.  Thus, they
        ;  are per channel, not per cell.
FNumberLowRegister      record
	FNLR_F_NUMBER_LOW:4 = 0
FNumberLowRegister      end

	;  The Block registers are likwise per channel.  Addresses B0-B8h.
FNumberHighRegister     record
        :2
	FNHR_KEY_ON     :1 = 0
        FNHR_BLOCK      :3 = 0
        FNHR_F_N_HIGH   :2 = 0
FNumberHighRegister     end

        ;  The Feedback register controlls feedback from the first
	;  modulation cell.
FeedBackRegister        record
        :4
        FBR_FEEDBACK    :3 = 0
        FBR_CONNECTION  :1 = 0
FeedBackRegister        end

        ;  The Wave select register determines the form of the signal
        ;  that gets generated.  Once per channel, addresses C0-C8h
WaveSelectRegister      record
        :6
        FSR_WAVE        :2 = 0
WaveSelectRegister      end

        ;  The Rythym register is on BDh.  It only has influence when
        ;  the board is set to rythym mode.
RythymRegister          record
	RR_VIB_DEPTH    :1 = 0
        RR_THM_DEPTH    :1 = 0
	RR_RHYTH_DRUM   :1 = 0
        RR_BASS_DRUM    :1 = 0
	RR_SNARE_DRUM   :1 = 0
        RR_TOM_TOM      :1 = 0
        RR_TOP_HAT      :1 = 0
        RR_HI_HAT       :1 = 0
RythymRegister          end


;-----------------------------------------------------------------------------
;               Operator Cell Map
;-----------------------------------------------------------------------------
OperatorCellMap         struct
	OP_CELL_1       byte    (0)
	OP_CELL_2       byte    (0)
	OP_CELL_3       byte    (0)
	OP_CELL_4       byte    (0)
	OP_CELL_5       byte    (0)
	OP_CELL_6       byte    (0)
			byte     2 dup (-1)
	OP_CELL_7       byte    (0)
	OP_CELL_8       byte    (0)
	OP_CELL_9       byte    (0)
	OP_CELL_10      byte    (0)
	OP_CELL_11      byte    (0)
	OP_CELL_12      byte    (0)
			byte     2 dup (-1)
	OP_CELL_13      byte    (0)
	OP_CELL_14      byte    (0)
	OP_CELL_15      byte    (0)
        OP_CELL_16      byte    (0)
	OP_CELL_17      byte    (0)
        OP_CELL_18      byte    (0)
                        byte    10 dup (-1)
OperatorCellMap ends

        .assert offset OP_CELL_1        eq      00h
        .assert offset OP_CELL_7        eq      08h
        .assert offset OP_CELL_13       eq     10h

CHANNEL_1_MOD   equ     OP_CELL_1
CHANNEL_1_CAR   equ     OP_CELL_4
CHANNEL_2_MOD   equ     OP_CELL_2
CHANNEL_2_CAR   equ     OP_CELL_5
CHANNEL_3_MOD   equ     OP_CELL_3
CHANNEL_3_CAR   equ     OP_CELL_6
CHANNEL_4_MOD   equ     OP_CELL_7
CHANNEL_4_CAR   equ     OP_CELL_10
CHANNEL_5_MOD   equ     OP_CELL_8
CHANNEL_5_CAR   equ     OP_CELL_11
CHANNEL_6_MOD   equ     OP_CELL_9
CHANNEL_6_CAR   equ     OP_CELL_12
CHANNEL_7_MOD   equ     OP_CELL_13
CHANNEL_7_CAR   equ     OP_CELL_16
CHANNEL_8_MOD   equ     OP_CELL_14
CHANNEL_8_CAR   equ     OP_CELL_17
CHANNEL_9_MOD   equ     OP_CELL_15
CHANNEL_9_CAR   equ     OP_CELL_18

DISP_FROM_MOD_TO_CAR    equ     CHANNEL_1_CAR - CHANNEL_1_MOD
			.assert DISP_FROM_MOD_TO_CAR    eq      3


;-----------------------------------------------------------------------------
;               Channel Map
;-----------------------------------------------------------------------------


ChannelMap      struct
        CM_CHANNEL_1    byte    (0)
        CM_CHANNEL_2    byte    (0)
        CM_CHANNEL_3    byte    (0)
        CM_CHANNEL_4    byte    (0)
        CM_CHANNEL_5    byte    (0)
        CM_CHANNEL_6    byte    (0)
        CM_CHANNEL_7    byte    (0)
        CM_CHANNEL_8    byte    (0)
        CM_CHANNEL_9    byte    (0)
ChannelMap      ends



;-----------------------------------------------------------------------------
;               FM Register Map
;-----------------------------------------------------------------------------
FMRegisterMap   struct
                        byte            (-1)
        FMRM_test       TestRegister            ; 01H    Test byte
        FMRM_timer1     TimerRegister           ; 02H    Timer-1
        FMRM_timer2     TimerRegister           ; 03H    Timer-2
        FMRM_control    TimerControlRegister    ; 04H    Timer Control
			byte    3 dup   (-1)
        FMRM_keysplit   KeyBoardSplitRegister   ; 08H    Keyboard Split
			byte   23 dup   (-1)
        FMRM_timbre     OperatorCellMap         ; 20-35H Timbre select
        FMRM_totalLevel OperatorCellMap         ; 40-55H Total Level select
        FMRM_attDec     OperatorCellMap         ; 60-75H Attack/Decay
        FMRM_susRel     OperatorCellMap         ; 80-95H Sustain/Release
        FMRM_freq       ChannelMap              ; A0-A8H F-Number (L)
                        byte    7 dup   (-1)
        FMRM_block      ChannelMap              ; B0-B8H Block Number
                        byte    4 dup   (-1)
        FMRM_drums      RythymRegister          ; BDH    Drum Register
                        byte    2 dup   (-1)
        FMRM_feedback   ChannelMap              ; C0-C8H Feedback
                        byte   23 dup   (-1)
        FMRM_waveform   OperatorCellMap         ; E0-F5H Wave Form Select
FMRegisterMap   ends
        .assert offset FMRM_keysplit    eq 008h
        .assert offset FMRM_timbre      eq 020h
	.assert offset FMRM_totalLevel  eq 040h
        .assert offset FMRM_attDec      eq 060h
        .assert offset FMRM_susRel      eq 080h
        .assert offset FMRM_freq        eq 0a0h
        .assert offset FMRM_block       eq 0b0h
        .assert offset FMRM_drums       eq 0bdh
        .assert offset FMRM_feedback    eq 0c0h
        .assert offset FMRM_waveform    eq 0e0h

DISP_FROM_TIMBRE_TO_LEVEL               equ     FMRM_totalLevel - FMRM_timbre
DISP_FROM_LEVEL_TO_ATTACK               equ     FMRM_attDec - FMRM_totalLevel
DISP_FROM_LEVEL_TO_FREQ                 equ     FMRM_freq - FMRM_totalLevel
DISP_FROM_FREQ_TO_BLOCK                 equ     FMRM_block - FMRM_freq
DISP_FROM_ATTACK_TO_SUSTAIN             equ     FMRM_susRel - FMRM_attDec
DISP_FROM_SUSTAIN_TO_WAVE               equ     FMRM_waveform - FMRM_susRel
DISP_FROM_MOD_WAVE_TO_CAR_TIMBRE        equ     FMRM_waveform - FMRM_timbre -                                                   DISP_FROM_MOD_TO_CAR
;-----------------------------------------------------------------------------
;                       MACROS
;-----------------------------------------------------------------------------



COMMENT @%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
                SBDDACWriteDSP Macro
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

SYNOPSIS:       Write to a DSP port

CALLED BY:      INTERNAL
PASS:           ds      -> dgroup of driver

RETURN:         nothing
DESTROYED:      ax, cx
SIDE EFFECTS:
		pauses until write register available

PSEUDO CODE/STRATEGY:
		examine write status byte
		if not available loop

		write data
		return

REVISION HISTORY:
	Name    Date            Description
	----    ----            -----------
	TS      10/26/92        Initial version
	DL	13.04.97	cx wird nicht geÑndert !
	DL	14.04.97	dx push/pop
	DL	28.06.97	out dx,al wird durch Unterprogramm aus-
				gefÅhrt. Dadurch bessere Debuggingmîgl.

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%@
SBDDACWriteDSP  macro   value,times
local   topOfLoop, writeToDSP
	;
	;  All writes to the DSP, except resets, take place
	;       through the dspData port.
	;  Before we write anything, we make sure the
	;       DSP is ready for us by polling the
	;       write data status port and checking for
	;       a zero msb.
	;  Notice however, that this is the same port
	;       we write data to.  Thus, there is only
	;       one port needed for to write data to
	;       the DAC.
	;
	push	dx
	mov     dx, ds:[writeStatusPort]        ; dx <- port address
	mov     ah, times
topOfLoop:
	in      al, dx                          ; al <- write status

	test    al, 080h                        ; check msb
	jz      writeToDSP
	dec     ah
	jnz     topOfLoop
	jz     	topOfLoop		; LA: Timeout aufgehoben

writeToDSP:
	;
	;  We can now write our data.
	mov     al, value                       ; al <- data to send
	call	SBDOutput			;(DL) out dx,al
	pop	dx
endm

COMMENT @%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%
		SBDDACReadDSP
%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%

SYNOPSIS:       Read from a DSP port

CALLED BY:      INTERNAL
PASS:           ds      -> dgroup of driver
RETURN:         al      <- data read
DESTROYED:      ah, dx
SIDE EFFECTS:
		pauses until read register available

PSEUDO CODE/STRATEGY:
		examine read status byte
		if not available loop

		read data
		return

REVISION HISTORY:
	Name    Date            Description
	----    ----            -----------
	TS      10/26/92        Initial version

%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%%@
SBDDACReadDSP   macro   times
local   topOfLoop, readFromDSP
	;
	;  We want to eventualy read data port al.  However,
	;       we must do this by having the port in dx
	;  As we may need to wait for the DSP to complete a
	;       previous command, we set up the register
	;       now and store it in a temporaty register.
	;  Then, we wait for the DSP to complete the
	;       previous result and immediatly read off
	;       the data.
	mov     dx, ds:[readStatusPort]         ; dx <- port address to check

	mov     ah, times
topOfLoop:
	in      al, dx                          ; al <- read status
	nop                                     ; delay for fast PC's

	test    al, 080h                        ; check msb
	jnz     readFromDSP
	dec     ah
	jnz     topOfLoop

readFromDSP:
	mov     dx, ds:[readDataPort]           ; dx <- port address to read
	in      al, dx                          ; read in data byte
endm

