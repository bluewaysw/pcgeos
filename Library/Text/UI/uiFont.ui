/*
 * Project:	PC GEOS
 * Module:	Text
 * File:	UI/uiFontControl.ui
 *
 * Author:	Tony Requist
 *
 *	This file contains the user interface description for a font control
 *
 *	$Id: uiFont.ui,v 1.1 97/04/07 11:17:46 newdeal Exp $
 *
 */

#if !NO_CONTROLLERS	/*+++++++++++++++++++++++++++++++++++++++++++++++*/

start FontControlUI, notDetachable;

ShortFontsList = GenItemGroup {
	genStates = default -usable;
	applyMsg = MSG_FC_SET_FONT;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	genAttributes = default +initiatesInputIgnore;
}

/* --- */

MoreFontsBox = TextSuspendOnApplyInteraction {
	genStates = default -usable;
	moniker = 'F', "Font Viewer";
	children = LongFontsList, FontSampleTextDisplay;
	type = properties;
	visibility = dialog;
	hints = {
		ATTR_GEN_HELP_CONTEXT { char "dbFontView", 0 }
	}
}

LongFontsList = GenItemGroup {
	applyMsg = MSG_FC_SET_FONT_FROM_LIST;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		HINT_ITEM_GROUP_SCROLLABLE,
		ATTR_GEN_ITEM_GROUP_STATUS_MSG {
			word	MSG_FC_USER_CHANGED_FONT_FROM_LIST
		},
		HINT_FIXED_SIZE {
			SpecWidth <>
			SpecHeight <SST_LINES_OF_TEXT, 10>
			word	10
		}
	}
	genAttributes = default +initiatesInputIgnore;
}

FontSampleTextDisplay = GenText {
	genAttributes = readOnly;
	textDisplayAttributes = default +singleLineText;
	text = "AaBbCc 123";
	hints = { 
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		ATTR_GEN_TEXT_DEFAULT_CHAR_ATTR {
			word DEF_CHAR_ATTR_FONT_SIZE(VTDF_URW_ROMAN, VTDS_18)
		}
	}
}

end FontControlUI;

/* --- */

start FontControlToolboxUI, notDetachable;

FontToolList = GenItemGroup {
	genStates = default -usable;
	applyMsg = MSG_FC_SET_FONT;
	destination = "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		HINT_ITEM_GROUP_MINIMIZE_SIZE,
		HINT_ITEM_GROUP_DISPLAY_CURRENT_SELECTION,
		HINT_CAN_CLIP_MONIKER_WIDTH,
		HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, 14>
			SpecHeight <>
		},
                HINT_MINIMUM_SIZE {
                        SpecWidth <>
                        SpecHeight <SST_LINES_OF_TEXT, 1>
                },
		HINT_GADGET_TEXT_COLOR {
			byte	C_BLUE
			byte	C_BLUE
		},
		ATTR_GEN_FOCUS_HELP {
			optr	PopupFontsHelp
		}
	}
	genAttributes = default +initiatesInputIgnore;
}

end FontControlToolboxUI;

/*
 * Strings for controllers
 */

start ControlStrings, data;

chunk FCName = "Font";

chunk ShortFontsName = "Short font list";
chunk LongFontsName = "More fonts list";
chunk PopupFontsName = "Popup fonts list";
chunk PopupFontsHelp = "Choose new font for selected text";

end ControlStrings;

#endif			/* !NO_CONTROLLERS ++++++++++++++++++++++++++++++*/

