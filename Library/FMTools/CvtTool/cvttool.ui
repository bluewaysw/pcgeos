/***********************************************************************
 *
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * PROJECT:	  
 * MODULE:	  UI Specification
 * FILE:	  convert.ui
 *
 * AUTHOR:  	  Adam de Boor: Aug 26, 1992
 *
 * REVISION HISTORY:
 *	Name	  Date	    Description
 *	----	  ----	    -----------
 *	ardeb	  8/26/92   Initial version
 *
 * DESCRIPTION:
 *	Monikers and UI for our tool aspect, primarily.
 *
 *
 * 	$Id: cvttool.ui,v 1.1 97/04/04 18:00:44 newdeal Exp $
 *
 ***********************************************************************/

#include	<generic.uih>
#include 	"Art/mkrConvert.ui"

/*
 * Subclass to set a global variable to stop the conversion process.
 */
class ConvertCancelTrigger = GenTrigger {
}

start 	AppLCGAMonikerResource;

visMoniker ConvertToolMoniker = "Upgrade 1.X Document";

end	AppLCGAMonikerResource;

start	ConvertUI, notDetachable;

ConvertFeedback = GenInteraction {
	/*
	 * Must be not usable to be added to the application object.
	 */
	genStates = default -usable;
	children = ConvertGlyph, ConvertText, ConvertStopTrigger;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_CENTER_CHILDREN_VERTICALLY
	}
	visibility = dialog;
	attributes = modal, notUserInitiatable;
	type = progress;
}

ConvertGlyph = GenGlyph {
	moniker = list {
		ConvertLCMoniker, ConvertLMMoniker, ConvertLCGAMoniker
	}
}

ConvertText = GenText {
	moniker = "Upgrading:";
	textDisplayAttributes = singleLineText;
	genAttributes = default -targetable, +readOnly;
	hints = {
		HINT_MINIMUM_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, 45>
			SpecHeight 0
		}
	}
}

ConvertStopTrigger = ConvertCancelTrigger {
	moniker = "Stop";
	action = MSG_CCT_STOP, ConvertStopTrigger;
	hints = {
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND { word IC_STOP }
	}
}

end ConvertUI;


start	ConvertStrings, data;

/*
 * Error strings
 */

chunk ConvertErrorString = "Error upgrading document: \\1";

end	ConvertStrings;
