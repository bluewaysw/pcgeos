/***********************************************************************
 *
 *	Copyright (c) Geoworks 1994 -- All Rights Reserved
 *
 * PROJECT:	  Navigation Library
 * MODULE:	  Navigate controller
 * FILE:	  navControl.ui
 *
 * AUTHOR:  	  Alvin Cham, Sep 26, 1994
 *
 * REVISION HISTORY:
 *	Name	Date		Description
 *	----	----		-----------
 *	AC	9/26/94   	Initial version
 *
 * DESCRIPTION:
 *	UI for Navigate controller of navigation library.
 *
 * 	$Id: navcontrol.ui,v 1.1 97/04/05 01:24:17 newdeal Exp $
 *
 ***********************************************************************/


/**********************************************************************
 *			Tool UI					      *
 **********************************************************************/
start NavigateToolUI, notDetachable;

NavigateToolGoBackHistTrigger = GenTrigger {
	genStates = default -usable;
	moniker = list {
		BackHistCMoniker,
		BackHistMMoniker
	}
	genAttributes = default +initiatesBusyState, +initiatesInputHoldUp;
	action = MSG_NC_GO_BACK, "TO_OBJ_BLOCK_OUTPUT";
}

NavigateToolGoForwardHistTrigger = GenTrigger {
	genStates = default -usable;
	moniker = list {
		BackHistCMoniker,
		BackHistMMoniker
	}
	genAttributes = default +initiatesBusyState, +initiatesInputHoldUp;
	action = MSG_NC_GO_FORWARD, "TO_OBJ_BLOCK_OUTPUT";
}


NavigateToolMainPageTrigger = GenTrigger {
	genStates = default -usable;
	moniker = list {
		BeginCMoniker,
		BeginMMoniker
	}
	genAttributes = default +initiatesBusyState, +initiatesInputHoldUp;
	action = MSG_NC_GOTO_HOME_PAGE, "TO_OBJ_BLOCK_OUTPUT";
}

NavigateToolHistoryTrigger = GenTrigger {
	genStates = default -usable;
	moniker = list {
		HistoryCMoniker,
		HistoryMMoniker
	}
	action = MSG_NC_INIT_HISTORY_LIST, "TO_OBJ_BLOCK_OUTPUT";
}

NavigateToolPageTurnGroup = GenInteraction {
	genStates = default -usable;
	children =
		NavigateToolPageTurnTriggers,
		NavigateToolPageTurnText;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_NO_TALLER_THAN_CHILDREN_REQUIRE,
 		HINT_MINIMIZE_CHILD_SPACING 
	}
}

NavigateToolPageTurnTriggers = GenInteraction {
	children =
		NavigateToolPreviousPageTrigger,
		NavigateToolNextPageTrigger;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_NO_TALLER_THAN_CHILDREN_REQUIRE,
		HINT_NO_WIDER_THAN_CHILDREN_REQUIRE,
		HINT_IF_SYSTEM_ATTRS { SystemAttrs mask SA_HORIZONTALLY_TINY},
			HINT_CUSTOM_CHILD_SPACING {
				SpecSizeSpec <SST_PIXELS, 1>
			},
			HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY,
		HINT_ENDIF
	}
}

chunk TurnPageText		= "Turn Page";

visMoniker PageTurnMoniker = {
	size = standard;
	color = gray1;
	aspectRatio = normal;
	gstring {
		GSBeginString
		GSSaveState
		GSSetFont  FID_UNIVERSITY, 8, 0
		GSDrawTextOptr	1, 1, TurnPageText
		GSRestoreState
		GSEndString
	}
}

NavigateToolPageTurnText = GenGlyph {
	moniker = PageTurnMoniker;
	hints = {
		HINT_CENTER_MONIKER,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT, 
	}
}

NavigateToolPreviousPageTrigger = GenTrigger {
        moniker = list {
                PrevCMoniker,
                PrevMMoniker
        }
	genAttributes = default +initiatesBusyState, +initiatesInputHoldUp;
	action = MSG_NC_PREVIOUS_PAGE, "TO_OBJ_BLOCK_OUTPUT";
}

NavigateToolNextPageTrigger = GenTrigger {
	moniker = list {
		NextCMoniker,
		NextMMoniker
	}
	genAttributes = default +initiatesBusyState, +initiatesInputHoldUp;
	action = MSG_NC_NEXT_PAGE, "TO_OBJ_BLOCK_OUTPUT";
}


end NavigateToolUI;

/**********************************************************************
 *			Features UI				      *
 **********************************************************************/

start NavigateUI, notDetachable;

NavigateGoBackHistTrigger = GenTrigger {
	genStates = default -usable;
	moniker = 'B',"Go Back History";
	kbdAccelerator = control 'b';
	genAttributes = default +initiatesBusyState, +initiatesInputHoldUp;
	action = MSG_NC_GO_BACK, "TO_OBJ_BLOCK_OUTPUT";
}

NavigateGoForwardHistTrigger = GenTrigger {
	genStates = default -usable;
	moniker = 'F',"Go Forward History";
	kbdAccelerator = control 'f';
	genAttributes = default +initiatesBusyState, +initiatesInputHoldUp;
	action = MSG_NC_GO_FORWARD, "TO_OBJ_BLOCK_OUTPUT";
}		

NavigateMainPageTrigger = GenTrigger {
	genStates = default -usable;
	moniker = 'G', "Go to beginning";
	kbdAccelerator = control 'g';
	genAttributes = default +initiatesBusyState, +initiatesInputHoldUp;
	action = MSG_NC_GOTO_HOME_PAGE, "TO_OBJ_BLOCK_OUTPUT";
}


NavigateHistoryTrigger = GenTrigger {
	genStates = default -usable;
	moniker = "Menus you've seen";
	kbdAccelerator = control 'h';
	action = MSG_NC_INIT_HISTORY_LIST, "TO_OBJ_BLOCK_OUTPUT";
}

/* Holds freqeuntly used triggers for 			  */
/* page-based Navigate (as opposed to hyperlink-based). */
NavigatePageTurnGroup = GenInteraction {
	genStates = default -usable;
	children =
		NavigatePreviousPageTrigger,	
		NavigateNextPageTrigger;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_DRAW_IN_BOX
	}
}

NavigatePreviousPageTrigger = GenTrigger {
	moniker = 'b', "Turn page back";
	kbdAccelerator = specificUI control '<';
	genAttributes = default +initiatesBusyState, +initiatesInputHoldUp;
	action = MSG_NC_PREVIOUS_PAGE, "TO_OBJ_BLOCK_OUTPUT";
}

NavigateNextPageTrigger = GenTrigger {
	moniker = 'f',"Turn page forward";
	kbdAccelerator = specificUI control '>';
	genAttributes = default +initiatesBusyState, +initiatesInputHoldUp;
	action = MSG_NC_NEXT_PAGE, "TO_OBJ_BLOCK_OUTPUT";
}


end NavigateUI;

start	NavigateTemplate;

/* This interaction holds the toolbox history list. */
NavigateHistoryGroup = GenInteraction {
	moniker = "Menus you've seen";
	type = command;
	visibility = dialog;
	genStates = default -usable;
	attributes = default +notUserInitiatable;
	children =
		NavigateHistoryList,
		NavigateHistoryApply;
	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_DRAW_IN_BOX,
		HINT_FIXED_SIZE {
			SpecWidth <SST_WIDE_CHAR_WIDTHS, \
				   NAV_MAX_ENTRY_BUFFER_SIZE>
			SpecHeight <>
			word 2	
		},
	}
}

/* This is the displayed history list. */
NavigateHistoryList = GenDynamicList {
	queryMsg = MSG_NC_GET_HISTORY_LIST_MONIKER;
	destination = "TO_OBJ_BLOCK_OUTPUT";
 	hints = {
		HINT_ORIENT_CHILDREN_VERTICALLY,
		HINT_ITEM_GROUP_SCROLLABLE,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT, 
	}
}

NavigateHistoryApply = GenTrigger {
	moniker = "Revisit";
	genAttributes = default +signalInteractionComplete,+initiatesBusyState;
	action = MSG_NC_GOTO_HISTORY, "TO_OBJ_BLOCK_OUTPUT";
	hints = {
		HINT_SEEK_REPLY_BAR,
		HINT_DEFAULT_DEFAULT_ACTION,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND {
			word	IC_APPLY
		}
	}
}

end	NavigateTemplate;

/*********************************
 * Here come the control strings *
 *********************************/

start NavigateStrings, data;

chunk NavigateName = "Content Navigate";

chunk MainPageTriggerName = "Begin";
chunk GoBackHistTriggerName = "Backward history";
chunk GoForwardHistTriggerName = "Forward history";
chunk NextPrevPageTriggerName = "Turn page forward/back";
chunk HistoryGroupName = "History list";
/*chunk HotlistGroupName = "Hot list";*/

end NavigateStrings;
