/***********************************************************************
 *
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * PROJECT:	  PCGEOS
 * MODULE:	  prefkbd.ui
 * FILE:	  prefkbd.ui
 *
 * AUTHOR:  	  Gene Anderson: Aug 25, 1992
 *
 * REVISION HISTORY:
 *	Date	  Name	    Description
 *	----	  ----	    -----------
 *	8/25/92	  gene	    Initial version
 *
 * DESCRIPTION:
 *	UI for Keyboard module of Preferences
 *
 *
 * 	$Id: prefbg.ui,v 1.3 98/04/24 00:32:02 gene Exp $
 *
 ***********************************************************************/

#include "generic.uih"
#include "config.uih"

#include "Art/mkrPrefBG.ui"


class PrefBGDialog = PrefDialog {
}

class PrefBGChooseList = PrefTocList {
}


/**************************************************************************
 *	UI FOR SOUND
 **************************************************************************/ 
start	PrefBGUI;


BackgroundDialog = PrefBGDialog {
	moniker = "Background";
	genStates = default -usable;
	visibility = dialog;
	attributes = default +modal, +notUserInitiatable;
	type = multipleResponse;

	children = BackgroundTitleGroup, 
		BackgroundSubgroup,
		BackgroundPasteGetFilenameBox,
		BackgroundClose,
		BackgroundConvertNotice;

	hints = {
		HINT_CENTER_CHILDREN_HORIZONTALLY,
		HINT_INTERACTION_RELATED_PROPERTIES,
		HINT_INTERACTION_COMPLEX_PROPERTIES,
		HINT_INTERACTION_FREQUENT_USAGE,
		ATTR_GEN_INIT_FILE_CATEGORY {
			char BACKGROUND_CATEGORY,0
		},
		ATTR_GEN_HELP_CONTEXT {
			char "dbBackground",0
		} 
	}

}

BackgroundConvertNotice = GenInteraction {
	visibility = dialog;
	attributes = default +modal, +notUserInitiatable;
	type = multipleResponse;
	children = BackgroundConvertGlyph;
}

BackgroundConvertGlyph = GenGlyph {
	moniker = "Updating old background...";
}

BackgroundOK = GenTrigger {
	/* Don't dismiss the interaction, so the user can see the
	effects of his/her changes */
	moniker = 'S', "Set Background";
	action = MSG_GEN_GUP_INTERACTION_COMMAND, BackgroundDialog;
	hints = {
		HINT_DEFAULT_DEFAULT_ACTION,
		ATTR_GEN_TRIGGER_ACTION_DATA {
			word IC_APPLY
		}
	}
}

BackgroundClose = GenTrigger {
	moniker = "OK";
	hints = {
		HINT_SEEK_REPLY_BAR,
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND {
			word IC_DISMISS
		}
	}
}

BackgroundTitleGroup = GenInteraction {
	children = BackgroundTitle, BackgroundHelp;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY
#ifdef DO_PIZZA
		, HINT_EXPAND_WIDTH_TO_FIT_PARENT
#endif
	}
}



BackgroundTitle = TitledGlyph {
	moniker = list {
		PrefBGTextMoniker,
		PrefBGMoniker
	}
}


BackgroundHelp = GenText {
	genAttributes = readOnly;
	text = "Choose a new background from the list below. Click \\\"Set Background\\\" to see the new background.";
	hints = {
		  HINT_MINIMUM_SIZE {
			SpecWidth <SST_PIXELS, 4*72>
			SpecHeight <>
		  },
		  HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}

BackgroundSubgroup = PrefInteraction {
	children = ChooseBackgroundList, RightHandSide;
	hints = {HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_CENTER_CHILDREN_VERTICALLY}
}

RightHandSide = PrefInteraction {
    children = BackgroundAttrList, BackgroundPasteTriggerGroup,
	       BackgroundColorList, BackgroundOK;
    hints = {
	HINT_ORIENT_CHILDREN_VERTICALLY,
	HINT_CENTER_CHILDREN_HORIZONTALLY
    }
}

ChooseBackgroundList = PrefBGChooseList {
	/* Default selection is item 0 */
	numSelections = 1;
	selection = 0;
	tokenChars = "'BKGD'";
	moniker = "Backgrounds Available:";
	hints = {
		ATTR_GEN_PATH_DATA {
			GenFilePath < SP_PUBLIC_DATA, <BACKGROUND_DIR,0>>
		},
		ATTR_PREF_TOC_LIST_EXTRA_ENTRY_1 {
			PrefTocExtraEntry <solidBG>
		},
		ATTR_GEN_INIT_FILE_KEY {
			char BACKGROUND_NAME_KEY,0
		},
		HINT_ITEM_GROUP_SCROLLABLE,
		HINT_PLACE_MONIKER_ABOVE,
		HINT_FIXED_SIZE {
			SpecWidth <SST_AVG_CHAR_WIDTHS, 26>
			SpecHeight <SST_LINES_OF_TEXT, 10>
			word	10
		}

	}
}

chunk solidBG = {Standard Solid Background}

BackgroundAttrList = PrefItemGroup {
        moniker = "";  /* Empty line for the spacing reason */
	initFileFlags = default +useItemStrings;
	numSelections = 1;
	selection = 2;
	hints = { 
		HINT_PLACE_MONIKER_ABOVE, 
		HINT_ORIENT_CHILDREN_VERTICALLY,
		ATTR_GEN_INIT_FILE_KEY {
			char BACKGROUND_ATTR_KEY,0
		},
		ATTR_GEN_INIT_FILE_CATEGORY {
			char BACKGROUND_CATEGORY,0
		}
	}
	children = UpperLeftAttrChoice, 
			CenterAttrChoice, 
			TileAttrChoice;
}

UpperLeftAttrChoice = PrefStringItem {
    	moniker = 'u', "Place graphic in upper-left of screen";
	identifier = 0;
	initFileString = "none";
}

CenterAttrChoice = PrefStringItem {
    	moniker = 'P', "Place graphic in center of screen";
	identifier = 1;
	initFileString = "center";
}

TileAttrChoice = PrefStringItem {
    	moniker = 'T', "Tile graphic to fill screen";
	identifier = 2;
	initFileString = "tile";
}

BackgroundColorList = PrefColorSelector {
	/* This object sets itself not usable on monochrome displays */
	color = {
		redOrIndex = C_CYAN;
	}
	moniker = "Background Color:";
	hints = { 
		ATTR_GEN_INIT_FILE_KEY {
			char BACKGROUND_COLOR_KEY,0
		},
		ATTR_GEN_CONTROL_PROHIBIT_UI {
			CSFeatures not (mask CSF_INDEX or mask CSF_OTHER)
		},
		HINT_DRAW_IN_BOX
	 }
}

BackgroundPasteTriggerGroup = GenInteraction {
	children = BackgroundPasteTrigger /*, BackgroundCutTrigger */;
}

BackgroundPasteTrigger = GenTrigger {
	genStates = default -enabled;
	moniker = "Get Background from Clipboard";
	action = MSG_PREF_BG_DIALOG_PASTE, BackgroundDialog;
	genAttributes = default +initiatesInputIgnore, 
				+initiatesBusyState;
	hints = {
	    HINT_TRIGGER_BRINGS_UP_WINDOW
	}
}

#if 0	/* not used */
BackgroundCutTrigger = GenTrigger {
	genStates = default -enabled;
	moniker = "Remove Background";
	action = MSG_PREF_BG_DIALOG_PASTE, BackgroundDialog;
	genAttributes = default +initiatesInputIgnore, 
				+initiatesBusyState;
	hints = {
	    HINT_TRIGGER_BRINGS_UP_WINDOW
	}
}
#endif

BackgroundPasteGetFilenameBox = GenInteraction {
	attributes = default +modal, +notUserInitiatable,
				+initiatedViaUserDoDialog;
	children = BackgroundPasteFilename, BackgroundPasteFNameTrigger;
	type = command;
	visibility = dialog;
}

BackgroundPasteFilename = GenText {
	moniker = "Enter name for this background:";
	textDisplayAttributes = default +singleLineText;
	hints = {
		ATTR_GEN_TEXT_LEGAL_FILENAMES, 
		HINT_PLACE_MONIKER_ABOVE,
		HINT_DEFAULT_FOCUS
		}
	maxLength = "FILE_LONGNAME_LENGTH";
}

BackgroundPasteFNameTrigger = GenTrigger {
	moniker = 't', "Create";
	genAttributes = default +signalInteractionComplete;
	hints = {
		ATTR_GEN_TRIGGER_INTERACTION_COMMAND {word IC_OK},
		HINT_SEEK_REPLY_BAR,
		HINT_DEFAULT_DEFAULT_ACTION
	}
}



/********************************
Right now, minimize doesn't work because we're a modal interaction.
In order to minimize, we'd need to dismiss the interaction and then
set the primary minimized.  There's then the usability issue of "why
doesn't the same dialog box I had up before come back up when I
unminimize?" 


BackgroundMinimize = GenTrigger {
	moniker = "Minimize";
	action = MSG_GEN_DISPLAY_SET_MINIMIZED, PrefMgrPrimary;
	hints = {
		HINT_SEEK_REPLY_BAR,
	}
}
********************************/


end	PrefBGUI;

start	Strings, data;


visMoniker PrefBGTextMoniker = "Background";


visMoniker PrefBGMonikerList = list {
		PrefBGTextMoniker,
		PrefBGMoniker
}

chunk   badPasteString = "The clipboard currently contains text that cannot be used for a background. You need to go back and select it with the pointer tool, then copy it to the clipboard and try again.\\r\\rError Code: PR-07";

chunk   overwriteString = "The file \\\"\\1\\\" already exists.\\rDo you wish to overwrite it?";

chunk   sharingDeniedString = "The file cannot be overwritten because it\'s currently in use or is a Read Only file.\\r\\rError Code: PR-08";

chunk   fullDiskString = "Unable to create the background file because the disk is full.\\r\\rError Code: PR-09";

chunk   dosErrorString = "Unable to create the background file because of a DOS or network error.\\r\\rError Code: PR-10";

chunk	noFilenameString = "You must enter a filename.";

end	Strings;
