/***********************************************************************
 *
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * PROJECT:	  PCGEOS
 * MODULE:	  prefos
 * FILE:	  prefos.ui
 *
 * AUTHOR:  	  Gene Anderson: Aug 25, 1992
 *
 * REVISION HISTORY:
 *	Date	  Name	    Description
 *	----	  ----	    -----------
 *	8/25/92	  gene	    Initial version
 *
 * DESCRIPTION:
 *	UI for Sound module of Preferences
 *
 *
 * 	$Id: prefos.ui,v 1.4 98/03/24 21:56:19 gene Exp $
 *
 ***********************************************************************/

#include "generic.uih"
#include "config.uih"
#include "Art/geos/mkrPrefOS.ui"

class PrefOSSwapText = PrefText {
}

/**************************************************************************
 *	UI FOR SOUND
 **************************************************************************/ 
start	PrefOSUI;


PrefOSTitle = TitledGlyph {
	moniker = list {
		PrefOSTextMoniker,
		PrefOSHCMoniker,
		PrefOSLCMoniker,
		PrefOSLMMoniker,
		PrefOSLCGAMoniker
	}
}



PrefOSRoot = PrefDialog vardataReloc {
	genStates = default -usable;
	moniker = "Geos";


	children = PrefOSTitleGroup, PrefOSSubgroup;

	hints = {
		HINT_INTERACTION_SINGLE_USAGE,
		HINT_INTERACTION_COMPLEX_PROPERTIES,
		HINT_INTERACTION_RELATED_PROPERTIES,
		ATTR_GEN_HELP_CONTEXT {
			char "dbOpSys",0
		},
		ATTR_PREF_REBOOT_STRING {
			UN_OPTR operatingSystemParamString
		}

	}

}



PrefOSTitleGroup = PrefInteraction {
	children = PrefOSTitle, PrefOSHelp;
	hints = {HINT_ORIENT_CHILDREN_HORIZONTALLY}
}

PrefOSHelp = GenText {
	genAttributes = readOnly;
	text = "CAUTION!  Incorrectly changing any of these options can seriously degrade your computer's performance and possibly even prevent the system from functioning at all.";
	hints = {
		  HINT_MINIMUM_SIZE {
			SpecWidth <SST_PIXELS, 400>
			SpecHeight <>
		  },
		  HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}

PrefOSSubgroup = PrefInteraction {
	children = SwapAndWaitPostGroup, HandlesGroup;
	hints = { 
		HINT_CENTER_CHILDREN_HORIZONTALLY, 
		HINT_EXPAND_WIDTH_TO_FIT_PARENT 
	}
}

SwapAndWaitPostGroup = PrefInteraction {
	children = SwapfileGroup, WaitPostGroup;
	hints = { HINT_ORIENT_CHILDREN_HORIZONTALLY }
}

/*
 *	SwapfileGroup
 */

SwapfileGroup = PrefInteraction {
	moniker = 'S', "Swap File";
	children = SwapfileText, SwapfileSpinGroup;
	hints = {
		HINT_PLACE_MONIKER_ABOVE,
		HINT_DRAW_IN_BOX,
		INIT_FILE_CATEGORY(diskswap)
	}
}

SwapfileText = PrefOSSwapText {
	prefAttrs = default +rebootIfChanged, +saveIfChanged;
	moniker = 'P', "Path:";
	textDisplayAttributes = singleLineText, noWordWrapping, \
		allowTextOffEnd, useTabForNavigation;
	maxLength = "PATH_BUFFER_SIZE";
	hints = {
		HINT_DEFAULT_FOCUS,
		ATTR_GEN_TEXT_LEGAL_DOS_PATH,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		INIT_FILE_KEY(file)
	}
}

SwapfileSpinGroup = PrefInteraction {
	children = SwapfilePageSpin, SwapfileSizeSpin;
	hints = {HINT_CENTER_CHILDREN_ON_MONIKERS}
}

SwapfilePageSpin = PrefValue {
	prefAttrs = default +rebootIfChanged;
	moniker = 'g', "Page Size (bytes):";
	value = 2048;
	minimum = 1024;
	maximum = 8192;
	increment = 512;
	hints = {
		INIT_FILE_KEY(page),
		ATTR_PREF_VALUE_ROUND {
			word 512
		}
	}
}

SwapfileSizeSpin = PrefValue {
	prefAttrs = default +rebootIfChanged;
	moniker = 'F', "Max File Size (Kbytes):";
	value = 2048;
	minimum = 512;
	maximum = 16384;
	increment = 512;
	hints = {
		INIT_FILE_KEY(size),
		ATTR_PREF_VALUE_ROUND {
			word 512
		}
	}
}

/*
 *	WaitPostGroup
 */

WaitPostGroup = PrefItemGroup {
	numSelections  = 1;
	selection = FALSE;
	prefAttrs = default +rebootIfChanged;
	moniker = 'W', "Wait/Post Support";
	hints = { 
		HINT_PLACE_MONIKER_ABOVE, 
		HINT_DRAW_IN_BOX,
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
		HINT_CENTER_CHILDREN_VERTICALLY,
		HINT_CENTER_CHILDREN_HORIZONTALLY,
		ATTR_GEN_ITEM_GROUP_INIT_FILE_BOOLEAN,
		INIT_FILE_KEY(waitpost),
		INIT_FILE_CATEGORY(system)
	}
	children = WaitPostOn, WaitPostOff;
}

WaitPostOn = GenItem {
	moniker = "On";
	identifier = TRUE;
}

WaitPostOff = GenItem {
	moniker = "Off";
	identifier = FALSE;
}

/*
 *	HandlesGroup
 */

HandlesGroup = PrefValue {
	prefAttrs = default +rebootIfChanged;
	moniker = 'H', "Number of Handles:";
	value = 2500;
	minimum = 1500;
	maximum = 3500;
	increment = 100;
	hints = {
		INIT_FILE_KEY(handles),
		INIT_FILE_CATEGORY(system),
		ATTR_PREF_VALUE_ROUND {
			word	100
		}
	}
}

end	PrefOSUI;

start	Strings, data;


visMoniker PrefOSTextMoniker = "Geos";

visMoniker PrefOSMonikerList = list {
		PrefOSTextMoniker,
		PrefOSHCMoniker,
		PrefOSLCMoniker,
		PrefOSLMMoniker,
		PrefOSLCGAMoniker
}

chunk operatingSystemParamString = "operating system parameters";

end	Strings;
