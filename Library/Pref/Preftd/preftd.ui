/***********************************************************************
 *
 *	Copyright (c) GeoWorks 1992 -- All Rights Reserved
 *
 * PROJECT:	  PCGEOS
 * MODULE:	  preftd
 * FILE:	  preftd.ui
 *
 * AUTHOR:  	  Gene Anderson: Aug 25, 1992
 *
 * REVISION HISTORY:
 *	Date	  Name	    Description
 *	----	  ----	    -----------
 *	8/25/92	  gene	    Initial version
 *
 * DESCRIPTION:
 *	UI for Date & Time (formerly Time & Date) module of Preferences
 *
 *
 * 	$Id: preftd.ui,v 1.2 98/04/24 01:16:26 gene Exp $
 *
 ***********************************************************************/

#include "generic.uih"
#include "config.uih"

#ifdef GPC_VERSION
#include "Art/mkrPrefGPC.ui"
#else
#include "Art/mkrPrefTD.ui"
#endif

/*---------------------------------------------------------------------------
	CONSTANTS DEFINITIONS
---------------------------------------------------------------------------*/

#define _NIKE	0

/*
 * Class declarations
 */
 
class	ClockItemGroup = PrefItemGroup {
}


/**************************************************************************
 *	UI FOR USER LEVEL
 **************************************************************************/ 
start	PrefTDUI;

PrefTDRoot = PrefInteraction {
	moniker = "Date & Time";
	attributes = default +modal, +notUserInitiatable;
	visibility = dialog;
	type = properties;
	genStates = default -usable;
	children =
		PrefTDTitleGroup,
#ifdef GPC_VERSION
		PrefTDControl, PrefTriggerGroup;
#else
		PrefTDControl;
#endif
	hints = {
		HINT_INTERACTION_SINGLE_USAGE,
#ifdef GPC_VERSION
		HINT_CENTER_CHILDREN_HORIZONTALLY,
#else
		HINT_INTERACTION_COMPLEX_PROPERTIES,
#endif
		ATTR_GEN_HELP_CONTEXT {
			char "dbDate&Time",0
		}
	}
}

#ifdef GPC_VERSION
PrefTriggerGroup = GenInteraction {
    children = PrefTDOK, PrefTDClose;
    hints = {
	HINT_ORIENT_CHILDREN_HORIZONTALLY
    }
}

PrefTDOK = GenTrigger {
    moniker = 'C', "Change";
    genStates = default -enabled;
    genAttributes = signalInteractionComplete;
    hints = {
	HINT_SEEK_REPLY_BAR,
	ATTR_GEN_TRIGGER_INTERACTION_COMMAND {
	    word IC_APPLY
	}
    }
}

PrefTDClose = GenTrigger {
    moniker = 'D', "Don't Change";
    hints = {
	HINT_SEEK_REPLY_BAR,
	ATTR_GEN_TRIGGER_INTERACTION_COMMAND {
	    word IC_DISMISS
	}
    }
}
#endif

PrefTDTitleGroup = PrefInteraction {
	children =
		PrefTDTitle,
		PrefTDHelp
		;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}

PrefTDTitle = TitledGlyph {
	moniker = list {
		PrefTDTextMoniker,
#ifdef GPC_VERSION
		PrefGPCMoniker
#else
		PrefTDLCMoniker,
		PrefTDLMMoniker,
		PrefTDLCGAMoniker
#endif
	}
}

PrefTDHelp = GenText {
	genAttributes = readOnly;
	text = "Change these options to set your computer's built-in clock.";
	hints = {
		HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}


PrefTDControl = PrefTimeDateControl {
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT
	}
}

end	PrefTDUI;


start	Strings, data;

visMoniker PrefTDTextMoniker = "Date & Time";

visMoniker PrefTDMonikerList = list {
		PrefTDTextMoniker,
#ifdef GPC_VERSION
		PrefGPCMoniker
#else
		PrefTDLCMoniker,
		PrefTDLMMoniker,
		PrefTDLCGAMoniker
#endif
}

end	Strings;


