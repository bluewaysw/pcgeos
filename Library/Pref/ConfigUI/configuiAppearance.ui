/***********************************************************************
 *
 *	Copyright (c) Designs in Light 2002 -- All Rights Reserved
 *
 * FILE:	  configuiAppearance.ui
 *
 ***********************************************************************/

class ConfigUICColorSelector = PrefColorSelector {
}

class PrefColorsSample = GenGadget {
}

class ConfigUIFontArea = PrefItemGroup {
}

class ConfigUICSchemeList = PrefDynamicList {
}

start ConfigUIUI;

AppearanceSettings = PrefInteraction {
    prefInteractionAttrs = default 	+loadOptionsOnInitiate,
					+saveOptionsOnApply;
    children =
	ColorGroup,
	AppearanceBottomGroup;
    hints = {
	HINT_ORIENT_CHILDREN_VERTICALLY,
	HINT_CENTER_CHILDREN_ON_MONIKERS,
	HINT_EXPAND_WIDTH_TO_FIT_PARENT
    }
}


AppearanceBottomGroup = PrefInteraction {
    children =
	SpecificUIGroup,
	UIFontsGroup;
    hints = {
	HINT_ORIENT_CHILDREN_HORIZONTALLY,
	HINT_EXPAND_WIDTH_TO_FIT_PARENT
    }
}

/******************************************************************************
 *	Specfic UI stuff
 *****************************************************************************/

SpecificUIGroup = PrefInteraction {
    moniker = "Look & Feel";
    children =
	UIList;
    hints = {
	HINT_DRAW_IN_BOX,
	HINT_CENTER_CHILDREN_HORIZONTALLY,
	HINT_EXPAND_HEIGHT_TO_FIT_PARENT
    }
}

UIList = PrefItemGroup vardataReloc {
    destination = ConfigUIRoot;
    children =
	UILISUI,
	UILMotif;
    prefAttrs = default +rebootIfChanged;
    initFileFlags = default +useItemMonikers;
    hints = {
	ATTR_PREF_REBOOT_STRING {
	    UN_OPTR	ConfigUIRebootString
	},
	HINT_ITEM_GROUP_SCROLLABLE,
	HINT_FIXED_SIZE {
	    SpecWidth <SST_AVG_CHAR_WIDTHS, 24>
	    SpecHeight <SST_LINES_OF_TEXT, 3>
	    word 3
	},
	ATTR_GEN_INIT_FILE_CATEGORY {char "ui", 0},
	ATTR_GEN_INIT_FILE_KEY {char "specname", 0},
	ATTR_GEN_ITEM_GROUP_STATUS_MSG {
	    word MSG_CUID_SPUI_CHANGED
	}
    }
}

UILMotif = GenItem {
    moniker = "Motif";
    identifier = CUIC_MOTIF;
}

UILISUI = GenItem {
    moniker = "Industry Standard";
    identifier = CUIC_ISUI;
}

/******************************************************************************
 *
 *	Color stuff
 *
 ******************************************************************************/

ColorGroup = PrefInteraction {
    moniker = "UI Colors";
    prefAttrs = default +rebootIfChanged;
    children =
	ColorLeftGroup,
	ColorRightGroup;
    hints = {
	HINT_EXPAND_WIDTH_TO_FIT_PARENT,
	HINT_ORIENT_CHILDREN_HORIZONTALLY,
	HINT_DRAW_IN_BOX,
	HINT_PLACE_MONIKER_ABOVE
    }
}

ColorSample = PrefColorsSample {
}

chunk SampleText = "Sample";
chunk SelectionSampleText = "Selection";

ColorLeftGroup = PrefInteraction {
    children =
	AreaList,
	ColorList;
    hints = {
	HINT_EXPAND_WIDTH_TO_FIT_PARENT
    }
}

AreaList = PrefItemGroup vardataReloc {
    destination = ConfigUIRoot;
    selection = CUIC_TITLE_BARS;
    children =
	TitleEntry,
	Title2Entry,
	DarkEntry,
	LightEntry,
	SelectionEntry,
	FileMgrEntry,
	HelpBGEntry;
    hints = {
	HINT_ITEM_GROUP_MINIMIZE_SIZE,
	HINT_ITEM_GROUP_DISPLAY_CURRENT_SELECTION,
	HINT_MINIMUM_SIZE {
	    SpecWidth <SST_AVG_CHAR_WIDTHS, 16>
	    SpecHeight <>
	},
	ATTR_GEN_ITEM_GROUP_STATUS_MSG {
	    word MSG_CUID_AREA_CHANGED
	}
    }
}

TitleEntry = GenItem {
    moniker = "Title bars";
    identifier = CUIC_TITLE_BARS;
}

Title2Entry = GenItem {
    moniker = "Title bar gradient";
    identifier = CUIC_TITLE_BAR_GRADIENT;
}

DarkEntry = GenItem {
    moniker = "Dark items";
    identifier = CUIC_DARK_ITEMS;
}

LightEntry = GenItem {
    moniker = "Light items";
    identifier = CUIC_LIGHT_ITEMS;
}

FileMgrEntry = GenItem {
    moniker = "File Folders";
    identifier = CUIC_FILE_MGR;
}

HelpBGEntry = GenItem {
    moniker = "Help Background";
    identifier = CUIC_HELP_BG;
}

SelectionEntry = GenItem {
    moniker = "Selections";
    identifier = CUIC_SELECTIONS;
}

ColorRightGroup = PrefInteraction {
    children =
	ColorSample,
	DefaultGroup,
	SchemeTriggers;
    hints = {
	HINT_EXPAND_WIDTH_TO_FIT_PARENT
    }
}

DefaultGroup = PrefInteraction {
    moniker = "Scheme:";
    children =
	SchemesList;
}

SchemesList = ConfigUICSchemeList {
    destination = ConfigUIRoot;
    initFileFlags = default +useItemMonikers;
    hints =  {
	ATTR_GEN_ITEM_GROUP_SET_MODIFIED_ON_REDUNDANT_SELECTION,
	ATTR_GEN_ITEM_GROUP_STATUS_MSG {
	    word MSG_CUID_SET_DEFAULT_COLORS
	},
	HINT_ITEM_GROUP_MINIMIZE_SIZE,
	HINT_WRAP_AFTER_CHILD_COUNT { word 10 },
	HINT_MAXIMUM_SIZE {
	    SpecWidth <SST_AVG_CHAR_WIDTHS, 16>
	    SpecHeight <>
	},
	HINT_ITEM_GROUP_DISPLAY_CURRENT_SELECTION,
	ATTR_GEN_INIT_FILE_CATEGORY {char "ui", 0},
	ATTR_GEN_INIT_FILE_KEY {char "scheme", 0}
    }
}

ColorList = ConfigUICColorSelector vardataReloc {
    output = ColorSample;
    prefAttrs = default +rebootIfChanged;
    hints = {
	ATTR_GEN_CONTROL_PROHIBIT_UI { word \
				       not (mask CSF_INDEX or \
					    mask CSF_OTHER)
	},
	ATTR_PREF_REBOOT_STRING {
	    UN_OPTR	ConfigUIColorRebootString
	}
    }
}

SchemeTriggers = GenInteraction {
    genStates = default -usable;
    children =
	STSaveAsDialog,
	STDelete;
    hints = {
	HINT_ORIENT_CHILDREN_HORIZONTALLY
    }
}

STSaveAsDialog = GenInteraction {
    moniker = "Save As";
    children =
	STSADName,
	STSADSave;
    type = command;
    visibility = dialog;
    attributes = default +modal;
}

STSADName = GenText {
    moniker = "Save this color scheme as:";
    maxLength = "COLOR_SCHEME_MAX_NAME_LENGTH";
    textDisplayAttributes = default +singleLineText;
    hints = {
	HINT_PLACE_MONIKER_ABOVE
    }
}

STSADSave = GenTrigger {
    moniker = "Save";
    action = MSG_CUIC_SCHEME_LIST_SAVE, SchemesList;
    hints = {
	HINT_SEEK_REPLY_BAR,
	HINT_DEFAULT_DEFAULT_ACTION
    }
}

STDelete = GenTrigger {
    moniker = "Delete";
    action = MSG_CUIC_SCHEME_LIST_DELETE, SchemesList;
}


/******************************************************************************
 *	Font Size Stuff
 *****************************************************************************/

UIFontsGroup = PrefInteraction vardataReloc {
    moniker = "Font";
    prefAttrs = default +rebootIfChanged;
    children =
	FontSizeAreaGroup,
	FontSizeGroup,
	FontSample;
    hints = {
	HINT_DRAW_IN_BOX,
	HINT_CENTER_CHILDREN_ON_MONIKERS,
	HINT_EXPAND_WIDTH_TO_FIT_PARENT,
	HINT_EXPAND_HEIGHT_TO_FIT_PARENT,
	ATTR_PREF_REBOOT_STRING {
	    UN_OPTR	ConfigUIFontRebootString
	}
    }
}

FontSizeAreaGroup = PrefInteraction {
    moniker = "Item:";
    children =
	FontSizeArea;
}

FontSizeArea = ConfigUIFontArea {
    destination = "TO_SELF";
    children =
	FSAMenus,
	FSAEditable,
	FSAFileFolder;
    selection = CUIFA_MENUS;
    hints = {
	HINT_ITEM_GROUP_MINIMIZE_SIZE,
	HINT_ITEM_GROUP_DISPLAY_CURRENT_SELECTION,
	ATTR_GEN_ITEM_GROUP_STATUS_MSG {
	    word MSG_CUIFA_FONT_AREA_CHANGED;
	}
    }
}

FSAMenus = GenItem {
    moniker = "Menus and Dialogs";
    identifier = CUIFA_MENUS;
}

FSAEditable = GenItem {
    moniker = "Editable Text";
    identifier = CUIFA_EDITABLE;
}

FSAFileFolder = GenItem {
    moniker = "File Folders";
    identifier = CUIFA_FOLDERS;
}

FontSizeGroup = PrefInteraction {
    moniker = "Font Size:";
    children =
	FontSizeList;
}

FontSizeList = GenItemGroup {
    destination = FontSizeArea;
    selection = 10;
    children =
	FSL9,
	FSL10,
	FSL12,
	FSL14,
	FSL18;
    hints = {
	HINT_ITEM_GROUP_MINIMIZE_SIZE,
	HINT_ITEM_GROUP_DISPLAY_CURRENT_SELECTION,
	ATTR_GEN_ITEM_GROUP_STATUS_MSG {
	    word MSG_CUIFA_FONT_SIZE_CHANGED;
	}
    }
}

FSL9 = GenItem {
    moniker = "9";
    identifier = 9;
}

FSL10 = GenItem {
    moniker = "10";
    identifier = 10;
}

FSL12 = GenItem {
    moniker = "12";
    identifier = 12;
}

FSL14 = GenItem {
    moniker = "14";
    identifier = 14;
}

FSL18 = GenItem {
    moniker = "18";
    identifier = 18;
}

FontSample = GenText {
    genAttributes = default +readOnly;
    textDisplayAttributes = default +singleLineText;
    text = "Sample text in selected font size.";
    hints = {
	ATTR_GEN_TEXT_DEFAULT_CHAR_ATTR {
	    word DEF_CHAR_ATTR_FONT_SIZE(VTDF_BERKELEY, VTDS_10)
	},
	ATTR_GEN_TEXT_NEVER_MAKE_SCROLLABLE,
	HINT_EXPAND_WIDTH_TO_FIT_PARENT,
	HINT_FIXED_SIZE {
	    SpecWidth <>
	    SpecHeight <SST_PIXELS, 30>
	}
    }
}

end ConfigUIUI;

start SchemeStrings, data;

/*
 * do not change the order of these strings without changing the
 * defaultColorLists table. 16 color schemes must come first
 */
chunk SLDI1 = "Motif Default";
chunk SLDI2 = "ISUI Default";
chunk SLDI3 = "Cyan";
chunk SLDI4 = "Earth Tones";
chunk SLDI5 = "Merlot";
chunk SLDI6 = "Corporate";
chunk SLDI7 = "Forest";
chunk SLDI8 = "Sedona";
chunk SLDI9 = "Stainless";
chunk SLDI10 = "High Contrast";
chunk SLDI11 = "Ocean";
chunk SLDI12 = "Blue Sky";
chunk SLDI13 = "Emerald City";
chunk SLDI14 = "Teal";
chunk SLDI15 = "Swimming Pool";
chunk SLDI16 = "Grey Sky";
chunk SLDI17 = "Cuppa Joe";
chunk SLDI18 = "Grey & Red";
chunk SLDI19 = "Grey & Green";

end SchemeStrings;
