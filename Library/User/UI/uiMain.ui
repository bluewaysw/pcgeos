/*
 * Project:	PC GEOS
 * Module:	UI
 * File:	UI/uiMain.ui
 *
 * Author:	Doug Fults
 *
 *	This file contains the interface description for the UI as an
 *	application.
 *
 *	$Id: uiMain.ui,v 1.1 97/04/07 11:46:57 newdeal Exp $
 *
 */

start ApplicationUI;

/* ----------------------------------------------------------------------------
	Application object
-----------------------------------------------------------------------------*/
class UIApplication = GenApplication {
}

UIApp = UIApplication {
	genAttributes = default -targetable;
	appState = default +avoidTransparentDetach, -focusable, -modelable;
						/* For Field object */
	appOptFlags = default +multipleInitFileCategories;
	children =
#if STATIC_PEN_INPUT_CONTROL
		SystemHWRBox,
#endif
		SysHelpObject,
		SysModalHelpObject;
	gcnList(MANUFACTURER_ID_GEOWORKS,MGCNLT_ACTIVE_LIST) =
#if STATIC_PEN_INPUT_CONTROL
		SystemHWRBox,
#endif
		SysHelpObject,
		SysModalHelpObject;
	hints = {
		ATTR_GEN_HELP_FILE { char "ui", 0 }
	}
}

end ApplicationUI;

start SystemFieldUI;

SystemField0 = GenField {
	internalFlags = needsWorkspaceMenu, loadBitmap, hasDefaultLauncher;
	hints = {
		/* no data, so we use default of 'uiFeatures' */
		ATTR_GEN_INIT_FILE_CATEGORY,
		/* must be present to get options to load */
		ATTR_GEN_INIT_FILE_KEY
	}
}

/* ADDITIONAL SYSTEM FIELDS HERE FOR EACH SCREEN */

end SystemFieldUI;

start DeleteStateUI, notDetachable;

/* dynamically attached with upward-link to UIApp */
DeleteStateConfirmBox = GenInteraction {
	children = DeleteStateText,
			DeleteStateContinue, DeleteStateReset;
	attributes = default +notUserInitiatable, +sysModal, +initiatedViaUserDoDialog;
	type = multipleResponse;
	visibility = dialog;
	hints = {
		HINT_CENTER_CHILDREN_HORIZONTALLY
	}
}

DeleteStateText = GenText {
	genAttributes = readOnly;
	text = "The software was not shut down completely the last time you used it. The computer may have been turned off or reset before you exited the program.\\r\\rClick Start Normally and the system will attempt to bring up your work area just as you left it. If you experience problems starting normally and end up back at this dialog box, click Reset to safely reset your work area.";
	textDisplayAttributes = 
		useTabForNavigation;
	hints = {
		HINT_FIXED_SIZE {
			SpecWidth <SST_PIXELS, 400>
			SpecHeight <>
		},
		HINT_MAXIMUM_SIZE {
			SpecWidth <SST_PCT_OF_FIELD_WIDTH, PCT_90>
			SpecHeight <>
		}
	}
}
DeleteStateContinue = GenTrigger {
	moniker = 'S', "Start Normally";
	genAttributes = signalInteractionComplete;
	action = MSG_USER_DELETE_STATE_FILES_DIALOG_RESPONSE, process;
	hints = {
		ATTR_GEN_TRIGGER_ACTION_DATA {
			word	IC_YES
		},
		HINT_DEFAULT_DEFAULT_ACTION,
		HINT_SEEK_REPLY_BAR
	}
}
DeleteStateReset = GenTrigger {
	moniker = 'R', "Reset";
	genAttributes = signalInteractionComplete;
	action = MSG_USER_DELETE_STATE_FILES_DIALOG_RESPONSE, process;
	hints = {
		ATTR_GEN_TRIGGER_ACTION_DATA {
			word	IC_NO
		},
		HINT_SEEK_REPLY_BAR
	}
}

end DeleteStateUI;


start ShutdownStatusUI, notDetachable;

ShutdownStatusBox = GenInteraction {
	attributes = default +notUserInitiatable, +sysModal;
	children = ShutdownStatusGlyph;
	type = organizational;
	visibility = dialog;
	hints = {
		HINT_WINDOW_NO_TITLE_BAR
		}
}
ShutdownStatusGlyph = GenGlyph {
	moniker = "One moment please...";
}

end ShutdownStatusUI;

start RemoteTransferStatusUI, notDetachable;

RemoteTransferStatusBox = GenInteraction {
	attributes = default +notUserInitiatable, +modal;
	children = RemoteTransferStatusGlyph;
	type = organizational;
	genStates = default -usable;
	visibility = dialog;
	hints = {
		HINT_WINDOW_NO_TITLE_BAR,
		HINT_CENTER_CHILDREN_HORIZONTALLY
		}
}
RemoteTransferStatusGlyph = GenGlyph {
	moniker = "Trying to connect...";
}
visMoniker SendingGlyph = "Sending data...";
visMoniker ReceivingGlyph = "Receiving data...";
end RemoteTransferStatusUI;

start HelpObjectUI, notDetachable;

SysHelpObject = HelpControl {
	moniker = "Help";
	helpType = "HT_SYSTEM_HELP";
	visibility = dialog;
	hints = {
		ATTR_GEN_CONTROL_REQUIRE_UI {
			word	mask HPCF_HELP
		}
		,
		ATTR_GEN_CONTROL_PROHIBIT_UI {
			word	mask HPCF_CLOSE
		},
		HINT_GEN_CONTROL_DESTROY_CHILDREN_WHEN_NOT_INTERACTABLE
	}
}

SysModalHelpObject = HelpControl {
	moniker = "Help";
	helpType = "HT_SYSTEM_MODAL_HELP";
	visibility = dialog;
	attributes = default +sysModal;
	hints = {
		ATTR_GEN_CONTROL_PROHIBIT_UI {
			word	mask HPCF_HISTORY 
		}
	}
}

end HelpObjectUI;



start ActivatingUI, notDetachable;

/* dynamically attached with upward-link to UIApp */
ActivatingBox = GenInteraction {
	genStates = default -usable;
	children = ActivatingGroup, WaitGroup;
	attributes = default +notUserInitiatable/*, +sysModal*/;
	type = organizational;
	visibility = dialog;
	hints = {
		HINT_WINDOW_NO_TITLE_BAR,
		HINT_CUSTOM_CHILD_SPACING {	
			SpecSizeSpec <SST_PIXELS, 10>
		},
		HINT_FULL_JUSTIFY_CHILDREN_VERTICALLY,
		HINT_INCLUDE_ENDS_IN_CHILD_SPACING
	}
}



ActivatingGroup = GenInteraction {
	children = ActivatingText, ApplText;
	hints = {
		HINT_MINIMIZE_CHILD_SPACING,
		HINT_CENTER_CHILDREN_HORIZONTALLY,
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_ORIENT_CHILDREN_HORIZONTALLY
	}
}

ActivatingText = GenGlyph {
	moniker = "Activating ";
}

ApplText = GenGlyph {
}

WaitGroup = GenInteraction {
	children = WaitGlyph, ActivatingGlyph;
	hints = {
		HINT_ORIENT_CHILDREN_HORIZONTALLY,
		HINT_CUSTOM_CHILD_SPACING {	
			SpecSizeSpec <SST_PIXELS, 15>
		},
		HINT_EXPAND_WIDTH_TO_FIT_PARENT,
		HINT_CENTER_CHILDREN_VERTICALLY,
		HINT_FULL_JUSTIFY_CHILDREN_HORIZONTALLY,
		HINT_INCLUDE_ENDS_IN_CHILD_SPACING
	}
}

WaitGlyph = GenGlyph {
	moniker = "One moment please...";
}

ActivatingGlyph = GenGlyph {
	moniker = list {
		BlankSCMoniker, 
		BlankSMMoniker,
		BlankSCGAMoniker,
		BlankYCMoniker, 
		BlankYMMoniker
	}
}

end ActivatingUI;


start AppSCMonikerResource, data;
visMoniker BlankSCMoniker = {
	size = standard;
	style = icon;
	aspectRatio = normal;
	color = color4;
	cachedSize = 48, 30;
	gstring {
		GSBeginString
		GSEndString
	}
}
end AppSCMonikerResource;

start AppSMMonikerResource, data;
visMoniker BlankSMMoniker = {
	size = standard;
	style = icon;
	aspectRatio = normal;
	color = gray1;
	cachedSize = 48, 30;
	gstring {
		GSBeginString
		GSEndString
	}
}
end AppSMMonikerResource;

start AppSCGAMonikerResource, data;
visMoniker BlankSCGAMoniker = {
	size = tiny;
	style = icon;
	aspectRatio = verySquished;
	color = gray1;
	cachedSize = 48, 14;
	gstring {
		GSBeginString
		GSEndString
	}
}
end AppSCGAMonikerResource;

start AppYCMonikerResource, data;
visMoniker BlankYCMoniker = {
	size = tiny;
	style = icon;
	aspectRatio = normal;
	color = color4;
	cachedSize = 32, 20;
	gstring {
		GSBeginString
		GSEndString
	}
}
end AppYCMonikerResource;

start AppYMMonikerResource, data;
visMoniker BlankYMMoniker = {
	size = tiny;
	style = icon;
	aspectRatio = normal;
	color = gray1;
	cachedSize = 32, 20;
	gstring {
		GSBeginString
		GSEndString
	}
}
end AppYMMonikerResource;




#if STATIC_PEN_INPUT_CONTROL

start SystemHWRUI, notDetachable;

SystemHWRBox = GenPenInputControl {
	genStates = default +enabled;
	attributes = default +notUserInitiatable;
	hints = {
		ATTR_GEN_PEN_INPUT_CONTROL_IS_FLOATING_KEYBOARD
	}
}

end SystemHWRUI;

#endif
