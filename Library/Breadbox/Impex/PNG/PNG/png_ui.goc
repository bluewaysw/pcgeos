/***********************************************************************
 *
 * MODULE:    PNG Import/Export UI
 *
 ***********************************************************************/

@include <stdapp.goh>
@include <Objects/gItemGC.goh>
@include <Objects/gValueC.goh>
@include <Objects/colorC.goh>
@include <object.h>

@class PngImportGroupClass, GenInteractionClass;

     @message (GEN_ITEM_GROUP_APPLY_MSG) MSG_PNG_ALPHA_METHOD_APPLY;
     @message void MSG_PNG_IMPORT_ATTACH(word uiHandle = cx);

     @instance optr PIG_methodGroup = NullOptr;
     @instance optr PIG_blendBox = NullOptr;
     @instance optr PIG_maskBox = NullOptr;

@endc

@classdecl PngImportGroupClass;

@start ImportInterface;

/*----- Alpha handling method selection -----*/

@object GenItemClass PngAlphaMethodBlend = {
    GI_visMoniker = "Blend color";
    GII_identifier = 1;
}

@object GenItemClass PngAlphaMethodMask = {
    GI_visMoniker = "Alpha to mask";
    GII_identifier = 0;
}

@object GenItemGroupClass PngAlphaMethodGroup = {
    GI_visMoniker = "Method:";
    GIGI_selection = 1;
    GIGI_behaviorType = GIGBT_EXCLUSIVE;
    GI_comp = @PngAlphaMethodBlend, @PngAlphaMethodMask;

    HINT_ITEM_GROUP_DISPLAY_CURRENT_SELECTION;
    HINT_ITEM_GROUP_RADIO_BUTTON_STYLE; // HINT_ITEM_GROUP_TAB_STYLE;
}

@object GenTextClass PngAlphaBlendExplain = {
    GI_attrs = @default | GA_READ_ONLY;
    GTXI_text = "Composite the image over a chosen background color, replacing per-pixel alpha with blended results.";

    HINT_FIXED_SIZE = {
        SST_AVG_CHAR_WIDTHS | 60,
        SST_LINES_OF_TEXT | 3,
        0
    };
}

/*----- Alpha blend color selection -----*/

@object ColorSelectorClass PngAlphaBlendColor = {
    GI_visMoniker = "Blend color";
    CSI_color = {C_WHITE, CF_INDEX, 0, 0};
    GI_states = @default | GS_USABLE | GS_ENABLED;

    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    ATTR_GEN_CONTROL_REQUIRE_UI = (CSF_INDEX | CSF_RGB | CSF_OTHER);
    ATTR_GEN_CONTROL_PROHIBIT_UI = (CSF_PATTERN | CSF_DRAW_MASK);
}

@object GenInteractionClass PngAlphaBlendBox = {
    GI_visMoniker = "Blend Settings";
    GI_comp = @PngAlphaBlendExplain, @PngAlphaBlendColor;

    HINT_DRAW_IN_BOX;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_ORIENT_CHILDREN_VERTICALLY;
    HINT_CENTER_CHILDREN_ON_MONIKERS;
}

/*----- Alpha to mask selection -----*/

@object GenTextClass PngAlphaMaskExplain = {
    GI_attrs = @default | GA_READ_ONLY;
    GTXI_text = "Convert the alpha channel to a 1-bit mask. Pixels with alpha at or above the chosen threshold stay opaque.";

    HINT_FIXED_SIZE = {
        SST_AVG_CHAR_WIDTHS | 60,
        SST_LINES_OF_TEXT | 3,
        0
    };
}

@object GenValueClass PngAlphaThresholdValue = {
    GI_visMoniker = "Alpha threshold";
    GVLI_minimum = MakeWWFixed(1);
    GVLI_maximum = MakeWWFixed(255);
    GVLI_increment = MakeWWFixed(1);
    GVLI_value = MakeWWFixed(192);
    GVLI_displayFormat = GVDF_INTEGER;

    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
}

@object GenInteractionClass PngAlphaMaskBox = {
    GI_visMoniker = "Mask Settings";
    GI_states = @default & ~GS_USABLE;
    GI_comp = @PngAlphaMaskExplain, @PngAlphaThresholdValue;

    HINT_DRAW_IN_BOX;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
    HINT_ORIENT_CHILDREN_VERTICALLY;
    HINT_CENTER_CHILDREN_ON_MONIKERS;
}

/*----- Reply bar and wrapper -----*/

@object GenTriggerClass PngImportOkButton = {
    ATTR_GEN_TRIGGER_INTERACTION_COMMAND = IC_OK; //IC_APPLY;
    GI_attrs = @default | GA_SIGNAL_INTERACTION_COMPLETE;

    HINT_SEEK_REPLY_BAR;
    HINT_DEFAULT_DEFAULT_ACTION;
}

@object GenInteractionClass PngImportReplyBar = {
    GI_comp = @PngImportOkButton;

    HINT_MAKE_REPLY_BAR;
}

@object PngImportGroupClass PngImportGroup = {
    GI_visMoniker = "Import Options";
    GI_states = @default & ~GS_USABLE;
    GII_visibility = GIV_DIALOG;
    GII_attrs = GIA_MODAL;
    GI_comp = @PngAlphaMethodGroup, @PngAlphaBlendBox, @PngAlphaMaskBox, @PngImportReplyBar;

    HINT_ORIENT_CHILDREN_VERTICALLY;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
}

@end ImportInterface;


@start ExportInterface;

@object GenItemClass PngExpBit1 = {
    GI_visMoniker = 'M', "Monochrome (1 bit/pixel)";
    GII_identifier = 0;
}

@object GenItemClass PngExpBit4 = {
    GI_visMoniker = '1', "16 colors (4 bits/pixel)";
    GII_identifier = 1;
}

@object GenItemClass PngExpBit8 = {
    GI_visMoniker = '2', "256 colors (8 bits/pixel)";
    GII_identifier = 2;
}

@object GenItemClass PngExpBit24 = {
    GI_visMoniker = 'T', "TrueColor (24 bits/pixel)";
    GII_identifier = 3;
}

@object GenItemGroupClass PngExpFormGroup = {
    GIGI_selection = 3;
    GIGI_behaviorType = GIGBT_EXCLUSIVE;
    GI_comp = @PngExpBit1, @PngExpBit4, @PngExpBit8, @PngExpBit24;

    HINT_ITEM_GROUP_MINIMIZE_SIZE;
    HINT_ITEM_GROUP_DISPLAY_CURRENT_SELECTION;
}

@object GenInteractionClass PngExpBitFormat = {
    GI_visMoniker = 'C', "Colors:";
    GI_comp = @PngExpFormGroup;
}

@object GenInteractionClass PngExportGroup = {
    GI_states = @default & ~GS_USABLE;
    GI_comp = @PngExpBitFormat;

    HINT_ORIENT_CHILDREN_VERTICALLY;
    HINT_EXPAND_WIDTH_TO_FIT_PARENT;
}

@end ExportInterface;


void _pascal
PngImportAttach(word uiHandle, word uiChunk)
{
    optr root;

    if (!uiHandle) {
        return;
    }

    root = ConstructOptr(uiHandle, uiChunk);
    if (root == NullOptr) {
        return;
    }

    @call root::MSG_PNG_IMPORT_ATTACH(uiHandle);
}

@method PngImportGroupClass, MSG_PNG_IMPORT_ATTACH
{
    if (!uiHandle) {
        pself->PIG_methodGroup = NullOptr;
        pself->PIG_blendBox = NullOptr;
        pself->PIG_maskBox = NullOptr;
        return;
    }

    pself->PIG_methodGroup = @call oself::MSG_GEN_FIND_CHILD_AT_POSITION(0);
    pself->PIG_blendBox = @call oself::MSG_GEN_FIND_CHILD_AT_POSITION(1);
    pself->PIG_maskBox = @call oself::MSG_GEN_FIND_CHILD_AT_POSITION(2);
}


@method PngImportGroupClass, MSG_VIS_OPEN
{
    optr selfOptr;
    word selection;

    @callsuper();

    if (pself->PIG_methodGroup == NullOptr) {
        return;
    }

    selfOptr = @call oself::MSG_META_GET_OPTR();
    @call pself->PIG_methodGroup::MSG_GEN_ITEM_GROUP_SET_DESTINATION(selfOptr);
    @call pself->PIG_methodGroup::MSG_GEN_ITEM_GROUP_SET_APPLY_MSG(MSG_PNG_ALPHA_METHOD_APPLY);

    selection = @call pself->PIG_methodGroup::MSG_GEN_ITEM_GROUP_GET_SELECTION();
    @call oself::MSG_PNG_ALPHA_METHOD_APPLY(selection, 1, 0);
}

@method PngImportGroupClass, MSG_PNG_ALPHA_METHOD_APPLY
{
    if (pself->PIG_blendBox == NullOptr || pself->PIG_maskBox == NullOptr) {
        return;
    }

    if (selection == GIGS_NONE) {
        @call pself->PIG_blendBox::MSG_GEN_SET_NOT_USABLE(VUM_DELAYED_VIA_UI_QUEUE);
        @call pself->PIG_maskBox::MSG_GEN_SET_NOT_USABLE(VUM_DELAYED_VIA_UI_QUEUE);
    } else if (selection == 1) {
        @call pself->PIG_blendBox::MSG_GEN_SET_USABLE(VUM_DELAYED_VIA_UI_QUEUE);
        @call pself->PIG_maskBox::MSG_GEN_SET_NOT_USABLE(VUM_DELAYED_VIA_UI_QUEUE);
    } else {
        @call pself->PIG_maskBox::MSG_GEN_SET_USABLE(VUM_DELAYED_VIA_UI_QUEUE);
        @call pself->PIG_blendBox::MSG_GEN_SET_NOT_USABLE(VUM_DELAYED_VIA_UI_QUEUE);
    }

    @call oself::MSG_GEN_RESET_TO_INITIAL_SIZE(VUM_DELAYED_VIA_UI_QUEUE);	// shrink to minimum size
}
